/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


mailing_page_: proc (mc, name, address);

dcl  mc char (*), name char (*), address char (*);

dcl  fmt1 char (12) aligned int static init ("^10x* ^30a *");
dcl  fmt2 char (12) aligned int static init ("^25x* ^30a *");

dcl  stars char (30) aligned int static init ((30)"*");
dcl  stars1 char (30) aligned int static init ((30)"*");

dcl  get_wdir_ entry () returns (char (168) aligned);
dcl  hcs_$terminate_noname entry (ptr, fixed bin (35));
dcl  hcs_$initiate entry (char (*) aligned, char (*) aligned, char (*) aligned, fixed bin (1), fixed bin (2),
     ptr, fixed bin (35));
dcl  sadp ptr int static init (null);
dcl  first bit (1) int static init ("1"b);
dcl  ec fixed bin (35);

dcl  uao char (30) int static init ("MIT IPC User Accounts Office");
dcl  uaoa char (30) int static init ("MIT 39-213");
dcl  banner (3) char (10) int static init ("MIT INTER-", "DEPARTMENT", "MAIL");

dcl  bigletter_$five entry (char (*), entry);
dcl  ioa_$ioa_stream entry options (variable);

dcl  null builtin;

%include sys_admin_data;

/* ======================================================= */

	if first then do;
	     first = "0"b;
	     call hcs_$initiate ((get_wdir_ ()), "sys_admin_data", "", 0, 1, sadp, ec);
	     if sadp ^= null then
		if sys_admin_data.user_accounts_office > "" then do;
		     uao = sys_admin_data.user_accounts_office;
		     uaoa = sys_admin_data.user_accounts_addr;
		     banner (1) = sys_admin_data.mailing_banner (1);
		     banner (2) = sys_admin_data.mailing_banner (2);
		     banner (3) = sys_admin_data.mailing_banner (3);
		end;
	     call hcs_$terminate_noname (sadp, ec);
	end;

	call ioa_$ioa_stream (mc, "^|^10xINTERDEPARTMENTAL^3/");

	call ioa_$ioa_stream (mc, fmt1, stars1);
	call ioa_$ioa_stream (mc, fmt1, "");
	call ioa_$ioa_stream (mc, fmt1, "FROM:");
	call ioa_$ioa_stream (mc, fmt1, uao);
	call ioa_$ioa_stream (mc, fmt1, uaoa);
	call ioa_$ioa_stream (mc, fmt1, "");
	call ioa_$ioa_stream (mc, fmt1, stars1);

	call ioa_$ioa_stream (mc, "^3/");

	call ioa_$ioa_stream (mc, fmt2, stars);
	call ioa_$ioa_stream (mc, fmt2, "");
	call ioa_$ioa_stream (mc, fmt2, "TO:");
	call ioa_$ioa_stream (mc, fmt2, "");
	call ioa_$ioa_stream (mc, fmt2, name);
	call ioa_$ioa_stream (mc, fmt2, "");
	call ioa_$ioa_stream (mc, fmt2, address);
	call ioa_$ioa_stream (mc, fmt2, "");
	call ioa_$ioa_stream (mc, fmt2, stars);

	call ioa_$ioa_stream (mc, "^3/");

	call bigletter_$five (banner (1), writer);
	call ioa_$ioa_stream (mc, "^/");
	call bigletter_$five (banner (2), writer);
	call ioa_$ioa_stream (mc, "^/");
	call bigletter_$five (banner (3), writer);
	call ioa_$ioa_stream (mc, "^3/^10xINTERDEPARTMENTAL");
	return;

writer:	proc (mp, ml);
dcl  mp ptr, ml fixed bin;

dcl  bcs char (ml) based (mp);

	     call ioa_$ioa_stream (mc, "^a", bcs);

	end writer;

     end mailing_page_;
