/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


/*	Entry to gather some statistics about the hash table. */

meter_token_table:
     procedure;

/* automatic */

dcl	(tokcnt,tokwords,empty_slots,maximum) fixed bin(15);
dcl	n fixed bin;
dcl	p ptr;
dcl	sysprint file print;
dcl	i fixed bin;

/* builtins */

dcl	(currentsize, null) builtin;

/* entries */

dcl	com_err_ entry options (variable);

/* external static */

dcl	cg_static_$debug bit (1) aligned external static;

/* include files */

%include pl1_token_hash_table;
%include token;
%include system;

/* program */

	if ^cg_static_$debug
	then do;
		call com_err_ (0, "meter_token_table", "No tree available. -debug control argument must be used.");
		return;
	     end;

	maximum, n, tokcnt, tokwords, empty_slots = 0;
	do i = lbound (hash_table, 1) to hbound (hash_table, 1);

	     p = hash_table (i);

	     if p = null
	     then empty_slots = empty_slots + 1;

	     do while (p ^= null);
		tokcnt = tokcnt + 1;
		tokwords = tokwords + currentsize (p -> token);
		n = n + 1;
		p = p -> token.next;
	     end;

	     maximum = max (maximum, n);		/* find the maximum # of tokens in a slot */
	     n = 0;
	end;

	put file (sysprint) skip data (tokcnt, tokwords, empty_slots, maximum);
	put file (sysprint) skip list ("Average tokens/slot = ", tokcnt / float (dim (hash_table, 1)));
	put file (sysprint) skip;
	return;

     end;
