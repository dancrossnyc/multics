/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


pause: proc;					/* program to pause for n seconds  */

/* Modified 8-17-82 by M. Toussaint to check the number of arguments. */

dcl  cu_$arg_count ext entry (fixed bin),
     cu_$arg_ptr ext entry (fixed bin, ptr, fixed bin, fixed bin),
     com_err_ entry options (variable),
     cv_dec_check_ entry (char (*), fixed bin) returns (fixed bin (35)),
     timer_manager_$sleep ext entry (fixed bin (71), bit (2));

dcl  arg_ptr ptr;

dcl  arg char (arg_len) based (arg_ptr);

dcl (arg_len, code, count, time) fixed bin, pause_time fixed bin (71);

dcl  error_table_$wrong_no_of_args fixed bin (35) external;



	call cu_$arg_count (count);

	if count = 0 then pause_time = 10;		/* pause for 10 seconds if no argument given */
	else if count > 1 then do;			/* make sure no more than one argument was given */
	     code = error_table_$wrong_no_of_args;
	     call com_err_ (code, "pause", "^/Usage:  pause {seconds}");
	     return;
	end;
	else do;
	     call cu_$arg_ptr (1, arg_ptr, arg_len, code);
	     time = cv_dec_check_ (arg, code);
	     if code ^= 0 then do;
		call com_err_ (0, "pause", " ""^a"" is not a decimal number of seconds.", arg);
		return;
	     end;
	     pause_time = time;
	end;

	call timer_manager_$sleep (pause_time, "11"b);

	return;

     end;
