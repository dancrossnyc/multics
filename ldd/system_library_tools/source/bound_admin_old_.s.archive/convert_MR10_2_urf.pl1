/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1984 *
   *                                                         *
   *********************************************************** */
/* CONVERT_MR10_2_URF: Program to convert a MR10.2 URF to a MR11 URF.
   MR11 URFs are Version 3 MSTBs rather than Version 1 or 2.  The 
   format of the URF entries themselves has not changed. */

/* format: style2 */
/* Written 1984-07-31 by Eric Swenson */

convert_MR10_2_urf:
     procedure options (variable);

/* Automatic */

	dcl     argument_idx	 fixed bin (17);
	dcl     argument_lth	 fixed bin (21);
	dcl     argument_ptr	 ptr;
	dcl     code		 fixed bin (35);
	dcl     dirname		 char (168);
	dcl     entryname		 char (32);
	dcl     n_arguments		 fixed bin (17);
	dcl     new_entryname	 char (32);
	dcl     old_entryname	 char (32);
	dcl     old_newname		 char (32);

/* Based */

	dcl     argument		 char (argument_lth) based (argument_ptr);

/* External Entries */

	dcl     com_err_		 entry () options (variable);
	dcl     convert_v2_mstb_	 entry (char (*), char (*), char (*), char (*), entry, fixed bin (18) unsigned,
				 fixed bin (35));
	dcl     convert_v2_mstb_$default_convert_proc
				 entry (ptr, ptr, ptr, fixed bin (35));
	dcl     cu_$arg_count	 entry (fixed bin, fixed bin (35));
	dcl     cu_$arg_ptr		 entry (fixed bin, ptr, fixed bin (21), fixed bin (35));
	dcl     expand_pathname_	 entry (char (*), char (*), char (*), fixed bin (35));
	dcl     hcs_$chname_file	 entry (char (*), char (*), char (*), char (*), fixed bin (35));
	dcl     ms_table_mgr_$delete	 entry (char (*), char (*), fixed bin (35));
	dcl     pathname_		 entry (char (*), char (*)) returns (char (168));
	dcl     unique_chars_	 entry (bit (*)) returns (char (15));

/* Constant */

	dcl     ME		 char (32) initial ("convert_MR10_2_urf") internal static options (constant);
	dcl     SAME_SIZE		 fixed bin (18) unsigned internal static options (constant) initial (0);
%page;
/* Program */

	call cu_$arg_count (n_arguments, code);
	if code ^= 0
	then do;
		call com_err_ (code, ME);
		return;
	     end;

	if n_arguments = 0
	then do;
		call com_err_ (0, ME, "Usage: ^a pathname_of_urf_to_convert", ME);
		return;
	     end;

	call cu_$arg_ptr (1, argument_ptr, argument_lth, (0));
	call expand_pathname_ (argument, dirname, entryname, code);
	if code ^= 0
	then do;
		call com_err_ (code, ME, "^a", argument);
		return;
	     end;

	new_entryname = rtrim (substr (entryname, 1, 16)) || "." || unique_chars_ (""b);

	call convert_v2_mstb_ (dirname, entryname, dirname, new_entryname, convert_v2_mstb_$default_convert_proc,
	     SAME_SIZE, code);
	if code ^= 0
	then do;					/* Error occured, delete new table (if created), and rename old one back. */
		call com_err_ (code, ME, "Converting v2 MSTB.");
		return;
	     end;

	old_newname = rtrim (substr (entryname, 1, 16)) || "." || unique_chars_ (""b);

/* Add shriekname to old MSTB */

	call hcs_$chname_file (dirname, entryname, "", old_newname, code);
	if code ^= 0
	then do;
		call com_err_ (code, ME, "Adding shriekname to old MSTB.");
		return;
	     end;

/* Delete real name from old MSTB */

	call hcs_$chname_file (dirname, entryname, entryname, "", code);
	if code ^= 0
	then do;
		call com_err_ (code, ME, "Deleting name from old MSTB.");
		return;
	     end;

/* Rename the new one to the real name */

	call hcs_$chname_file (dirname, new_entryname, new_entryname, entryname, code);
	if code ^= 0
	then do;
		call com_err_ (code, ME, "Renaming new MSTB.");
		return;
	     end;

	return;

     end convert_MR10_2_urf;
