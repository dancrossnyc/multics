" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

" BINOCT - BINARY TO OCTAL ASCII CONVERSION FOR PL/1 PROGRAMS
"	4/2/74 - Richard A. Barnes
"	Improved 9/3/76 by Noel I. Morris
"
" This version based on bin_to_oct by Gordon Benedict
"
" Calling sequence:
"
" dcl	b36 aligned bit(36), s12 aligned char(12),
"	binoct entry(aligned bit(36)) returns(aligned char(12));
"
"	s12 = binoct(b36);
"
	segdef	binoct
"
	equ	pr0,0
	equ	pr1,1
	equ	pr2,2
"
binoct:	
	epp1	pr0|2,*		get ptr to number to be converted
	epp2	pr0|4,*		get ptr to result
"
"	we will use last few bytes of the result string
"	as a temporary location for the translation
"
	mvt	(pr),(pr)		unpack and translate
	desc6a	pr1|0,6		6-bit bytes...
	desc9a	pr2|1(2),6	-> 9-bit bytes...
	arg	table		under translation
"
	mvn	(pr),(pr)		make into ASCII
	desc4ns	pr2|1(4),12	4-bit bytes...
	desc9ns	pr2|0,12		-> 9-bit bytes
"
	short_return
"
table:
	vfd	5/0,4/0,5/0,4/1,5/0,4/2,5/0,4/3,5/0,4/4,5/0,4/5,5/0,4/6,5/0,4/7
	vfd	5/1,4/0,5/1,4/1,5/1,4/2,5/1,4/3,5/1,4/4,5/1,4/5,5/1,4/6,5/1,4/7
	vfd	5/2,4/0,5/2,4/1,5/2,4/2,5/2,4/3,5/2,4/4,5/2,4/5,5/2,4/6,5/2,4/7
	vfd	5/3,4/0,5/3,4/1,5/3,4/2,5/3,4/3,5/3,4/4,5/3,4/5,5/3,4/6,5/3,4/7
	vfd	5/4,4/0,5/4,4/1,5/4,4/2,5/4,4/3,5/4,4/4,5/4,4/5,5/4,4/6,5/4,4/7
	vfd	5/5,4/0,5/5,4/1,5/5,4/2,5/5,4/3,5/5,4/4,5/5,4/5,5/5,4/6,5/5,4/7
	vfd	5/6,4/0,5/6,4/1,5/6,4/2,5/6,4/3,5/6,4/4,5/6,4/5,5/6,4/6,5/6,4/7
	vfd	5/7,4/0,5/7,4/1,5/7,4/2,5/7,4/3,5/7,4/4,5/7,4/5,5/7,4/6,5/7,4/7
"
	end
