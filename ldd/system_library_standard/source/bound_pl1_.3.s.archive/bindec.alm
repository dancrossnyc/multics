" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

" quick binary to decimal conversion program for v2pl1
" Barry L. Wolman, 17 May 1973
"
"	dcl bindec entry(fixed bin) returns(char(12) aligned)
"	y = bindec(x)
"
"	dcl bindec$vs entry(fixed bin) returns(char(12) varying)
"	z = bindec$vs(x)
"
	entry	bindec
	entry	vs

	equ	t,56		in stack frame of caller!!!
"
	bool	blank,040

	include	eis_micro_ops

bindec:	epp2	0|2,*		get ptr to x
	btd	(pr),(pr)		convert to decimal
	desc9a	2|0,4
	desc9ls	6|t,12
	epp2	0|4,*		get ptr to y
	mvne	(pr),(),(pr)		move into y with editing
	desc9ls	6|t,12
	vfd	18/edit,18/5
	desc9a	2|0,12
	short_return
"
vs:	epp2	0|2,*		get ptr to x
	btd	(pr),(pr)		and convert
	desc9a	2|0,4
	desc9ls	6|t,12
	epp2	0|4,*		get ptr to z
	mvne	(pr),(),(pr)	move into t(pr)get with editing
	desc9ls	6|t,12
	vfd	18/edit,18/5
	desc9a	2|0,12
	scmr	(pr),(du)		scan from right for blank
	desc9a	2|0,12
	vfd	o9/040
	arg	6|t
	ttn	no_blanks
	ldq	6|t		get number non-blank characters
	stq	2|-1		set length of varying string
	lda	12,dl		compute number of leading blanks
	sba	6|t
	mlr	(pr,rl,al),(pr,rl)	eliminate leading blanks
	desc9a	2|0,ql
	desc9a	2|0,ql
	short_return
"
no_blanks:
	lda	12,dl
	sta	2|-1
	short_return
"
edit:	vfd	9/lte+3,o9/blank,9/mfls+10,9/enf,9/mvc+1
	end
