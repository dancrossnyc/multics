/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1985 *
   *                                                         *
   *********************************************************** */
/* format: style5,ind5 */

check_password_:
     procedure (Password, ErrorMessage, Code);

/* Parameters */

     dcl	   Password	       char (*) parameter;
     dcl	   ErrorMessage	       char (*) parameter;
     dcl	   Code		       fixed bin (35) parameter;

/* Automatic */

     dcl	   code		       fixed bin (35);
     dcl	   ip		       ptr;	      /* pointer to >sc1>installation_parms */

/* Entries */

     dcl	   initiate_file_	       entry (char (*), char (*), bit (*), ptr,
			       fixed bin (24), fixed bin (35));
     dcl	   ioa_$rsnnl	       entry () options (variable);
     dcl	   pathname_	       entry (char (*), char (*))
			       returns (char (168));
     dcl	   terminate_file_	       entry (ptr, fixed bin (24), bit (*),
			       fixed bin (35));

/* External */

     dcl	   error_table_$bad_arg    fixed bin (35) external;

/* Constant */

     dcl	   SC1		       char (168)
			       initial (">system_control_1")
			       internal static options (constant);
     dcl	   INSTALLATION_PARMS      char (32)
			       initial ("installation_parms")
			       internal static options (constant);

/* Builtins */

     dcl	   length		       builtin;
     dcl	   null		       builtin;
     dcl	   rtrim		       builtin;

/* Conditions */

     dcl	   cleanup	       condition;
%page;
/* Program */

     ip = null ();				      /* setup for cleanup handler */
     on condition (cleanup) call CLEAN_UP ();

     call initiate_file_ (SC1, INSTALLATION_PARMS, R_ACCESS, ip, (0), code);
     if code ^= 0 then
	call ioa_$rsnnl (
	    "Unable to check password_min_length parameter in ^a.",
	    ErrorMessage, (0), pathname_ (SC1, INSTALLATION_PARMS));
     else if length (rtrim (Password)) < installation_parms.password_min_length
         then
	do;
	call ioa_$rsnnl ("Password must be at least ^d characters long",
	    ErrorMessage, (0), installation_parms.password_min_length);
	code = error_table_$bad_arg;
	end;
     else if Password = "help" | Password = "HELP" | Password = "?"
         | Password = "quit" | Password = "QUIT" then
	do;
	call ioa_$rsnnl ("""^a"" is reserved for special use during login",
	    ErrorMessage, (0), Password);
	code = error_table_$bad_arg;
	end;
     else
	ErrorMessage = "";

     call CLEAN_UP ();
     Code = code;
     return;
%page;
CLEAN_UP:
     procedure ();

     dcl	   temp_ptr	       ptr automatic;     /* temporary so we don't have windows */

     if ip ^= null () then
	do;
	temp_ptr = ip;
	ip = null ();
	call terminate_file_ (temp_ptr, 0, TERM_FILE_TERM, (0));
	end;
     return;
     end CLEAN_UP;

/* format: off */
%page; %include access_mode_values;
%page; %include installation_parms;
%page; %include terminate_file;
/* format: on */
     end check_password_;
