" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Bull Inc., 1988                *
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************
	name	prwrd_util_
	entry	pc
	entry	inst


	include	eis_micro_ops



pc:
	eppbp	ap|2,*		bp -> output line
	eppbb	ap|4,*		bb -> loc counter

	mvt	(pr),(pr)		convert
	desc6a	bb|0,6
	desc9a	lp|temp,6
	arg	table

	mvn	(pr),(pr)		copy to output line
	desc4ns	lp|temp(6),6
	desc9ns	bp|1,6

	short_return



inst:
	eppbp	ap|2,*		bp -> output line
	eppbb	ap|4,*		bb -> instruction word

	lda	ap|6,*		get format
	eax1	0		initialize index
	rpt	nfmts,2,tze	search for correct one
	cmpa	fmts,1		..
	ldq	-1,1		get pointer and length
	eppab	0,qu		ab -> micro-op string
	eax2	0,ql		X2 contains length of micro-op

	mvt	(pr),(pr)		convert
	desc6a	bb|0,6
	desc9a	lp|temp,6
	arg	table

	mvne	(pr),(pr,rl),(pr)
	desc4ns	lp|temp,12
	desc9a	ab|0,x2
	desc9a	bp|4,15

	short_return


fmts:	aci	"642 "
	zero	f642,6
	aci	" 6  "
	zero	fb6,4
	aci	"1542"
	zero	f1542,7
	aci	"3333"
	zero	f3333,7
	aci	"66  "
	zero	f66,5

	equ	nfmts,(*-fmts)/2


f642:	vfd	9/insm+1,9/mvc+6,9/insm+1,9/mvc+4,9/insm+1,9/mvc+2
fb6:	vfd	9/ign+6,9/insm+8,9/mvc+6,9/insm+1
f1542:	vfd	9/mvc+1,9/insm+1,9/mvc+5,9/insm+1,9/mvc+4,9/insm+1,9/mvc+2
f3333:	vfd	9/mvc+3,9/insm+1,9/mvc+3,9/insm+1,9/mvc+3,9/insm+1,9/mvc+3
f66:	vfd	9/insm+1,9/mvc+6,9/insm+1,9/mvc+6,9/insm+2



table:	ac4	"0001020304050607"
	ac4	"1011121314151617"
	ac4	"2021222324252627"
	ac4	"3031323334353637"
	ac4	"4041424344454647"
	ac4	"5051525354555657"
	ac4	"6061626364656667"
	ac4	"7071727374757677"



	use	is
	join	/link/is

temp:	bss	,2


	end
