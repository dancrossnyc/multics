" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1983 *
" *                                                         *
" ***********************************************************
" " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " "
"
"	dm_gen_checksum_ - subroutine to calculate checksums for data
"	management tables using technique described in MPM Reference
"	Guide.
"
"
"	Usage:
"	     dcl  dm_gen_checksum_ entry (ptr, fixed bin (18), fixed bin (35));
"	     call dm_gen_checksum_ (table_ptr, n_words, checksum);
"
"	written March 1983 by M. Pandolf
"
" " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " "

	name	dm_gen_checksum_
	entry	dm_gen_checksum_

	equ	offset,1		"x1 is an offset into the table
	equ	words_left,2	"x2 counts number of words still to
				"be summed
	equ	table_ptr,2	"parm_no of table_ptr
	equ	table_size,4	"parm_no of n_words
	equ	checksum,6	"parm_no of checksum
	equ	indics,6		"use last parm as storage for
				"indicators during calculations
	bool	ovfl_mask,004000	"mask for inhibiting overflow

dm_gen_checksum_:

	eppbp	ap|table_ptr,*	"get pointer to pointer to table
	eppbp	bp|0,*		"get pointer to table

	ldi	ovfl_mask,dl	"inhibit overflow fault
	sti	ap|indics,*	"save indicators
	lda	0,dl		"initialize A-reg to zero
	eax	offset 0		"offset into table initialized

	lxl	words_left ap|table_size,*
				"load x2 with word count
loop:	tze	done		"if x2 zero, then finish

	ldi	ap|indics,*	"restore indicators
	awca	bp|0,offset	"add with carry to checksum
	sti	ap|indics,*	"save indicators because they become
				"modified by eax
	alr	1		"rotate A-reg left
	eax	offset 1,offset	"and bump offset counter
	eax	words_left -1,words_left
				"decrement amount still to go
	tra	loop		"see if any more to do

done:	ldi	ap|indics,*	"restore indicators
	awca	0,dl		"add in carry, if any
	awca	0,dl		"pick up carry generated by
				"prev instruction
	sta	ap|checksum,*	"return arg

	short_return

	end
