/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


need_temp:	proc(pt,bit2);

/*	Modified:	9 April 1977 by RAB to remove mod_word operator	*/

dcl	(o,p,pt,q) ptr;
dcl	bit2 bit(2) aligned;
dcl	(check_length, check_qual_and_off) bit(1) aligned;
dcl	(null,substr) builtin;

%include operator;
%include reference;
%include nodes;


/* need_temp is called by expmac when pt has a reference count of 1 to try to prevent
   unnecessary saving of temporary values whose reference counts are momentarily to be
   counted down to zero */

	p = pt;
	check_qual_and_off = substr(bit2,1,1);
	check_length = substr(bit2,2,1);

	if check_length
	then do;
	     if p ->reference.value_in.string_aq
		then p -> reference.dont_save = "1"b;
	     q = p -> reference.length;
	     if q ^= null
	     then do;
		if q -> node.type = operator_node
		     then q = q -> operand(1);
		call check;
		end;
	     end;

	if ^ check_qual_and_off then return;

	q = p -> reference.qualifier;
	if q ^= null
	then do;
	     if q -> node.type = operator_node
		then q = q -> operand(1);
	     if q -> node.type = reference_node
		then call check;
	     end;

	q = p -> reference.offset;
	if q = null then return;

	if q -> node.type = operator_node
	     then q = q -> operand(1);

	call check;

check:	proc;

	if q -> reference.shared then return;
	if q -> reference.dont_save then return;
	if q -> reference.ref_count ^= 1 then return;
	q -> reference.dont_save = "1"b;
	call need_temp(q,"11"b);
end;

end;
