/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */

/* modified by A. Downing on 07/16/73 to use round builtin */

tanh_: procedure (number) returns (float binary (27));

/*     compute the hyperbolic tangent of a single-precision floating-point number     */
declare (number, n, r) float binary (27),
        (f, p, q) float binary (63);
dcl  (abs, exp, round) builtin;
	n = number;
	f = abs (n);
if f >= 0.55e0 then if f >= 10.5e0 then f = 1.e0; else large: do;
	     r = f + f;
	     f = exp (r);
	     f = (f - 1.e0) / (f + 1.e0);
end large; else if f >= 5.e-5 then small: do;
	     p = f*f;
	     q = (((0.2480158730158730159e-4 * p + 0.1388888888888888889e-2) * p + 0.4166666666666666667e-1) * p
	     + 0.5e0) * p + 1.e0;
	     f = ((((0.2755731922398589065e-5 * p + 0.1984126984126984127e-3) * p
	     + 0.8333333333333333333e-2)* p + 0.1666666666666666667e0) * p + 1.e0) * f / q;
	end small;
	r = round (f, 28);
	if n < 0.0e0 then r = -r;
	return (r);
     end tanh_;
