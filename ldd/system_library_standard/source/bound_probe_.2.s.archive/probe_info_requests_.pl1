/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1982 *
        *                                                         *
        * Copyright (c) 1972 by Massachusetts Institute of        *
        * Technology and Honeywell Information Systems, Inc.      *
        *                                                         *
        *********************************************************** */

/* format: style1,insnl,ifthendo,indthenelse,^indnoniterdo,^inditerdo,indcom,^indthenbegin,^indprocbody,ind2,ll78,initcol0,dclind4,idind24,struclvlind1,comcol41 */

/****  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  */

/****^  HISTORY COMMENTS:
  1) change(86-01-21,LJAdams), approve(86-01-21,MCR7327),
     audit(86-04-17,Lippard), install(86-04-24,MR12.0-1048):
     Added ssu_ references so subsystem calls to help_ will work properly.
     Added include file "help_args" which is a structure that contains all
     needed help data includeing the new version number Vhelp_args_2.
  2) change(87-08-20,LJAdams), approve(87-09-03,MCR7766),
     audit(88-08-07,GDixon), install(88-09-13,MR12.2-1109):
     Changed Vhelp_args_2 to Vhelp_args_3.
  3) change(88-09-07,WAAnderson), approve(88-09-30,MCR7952),
     audit(88-09-30,JRGray), install(88-10-24,MR12.2-1184):
     Added format control comment to make the source more readable.
                                                   END HISTORY COMMENTS */


probe_info_requests_:
  proc ();

  call probe_error_$malfunction ();	/* dummy entry */

  /* *	This procedure implements a handful of random requests, most of which were
   *	stolen more or less bodily from probe_requests_.
   *
   *	07/15/79 W. Olin Sibert
   *	Recoded to use the awful, but powerful help_, instead of help JRD Oct 1 79 */
  /* Changed to use info search paths unless nonstandard dirs listed in probe_static_info 09/28/81 S. Herbst */
  /* Modified April, 1985 by L. Adams - use new help_args_.incl.pl1                    */

  dcl P_probe_info_ptr	pointer parameter;

  dcl i			fixed bin;
  dcl desired_topic		char (32) aligned;
  dcl code		fixed bin (35);

  dcl ioa_$ioa_switch	entry options (variable);
  dcl probe_error_		entry options (variable);
  dcl probe_error_$warning	entry options (variable);
  dcl probe_error_$malfunction
			entry options (variable);

  dcl probe_request_mgr_$list_requests
			entry (ptr);
  dcl probe_request_mgr_$list_builtins
			entry (ptr);

  dcl probe_et_$too_many_args fixed bin (35) external static;

  dcl null		builtin;

  dcl cleanup		condition;


  /*  */

print_request_summary:
  entry (P_probe_info_ptr);

  probe_info_ptr = P_probe_info_ptr;
  call reject_argument ();

  call ioa_$ioa_switch (probe_info.output_switch,
       "^/Summary of probe requests:^/");

  call probe_request_mgr_$list_requests (probe_info_ptr);

  call ioa_$ioa_switch (probe_info.output_switch,
       "^/Type ""help"" for more information.");

  return;				/* end of code for $print_request_summary */

print_builtin_summary:
  entry (P_probe_info_ptr);

  probe_info_ptr = P_probe_info_ptr;
  call reject_argument ();

  call ioa_$ioa_switch (probe_info.output_switch,
       "^/Summary of probe builtins:^/");
  call probe_request_mgr_$list_builtins (probe_info_ptr);
  return;



help_request:
  entry (P_probe_info_ptr);

  probe_info_ptr = P_probe_info_ptr;

  if probe_info.ct -> token.type >= probe_info.end_token
    then
         desired_topic = "summary";
  else if probe_info.ct -> token.type = TIMES then do;
      desired_topic = "toc";
      call bump_ct ();
    end;
  else if probe_info.ct -> token.type = NAME_TYPE then do;
      desired_topic = current_identifier_name;
      call bump_ct ();
    end;
  else call probe_error_ (probe_info_ptr, 0, "Usage: help { * TOPIC }");

  call reject_argument ();

  goto help_common;



list_help_request:
  entry (P_probe_info_ptr);

  probe_info_ptr = P_probe_info_ptr;

  call reject_argument;
  desired_topic = "toc";
  goto help_common;

help_common:
  Phelp_args = null;

  on cleanup
    call help_$term ("probe", Phelp_args, (0));

  call help_$init ("probe", "info", "", Vhelp_args_3, Phelp_args, code);

  if code ^= 0
    then call probe_error_ (probe_info_ptr, code);

  if probe_static_info.info_directory_info.array_size > 1
       | probe_info_directories (1) ^= ">doc>info" then do;
				/* use info search paths unless dirs specified */
      help_args.Nsearch_dirs =
	 probe_static_info.info_directory_info.array_size;
      do i = 1 to probe_static_info.info_directory_info.array_size;
        help_args.search_dirs (i) = probe_info_directories (i);
      end;
    end;

  help_args.Npaths = 1;
  help_args.Ncas = 0;		/* yields illegal PL/1 */
  help_args.Nscns = 0;		/* 0-extent arrays */
  help_args.Nsrhs = 0;

  if desired_topic = "bugs" | desired_topic = "changes"
    then help_args.path (1).value = "probe." || desired_topic;
    else do;
      help_args.path (1).value = "probe";
      help_args.path (1).S.info_name_not_starname = "1"b;
      help_args.path (1).info_name = desired_topic;
    end;

  call help_ ("probe", Phelp_args, "info", (0), code);
  if code ^= 0
    then
         call probe_error_$warning (probe_info_ptr, 0,
	    "There is no info available for ""^a"".", desired_topic);

  call help_$term ("probe", Phelp_args, (0));

  return;				/* of code for help request */




bump_ct:
  proc;
  probe_info.ct = probe_info.ct -> token.next;
  end bump_ct;

reject_argument:
  proc;
  if probe_info.ct -> token.type < probe_info.end_token
    then call probe_error_ (probe_info_ptr, probe_et_$too_many_args);
  end reject_argument;


  /*	INCLUDE FILES	*/
%include probe_info;
%include probe_static_info;
%include probe_tokens;
%include probe_operators;

%include help_args_;
  end probe_info_requests_;
