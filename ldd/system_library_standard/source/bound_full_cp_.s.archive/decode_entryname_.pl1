/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


decode_entryname_:	procedure (cname, rname, ename);


/*

	This procedure, given an entryname of the form "a$b" will
	return the refname and entry portions separately, i.e. "a"
	and "b".  If no "$" is found in the input string, "a$a" is
	assumed.  If the input string is of the form "a$", "a" and
	"" are returned.

	P. Bos, May 1972

*/

     dcl	cname			char(*),		/* entryname, "a$b" */
	rname			char(32),		/* refname "a" */
	ename			char(32);		/* entry "b" */

     dcl (index, length, substr)	builtin;

     dcl (i, l)			fixed bin;


/*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  */


	l = length (cname);				/* get length of input string */
	i = index (cname, "$");			/* scan for delimiter */
	if i = 0 then do;				/* no "$", assume "a$a" */
	     rname, ename = cname;			/* set return values */
	     return;				/* and exit */
	     end;
	rname = substr (cname, 1, i-1);		/* extract "a" portion */
	if i < l then				/* if "$" was not last char */
	     ename = substr (cname, i+1, l-i);		/* then extract "b" */
	else					/* input was "a$" */
	     ename = "";				/* give null string for entry */

	return;					/* exit */


	end decode_entryname_;
