/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1983 *
   *                                                         *
   *********************************************************** */
get_population: proc (I_rel_cursor_ptr, O_number_of_tuples, O_code);


/*  BEGIN_DESCRIPTION

    This routine will return the number of tuples in the specified relation
    by asking vfile_ for its count. This is very fast and very accurate. 

    END_DESCRIPTION
*/

/*  HISTORY

    19 September 1983  R. Harvey:  Initially written

*/
%page;
/* get_population: proc (I_rel_cursor_ptr, O_number_of_tuples, O_code); */


/*    Parameters    */

	dcl     I_rel_cursor_ptr	 ptr;
	dcl     O_number_of_tuples	 fixed bin (35);
	dcl     O_code		 fixed bin (35);


	O_number_of_tuples = 0;			/* init */
	O_code = 0;

	vrm_cursor_ptr = I_rel_cursor_ptr;
	vrm_open_info_ptr = vrm_cursor.open_info_ptr;
	iocb_ptr = vrm_cursor.iocb_ptr;

	vrm_rel_desc_ptr = vrm_open_info.relation_model_ptr;

	vfsi.info_version = vfs_version_1;
	call iox_$control (iocb_ptr, "file_status", addr (vfsi), code);
	if code ^= 0
	then call error (code);

/* Now we have a count of what vfile_ thinks is there. We need to adjust this  to conform to reality */

	if vrm_rel_desc.switches.MRDS_compatible
	then O_number_of_tuples = vfsi.non_null_recs - 1; /* because there is really a null record */
	else O_number_of_tuples = vfsi.non_null_recs - 1 - vrm_open_info.number_of_index_collections;


Exit:	return;



error: proc (ecode);

	dcl     ecode		 fixed bin (35) parameter;

	O_code = ecode;
	go to Exit;

     end error;
%page;
%include vrm_open_info;
%page;
%include vrm_rel_desc;
%page;
%include vrm_cursor;
%page;
%include vfs_info;
%page;
/* Automatic */

	dcl     code		 fixed bin (35);
	dcl     info		 fixed bin;	/* dummy variable to keep compiler happy */
	dcl     iocb_ptr		 ptr;
	dcl     1 vfsi		 like indx_info;


/* Builtin */

	dcl     addr		 builtin;


/*  External entry */

	dcl     iox_$control	 entry (ptr, char (*), ptr, fixed bin (35));




     end get_population;
