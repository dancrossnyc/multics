" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

"
"   Written by R.J.C. Kissel, 11/17/76, for use by dump_segment.
"   Modified by Linda Pugh, 10/4/82, to spell misspelled word correctly
"   so this program would compile.

" HISTORY COMMENTS:
"  1) change(86-07-30,Kissel), approve(86-07-30,MCR7476), audit(86-08-01,Ex),
"     install(86-08-19,MR12.0-1132):
"     Changed the name from translate_to_hex9 which was non-standard to
"     translate_bytes_to_hex9_, which is better.  This was done because this
"     entry is now retained for external use.
"                                                      END HISTORY COMMENTS

"
"   PL/I Usage:
"   dcl translate_bytes_to_hex9_ (bit(*), char(*))
"   call translate_bytes_to_hex9_ (input_bits, output_chars)
"
	name	translate_bytes_to_hex9_
	entry 	translate_bytes_to_hex9_
translate_bytes_to_hex9_:
	epp1	ap|2,*		address of source string to pr1
	epp3	ap|4,*		address of target string to pr3
	ldx3	0,du		set x3 not to skip parent pointer if none
	lxl2	ap|0		load arg list code value
	canx2	=o0000004,du	check for no parent pointer (code 4)
	tnz	*+2		transfer if no parent pointer
	ldx3	2,du		parent pointer, set x3 to skip it
	ldq	ap|6,x3*		load source string descriptor
	anq	mask		drop all but string size  bits
	qls	1		get length of string in q-reg
	div	9,dl
	lda	ap|8,x3*		load target string descriptor
	ana	mask		drop all but string size bits
	even			"EIS address must be even
	mvt	(pr,rl),(pr,rl),fill(00)	now do the bcd to ascii
	desc4a	1|0,ql
	desc9a	3|0,al
	arg	table
	short_return
mask:	oct	000077777777
	even

table:	aci	"0123456789ABCDEF"
	end
