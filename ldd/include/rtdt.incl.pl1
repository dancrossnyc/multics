/* --------------- BEGIN include file rtdt.incl.pl1 --------------- */

dcl 1 rtdt aligned based (rtdtp),			/* resource type description table */
%include author;
    2 version fixed bin,				/* version number */
    2 installed_under_resource_mgt bit (1) aligned,	/* resource mgt. was ON when this was installed */
    2 charge_type_table_ptr offset,			/* points to charge_type_table */
    2 first_resource offset,				/* chain for RTDE's */
    2 rtdt_area area (RTDT_area_len);			/* all following items allocated here */

dcl 1 charge_type_table aligned based (cttp),		/* describes charges for resource types */
    2 n_charge_types fixed bin,			/* number of distinct charge types */
    2 charge_types (N_CHARGE_TYPES refer (charge_type_table.n_charge_types)) aligned char (32),
    2 flagword fixed bin (35) aligned;			/* this word simply help us set bitcount properly */

dcl 1 rtde aligned based (rtdep),			/* describes one resource type */
    2 fixed_info aligned,
      3 next_resource offset,				/* chains to next type, or nullo */
      3 name char (32),				/* name of resource type, e.g. "tape_drive" */
      3 syn_to char (32),				/* if is_synonym this is master syn */
      3 precanon_proc char (64),			/* name of routine to standardize resource name */
      3 pad_1 (16) fixed bin (35),
      3 flags unaligned,
        4 (valid,					/* resource type hasn't been deleted */
     is_volume,					/* specifies volume or device type */
     manual_clear,					/* volumes of this type to be "degaussed" between owners */
     addition_pending,				/* bookkeeping bit for upd_rtdt_ */
     deletion_pending,
     is_synonym) bit (1) unaligned,			/* ditto */
        4 pad bit (12) unaligned,
      3 (process_limit,				/* how many can you assign at one time */
     default_time,					/* implicit reservations are for how many minutes */
     max_time,					/* how long can you reserve it for */
     advance_notice_time,				/* warn operator to prepare mount ahead of time */
     pad2,
     n_exclusion_specs,				/* number of distinct "name=" fields in attributes */
     n_mates,					/* number of mating devs/vols for this vol/dev */
     n_subtypes,					/* number of registration subtypes */
     n_defined_attributes) fixed bin (17) unaligned,	/* number of defined attributes */
      3 pad_2 (8) fixed bin (35),
      3 attributes_valid bit (72) aligned,		/* "1"b if corresp. attribute undeleted */
      3 attributes_to_match bit (72) aligned,		/* potential mate must possess these attributes */
      3 attribute_names (72) char (12) aligned,		/* all possible attributes for this resource */
      3 exclusion_specs (36) bit (72) aligned,		/* each masks all attrributes of the form "key=val" */
      3 pad_3 (32) fixed bin (35),
      3 registration_defaults aligned,			/* applied at reg. time if none given */
        4 default_flags aligned,
	5 (potential_attributes_given,		/* "1"b = there are default potential_attributes */
     attributes_given,				/* and similarly, etc. */
     aim_range_given,
     charge_type_given) bit (1) unaligned,
	5 pad bit (31) unaligned,
        4 potential_attributes bit (72) aligned,		/* for registration, if given */
        4 attributes bit (72) aligned,			/* for registration and also for runtime "I-don't-care" */
        4 aim_range (2) bit (72) aligned,		/* and similarly, etc. */
        4 charge_type fixed bin,
        4 pad_4 (8) fixed bin (35) aligned,
    2 mates (N_MATES refer (rtde.n_mates)) char (32) aligned,
						/* the volume type that mounts on this device, or vice versa */
    2 subtypes (N_SUBTYPES refer (rtde.n_subtypes)) aligned, /* named registration default groups */
      3 subtype_name char (32),			/* name of the group */
      3 subtype_defaults like rtde.registration_defaults aligned;

dcl  RTDT_version_3 fixed bin static options (constant) initial (3),
     RTDT_version_2 fixed bin static options (constant) initial (2),
						/* same format, but without precanon_proc */
    (N_MATES, N_SUBTYPES, N_CHARGE_TYPES) fixed bin,
     RTDT_area_len fixed bin (18);

dcl (rtdep, rtdtp, cttp) pointer;

/* ---------------- END include file rtdt.incl.pl1 ---------------- */
