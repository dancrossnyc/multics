/* BEGIN INCLUDE FILE ... absentee_user_table.incl.pl1 */

/* *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  * */
/*							       */
/* The absentee user table is the control data base for the absentee	       */
/* facility.  It has a header, with scheduling parameters, then one user     */
/* table entry for each logged-in absentee job.			       */
/*							       */
/* Programs which use this table must also include		       */
/* user_table_header.incl.pl1 and user_table_entry.incl.pl1.	       */
/*							       */
/* *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  * */


/****^  HISTORY COMMENTS:
  1) change(81-09-10,Kittlitz), approve(), audit(), install():
     Replace abs_user_tab.incl.pl1.
  2) change(87-04-26,GDixon), approve(87-07-13,MCR7741),
     audit(87-07-16,Brunelle), install(87-08-04,MR12.1-1056):
     Use ut_header common to all user control tables.
                                                   END HISTORY COMMENTS */


dcl  AUTBL_version_4 fixed bin int static options (constant) init (4);

dcl  autp ptr automatic init (null);

dcl 1 autbl based (autp) aligned,			/* a per system table to store info on absentee processes */
    2 header like ut_header,				/* Header common to all user tables. */

/* Counter elements */
    2 n_abs_run fixed bin,				/* actual number of absentee processes current */
    2 max_abs_users fixed bin,			/* max number of background absentee processes permitted */
    2 n_background_abs fixed bin,			/* number of absentee processes not from foreground queue */
    2 n_sec_fg fixed bin,				/* number of secondary foreground absentee jobs */
    2 idle_units fixed bin,				/* used in adjusting abs_maxu dynamically */
    2 abs_units fixed bin,				/* part of anstbl.n_units used by background absentees */
    2 n_abs (4) fixed bin,				/* number of processes from each background queue */
    2 qres (4) fixed bin,				/* number of slots reserved for each background queue */
    2 rsc_waiting (-1:4) fixed bin,			/* number of requests waiting for resources, from each queue */
    2 qerr (-1:4) fixed bin,				/* number of consecutive errors for q (dropped if > QERR_MAX) */
    2 cpu_limit (4) fixed bin (35),			/* current per-queue cpu time limits */

/* absentee user manager values. */
    2 defer_channel fixed bin (71),			/* IPC for time-defered queue entries */
    2 absentee_rq_chn fixed bin (71),			/* IPC channel for absentee requests */
    2 last_queue_searched fixed bin,			/* highest queue looked at by scheduler */
    2 debugging fixed bin,				/* take asdumps at strategic places */
    2 control,					/* see also whotab.incl.pl1 */
      3 mnbz bit (1) unal,				/* must not be zero */
      3 abs_maxu_auto bit (1) unal,			/* 1 if abs maxu has not been set by operator command */
      3 abs_maxq_auto bit (1) unal,			/* 1 if abs maxq has not been set by operator command */
      3 abs_qres_auto bit (1) unal,			/* 1 if abs qres has not been set by operator command */
      3 abs_cpu_limit_auto bit (1) unal,		/* 1 if abs cpu_limit has not been set by operator command */
      3 queue_dropped (-1:4) bit (1) unal,		/* 1 if queue has been dropped because of errors */
      3 abs_up bit (1) unal,				/* 1 if absentee facility is running */
      3 abs_stopped bit (1) unal,			/* 1 if abs stop command was issued */
						/* both 1 if we're waiting for the last jobs to log out */
      3 aum_ctl bit (1) unal,				/* for AUM to tell AU to start a new sequence */
      3 lc_list bit (1) unal,				/* for AU to tell AUM that the lc list is non-empty */
						/* comments in AU and AUM explain aum_ctl and lc_list */
      3 control_pad (21) bit (1) unal,			/* fill out rest of word */
    2 aum_pad fixed bin,

    2 entry_pad (80) fixed bin,			/*  pad table header to 128 words */
    2 entry (0 refer (autbl.current_size)) like ute;

dcl AUTBL_CONTROL_NAMES (15) char(20) varying int static options(constant) init(
	"mnbz",
	"abs_maxu_auto",
	"abs_maxq_auto",
	"abs_qres_auto",
	"abs_cpu_limit_auto",
	"queue_dropped(-1)",
	"queue_dropped( 0)",
	"queue_dropped( 1)",
	"queue_dropped( 2)",
	"queue_dropped( 3)",
	"queue_dropped( 4)",
	"abs_up",
	"abs_stopped",
	"aum_ctl",
	"lc_list");

/* END INCLUDE FILE ... absentee_user_table.incl.pl1 */
