
link_symbol:	procedure(previous,s);

declare	(previous,s) pointer parameter;
declare	q pointer;
declare	level fixed binary(15);

	level = s->symbol.level;
	if level > 1
		then do;
			s->symbol.member,s->symbol.internal = "1"b;
			if previous = null then call declare_parse$abort(16,null);
		     else if previous->symbol.level = 0 then call declare_parse$abort(16,null);
		     else if level > previous->symbol.level
				then do;
					s->symbol.father = previous;
					previous->symbol.son = s;
					previous->symbol.structure = "1"b;
				     end;
				else do;
					/* loop is executed at least once */
					do while(level <= previous->symbol.level);
					q = previous; /* remember possible brother */
					previous = previous->symbol.father;
					end;
					q->symbol.brother = s;
					s->symbol.father = previous;
				     end;
		     end;
		else if level = 1 then s->symbol.structure = "1"b;
	previous = s;
end link_symbol;
