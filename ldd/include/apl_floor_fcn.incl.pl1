/* ====== BEGIN INCLUDE SEGMENT	apl_floor_fcn.incl.pl1 ================================== */

apl_floor_:
	procedure (bv_value) returns (float);

/* Function to compute the floor of an apl value, taking
   integer fuzz into account.  In apl, for both the ceiling and floor functions,
   if the input argument is within "integer fuzz" of its integer value,
   then the result of the floor is this integer value.  Otherwise, the result
   is the normal floor (or ceiling).  This procedure is followed so that
   binary numbers which are within a few bits of the exact decimal
   representation will behave properly.

   Written 750714 by PG
*/

/* parameters */

dcl  bv_value float;

/* automatic */

dcl  (value, result) float;

/* builtins */

dcl  (abs, floor) builtin;

/* this function requires the following include files:
   %include apl_number_data;
   %include apl_ws_info;
*/

/* program */

	value = bv_value;				/* copy argument for efficiency */
	result = floor (value + .5e0);		/* form trial result */

	if abs (result - value) >= integer_fuzz		/* if trial not eq input value */
	then result = floor (value);			/* then use normal floor */

	return (result);

     end apl_floor_;

/* ------ END INCLUDE SEGMENT		apl_floor_fcn.incl.pl1 ---------------------------------- */
