/* ---------- BEGIN include file tape_archive_table_dcl.incl.pl1 ---------- */

/* Version 2 lengthened volume names from 6 to 32 characters */
/* Version 3 introduced density field, workspace strategy for updates, */
/* and the mount lock, which is less of a lock than a retain indicator. */
/* Version 4 expanded density field to an array for independent volume set */
/* densities and added uid fields for table deletion checking. */
/* Modified 09/21/80 by C. D. Tavares for version 3 */
/* Modified 12/9/81 by CDT to add highest_mount_type field */
/* Last modified 83-03-16 by S. G. Harris (UNCA) for version 4. */
/* Modified 09/16/83 by S. Krupp to add the TAPE_ARCHIVE_IO_MODULE
   declaration for conversion to the mtape_ I/O module. */

dcl 1 tape_archive_table aligned based (table_ptr),	/* online segment for tape_archive command */
    2 nonvolatile_part aligned,			/* temp table moving doesn't touch these */
      3 version_number fixed bin,			/* of table */
      3 magic_constant char (8),			/* "ta table" */
      3 compaction_warning_threshold float bin,		/* when to suggest compaction to user */
      3 auto_compaction_threshold float bin,		/* when to compact when user doesn't take hint */
      3 table_is_consistent bit (1) unaligned,		/* update in progress indicator */
      3 lock bit (36),				/* allows shared tape archives */
      3 lock_reason fixed bin,			/* reason seg already locked, for failure msg */
      3 locker_id char (32),				/* userid of locking process, for failure msg */
      3 io_module_name char (32),			/* tape_ansi_ or tape_ibm_ */
      3 recovery_info_offset bit (18) aligned,		/* holds address of workspace for table updates */
      3 perm_table_uid bit (36),			/* used to prevent table deletion */
      3 future_expansion (6) bit (36),			/* ... */
    2 volatile_part aligned,				/* these can get altered due to temp table moves */
      3 n_components fixed bin,			/* number valid (undeleted) components in archive */
      3 n_component_slots fixed bin,			/* number of used component slots in table */
      3 n_queued_requests fixed bin,			/* number requests to be performed next mount */
      3 n_request_slots fixed bin,			/* number of used request slots in table */
      3 next_mount_type fixed bin,			/* read only, write, or compact */
      3 date_time_tape_modified bit (36),		/* last time tape written on */
      3 date_time_last_compacted bit (36),		/* last time tape compacted */
      3 total_records fixed bin (35),			/* data out on tape, in Multics records */
      3 dead_records fixed bin (35),			/* records wasted (deletions, replacements) */
      3 incomplete_write_op_last bit (1),		/* ON if determined that user or sys crashed while writing tape */
      3 last_tape_file_no fixed bin,			/* number of last file existing on volume set */
      3 last_table_no fixed bin,			/* number of copies of online table on this volume set */
      3 mount_lock bit (36) aligned,			/* set to lock_id while volume set mounted */
      3 highest_mount_type fixed bin,			/* max of next_mount_type since retained */
      3 future_expansion (6) bit (36),			/* ... */
      3 tape_info aligned,
        4 density (2) fixed bin (17) unal,		/* post version 3 volume set densities */
        4 active_set fixed bin,			/* which of the two is most current */
        4 n_volumes_in_set (2) fixed bin,		/* how many reels comprise volume set */
        4 volume_set (2) aligned,
	5 volume_id (8) char (168),			/* physical reel and slot info */
      3 component_table (0 refer (tape_archive_table.n_component_slots)) aligned like component,
      3 request_queue (0 refer (tape_archive_table.n_request_slots)) aligned like request;

dcl 1 component aligned based (component_ptr),		/* format of one component entry */
    2 entry_status_descriptor like entry_status_descriptor aligned,
    2 previous_instance_backchain fixed bin,		/* index of file it replaced, if any */
    2 associated_request_index fixed bin,		/* if extract pending, index of request with dirpath */
    2 date_time_deleted bit (36) aligned,		/* if logically deleted, when. */
    2 future_expansion (5) bit (36) aligned;		/* ... */

dcl 1 request aligned based (request_ptr),		/* format of a queued request */
    2 entry_status_descriptor like entry_status_descriptor aligned,
    2 directory_name char (168) unaligned,		/* in Multics file system */
    2 requested_ops unaligned,
      3 append bit (1) unaligned,			/* add to tape */
      3 replace bit (1) unaligned,			/* replace to tape */
      3 extract bit (1) unaligned,			/* extract from tape */
      3 delete bit (1) unaligned,			/* delete from tape or file system */
      3 force bit (1) unaligned,			/* forcibly do any of the above */
      3 single_name bit (1) unaligned,			/* append/extract with only given name */
      3 future_expansion bit (30) unaligned,
    2 existing_reference fixed bin,			/* index of component block, if exists (e.g. read) */
    2 future_expansion (4) bit (36) aligned;

dcl 1 entry_status_descriptor aligned based,
    2 valid bit (1) unaligned,			/* an undeleted component or request if on */
    2 no_final_newline bit (1) unaligned,		/* ON if last char in file was not newline */
    2 safety_switch bit (1) unaligned,			/* reflects safety switch of file while online */
    2 pad bit (33) unaligned,
    2 file_info aligned,				/* main tape file for this entry */
      3 tape_file_name char (17),
      3 entry_name char (32) unaligned,			/* for Multics file system */
      3 date_time_archived bit (36),			/* when it was put on tape */
      3 recording_mode char (8) aligned,		/* "ascii", "binary", "ebcdic", etc. */
      3 date_time_branch_modified bit (36),		/* last modified while in file system */
      3 date_time_dumped bit (36),			/* by backup facility */
      3 bitcount_author char (32),			/* last person who modified it while online */
      3 file_length fixed bin (35),			/* in Multics pages */
      3 attribute_file_no fixed bin,			/* file number of attribute file on tape */
      3 n_tape_records fixed bin (35),			/* number of logical records on tape for this file */
    2 uid bit (36) aligned,				/* used to prevent table deletion */
    2 future_expansion bit (36) aligned;

dcl 1 workspace based (workspace_ptr) aligned,		/* to prevent update inconsistencies */
    2 workspace_id char (8),				/* "wrkspace" */
    2 component_no fixed bin,
    2 request_no fixed bin,
    2 n_queued_requests fixed bin,
    2 dead_records fixed bin,
    2 total_records fixed bin,
    2 n_components fixed bin,
    2 n_request_slots fixed bin,
    2 next_mount_type fixed bin,
    2 future_expansion (70) fixed bin (35),
    2 tape_info_copy aligned like tape_archive_table.tape_info,
    2 future_expansion2 (16) fixed bin (35),
    2 component_copy like component aligned,
    2 future_expansion3 (16) fixed bin (35),
    2 request_copy like request aligned;

dcl 1 based_tape_info based (tape_info_ptr) like tape_archive_table.tape_info aligned;

dcl (table_ptr, component_ptr, request_ptr, workspace_ptr, tape_info_ptr) pointer;

dcl ((None initial (0),				/* next scheduled processing flags */
     Delete initial (1),
     Read initial (2),
     Write initial (3),
     Compact initial (4),

     Examine initial (1),				/* current lock reason flags */
     Modify initial (2),
     Process_tape initial (3),

     Deletion initial (1),				/* pending_operation flags */
     Extraction initial (2),
     Replacement initial (3),
     Cancellation initial (4),
     Deletion_cancellation initial (5),
     Volume_alteration initial (6),
     Table_creation initial (7),
     Table_copy initial (8),

     Default_density initial (1600),

     tape_archive_version_4 initial (4)) fixed bin,

     TAPE_ARCHIVE_IO_MODULE char(6) init("mtape_"),

     Lock_reasons (0:3) char (24) initial
    ("no discernable reason", "table examination", "table modification", "tape processing"),

     Magic_constant char (8) initial ("ta table"),
     Magic_workspace_constant char (8) initial ("wrkspace")) static options (constant);

/* ---------------- END include file tape_archive_table_dcl.incl.pl1 ---------------- */
