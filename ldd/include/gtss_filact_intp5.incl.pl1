/* BEGIN INCLUDE FILE gtss_filact_intp5.incl.pl1 */
/*
  Created: (Wardd Multics)  10/05/78 1602.7 mst Thu 
  Modified  Ron Barstad  82-09-23  to fix unspec-substr err of permission bit
*/
get_multics_seg_acl: proc (n, opp, sap);

/**	Obtain a list of (n) Multics segment access mode values
	in the segment_acl structure (based sap) from a
	filact options list (based opp).
**/
dcl  n                        fixed bin parm;
dcl  opp                      ptr parm;
dcl  sap                      ptr parm;

	op = opp;
	acl_ptr = sap;
	acl_count = max_options;

	ln = n;					/* Local n. */
	do i = 1 to ln while (i <= max_options);
						/* Ignore create permission.      Ignore delete also. */
	     if substr (unspec (permission_bit (i)), 1, 10) ^= "0000000010"b & unspec (permission_bit (i)) ^= "0"b then do;
		k = k+1;
		if permission_bit.bits10_35 (i) = "00000001"b then segment_acl.modes (k) = "0"b; /* Exclude => null */
		else segment_acl.modes (k) = "1"b||permission_bit.e (i)||permission_bit.w (i);
		call gtss_bcd_ascii_ (addr (ol (i).USERID), 12, addr (id));
		segment_acl.access_name (k) = "*."||rtrim (id)||".*";
	     end;
	end;
	return;

dcl 1 ol (ln)aligned based(op) like optionsx.optional_specific_permissions;
dcl  id                       char(12)aligned;
dcl  ln                       fixed bin;
dcl  op                       ptr init(null());
     end						/* get_multics_seg_acl */;
/**	Multics include file "acls".	**/
%include acls;
/*   END INCLUDE FILE gtss_filact_intp5.incl.pl1 */
