/* BEGIN INCLUDE FILE gtss_checksum.incl.pl1 */

checksum: proc (p, n) returns (bit (36)aligned);

/* Calculate checksum of "n" words pointed to by "p".

   Author: Al Kepner 1978
   Change: Al Dupuis 09/24/79 Added an extra add after do loop in case carry was generated.
*/
dcl  p                        ptr parm;
dcl  n                        fixed bin (18)parm;

/* Calculate the checksum for n words starting at p */

	a = 0;
	do i = 1 to n;
	     b = fixed (array (i), 36) + fixed (sum, 36) + fixed (carry, 1);
	     a = b;
	end;
	b = fixed (sum, 36) + fixed (carry, 1); /* last add may have generated carry */
	a = b;
	return (sum);

dcl  a                        fixed bin(71);
dcl  b                        fixed bin(71);
dcl  i                        fixed bin(24);
dcl  array                    (n) bit (36) based (p);
dcl 1 a_overlay aligned based (addr (a)),
    2 fill bit (35) unal,
    2 carry bit (1) unal,
    2 sum bit (36);

     end checksum;

/*   END INCLUDE FILE gtss_checksum.incl.pl1 */
