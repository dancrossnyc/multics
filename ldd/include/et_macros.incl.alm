" Begin include file ...... et_macros.incl.alm
"	Written 6/15/77 by Noel I. Morris
"	Modified '82 so error code entries are in first page of segment.

" This include file contains macros for generating error tables
" for both system and non-system use.

" ET MACRO
"
" This macro is used to initialize the error table for either
" system or non-system use.  It is invoked at the beginning of
" the error table as follows:
"
"	et	table_name{,system}
"

macro	et
	maclist	on

	name	&1

&^=&2,system&[

	include	stack_header



	use	codes
.code_start:

	use	past_codes
.code_end:

	join	/link/codes,past_codes

	use	messages

.trapproc:
	epaq	0,ic
	eax0	0,au
	epbpsb	sp|0
	lprplp	sb|stack_header.lot_ptr,*au
	eax1	.code_start         
.loop:	stx0	lp|0,x1
	eax1	1,x1
	cmpx1	.code_end,du
	tmi	.loop-*,ic
	short_return

	firstref	<*text>|.trapproc

&;	use	codes
	use	messages

	join	/text/codes,messages

&]

	bool	.segno,77777

	maclist	off

	&end



" EC MACRO
"
" This macro is used to define an error code as shown:
"
"	ec	name,{short_mess},(long_mess)
"
" If more than a single name is to be defined for a given message,
" a list of names separated by commas and enclosed in parentheses
" must be given.   short_mess must be 8 or less characters.  If omitted,
" the short_mess will be set to the name.  long_mess is enclosed
" in parentheses in order to cause ALM to ignore embedded blanks.  Please
" note that double quote characters in either short_mess or long_mess
" must be doubled in order to make the ALM assembly work properly.
" Note that a macro invocation line terminated by a comma will cause that
" statement to be continued on the next line.

macro	ec
	use	codes

	maclist	on,save
&(1	segdef  &i
&)	maclist	restore
&(1&i:
&)	maclist	object,save
	zero	.segno,&U
	maclist	restore
	use	messages
&^=&l2,0&[	shortname    &2
&;	shortname    &1
&]
&U:
	maclist	on,save
	acc  "&3"

	maclist	restore
	&end

macro	shortname
	maclist	on,save
	aci  "&1",8
	maclist	restore
	&end



" A typical error table source program might have the following format:
"
"	include	et_macros
"
"	et	name_of_error_table
"
"	ec	error_code_1,ecode1,(This is error code 1.)
"
"	ec	error_code_2,ecode2,(This is error code 2.)
"
"	:	     :	      :		:
"
"	ec	error_code_n,ecoden,(This is the nth error code.)
"
"	end
"

" End of include file ...... et_macros.incl.alm

