/* BEGIN INCLUDE FILE ... pl1_macro_next_token.incl.pl1 */

get_next_token:
     procedure;

/* In the standard entry, find the next non-white-space or comment token. 
   Otherwise, return after the next token */

	declare standard_entry	 bit (1);

	standard_entry = TRUE;
	goto common_code;

get_next_token$retain_white_space:
     entry;

	standard_entry = FALSE;
	goto common_code;

common_code:
	do while (TRUE);
	     token_index = token_index + 1;
	     if token_index < lbound (token, 1) | token_index > last_token
	     then do;
		     token_type = no_token;
		     pct_type = none;
		     token_length = 0;
		     token_start = 0;
		     token_ptr = null ();
		end;
	     else do;
		     token_type = token (token_index).type;
		     pct_type = token (token_index).pct_type;
		     token_length = token (token_index).string_size;
		     token_ptr = token (token_index).string_ptr;
		     token_start = char_offset_ (token_ptr);
		end;

	     if (standard_entry & (token_type ^= white_space_token & token_type ^= comment_token)) | ^standard_entry
	     then return;

	end;

     end get_next_token;

/* END INCLUDE FILE ... pl1_macro_next_token.incl.pl1  */