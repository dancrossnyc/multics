/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) BULL HN Information Systems Inc., 1989   *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1982 *
        *                                                         *
        * Copyright (c) 1972 by Massachusetts Institute of        *
        * Technology and Honeywell Information Systems, Inc.      *
        *                                                         *
        *********************************************************** */




/****^  HISTORY COMMENTS:
  1) change(89-04-23,Zimmerman), approve(89-04-23,MCR8060),
     audit(89-05-05,RWaters), install(89-05-24,MR12.3-1048):
     MCR8060 cobol_iomode.pl1 Reformatted code to new Cobol standard.
                                                   END HISTORY COMMENTS */


/* Modified on 04/02/77 by GM to check for device 2 and set organization to 4 */
/* Modified on 01/14/77 by ORN to signal command_abort_ rather than cobol_compiler_error */
/* modified since Version 2.0 */

/* format: style3 */
cobol_iomode:
     proc (ft_ptr, eos_ptr, cobol_mode, multics_mode, extend_sw);

dcl	cobol_mode	fixed bin;
dcl	multics_mode	fixed bin;
dcl	extend_sw		bit (1) aligned;

/***********************************************/
/*	SEQUENTIAL	RANDOM		DYNAMIC
STREAM
 Input	21,1,1st_byte
 Output	61,2,eof
 Extend	01,2,eof(*)

SEQUENTIAL
 Input	25,4,1st_rec
 Output	65,5,eof
 I-O	45,7,1st_rec
 Extend	05,6,eof(*,**)

RELATIVE
 Input	31,8,1st_rec	32,11,-		33,8,1st_rec
 Output	71,9,eof		72,12,-		73,10,1st_rec(**,***)
 I-O	51,10,1st_rec	52,13,-		53,10,1st_rec

INDEXED
 Input	35,8,1st_rec	36,11,-		37,8,1st_rec
 Output	75,9,eof		76,12,-		77,10,1st_rec(**,***)
 I-O	55,10,1st_rec	56,13,-		57,10,1st_rec

*   extend_sw = "1"b
**  reads are prevented by run-time check
*** positioned to eof by special action at open

/***********************************************/

/*
KEY TO cobol_mode:
	"MMOOAA"b

MM (mode):	00 extend
		01 input
		10 i/o
		11 output

OO (organization):	00 stream
		01 sequential
		10 relative
		11 indexed

AA (access method):	00 invalid
		01 sequential
		10 random
		11 dynamic

MEANINGFUL cobol_modes:
1	01,2
5	05,6
17	21,1
21	25,4
25	31,8
26	32,11
27	33,8
29	35,8
30	36,11
31	37,8
37	45,7
41	51,10
42	52,13
43	53,10
45	55,10
46	56,13
47	57,10
49	61,2
53	65,5
57	71,9
58	72,12
59	73,10
61	75,9
62	76,12
63	77,10
*/

dcl	mode		(3 /*access*/, 4 /*organization*/, 0:3 /*mode*/, 2 /*cobol_mode,multics_mode*/) fixed
			bin static
			init (/* access: (1)sequential, (2)random, (3)dynamic
   organization: (1)sequential, (2)relative, (3)indexed, (4)stream
   mode: (0)input, (1)output, (2)i/o, (3)extend  */
			/* INPUT	OUTPUT	I/O	EXTEND */ /* SEQUENTIAL SEQUENTIAL */ 21, 4, 53, 5, 37, 7, 5,
			6,			/**/
						/* SEQUENTIAL RELATIVE */
			25, 8, 57, 9, 41, 10, 0, 0,	/* SEQUENTIAL INDEXED */
			29, 8, 61, 9, 45, 10, 0, 0,	/* SEQUENTIAL STREAM */
			17, 1, 49, 2, 0, 0, 1, 2,	/**/
						/* RANDOM SEQUENTIAL */
			0, 0, 0, 0, 0, 0, 0, 0,	/* RANDOM RELATIVE */
			26, 11, 58, 12, 42, 13, 0, 0, /* RANDOM INDEXED */
			30, 11, 62, 12, 46, 13, 0, 0, /* RANDOM STREAM */
			0, 0, 0, 0, 0, 0, 0, 0,	/* DYNAMIC SEQUENTIAL */
			0, 0, 0, 0, 0, 0, 0, 0,	/* DYNAMIC RELATIVE */
			27, 8, 59, 10, 43, 10, 0, 0,	/* DYNAMIC INDEXED */
			31, 8, 63, 10, 47, 10, 0, 0,	/* DYNAMIC STREAM */
			0, 0, 0, 0, 0, 0, 0, 0);

dcl	1 error_info,
	  2 name		char (32),
	  2 meslen	fixed bin,
	  2 mes		char (120);
dcl	ioa_$rsnnl	entry options (variable);
dcl	signal_		entry (char (*), ptr, ptr);
dcl	access_con	(3) char (10) static init ("sequential", "random", "dynamic");
dcl	org_con		(4) char (10) static init ("sequential", "relative", "indexed", "stream");
dcl	mode_con		(0:3) char (6) static init ("input", "output", "i-o", "extend");

dcl	access		fixed bin;
dcl	org		fixed bin;
dcl	open_mode		fixed bin;

dcl	ft_ptr		ptr;


/*************************************/
start:
	if file_table.organization = 5
	then file_table.organization = 4;		/* temporary */
	open_mode = fixed (end_stmt.d, 2);
	if file_table.device = 2
	then file_table.organization = 4;
	if (file_table.device = 1 | file_table.device = 3) & file_table.organization = 1
	then org = 4;
	else org = file_table.organization;
	access = file_table.access;
	if access = 0
	then access = 1;

	cobol_mode = mode (access, org, open_mode, 1);
	multics_mode = mode (access, org, open_mode, 2);
	if cobol_mode = 0
	then go to error;
	if cobol_mode < 6
	then extend_sw = "1"b;
	else extend_sw = "0"b;
	return;


/*************************************/
error:
	error_info.name = "cobol_iomode";
	call ioa_$rsnnl ("Attempt to open file ^a in inconsistent mode - access: ^a; organization: ^a; mode: ^a",
	     error_info.mes, error_info.meslen, file_table.ifn, access_con (access), org_con (org),
	     mode_con (open_mode));
	call signal_ ("command_abort_", null (), addr (error_info));
	return;


/*************************************/
translate:
     entry (inmode, outmode, outbit);

dcl	inmode		fixed bin;
dcl	outmode		fixed bin;
dcl	outbit		bit (1) aligned;

start_translate:
	open_mode = fixed (substr (unspec (inmode), 31, 2), 2);
	org = fixed (substr (unspec (inmode), 33, 2), 2);
	access = fixed (substr (unspec (inmode), 35, 2), 2);
	outmode = mode (access, org, open_mode, 2);
	if outmode < 6
	then outbit = "1"b;
	else outbit = "0"b;
	return;


/*************************************/

/*****	Declaration for builtin function	*****/

dcl	(substr, mod, binary, fixed, addr, addrel, rel, length, string, unspec, null, index)
			builtin;

/*****	End of declaration for builtin function	*****/

%include cobol_type19;
%include cobol_file_table;
     end cobol_iomode;
