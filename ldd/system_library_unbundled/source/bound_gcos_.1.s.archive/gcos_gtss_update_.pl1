/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */
/*
*  Written ???
*  Modified: R. Barstad  11/16/82  Fix actid format for console messages
*/
gcos_gtss_update_: proc;
dcl (activity_val, status_val) fixed bin(24)parm;


end_gcos_gtss_update:				/* bail_out point for error_processing */
	return;
%page;
status_update: entry (status_val);

	call setup;

	job_status (snumb_index) = status_val;
	return;



new_activity: entry (activity_val, status_val);

	call setup;

	gtss_snumb_xref_.activity_no (snumb_index) = activity_val;
	job_status (snumb_index) = status_val;

	return;
%page;
setup:	proc;

	     if ^initialized then do;
		call user_info_$homedir (home_path);
		call hcs_$initiate (home_path, "gtss_user_state_.gtss", "gtss_user_state_",
		     0, 1, u_state_ptr, c);
		if u_state_ptr = null () then
		     call ioa_ ("Can't access gtss_user_state_, hcs_$initiate returned ^i.", c);
		initialized = "1"b;
	     end;

	     if u_state_ptr = null () then goto end_gcos_gtss_update;

	     if snumb_index = 0 then do;
		do i = 1 to entry_count while (gtss_snumb_xref_.snumb (i) ^= substr (save_data.actid, 1, 5) | job_status (i) = COMPLETE);
		end;

		if i <= entry_count then
		     snumb_index = i;
		else do;
		     call ioa_ ("Can't find snumb ^5a in gtss tables.", substr (save_data.actid, 1, 5));
		     u_state_ptr = null ();
		     goto end_gcos_gtss_update;
		end;
	     end;


dcl  c                        fixed bin(35);
dcl  i                        fixed bin(24);
dcl  initialized              bit int static init ("0"b);

	end setup;
%page;
/*   Variables for gcos_gtss_update_:		 */
/*   IDENTIFIER		ATTRIBUTES	 */
dcl null builtin;
dcl  hcs_$initiate            entry (char(*), char(*), char(*), fixed bin(1), fixed bin(2), ptr, fixed bin(35) );
dcl  ioa_                     entry options (variable);
dcl  user_info_$homedir       entry (char(*));
%page;
%include gcos_gtss_dcls;
%page;
%include gcos_ext_stat_;
     end gcos_gtss_update_;
