/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


/* *******************************************************************************************
   *******************************************************************************************
   *
   *
   *	Written by M. R. Jordan, October 1977
   *
   *******************************************************************************************
   ******************************************************************************************* */




gcos_verify_access_: proc (dirname, ename, fms_perms, buf_ptr, fms_code);

dcl  buf_ptr ptr;
dcl  code fixed bin(35);
dcl  dirname char (*);
dcl  ename char (*);
dcl  error_table_$no_s_permission fixed bin(35) ext;
dcl  fms_code bit (72) aligned;
dcl  fms_perms bit (6);
dcl  gcos_et_$fms_bad_cfdesc fixed bin(35) ext;
dcl  gcos_et_$fms_perms_denied fixed bin(35) ext;
dcl  gcos_et_$unimp_mme fixed bin(35) ext;
dcl  gcos_fms_error_ entry (fixed bin(35), ptr, bit (72) aligned);
dcl  gcos_mme_bort_$system entry options (variable);
dcl  hcs_$status_ entry (char (*), char (*), fixed bin(1), ptr, ptr, fixed bin(35));

	call hcs_$status_ (dirname, ename, 1, addr (status_info_branch), null (), code);
	if code ^= 0 & code ^= error_table_$no_s_permission then do;
	     call gcos_fms_error_ (gcos_et_$fms_bad_cfdesc, buf_ptr, fms_code);
	     return;
	end;


	goto Access (fixed (substr (fms_perms, 1, 2), 2));


Access (0):					/* none - error */

	call gcos_mme_bort_$system (gcos_et_$unimp_mme,
	     "Request for access other than read, write or read/write are not supported.");


Access (1):					/* write */
Access (3):					/* read/write */

	if (status_info_branch.mode & "01010"b) ^= "01010"b then
	     call gcos_fms_error_ (gcos_et_$fms_perms_denied, buf_ptr, fms_code);
	return;


Access (2):					/* read */

	if ^substr (status_info_branch.mode, 2, 1) then call gcos_fms_error_ (gcos_et_$fms_perms_denied, buf_ptr, fms_code);
	return;

%include status_info_branch;


     end gcos_verify_access_;
