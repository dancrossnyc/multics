/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */
rmdb_free_db: proc (absolute_db_path, saved_res_version_ptr, error_message, code);

/*
   .		       BEGIN_DESCRIPTION
   This  procedure  calls  mu_quiesce  to  unquiesce  the   db   and
   create_copy_res  to  create  an  updated  version  of  the  saved
   resultant. It is a separate   routine  instead  of  part  of  the
   close_db_request  so  that  there is a subroutine level interface
   independent of the subsystem.
   .		       END_DESCRIPTION

   Known Bugs:

   Other Problems:

   .                       HISTORY
   82-03-23 Davids: Written

   82-05-26 Davids: changed   to   be   compatable   with   the  new
   .                mdbm_util_$quiesce_free calling sequence.

   82-06-23 Davids: removed declared but unused builtins

   82-07-01 Roger Lackey : Changed mu_quiesce$ to mdbm_util_$quisce_ for 
   binding.

   83-05-24 Davids: Added the saved_res_version_ptr parameter. Also
   it now checks the saved_res_version and if its not what it is
   suppose to be it creates a new saved res.
   
/* PARAMETERS */

	dcl     absolute_db_path	 char (168);	/* (input) data base path */
	dcl     saved_res_version_ptr	 ptr;		/* (input) pointer to the saved_res_version */
	dcl     error_message	 char (500);	/* (output) text of error message in case of problems */
	dcl     code		 fixed bin (35);	/* (output) standard error code */

/* BASED */

	dcl     based_char8		 char (8) based;	/* overlay for saved_res_version */

/* ENTRIES */

	dcl     dsl_$create_res_copy	 entry options (variable);
	dcl     ioa_$rs		 entry () options (variable);
	dcl     mdbm_util_$quiesce_free entry (char (168), fixed bin (35));

/* EXTERNAL STATIC */

	dcl     mrds_data_$saved_res_version char (8) external static;

/* BUILTINS */

	dcl     length		 builtin;

/*
   Initialize the output parameters
*/

	code = 0;
	error_message = "";




/*
   Create an upto date saved resultant and unquiesce the data base
*/
	if saved_res_version_ptr -> based_char8 ^= mrds_data_$saved_res_version
	then do;
		call dsl_$create_res_copy (absolute_db_path, code);
		if code ^= 0
		then do;
			call ioa_$rs ("Could not create a resultant copy for the database ^a, database not freed",
			     error_message, length (error_message), absolute_db_path);
			goto exit_rmdb_free_db;
		     end;
	     end;

	call mdbm_util_$quiesce_free (absolute_db_path, code);
	if code ^= 0
	then call ioa_$rs ("The database ^a cannot be unquiesced", error_message, length (error_message), absolute_db_path);


exit_rmdb_free_db:
	return;

     end rmdb_free_db;
