/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */


rmdb_init_file_model: proc (I_dbm_ptr, I_fm_ptr, O_err_msg, O_err_code);

/*                 BEGIN_DESCRIPTION
   This procedure provided a pointer to the segment to be used as the file_model
   initializes the file_model structure and the associated rel_info structure. 
                   END_DESCRIPTION  
*/

/* HISTORY
82-06-01 Create by Roger Lackey 
*/

	dcl     I_dbm_ptr		 pointer parameter; /* Pointer to db_model */
	dcl     I_fm_ptr		 pointer parameter; /* Pointer to based of file_model seg */
	dcl     O_err_msg		 char (*) parameter;/* Text of returned error message */
	dcl     O_err_code		 fixed bin (35) parameter; /* Error code */

	O_err_msg = "";
	O_err_code = 0;
	dbm_ptr = I_dbm_ptr;
	fm_ptr = I_fm_ptr;

/* Derive  file_name from fm_ptr (file_model segment pointer)  */

	call hcs_$fs_get_path_name (fm_ptr, dir, ldn, file_name, code);
	if code ^= 0 then do;
		O_err_msg = "Getting file_model_pathname ";
		O_err_code = code;
		return;
	     end;

	rel_name = before (file_name, ".m");		/* Rel_name is file name without the  .m suffix */
%page;
	like_file_model = init_file_model;		/* initialize all but file_model area */
	file_model.fm_area = empty ();		/* initialize file_model area */

	file_model.changer_ptr = db_model.changer_ptr;

	call load_rel_info;

exit:	return;
%page;
/*  * * * * * * * * * * * * * * *    load_rel_info    * * * * * * * * * *    */

load_rel_info: procedure ();

/* routine to allocate and initialize the rel_info structure for
   the current relation in this file, using the file model area,
   and updating the file_model and global file element as necessary */


	allocate rel_info in (fm_area) set (ri_ptr);

	file_model.rel_ptr = rel (ri_ptr);

/* initialize relation detailed information */

	rel_info = init_rel_info;			/* copy template */
	rel_info.name = rel_name;
	rel_info.changer_ptr = db_model.changer_ptr;

     end load_rel_info;
%page;
	dcl     addr		 builtin;
	dcl     before		 builtin;
	dcl     code		 fixed bin (35);
	dcl     dir		 char (168);
	dcl     empty		 builtin;
	dcl     file_name		 char (32);
	dcl     fixed		 builtin;
	dcl     hcs_$fs_get_path_name	 entry (ptr, char (*), fixed bin, char (*), fixed bin (35));
	dcl     ldn		 fixed bin;
	dcl     rel		 builtin;
	dcl     rel_name		 char (32);
	dcl     sys_info$max_seg_size	 fixed bin (35) external; /* system constant */
%page;
%include mdbm_file_model;
%page;
%include mdbm_file_model_init;
%page;
%include mdbm_db_model;

     end rmdb_init_file_model;
