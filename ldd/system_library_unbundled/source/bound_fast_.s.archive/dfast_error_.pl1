dfast_error_: proc (code, name, additional_info);

dcl  code fixed bin (35);
dcl  name char (*);
dcl  additional_info char (*);

dcl  extra_message fixed bin;				/*  = 1 for a non-null message */
dcl  print_name fixed bin;				/* = 1 for non-null name */
dcl  message char (100) aligned;
dcl  shortinfo char (8) aligned;

dcl  hbound builtin;
dcl  iox_$user_output ptr ext;
dcl  com_err_$convert_status_code_ entry (fixed bin (35), char (*) aligned, char (*) aligned);
dcl  ioa_$ioa_switch entry options (variable);

%include dfast_error_codes;

dcl  err_mess (-1:43) char (60) var int static options (constant) init (
     "",
     "",						/* 0 */
     "alter file is empty",				/* alt_empty */
     "file would exceed maximum size",			/* max_size */
     "current file is empty",				/* cur_empty */
     "file is not saved",				/* not_saved */
     "name duplication (save denied)",			/* name_dup */
     "input line is too long",			/* long_rec */
     "unknown argument",				/* unknown_arg */
     "no explain file for",				/* no_expl */
     "illegal character in name",			/* bad_name */
     "unknown command",				/* bad_req */
     "syntax error in string specification",		/* syntax_string */
     "current segment does not have a name",		/* name_miss */
     "error in compilation",				/* no_comp */
     "no main program",				/* no_main */
     "syntax error in range specification",		/* block_spec */
     "command is not permitted for object code",		/* obj_nob */
     "current file must be saved",			/* sav_cur */
     "unknown terminal type",				/* bad_type */
     "system is not implemented",			/* unk_sys */
     "suffix missing: "".basic"" or "".fortran""",	/* no_suffix */
     "current file does not end with a new_line",		/* no_nl */
     "current file is out of order",			/* bad_sort */
     "command expects a line number",			/* no_num */
     "line was not found",				/* line_miss */
     "request is missing",				/* request_miss */
     "syntax error in line number",			/* bad_line */
     "could not find",				/* no_string */
     "line numbers must be in increasing order",		/* line_order */
     "maximum of 16 lines per request exceeded",		/* max_lines */
     "illegal pathname",				/* bad_pathname */
     "ZZZ",					/* access_mode */
     "delimitor is missing",				/* delimiter_miss */
     "record would exceed the size specified.  Length =",	/* size_fixed_record */
     "record length is expected",			/* error_no_rec_len */
     "maximum string size for replacement is 256",	/* max_string_size */
     "maximum line number has 6 digits",		/* max_line_number */
     "maximum number of arguments for a command is 10",	/* max_arg */
     "system can't be changed to conflict with name",	/*  name_sys */
     "only one segment can be printed with the ""-map"" option", /* dprint_map */
						/* fst */
     "maximum line number is 99999",			/* max_num */
     "change would exceed maximum line number (99999)", /* edit_max_num */
     "text contains un-numbered line",			/* un_num */
    "segment does not end with a new_line");		/* no_new_line */

	if code > hbound (err_mess, 1) | code < -1 then call com_err_$convert_status_code_ (code, shortinfo, message);
	else message = err_mess (code);
	if additional_info = "" then extra_message = 0;
	else extra_message = 1;
	if name = "" then print_name = 0;
	else print_name = 1;
	call ioa_$ioa_switch (iox_$user_output, "^a^v(:  ^)^a  ^v(^a", name, print_name, message, extra_message, additional_info);

	return;

     end dfast_error_;
