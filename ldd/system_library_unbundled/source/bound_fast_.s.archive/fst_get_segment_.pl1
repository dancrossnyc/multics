fst_get_segment_: proc (create_if_not_found, arg_pathname, working_dir, seg_ptr, seg_length, code);

/* This procedure returns a pointer to the segment specified with arg_pathname.
   If the segment is found, it must end with a new_line character.
   If the segment is not found, and the create_if_not_found switch is set, the segment will be created with re access.

   Written 3/76 by S.E. Barr
*/
/*  parameters */

dcl  create_if_not_found bit (1) unal;
dcl  arg_pathname char (*) var;
dcl  working_dir char (*) var;
dcl  seg_ptr ptr;
dcl  seg_length fixed bin (21);
dcl  code fixed bin (35);

/* automatic */

dcl  bit_count fixed bin (24);			/* length of segment */
dcl  i fixed bin (35);
dcl  directory_name char (168);
dcl  entry_name char (32);
dcl 1 o aligned like object_info;
dcl  pathname char (256) var;				/* complete pathname */

dcl  seg char (seg_length) based (seg_ptr);

dcl (addr, divide, index, length, max, null, reverse, substr) builtin;

/* external */

dcl  error_table_$noentry fixed bin (35) ext;
dcl  error_table_$segknown fixed bin (35) ext;
dcl  hcs_$initiate_count entry (char (*), char (*), char (*), fixed bin (24), fixed bin (12), ptr, fixed bin (35));
dcl  hcs_$make_seg entry (char (*), char (*), char (*), fixed bin (5), ptr, fixed bin (35));
dcl  object_info_$display entry (ptr, fixed bin (24), ptr, fixed bin (35));

/* constants */

dcl  NEW_LINE char (1) int static options (constant) init ("
");
dcl  RW_access fixed bin (5) int static init (01010b) options (constant);

/*  */
%include dfast_error_codes;
%include object_info;

/* */
	seg_ptr = null;

	if substr (arg_pathname, 1, 1) = ">" then pathname = arg_pathname;
	else pathname = working_dir || ">" || arg_pathname;
	i = length (pathname) - index (reverse (pathname), ">");
	directory_name = substr (pathname, 1, max (i,1));
	entry_name = substr (pathname, i + 2);

	call hcs_$initiate_count (directory_name, entry_name, "", bit_count, 1, seg_ptr, code);
	if code = error_table_$segknown then code = 0;
	if code = 0 then do;
	     seg_length = divide (bit_count + 8, 9, 21, 0);

/* Do not allow object segments to edited. */

	     if seg_length > 0 then if substr (seg, seg_length, 1) ^= NEW_LINE then do;
		call object_info_$display (seg_ptr, bit_count,  addr (o), code);
		if code = 0 then code = error_obj_nop;
		else code = error_no_new_line;
	     end;
	end;

	else if code = error_table_$noentry then do;
	     if create_if_not_found then do;
		call hcs_$make_seg (directory_name, entry_name, "", RW_access, seg_ptr, code);
		if code = 0 then seg_length = 0;
	     end;
	end;

	return;

     end fst_get_segment_;
