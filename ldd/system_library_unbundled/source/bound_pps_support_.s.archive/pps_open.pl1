/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


pps_open: proc (iocb_ptr_arg, mode, extend, code);



/*		PARAMETERS		*/


dcl  code fixed bin (35);
dcl  extend bit (1) aligned;
dcl  iocb_ptr_arg ptr;
dcl  mode fixed bin;


/*		ENTRY CONSTANTS		*/


dcl  default_handler_$set entry (entry);
dcl  hcs_$reset_ips_mask entry (fixed bin (35), fixed bin (35));
dcl  hcs_$set_ips_mask entry (fixed bin (35), fixed bin (35));
dcl  iox_$propagate entry (ptr);
dcl  pps_close entry (ptr, fixed bin (35));
dcl  pps_put_chars entry (ptr, ptr, fixed bin (21), fixed bin (35));
dcl  pps_report_man_$attach entry (ptr, fixed bin (35));
dcl  pps_report_man_$init entry (ptr, fixed bin (35));
dcl  pps_report_man_$start entry (ptr, fixed bin (35));


/*		EXTERNAL DATA		*/


dcl  error_table_$incompatible_attach fixed bin (35) ext;


/*		BUILTIN FUNCTIONS		*/


dcl  addr builtin;


/*		AUTOMATIC STORAGE		*/


dcl  iocb_ptr ptr;
dcl  mask fixed bin (35);


/*		CONSTANTS			*/


dcl  STR_OUT fixed bin static internal options (constant) init (2);


/*		INTERNAL STATIC		*/




/*		BASED VARIABLES		*/

/*

   Initialize necessary data items.

*/


	code = 0;
	mask = 0;
	iocb_ptr = iocb_ptr_arg -> iocb.actual_iocb_ptr;
	ppsab_ptr = iocb.attach_data_ptr;

	call default_handler_$set (Default_Condition_Handler);


/*

   Validate the opening mode requested.

*/


	if (mode ^= STR_OUT) | extend then do;
	     code = error_table_$incompatible_attach;
	     return;
	end;


/*

   Attach the target I/O switch.

*/


	call pps_report_man_$attach (iocb_ptr, code);
	if code ^= 0 then return;


/*

   Assign values to the proper entry variables in the iocb.

*/


	call hcs_$set_ips_mask (0, mask);

	ppsab.open_descrip = "stream_output";

	call pps_report_man_$init (iocb_ptr, code);	/* code will always be 0 */

	iocb.close = pps_close;
	iocb.put_chars = pps_put_chars;
	iocb.open_descrip_ptr = addr (ppsab.open_descrip);
	call iox_$propagate (iocb_ptr);

	call hcs_$reset_ips_mask (mask, mask);


/*

   Now start the new report.

*/


	call pps_report_man_$start (iocb_ptr, code);


	return;

/*

   Internal procedure to handle faults while IPS interrupts are masked. While not masked, any signals are simply
   passed on up the stack to their normal handlers. For a fault while masked, the process is terminated (with the
   reason "unable to do critical I/O") because the I/O control blocks are in an inconsistent state, and we can
   tolerate neither spawning a command loop with interrupts masked nor a restart with a possibly changed mask.

*/


Default_Condition_Handler: proc (p1, name, p2, p3, continue);


dcl  continue bit (1) aligned;
dcl  error_table_$unable_to_do_io fixed (35) ext;
dcl  name char (*);
dcl  p1 ptr;
dcl  p2 ptr;
dcl  p3 ptr;
dcl  terminate_process_ entry (char (*), ptr);

dcl 1 ti aligned,
    2 version fixed,
    2 code fixed (35);


	     if mask ^= 0 then do;
		ti.version = 0;
		ti.code = error_table_$unable_to_do_io;
		call terminate_process_ ("fatal_error", addr (ti));
	     end;
	     if name ^= "cleanup" then continue = "1"b;
	     return;


	end Default_Condition_Handler;

%include iocbv;

%include pps_attach_block;

%include pps_control_block;


     end pps_open;
