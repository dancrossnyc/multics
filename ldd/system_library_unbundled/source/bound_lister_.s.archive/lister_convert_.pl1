/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */

/* Program to convert version 1 lister file to version 2 lister file.

   Written by Paul W. Benjamin, October 22, 1980.
*/

lister_convert_:
     procedure (bv_in_file_ptr, bv_code) options (packed_decimal);

						/* parameters */

declare (
         bv_in_file_ptr ptr,
         bv_code fixed bin (35)
         );

						/* automatic */

declare (
         temp_uid fixed bin (24) unsigned
         );

/* builtin */

declare (
         null
         ) builtin;

/* conditions */

declare (
         no_write_permission,
         not_in_write_bracket
         ) condition;

/* external static */

declare (
         error_table_$bad_ring_brackets,
         error_table_$moderr
         ) fixed bin (35) external static;

/* include */

%include lister_structures;

/* program */

	on no_write_permission goto nowrite;
	on not_in_write_bracket goto badring;

	in_file_ptr = bv_in_file_ptr;
	input_file.version = lister_file_version_2;

	temp_uid = 1;

	do recordp = input_file.record_head repeat input_record.next while (recordp ^= null);
	     input_record.uid = temp_uid;
	     temp_uid = temp_uid + 1;
	end;

	input_file.next_uid = temp_uid;

	bv_code = 0;
	return;

nowrite:	bv_code = error_table_$moderr;
	return;

badring:	bv_code = error_table_$bad_ring_brackets;
	return;

     end;

