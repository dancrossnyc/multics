" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" ***********************************************************
"	bcd to ascii (upper case)
"
"	dcl gfms_bcd_ascii_ entry(ptr,fixed bin(24),ptr);
"	Arguments:
"		1. Pointer to input bcd characters (6 bit).
"		2. Number of characters.
"		3. Pointer to ascii output string.
"
"	Author:	Dave Ward		08/03/81
"		From Mike Jordan's gdb_bcd_ascii_.alm
"
	name	gfms_bcd_ascii_
	entry	gfms_bcd_ascii_
gfms_bcd_ascii_:
	eppbp	ap|2,*		bp->input data
	eppbp	bp|0,*
	lda	ap|4,*		get char count
	eppap	ap|6,*		ap->output buffer
	eppap	ap|0,*
	mvt	(pr,rl),(pr,rl)
	desc6a	bp|0,al
	desc9a	ap|0,al
	arg	upper
	short_return

	entry	lc
lc:
	eppbp	ap|2,*		bp->input data
	eppbp	bp|0,*
	lda	ap|4,*		get char count
	eppap	ap|6,*		ap->output buffer
	eppap	ap|0,*
	mvt	(pr,rl),(pr,rl)
	desc6a	bp|0,al
	desc9a	ap|0,al
	arg	lower
	short_return

upper:	aci	|0123456789[#@:>?|
	aci	| ABCDEFGHI&.](<\|
	aci	|^JKLMNOPQR-$*);'|
	aci	|+/STUVWXYZ_,%="!|

lower:	aci	|0123456789[#@:>?|
	aci	| abcdefghi&.](<\|
	aci	|^jklmnopqr-$*);'|
	aci	|+/stuvwxyz_,%="!|
	end
