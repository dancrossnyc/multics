3REM T R I A N G
4REM
6 PRINT "TRIANG"
10 DIM A(10),B(10),C(10),H(10),R(10),S(10)
100 PRINT
105 DEF FNS(I)= .01*INT(6000*(B(I)-INT(B(I)))+.5)
110 PRINT "THIS PROGRAM WILL FIND THE UNKNOWN FEATURES OF ANY"
115 PRINT "TRIANGLE, GIVEN ONE SIDE AND ANY TWO OTHER PARTS."
120 PRINT
125 PRINT
130 PRINT "WHAT WILL BE GIVEN (1=SSS, 2=SAS, 3=SSA, 4=ASA, 5=AAS)";
135 INPUT N
140 PRINT
145 LET R8=0
150 LET R9=0
155 IF (N-1)*(N-2)*(N-3)*(N-4)*(N-5)=0 THEN 170
160 PRINT "ANSWER 1, 2, 3, 4, OR 5 PLEASE...."
165 GOTO 125
170 IF N=1 THEN 210
175 PRINT "NOTE: SPECIFY ANGLES AS 'DEGREES, MINUTES, SECONDS'  OR"
180 PRINT "      'DEGREES, MINUTES.DECIMAL, 0' (I.E., SECONDS=0)"
185 PRINT
190 IF N=2 THEN 230
195 IF N=4 THEN 270
200 IF N=5 THEN 335
205 IF N=3 THEN 385
210 PRINT "WHAT ARE SIDE1, SIDE2, SIDE3";
215 INPUT S(1),S(2),S(3)
220 GOSUB 540
225 GOTO 630
230 PRINT "WHAT ARE SIDE1, ANGLE3, SIDE2";
235 INPUT S(1),A(3),B(3),C(3),S(2)
240 GOSUB 830
245 LET I=3
250 GOSUB 620
255 LET S(3)=SQR(S(1)^2+S(2)^2-2*S(1)*S(2)*COS(R(3)))
260 GOSUB 540
265 GOTO 630
270 PRINT "WHAT ARE ANGLE1, SIDE3, ANGLE2";
275 INPUT A(1),B(1),C(1),S(3),A(2),B(2),C(2)
280 GOSUB 830
285 FOR I=1 TO 2
290 GOSUB 620
295 NEXT I
300 LET R(3)=3.14159265-R(1)-R(2)
305 LET I=3
310 GOSUB 575
315 FOR I=1 TO 2
320 GOSUB 530
325 NEXT I
330 GOTO 630
335 PRINT "WHAT ARE ANGLE3, ANGLE1, SIDE3";
340 INPUT A(3),B(3),C(3),A(1),B(1),C(1),S(3)
345 GOSUB 830
350 FOR I=1 TO 3 STEP 2
355 GOSUB 620
360 NEXT I
365 LET R(2)=3.14159265-R(1)-R(3)
370 LET I=2
375 GOSUB 575
380 GOTO 315
385 PRINT "WHAT ARE SIDE1, SIDE3, ANGLE1";
390 INPUT S(1),S(3),A(1),B(1),C(1)
395 GOSUB 830
400 LET R8=1
405 LET I=1
410 GOSUB 620
415 LET T=(S(3)*SIN(R(1)))/S(1)
420 IF T-1>0.0000001 THEN 515
425 IF 1-T>0.00000001 THEN 440
430 LET R(3)=1.57079632
435 GOTO 445
440 LET R(3)=ATN(T/SQR(1-T^2))
445 LET R(2)=3.14159265-R(1)-R(3)
450 LET I=2
455 GOSUB 530
460 FOR I=2 TO 3
465 GOSUB 575
470 NEXT I
475 IF R9=1 THEN 510
480 IF S(3)>S(1) THEN 490
485 GO TO 505
490 PRINT
495 PRINT "ONE SOLUTION IS:"
500 GO TO 510
505 LET R8 = 0
510 GOTO 630
515 PRINT
520 PRINT "NO SOLUTION POSSIBLE...."
525 GOTO 800
530 LET S(I)=(S(3)/SIN(R(3)))*SIN(R(I))
535 RETURN
540 LET P=(S(1)+S(2)+S(3))/2
545 LET R=SQR(((P-S(1))*(P-S(2))*(P-S(3)))/P)
550 FOR I=1 TO 3
555 LET R(I)=2*ATN(R/(P-S(I)))
560 GOSUB 575
565 NEXT I
570 RETURN
575 LET X1=57.2957795*R(I)
580 LET A(I)=INT(X1)
585 LET B(I)=60*(X1-A(I))
590 IF ABS(60-B(I))>0.00005 THEN 605
595 LET B(I)=0
600 LET A(I)=A(I)+1
605 IF ABS(B(I))>0.0001 THEN 615
610 LET B(I)=0
615 RETURN
620 LET R(I)=(A(I)+B(I)/60)/57.2957795
625 RETURN
630 GOSUB 540
635 LET W=R*P
640 LET R1=S(1)/(2*SIN(R(1)))
645 LET H(1)=S(3)*SIN(R(2))
650 LET H(2)=S(1)*SIN(R(3))
655 LET H(3)=S(2)*SIN(R(1))
660 PRINT
665 IF R9=1 THEN 685
670 PRINT
675 PRINT " ","        1","        2","        3"
680 PRINT " ","     -------","     -------","     -------"
685 PRINT
690 PRINT "SIDE",S(1),S(2),S(3)
695 PRINT
700 PRINT "ANGLE (RAD)",R(1),R(2),R(3)
705 PRINT
710 PRINT "ANGLE (DEG)",A(1),A(2),A(3)
715 PRINT "      (MIN)", INT(B(1)),INT(B(2)),INT(B(3))
720 PRINT "      (SEC)", FNS(1), FNS(2), FNS(3)
725 PRINT
730 PRINT "ALT TO SIDE",H(1),H(2),H(3)
735 PRINT
740 PRINT
745 PRINT "RADIUS OF CIRCUMSCR CIRCLE = ";R1
750 PRINT "RADIUS OF INSCRIBED CIRCLE = ";R
755 PRINT "          AREA OF TRIANGLE = ";W
760 PRINT
765 IF R9=1 THEN 800
770 IF R8=0 THEN 800
775 PRINT
780 PRINT "A SECOND SOLUTION IS:"
785 LET R9=1
790 LET R(3)=3.14159265-R(3)
795 GOTO 445
800 PRINT
805 PRINT
810 PRINT "ANOTHER CASE (1=YES)";
815 INPUT Q
820 IF Q=1 THEN 120
825 STOP
830 FOR I=1 TO 3
835 LET B(I)= B(I) + C(I)/60
840 NEXT I
845 RETURN
9999 END
