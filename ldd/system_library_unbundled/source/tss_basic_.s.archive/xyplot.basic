3REM X Y P L O T
9 LET J=0
10 GO TO 610
11 IF J=0 THEN 130
12 RETURN
20 PRINT "                                XYPLOT"
130 PRINT
140 PRINT "WHAT ARE  YMIN,YMAX,XMIN,XMAX,DELX";
150 INPUT Q0,Q1,Q2,Q3,Q4
160 LET Q5=(Q1-Q0)/60
170 LET Q6=0
180 LET J=2
190 FOR X = Q2 TO Q3 STEP Q4
200 GOSUB 10
210 IF Q6 = 0 THEN 470
220 IF Q6 = 20 THEN 250
230 PRINT " . ";
240 GOTO 270
250 PRINT " - ";
260 LET Q6 = 10
270 IF Y > Q1 THEN 440
280 IF Y < Q0 THEN 440
290 LET Q7 = Q0 + 2*Q5
300 LET Z=Q7+0.5*Q5
310 IF Z<Y THEN 410
320 LET Q6 = Q6 + 1
330 IF Z-Y>=2*Q5 THEN 390
340 IF Z-Y>=Q5 THEN 370
350 PRINT "  +"
360 GOTO 530
370 PRINT " +"
380 GOTO 530
390 PRINT "+"
400 GOTO 530
410 LET Q7 = Q7 + 3*Q5
420 PRINT "   ";
430 GOTO 300
440 PRINT "OFF SCALE:  (X,Y) =  ";X;",  ";Y
450 LET Q6 = Q6 + 1
460 GOTO 530
470 PRINT
480 PRINT "FOR X:    TOP = ";Q2;" BOTTOM = ";Q3;" INCREMENT = ";Q4
490 PRINT "FOR Y:   LEFT = ";Q0;"  RIGHT = ";Q1;" INCREMENT = ";Q5
500 PRINT
510 PRINT "   I.........I.........I.........I.........I.........I.........I"
520 GOTO 250
530 NEXT X
540 PRINT
550 PRINT "TYPE '0' TO STOP OR '1' TO CHANGE LIMITS.  WHICH";
560 INPUT Q8
570 IF Q8 = 0 THEN 9999
580 IF Q8 = 1 THEN 130
590 PRINT
600 GOTO 540
610 PRINT
620 PRINT "THIS PROGRAM WILL PLOT SINGLE-VALUED FUNCTIONS OF X,"
630 PRINT "WITH X ON THE VERTICAL AXIS.  TO USE, TYPE:"
640 PRINT
650 PRINT "   10 LET Y= (THE FUNCTION TO BE PLOTTED)"
660 PRINT "   RUN"
670 PRINT
680 PRINT "DURING RUNNING, THE PROGRAM WILL ASK FOR YMIN AND"
690 PRINT "YMAX (THE LIMITS ON THE HORIZONTAL Y-AXIS), FOR"
700 PRINT "XMIN AND XMAX (THE LIMITS ON THE VERTICAL X-AXIS),"
710 PRINT "AND FOR DELX, THE INCREMENT TO BE USED ALONG THE"
720 PRINT "X-AXIS.  NOTE: LINES 70-99 OF THE PROGRAM MAY BE"
730 PRINT "USED AS DESIRED TO EXPRESS COMPLICATED FUNCTIONS."
9999 END
