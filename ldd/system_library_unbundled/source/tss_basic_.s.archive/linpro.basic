100 REM HONEYWELL TIME SHARING APPLICATIONS
110 PRINT "L I N P R O"
120 REM
130 REM REVISED APRIL, 1971
140 REM
150 READ G9
160 IF G9=1E38 THEN 1890
170 RESET
180 LET C=1
190 DIM A(19,31)
200 PRINT"TYPE '2' FOR OUTPUT OF OF TABLEAUS AND BASIS AT EACH ";
210 PRINT"ITERATION,"
220 PRINT"     '1' FOR THE BASIS ONLY, OR"
230 PRINT"     '0' FOR JUST THE SOLUTION."
240 PRINT"WHICH";
250 INPUT P5
260 PRINT
270 PRINT "WHAT ARE M AND N OF THE DATA MATRIX";
280 INPUT M,N
290 PRINT
300 PRINT "HOW MANY 'LESS THANS','EQUALS','GREATER THANS'";
310 INPUT L, E, G
320 IF L+E+G = M THEN 350
330 PRINT "INPUT DATA NOT CONSISTENT."
340 STOP
350 LET B = M+N+G+2
360 LET W = M
370 IF B*(W+2)<540 THEN 400
380 PRINT "PROBLEM TOO LARGE"
390 STOP
400 IF B>31 THEN 420
410 IF W+1<19 THEN 520
420 PRINT"CHANGE DIM STATEMENT (NO. 50) FOR A";W+2;"BY";B;"MATRIX"
430 PRINT
440 PRINT"EITHER STATEMENT 260 OR 270 OR BOTH SHOULD BE CHANGED"
450 PRINT"AS FOLLOWS:"
460 PRINT
470 IF B<32 THEN 490
480 PRINT "     260 IF B>";B;"THEN 280"
490 IF W+1<19 THEN 510
500 PRINT "      270 IF W+2<";W+2;"THEN 380"
510 STOP
520 LET M = M - 1
530 LET H = 1
540 FOR I = 1 TO W+2
550 FOR J = 2 TO B 
560 LET A(I,J) = 0
570 NEXT J
580 NEXT I
590 FOR I = 1 TO M+1
600 FOR J = 2 TO N+1
610 READ A(I,J)
620 NEXT J
630 NEXT I
640 FOR I = 1 TO M+1
650 READ A(I,B)
660 NEXT I
670 FOR J = 2 TO N+1
680 READ A(W+1,J)
690 LET A(W+1,J) = -A(W+1,J)
700 NEXT J
710 FOR K = 2 TO M+2
720 LET A(K-1,N+G+K) = 1
730 LET A(K-1,1) = K+N+G
740 NEXT K
750 IF E <> 0 THEN 770
760 IF G=0 THEN 920
770 FOR K = L+E+2 TO M+2
780 LET A(K-1,K+N-L-E) = -1
790 NEXT K
800 LET W = W + 1
810 LET Q = 0
820 FOR J = 2 TO N+1 + G
830 LET S = 0
840 FOR I = M-G-E+2 TO M+1
850 LET S = S + A(I,J)
860 NEXT I
870 LET A(W+1,J) = -S
880 IF A(W+1,J) > Q THEN 910
890 LET Q = A(W+1,J)
900 LET C = J
910 NEXT J
920 PRINT
930 PRINT "      YOUR VARIABLES";H;"THROUGH";N
940 IF G = 0 THEN 960
950 PRINT "   SURPLUS VARIABLES";N+1;"THROUGH";N+G
960 IF L = 0 THEN 980
970 PRINT "     SLACK VARIABLES";N+G+1;"THROUGH";N+G+L
980 IF G+E = 0 THEN 1000
990 PRINT "ARTIFICIAL VARIABLES";N+G+L+1;"THROUGH";B-2
1000 IF P5=0 THEN 1020
1010 GOSUB 1770
1020 IF G+E=0 THEN 1360
1030 IF Q=0 THEN 1430
1040 IF P5>0 THEN 1560
1050 LET H = H + 1
1060 LET Q = 1E38
1070 LET R = 0
1080 FOR I = 1 TO M+1
1090 IF A(I,C) <= 0 THEN 1130
1100 IF A(I,B)/A(I,C) > Q THEN 1130
1110 LET Q = A(I,B)/A(I,C)
1120 LET R = I
1130 NEXT I
1140 IF R >  0 THEN 1180
1150 PRINT "SOLUTION UNBOUNDED"
1160 GOSUB 1770
1170 STOP
1180 LET P = A(R,C)
1190 LET A(R,1)=C
1200 FOR J = 2 TO B
1210 LET A(R,J) = A(R,J)/P
1220 NEXT J
1230 FOR I = 1 TO W+1
1240 IF I = R THEN 1310
1250 FOR J = 2 TO B
1260 IF J = C THEN 1300
1270 LET A(I,J) = A(I,J) - A(R,J)*A(I,C)
1280 IF ABS(A(I,J)) > 1E-8 THEN 1300
1290 LET A(I,J) = 0
1300 NEXT J
1310 NEXT I
1320 FOR I = 1 TO W+1
1330 LET A(I,C) = 0
1340 NEXT I
1350 LET A(R,C) = 1
1360 LET Q = 0
1370 FOR J = 2 TO N+1+G+L
1380 IF A(W+1,J) > Q THEN 1410
1390 LET Q = A(W+1,J)
1400 LET C = J
1410 NEXT J
1420 GOTO 1030
1430 IF W=M+1 THEN 1460
1440 LET W = W-1
1450 GOTO 1360
1460 PRINT
1470 PRINT
1480 FOR I = 1 TO M+1
1490 IF A(I,1)<N+G+L+2 THEN 1510
1500 IF A(I,B)<>0 THEN 1530
1510 NEXT I
1520 GO TO 1550
1530 PRINT"NO FEASIBLE  SOLUTION FOUND"
1540 GO TO 99999
1550 PRINT "ANSWERS:"
1560 PRINT
1570 IF Q = 0 THEN 1590
1580 PRINT "        BASIS BEFORE ITERATION";H
1590 PRINT "        VARIABLE       VALUE"
1600 FOR I=1 TO M+1
1610 IF A(I,1) = 0 THEN 1630
1620 PRINT A(I,1)-1,A(I,B)
1630 NEXT I
1640 PRINT
1650 PRINT "OBJECTIVE FUNCTION VALUE",A(W+1,B)
1660 IF Q <> 0 THEN 1050
1670 PRINT
1680 PRINT
1690 PRINT "DUAL VARIABLES:"
1700 PRINT "         COLUMN       VALUE"
1710 FOR J=N+2 TO B-G-1
1720 PRINT J-1,A(W+1,J)
1730 NEXT J
1740 IF P5=0 THEN 1760
1750 GOSUB 1770
1760 GOTO 99999
1770 PRINT
1780 IF P5=1 THEN 1880
1790 PRINT
1800 PRINT "TABLEAU AFTER";H-1;"ITERATIONS"
1810 FOR I = 1 TO W+1
1820 FOR J = 2 TO B
1830 PRINT A(I,J),
1840 NEXT J
1850 PRINT
1860 PRINT
1870 NEXT I
1880 RETURN
1890 PRINT"                     INSTRUCTIONS FOR LINPRO"
1900 PRINT
1910 PRINT"THIS CODE MAXIMIZES THE OBJECTIVE FUNCTION" 
1920 PRINT"BY THE TWO-PHASE METHOD."  
1930 PRINT "MAXIMUM SIZE 18X30.  LARGE PROBLEMS MAY RESULT IN"
1940 PRINT "EXCESSIVE RUNNING TIME AND LARGE ROUNDOFF ERROR."
1950 PRINT"ENTER DATA USING LINES 1-99 AND 10000,90000:"
1960 PRINT"  FIRST - ARRANGE YOUR RESTRICTIONS SO THAT THE"
1970 PRINT"  'LESS THAN' INEQUALITIES PRECEDE THE STRICT EQUALITIES,"
1980 PRINT"  WHICH , IN TURN, PRECEDE THE 'GREATER THAN' INEQUALITIES."
1990 PRINT
2000 PRINT
2010 PRINT"THEN-TYPE IN THE COEFFICIENTS OF THE RESTRICTIONS ROW BY ROW"
2020 PRINT"    DO NOT INCLUDE COEFFICIENTS FOR SLACK, SURPLUS,"
2030 PRINT"    OR ARTIFICIAL VARIABLES."
2040 PRINT"NEXT - TYPE IN THE ''B'' VECTOR (THE CONSTANTS OF THE"
2050 PRINT"RESTRICTIONS.)  THESE VALUES MUST BE NON-NEGATIVE"
2060 PRINT
2070 PRINT
2080 PRINT"FINALLY - TYPE IN THE COEFFICIENTS OF THE COST FUNCTION."
2090 PRINT
2100 PRINT
2110 PRINT"FOR A MINIMIZATION PROBLEM, REVERSE THE SIGNS OF THE"
2120 PRINT"COEFFICIENTS OF THE OBJECTIVE FUNCTION."
2130 PRINT
2140 PRINT
2150 PRINT"AT RUN TIME YOU WILL BE ASKED TO:"
2160 PRINT"  (1) INDICATE THE LEVEL OF DETAIL FOR OUTPUT."
2170 PRINT"  (2) INPUT THE NUMBER OR RESTRICTIONS AND VARIABLES - "
2180 PRINT"      M AND N RESPECTIVELY."
2190 PRINT"  (3) AND TO INPUT THE NUMBER OF LESS THAN INEQUALITIES,"
2200 PRINT"      STRICT EQUALITIES, AND GREATER THAN INEQUALITIES,"
2210 PRINT"      RESPECTIVELY."
2220 PRINT
2230 PRINT"REMEMBER TO ALWAYS SUPPLY ZEROES WHEN THEY ARE APPLICABLE"
99998 DATA 1E38
99999 END
