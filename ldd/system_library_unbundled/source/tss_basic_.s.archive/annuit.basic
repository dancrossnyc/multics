3 PRINT "ANNUIT"
5 REM REVISED NOV 07. 1969
30 PRINT "WANT INSTRUCTIONS FIRST (1=YES, 2=NO)";
40 INPUT L
50 IF L<>1 THEN 70
60 GOSUB 600
70 LET S=0
80 PRINT
90 PRINT "WHICH ANNUITY TYPE (1=PAYMENT, 2=WITHDRAWAL)";
100 DEF FNA(U)=.01*INT(100*U+.5)
110 INPUT A
120 GOTO 170
130 IF A=1 THEN 160
140 LET  A=1
150 GOTO 170
160 LET  A=2
170 IF  A=1 THEN 220
180 IF A=2 THEN 1050
190 PRINT
200 PRINT "   ANSWER 1 OR 2 PLEASE."
210 GOTO 80
220 IF S=1 THEN 260
230 PRINT
240 PRINT "WHICH VARIABLE IS UNKNOWN (1=N,2=A,3=I,4=R)";
250 INPUT D
260 IF D=1 THEN 330
270 IF D=2 THEN 410
280 IF D=3 THEN 490
290 IF D=4 THEN 520
300 PRINT
310 PRINT "   ANSWER 1,2,3,OR 4 PLEASE."
320 GOTO 230
330 PRINT
340 PRINT "WHAT ARE A($),I(PCT),R($)";
350 INPUT A1,I1,R1
360 LET I1=I1/100
370 LET N1=LOG((A1*I1/R1)+1)/LOG(1+I1)
380 PRINT
390 PRINT "NUMBER OF PERIODS = N =";N1
400 GOTO 920
410 PRINT
420 PRINT "WHAT ARE N(INTEGER),I(PCT),R($)";
430 INPUT N1,I1,R1
440 LET I1=I1/100
450 LET A1=R1*((1+I1)^N1-1)/I1
460 PRINT
470 PRINT "AMOUNT AT END OF N PERIODS = A =";A1
480 GOTO 920
490 PRINT
500 PRINT "WHAT ARE N(INTEGER),A($),R($)";
510 GOTO 1760
520 PRINT
530 PRINT "WHAT ARE N(INTEGER),A($),I(PCT)";
540 INPUT N1,A1,I1
550 LET I1=I1/100
560 LET R1=A1*I1/((1+I1)^N1-1)
570 PRINT
580 PRINT "PAYMENT EACH PERIOD = R =";R1
590 GOTO 920
600 PRINT
610 PRINT "THIS PROGRAM PERFORMS CALCULATIONS FOR"
620 PRINT "BOTH PAYMENT AND WITHDRAWAL ANNUITIES."
630 PRINT
640 PRINT "VARIABLES ARE DENOTED AS FOLLOWS:"
650 PRINT
660 PRINT "   N = NUMBER OF PERIODS"
670 PRINT "   P = ORIGINAL PRINCIPAL AMOUNT"
680 PRINT "   A = TOTAL AMOUNT AT END OF N PERIODS"
690 PRINT "   I = INTEREST RATE PER PERIOD, IN PERCENT"
700 PRINT "   R = AMOUNT OF PAYMENT/WITHDRAWAL EACH PERIOD"
710 PRINT
720 PRINT "FOR A PAYMENT ANNUITY, YOU MAY GIVE ANY"
730 PRINT "  THREE OF N,A,I,R AND FIND THE FOURTH."
740 PRINT
750 PRINT "FOR A WITHDRAWAL ANNUITY, YOU GIVE ANY"
760 PRINT "  THREE OF N,P,I,R AND FIND THE FOURTH."
770 PRINT
780 PRINT "NOTE: FOR LOAN OR MORTGAGE, USE THE"
790 PRINT "      WITHDRAWAL ANNUITY OPTION."
800 PRINT
810 PRINT "AFTER EACH CASE, YOU MAY CHOOSE ONE"
820 PRINT "OF THE FOLLOWING ALTERNATIVES:"
830 PRINT
840 PRINT "   1 = ANOTHER CASE, SAME UNKNOWN VARIABLE"
850 PRINT "   2 = ANOTHER CASE, DIFFERENT UNKNOWN"
860 PRINT "   3 = ANOTHER CASE, OTHER TYPE OF ANNUITY"
870 PRINT "   4 = TOTAL INTEREST PAID OVER N PERIODS"
880 PRINT "   5 = TABLE OF WITHDRAWALS, PRINCIPAL, AND INTEREST"
890 PRINT "   6 = STOP THE PROGRAM"
900 PRINT
910 RETURN
920 PRINT
930 PRINT "ANOTHER CASE (TYPE CODE NUMBER)";
940 INPUT S
950 IF S=1 THEN 170
960 IF S=2 THEN 170
970 IF S=6 THEN 9999
980 IF S=3 THEN 130
990 IF A<>1 THEN 1030
1000 PRINT
1010 PRINT "ALTERNATIVE NOT APPLICABLE TO PAYMENT ANNUITIES."
1020 GOTO 920
1030 IF S=4 THEN 1240
1040 IF S=5 THEN 1240
1050 IF S=1 THEN 1090
1060 PRINT
1070 PRINT "WHICH VARIABLE IS UNKNOWN (1=N,2=P,3=I,4=R)";
1080 INPUT A1
1090 IF A1=1 THEN 1160
1100 IF A1=2 THEN 1660
1110 IF A1=3 THEN 1740
1120 IF A1=4 THEN 2090
1130 PRINT
1140 PRINT "   ANSWER 1,2,3, OR 4 PLEASE."
1150 GOTO 1080
1160 PRINT
1170 PRINT "WHAT ARE P($),I(PCT),R($)";
1180 INPUT P1,I1,R1
1190 LET I1=I1/100
1200 LET N1=LOG(R1/(R1-P1*I1))/LOG(1+I1)
1210 PRINT
1220 PRINT "NUMBER OF PERIODS = N =";N1
1230 GOTO 920
1240 LET N1=N1+.00001
1250 LET P2=INT(N1)-N1
1260 IF ABS(P2)>.00001 THEN 1290
1270 LET N1=N1-1
1280 GOTO 1300
1290 LET N1=INT(N1)
1300 LET P2=1
1310 IF S=4 THEN 1380
1320 IF S=5 THEN 1330
1330 LET P2=0
1340 PRINT 
1350 PRINT "PERIOD","PRINCIPAL","INTEREST","PRINC BAL","INT TO DATE"
1360 PRINT
1370 PRINT " 0"," "," ",P1
1380 LET X=0
1390 LET X9=P1
1400 LET Y=0
1410 FOR S=1 TO N1
1420 LET A3=(R1-P1*I1)*(1+I1)^(S-1)
1430 LET A2=FNA(A3)
1440 LET C=R1-A3
1450 LET B=FNA(C)
1460 LET X=X+A3
1470 LET X9=X9-A3
1480 LET Y=FNA(Y+C)
1490 IF P2 <>0 THEN 1510
1500 PRINT S,A2,B,X9,Y
1510 NEXT S
1520 LET S=N1+1
1530 LET A3=P1-X
1540 LET A2=FNA(A3)
1550 LET B1=I1*A3
1560 LET B=FNA(B1)
1570 LET X=X+A3
1580 LET X9=0
1590 LET Y=Y+B1
1600 IF P2=0 THEN 1640
1610 PRINT
1620 PRINT "TOTAL INTEREST PAID =";Y
1630 GOTO 920
1640 PRINT S,A2,B,X9,Y
1650 GOTO 920
1660 PRINT
1670 PRINT "WHAT ARE N(INTEGER),I(PCT),R($)";
1680 INPUT N1,I1,R1
1690 LET I1=I1/100
1700 LET P1=R1*((1+I1)^N1-1)/(I1*(1+I1)^N1)
1710 PRINT
1720 PRINT "ORIGINAL PRINCIPAL AMOUNT IS";P1
1730 GOTO 920
1740 PRINT
1750 PRINT "WHAT ARE N(INTEGER),P($),R($)";
1760 INPUT N1,P1,R1
1770 PRINT
1780 IF N1*R1<=P1 THEN 2170
1790 LET I1=.1
1800 LET I2=0
1810 LET I3=1
1820 IF A<>1 THEN 1850
1830 LET P3=R1*((1+I1)^N1-1)/I1
1840 GOTO 1860
1850 LET P3=R1*((1+I1)^N1-1)/(I1*(1+I1)^N1)
1860 LET P4=ABS(P1-P3)
1870 IF P4>0.001 THEN 1900
1880 PRINT "INTEREST PER PERIOD (PCT) = I =";100*I1
1890 GOTO 920
1900 IF P4/P1>.3 THEN 2010
1910 IF A=1 THEN 1940
1920 IF P3<P1 THEN 1980
1930 GOTO 1950
1940 IF P3>P1 THEN 1980
1950 LET I2=I1
1960 LET I1=(I1+I3)/2
1970 GOTO 1820
1980 LET I3=I1
1990 LET I1=(I1+I2)/2
2000 GOTO 1820
2010 IF A=1 THEN 2040
2020 IF P3<P1 THEN 2060
2030 GOTO 1950
2040 IF P3>P1 THEN 2060
2050 GOTO 1950
2060 LET I3=I1
2070 LET I1=(I1+I2)/4
2080 GOTO 1820
2090 PRINT
2100 PRINT "WHAT ARE N(INTEGER),P($),I(PCT)";
2110 INPUT N1,P1,I1
2120 LET I1=I1/100
2130 LET R1=P1*I1*(1+I1)^N1/((1+I1)^N1-1)
2140 PRINT
2150 PRINT "WITHDRAWAL EACH PERIOD = R =";R1
2160 GOTO 920
2170 PRINT"THE PRINCIPAL [P] IS NOT RECOVERED; [I] MUST BE POSITIVE."
2180 GO TO 1740
9999 END
