3REM C U R F I T
4 REM MODIFIED FEB 6,1975
5 PRINT
6 PRINT "C U R F I T"
7 PRINT
12 PRINT "DO YOU WANT INSTURCTIONS?  (YES OR NO)"
13 INPUT Q1$
14 IF Q1$ = "YES" THEN 20
15 IF Q1$ = "yes" THEN 20
16 IF Q1$ = "no" THEN 700
17 IF Q1$ = "NO" THEN 700
18 PRINT "ANSWER EITHER ""YES"" OR ""NO"""
19 GO TO 12
20 PRINT "LINE NUMBERS 50-699 ARE FREE FOR USE AS DATA STATEMENTS, AS REQUIRED"
22 PRINT "ALL VALUES FOR ONE VARIABLE ARE GIVE IN DATA STATEMENTS, FOLLOWED BY"
24 PRINT "ALL VALUES FOR A SECOND VARIABLE, AND SO ON."
26 PRINT "THE WAY TO ARRANGE INPUT IS TO PUT ALL VALUES OF VARIABLE 1 AT LINES"
28 PRINT "100-199, ALL FOR VARIABLE 2 AT LINES 200-299, ECT."
30 PRINT
31 PRINT " CURFIT ACCEPTS UP TO 200 OBSERVATIONS ON TWO VARIABLES. THE DATA FOR"
32 PRINT " THE DEPENDENT VARIABLE (Y) IS SUPPLIED FIRST, FOLLOWED BY THE"
33 PRINT " DATA FOR THE INDEPENDENT VARIABLE (X)."
34 PRINT
35 STOP
700DATA1E38,1E38
701DATA0,0
702 REM
703PRINT
704READP
705DIMX(200),Y(200),U(200),V(200),A(6),B(6),C(6),S(6),F(6)
706IFP=1E38THEN911
707FORK=1TO6
708LETF(K)=1
709NEXTK
710RESET
711PRINT
712PRINT"PLEASE SPECIFY THE NUMBER OF VALUES (N) GIVEN AS DATA"
713PRINT"FOR THE TWO INPUT VARIABLES, AND THE OUTPUT CODE (D)."
714PRINT"(D=1 IF OUTPUT IS TO BE IN ORDER OF INCREASING VALUES"
715PRINT"OF THE INDEPENDENT VARIABLE, ELSE D=0).  N,D = ";
716INPUTN,D
717PRINT
718FORI=1TON
719READY(I)
720NEXTI
721FORI=1TON
722READX(I)
723NEXTI
724READX,Y
725 IF X < 1E38  THEN  916
726 IF Y < 1E38  THEN  916
727PRINT
728PRINT
730 PRINT " L E A S T   S Q U A R E S   C U R V E   F I T"
731PRINT
732 PRINT"CURVE TYPE","  INDEX  OF","        A","       B"
733PRINT" ","DETERMINATION"
734PRINT
735FORI=1TO6
736FORI1=1TO6
737LETS(I1)=0
738NEXTI1
739GOSUB844
740IF(I-5)*(I-6)=0THEN755
741IF(I-2)*(I-3)=0THEN748
742FORJ=1TON
743LETV(J)=Y(J)
744GOSUB822
745NEXTJ
746IFI=1THEN765
747GOTO776
748FORJ=1TON
749IFY(J)<=0THEN762
750LETV(J)=LOG(Y(J))
751GOSUB822
752NEXTJ
753IFI=3THEN770
754GOTO765
755FORJ=1TON
756IFY(J)=0THEN762
757LETV(J)=1/Y(J)
758GOSUB822
759NEXTJ
760IFI=6THEN776
761GOTO765
762PRINT"CAN'T FIT"
763LETF(I)=0
764GOTO783
765FORJ=1TON
766LETU(J)=X(J)
767GOSUB825
768NEXTJ
769GOTO781
770FORJ=1TON
771IFX(J)<=0THEN762
772LETU(J)=LOG(U(J))
773GOSUB825
774NEXTJ
775GOTO781
776FORJ=1TON
777IFX(J)=0THEN762
778LETU(J)=1/X(J)
779GOSUB825
780NEXTJ
781GOSUB886
782PRINTC(I),A(I),B(I)
783NEXTI
784IFD<>1THEN786
785GOSUB829
786PRINT
787PRINT
788PRINT
789PRINT"DETAILS FOR";
790INPUTI
791LETK=I
792LETD1=D
793PRINT
794IFF(I)=1THEN798
795GOSUB844
796PRINT" COULD NOT BE FIT."
797GOTO786
798GOSUB862
799IF(I-1)*(I-5)*(I-6)<>0THEN810
800FORJ=1TON
801LETY=A(I)+B(I)*X(J)
802IFI=1THEN806
803LETY=1/Y
804IFI=5THEN806
805LETY=X(J)*Y
806GOSUB901
807NEXTJ
808LETD=D1
809GOTO786
810FORJ=1TON
811IFI=2THEN817
812IFI=3THEN815
813LETY=A(4)+B(4)/X(J)
814GOTO818
815LETY=A(3)*(X(J)^B(3))
816GOTO818
817LETY=A(2)*EXP(B(2)*X(J))
818GOSUB901
819NEXTJ
820LETD=D1
821GOTO786
822LETS(5)=S(5)+V(J)^2
823LETS(3)=S(3)+V(J)
824RETURN
825LETS(1)=S(1)+U(J)
826LETS(2)=S(2)+U(J)^2
827LETS(4)=S(4)+U(J)*V(J)
828RETURN
829FORI=1TON-1
830LETM=I
831FORJ=I+1TON
832IFX(M)<=X(J)THEN834
833LETM=J
834NEXTJ
835IFM=ITHEN842
836LETP=X(M)
837LETQ=Y(M)
838LETX(M)=X(I)
839LETY(M)=Y(I)
840LETX(I)=P
841LETY(I)=Q
842NEXTI
843RETURN
844LETK=I
845IFK=1THEN860
846IFK=2THEN858
847IFK=3THEN856
848IFK=4THEN854
849IFK=5THEN852
850PRINT"6. Y=X/(A+B*X) ";
851RETURN
852PRINT"5. Y=1/(A+B*X) ";
853RETURN
854PRINT"4. Y=A+(B/X)",
855RETURN
856PRINT"3. Y=A*(X^B)",
857RETURN
858PRINT"2. Y=A*EXP(B*X)";
859RETURN
860PRINT"1. Y=A+(B*X)",
861RETURN
862PRINT"   ";
863GOSUB845
864PRINT" IS A";
865IFK=1THEN870
866IFK=2THEN872
867IFK=3THEN874
868PRINT" HYPERBOLIC";
869GOTO875
870PRINT" LINEAR";
871GOTO875
872PRINT"N EXPONENTIAL";
873GOTO875
874PRINT" POWER";
875PRINT" FUNCTION.  THE RESULTS"
876IFK=1THEN878
877PRINT"      OF A LEAST-SQUARES FIT OF ITS LINEAR TRANSFORM"
878IFD<>1THEN880
879PRINT"      (SORTED IN ORDER OF ASCENDING VALUES OF X)"
880PRINT"      ARE AS FOLLOWS:"
881PRINT
882 PRINT "       X-ACTUAL       Y-ACTUAL    Y-CALC      PCT DIFFER"
883PRINT
884RETURN
885PRINT
886LETB=(N*S(4)-S(1)*S(3))/(N*S(2)-(S(1)^2))
887LETA=(S(3)-B*S(1))/N
888LETS1=S(5)-(S(3)^2)/N
889LETS2=(B^2)*(S(2)-(S(1)^2)/N)
890LETC(I)=S2/S1
891IF(I-1)*(I-4)*(I-5)=0THEN898
892IF(I-2)*(I-3)=0THEN896
893LETA(6)=B
894LETB(6)=A
895RETURN
896LETA(I)=EXP(A)
897GOTO899
898LETA(I)=A
899LETB(I)=B
900RETURN
901PRINTX(J),Y(J),Y,
902LETD=Y(J)-Y
903LETD=.1*SGN(D)*INT(1000*ABS(D/Y))
904IFD<0THEN909
905IFD>0THEN908
906PRINT"      0"
907RETURN
908PRINT"       ";
909PRINTD
910RETURN
911 PRINT
912 PRINT " DATA STATEMENTS ARE REQUIRED, SEE INSTRUCTIONS."
914 PRINT
915 STOP
916 PRINT
917 PRINT"CHECK INPUT"
9999 END
