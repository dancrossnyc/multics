2 PRINT
3 PRINT"               BLKJAK"
4 PRINT
20 DIM W(10),E(10),T(10),V(10)
24 PRINT"This demonstration shows the versatility of the Multics"
26 PRINT"BASIC languague by simulating a game of Blackjack."
27 PRINT" "
28 PRINT"DO YOU NEED INSTRUCTIONS (1=YES, 0=NO)"
29 INPUT k
30 IF K=0 THEN 112
32 PRINT"HERE ARE THE LAS VEGAS RULES FOR PLAYING BLACKJACK:"
34 PRINT
36 PRINT">WAGER: THE HOUSE LIMIT IS $500, SO TYPE IN A NUMBER "
38 PRINT"  FROM 0 TO 500. TO TERMINATE GAME, ENTER ZERO."
40 PRINT
42 PRINT">THE DEAL: I DEAL MYSELF 2 CARDS AND SHOW YOU ONE. THEN I"
44 PRINT"  DEAL YOU TWO CARDS, AND ASK IF YOU WANT A HIT (ANOTHER"
46 PRINT"  CARD). YOU HAVE SEVERAL OPTIONS DEPENDING ON THE CARDS"
48 PRINT"  YOU HOLD AND MY UP CARD:"
50 PRINT"    * STAND - BY TYPING A ZERO"
52 PRINT"    * TAKE A HIT - BY TYPING A ONE"
54 PRINT"    * GO DOWN FOR DOUBLES - BY TYPING A TWO"
56 PRINT"    * SPLIT A PAIR - BY TYPING A THREE"
58 PRINT
60 PRINT">INSURANCE: IF MY UP CARD IS AN ACE, I WILL ASK IF YOU"
62 PRINT"  WANT INSURANCE. IF YOU DO TYPE A ONE, BETTING ONE-HALF"
64 PRINT"  OF YOUR WAGER THAT I DO HAVE BLACKJACK. IF I DO, I PAY"
66 PRINT" 2-TO-1 ON YOUR INSURANCE BET. YOU LOSE YOUR ORIGINAL WAGER"
68 PRINT"  SINCE I HAVE BLACKJACK, SO WE ARE EVEN FOR THE HAND."
70 PRINT"  IF I DON'T HAVE BLACKJACK, YOU LOSE YOUR INSURANCE BET"
72 PRINT"  AND THE GAME CONTINUES. "
74 PRINT
76 PRINT"  IF YOU REFUSE INSURANCE (BY TYPING A ZERO) THE GAME"
78 PRINT"  CONTINUES AS NORMAL."
80 PRINT
82 PRINT">THE PLAY: WHEN YOU FINALLY STAND (BY TYPING A ZERO)"
84 PRINT"  I WILL DRAW CARDS UNTIL:"
86 PRINT"    *I HAVE AT LEAST A HARD 17 (HARD MEANS THE TOTAL"
88 PRINT"     DOES NOT INCLUDE AN ACE BEING COUNTED AS 11)"
90 PRINT"    *I HAVE A SOFT 18 (SOFT MEANS THE TOTAL INCLUDES AN"
92 PRINT"     ACE COUNTED AS 11)"
94 PRINT"    *I REACH A TOTAL OF 21"
96 PRINT"    *I EXCEED 21 AND BUST"
98 PRINT
100 PRINT">ITEMS:"
102 PRINT"    *I PAY 1.5-TO-1 ON BLACKJACK"
104 PRINT"    *I DON'T RECOGNIZE 5-CARDS-AND-UNDER"
106 PRINT"    *YOU MAY DOUBLE DOWN ON A SPLIT HAND"
108 PRINT"    *YOU DON'T LOSE ON A TIE HAND...WE PUSH"
110 PRINT"           <<<GOOD LUCK>>>"
112 PRINT"MULTICS IS THE DEALER AND GETS A BREAK AT 1945 HOURS. WHAT"
113 randomize
114 PRINT"TIME IS IT NOW";
116 LET K=0
118 LET W1=0
120 INPUT N
122 FOR I = 1TO N/2
124  let x=t(10*rnd)
126 NEXT I
128 DIM D(52)
130 FOR A=1 TO 52
132 LET D(A) = 0
134 NEXT A
136 DIM Q(52)
138 FOR A=0 TO 39 STEP 13
140 FOR C=1 TO 13
142 LET Q(A+C)=C
144 NEXT C
146 NEXT A
148 PRINT 
150 LET K=K+1
152 FOR P=1 TO 5
154 LET E(P)=0
156 LET V(P) = 0
158 LET T(P)=0
160 NEXT P
162 LET V(3)=1
164 PRINT 
166 PRINT "WAGER";
168 LET P=1
170 INPUT W
172 LET W(2)=W
174 IF W<=0 THEN 582
176 IF W<=500 THEN 182
178 PRINT "THAT'S TOO MUCH - HOUSE LIMIT IS $500."
180 GOTO 166
182 PRINT
184 PRINT "I SHOW           ",
186 GOSUB 366
188 IF E(1)=0 THEN 192
190 LET V(4)=1
192 LET V(5)=1
194 GOSUB 366
196 LET M=X
198 LET P=2
200 PRINT "FIRST CARD IS",
202 GOSUB 366
204 LET G=X
206 PRINT "NEXT  CARD IS",
208 GOSUB 366
210 IF V(2)>0 THEN 262
212 LET S=X
214 IF V(3)<>1 THEN 262
216 IF T(P)<>21 THEN 230
218 PRINT "***BLACKJACK***"
220 PRINT "MY HOLE CARD WAS ",
222 LET X=M
224 GOSUB 434
226 LET W1=W1+1.5*W
228 GOTO 538
230 IF V(4)=0 THEN 250
232 PRINT "INSURANCE ANYONE  (TYPE 1 OR 0)";
234 INPUT I
236 IF I=0 THEN 250
238 IF T(1)<>21 THEN 246
240 LET W1=W1+W
242 PRINT "YOU WIN $";W;" ON YOUR INSURANCE BET";
244 GOTO 250
246 LET W1=W1-W/2
248PRINT"YOU LOST $";W/2;" ON INSURANCE BET - I DONT HAVE BLKJAK"
250 IF T(1)<>21 THEN 262
252 PRINT "**I HAVE BLACKJACK**"
254 PRINT "MY HOLE CARD IS ",
256 LET X=M
258 GOSUB 434
260 GOTO 534
262 IF T(P)<=21 THEN 278
264 IF E(P)>0 THEN 274
266 PRINT "YOU BUSTED, ";
268 PRINT "YOUR TOTAL IS ";T(P)
270 IF V(2)=1 THEN 328
272 GOTO 494
274 LET E(P)=E(P)-1
276 LET T(P)=T(P)-10
278 IF V(1)=2 THEN 268
280 LET V(3)=V(3)+1
282 PRINT "HIT";
284 INPUT V(1)
286 IF V(1)<>3 THEN 350
288 IF V(2)>0 THEN 346
290 IF V(3)<>2 THEN 346
292 IF Q(G)=Q(S) THEN 298
294 PRINT "NOW IS THAT A PAIR "
296 GOTO 282
298 LET V(2)=1
300 IF Q(G)<>1 THEN 304
302 LET V(1)=2
304 LET P=3
306 PRINT "       PLAY HAND ONE NOW"
308 PRINT "FIRST CARD IS",
310 LET W(3)=W
312 LET X=G
314 GOSUB 318
316 GOTO 206
318 GOSUB 414
320 GOSUB 434
322 LET V(3)=1
324 LET T(P)=C
326 RETURN
328 LET P=2
330 LET V(2)=2
332 PRINT "       PLAY HAND TWO NOW"
334 PRINT "FIRST CARD IS",
336 LET X=S
338 GOSUB 318
340 IF Q(G)=1 THEN 344
342 LET V(1)=0
344 GOTO 206
346 PRINT "NO SPLITS NOW -- TRY AGAIN"
348 GOTO 282
350 IF V(1)<>2 THEN 360
352 IF V(3)=2 THEN 358
354 PRINT "TOO LATE TO DOUBLE, CHARLIE."
356 GOTO 282
358 LET W(P)=2*W(P)
360 IF V(1)>0 THEN 206
362 GOTO 268
364 REM DEAL
366 GOSUB 380
368 LET T(P)=T(P)+C
370 IF V(5)=0 THEN 376
372 LET V(5)=0
374 RETURN
376 GOSUB 434
378 RETURN
380 LET N=10 *(1+ABS(COS(N+W1)))
382 FOR A=1 TO N
384  let x=int(52.9999999*rnd)
386 IF X=0 THEN 384
388 NEXT A
390 IF D(X)=0 THEN 410
392 LET R=R+1
394 IF R<50 THEN 380
396 FOR A=1 TO 52
398 IF D(A) =K THEN 402
400 LET D(A)=0
402 NEXT A
404 LET R=0
406 PRINT "I RESHUFFLED"
408 GOTO 380
410 LET R=0
412 LET D(X)=K
414 IF Q(X)<>1 THEN 422
416 LET C=11
418 LET E(P)=E(P)+1
420 RETURN
422IF Q(X)>10 THEN 428
424 LET C=Q(X)
426 RETURN
428 LET C=10
430 RETURN
432 REM PRINT CARD
434 GOSUB 440
436 GOSUB 470
438 RETURN
440 IF Q(X)<>1 THEN 446
442 PRINT " ACE ";
444 RETURN
446 IF Q(X)>10 THEN 452
448 PRINT Q(X);
450 RETURN
452 IF Q(X)>11 THEN 458
454 PRINT " JACK ";
456 RETURN
458 IF Q(X)>12 THEN 464
460 PRINT " QUEEN ";
462 RETURN
464 PRINT " KING ";
466 RETURN
468 REM PRINT SUIT
470 IF X>39 THEN 480
472 IF X>26 THEN 484
474 IF X>13 THEN 488
476 PRINT "OF SPADES"
478 RETURN
480 PRINT "OF CLUBS"
482 RETURN
484 PRINT "OF HEARTS"
486 RETURN
488 PRINT "OF DIAMONDS"
490 RETURN
492 REM DEALER PLAYS
494 LET P=2
496 PRINT "MY HOLE CARD IS  ",
498 LET X=M
500 GOSUB 434
502 IF T(2)<22 THEN 508
504 IF V(2)=0 THEN 534
506 IF T(3)>21 THEN 534
508 LET P=1
510 IF T(1)<17 THEN 548
512 IF T(1)>17 THEN 516
514 IF E(1)>0 THEN 548
516 IF T(1)>21 THEN 554
518 LET P=2
520 PRINT "MY TOTAL IS ";T(1)
522 IF T(P)>21 THEN 534
524 IF T(1)>21 THEN 530
526 IF T(1)>T(P) THEN 534
528 IF T(1)=T(P) THEN 536
530 LET W1=W1+W(P)
532 GOTO 536
534 LET W1=W1-W(P)
536 IF V(2)>0 THEN 542
538 GOSUB 566
540 GOTO 150
542 LET P=3
544 LET V(2)=0
546 GOTO 522
548 PRINT "I DRAW           ",
550 GOSUB 366
552 GOTO 508
554 IF E(1)= 0 THEN 562
556 LET E(1) = E(1) -1
558 LET T(1)=T(1)-10
560 GOTO 508
562 PRINT "I BUSTED***";
564 GOTO 518
566 IF W1<0 THEN 574
568 IF W1=0 THEN 578
570 PRINT"YOU'RE AHEAD $";W1
572 RETURN
574 PRINT "YOU'RE BEHIND $";-W1
576 RETURN
578 PRINT "YOU'RE EVEN"
580 RETURN
582 END
