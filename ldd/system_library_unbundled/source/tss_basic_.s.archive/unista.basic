3 PRINT "UNISTA"
5 REM REVISED FEB 5, 1975
6 REM THE SORT USED IN THIS PROGRAM IS SLOW
10 REM:   UNISTA      [DO NOT RESEQUENCE]
12 PRINT "DO YOU WANT INSTURCTIONS?  (YES OR NO)"
13 INPUT Q1$
14 IF Q1$ = "YES" THEN 20
15 IF Q1$ = "yes" THEN 20
16 IF Q1$ = "no" THEN 700
17 IF Q1$ = "NO" THEN 700
18 PRINT "ANSWER EITHER ""YES"" OR ""NO"""
19 GO TO10
20 PRINT "LINE NUMBERS 50-699 ARE FREE FOR USE AS DATA STATEMENTS, AS REQUIRED"
22 PRINT "ALL VALUES FOR ONE VARIABLE ARE GIVE IN DATA STATEMENTS, FOLLOWED BY"
24 PRINT "ALL VALUES FOR A SECOND VARIABLE, AND SO ON."
26 PRINT "THE WAY TO ARRANGE INPUT IS TO PUT ALL VALUES OF VARIABLE 1 AT LINES"
28 PRINT "100-199, ALL FOR VARIABLE 2 AT LINES 200-299, ECT."
30  PRINT "UNISTA ACCEPTS UP TO 300 OBSERVATIONS ON ONE VARIABLE."
32 PRINT
34 STOP
700 PRINT
730 RESET
740 PRINT
750 PRINT"TYPICAL INTERVAL FOR FREQUENCY DISTRIBUTIONS:";
760 PRINT" L,U =";
770 INPUT W1,W2
780 PRINT
790 PRINT
800 DIM X(300),S(4),Y(4),Z(4)
810 DIM C(50),G(50),E(50),F(50)
820 DEF FNR(M)=.001*INT(1000*M+.5)
830 DATA 1E38,0,0,0,0,0,0,0
840 LET I = 0
850 LET I = I+1
860 READ X(I)
870 IF  X(I) <> 1E38 THEN 850
880 LET N1 = I-1
890 LET N = N1-1
900 FOR I=1 TO 4
910 LET S(I) = 0
920 NEXT I
930 FOR I = 1 TO N1
940 LET Y(1) = X(I)
950 LET Y(2) = X(I)^2
960 LET Y(3) = Y(1)*Y(2)
970 LET Y(4) = Y(2)^2
980 FOR J=1 TO 4
990 LET S(J)=S(J)+Y(J)
1000 NEXT J
1010 NEXT I
1020 LET S = S(1)
1030 FOR I = 1 TO 4
1040 LET Y(I)=(1/N1)*S(I)
1050 NEXT I
1060 LET S(2) = Y(2) - Y(1)^2
1070 LET S(3) = Y(3) - 3*Y(1)*Y(2) + 2*Y(1)^3
1080 LET S(4) = Y(4) - 4*Y(3)*Y(1) + 6*Y(2)*Y(1)^2 - 3*Y(1)^4
1090 LET Y(2) = SQR(S(2))
1100 LET Y(3) = S(3) / (S(2)*Y(2))
1110 LET Y(4) = S(4)/S(2)^2 - 3
1120 PRINT
1130 PRINT "S U M M A R Y    S T A T I S T I C S"
1140 PRINT
1150 PRINT"    NUMBER OF VARIATES =";N1
1160 PRINT"       ARITHMETIC MEAN =";Y(1)
1170 PRINT"    STANDARD DEVIATION =";Y(2)
1180 PRINT"              VARIANCE =";S(2)
1190 PRINT"    COEFF OF VAR (PCT) =  ";FNR(100*Y(2)/Y(1))
1200 PRINT"     STANDARD SKEWNESS = ";FNR(Y(3))
1210 PRINT"       STANDARD EXCESS = ";FNR(Y(4))
1220 PRINT
1230 PRINT
1240 REM  SORT
1250 FOR I = 1 TO N
1260 FOR J = I+1 TO N1
1270 IF X(I)<X(J) THEN  1310
1280 LET Y1=X(I)
1290 LET X(I)=X(J)
1300 LET X(J)=Y1
1310 NEXT J
1320 NEXT I
1330 PRINT"O R D E R   S T A T I S T I C S"
1340 PRINT
1350 PRINT"      SMALLEST VARIATE =";X(1)
1360 LET P1=10
1370 GOSUB 2360
1380 LET T1=P2
1390 PRINT"          LOWER DECILE =";T1
1400 LET P1=25
1410 GOSUB 2360
1420 LET T2=P2
1430 PRINT"        FIRST QUARTILE =";T2
1440 LET P1=50
1450 GOSUB 2360
1460 PRINT"                MEDIAN =";P2
1470 LET P5=P2
1480 LET P1=75
1490 GOSUB 2360
1500 LET T3=P2
1510 PRINT"        THIRD QUARTILE =";T3
1520 LET P1=90
1530 GOSUB 2360
1540 LET T4=P2
1550 PRINT"          UPPER DECILE =";T4
1560 PRINT"       LARGEST VARIATE =";X(N1)
1570 PRINT
1580 LET  U=X(N1)-X(1)
1590 PRINT"           TOTAL RANGE =";U
1600 PRINT"          DECILE RANGE =";T4-T1
1610 PRINT"   SEMI-QUARTILE RANGE =";(T3-T2)/2
1620 PRINT"     BOWLEY'S SKEWNESS = ";FNR((T3+T2-2*P5)/(T3-T2))
1630 PRINT"      PEARSON SKEWNESS = ";FNR(3*(Y(1)-P5)/Y(2))
1640 PRINT
1650 PRINT
1660 PRINT
1670 LET D=ABS(W2-W1)
1680 IF D=0 THEN  2510
1690 LET  Y1=W1-INT((W1-X(1))/D+ 1.99999)*D
1700 LET  L=INT((X(N1)-Y1+.00001)/D)+1
1710 IF  L>50  THEN  2450
1720 FOR I=1 TO L+2
1730 LET  C(I)=Y1+ (I-1)*D
1740 LET  F(I)=0
1750 LET  E(I)=0
1760  NEXT  I
1770 PRINT"      F R E Q U E N C Y     D I S T R I B U T I O N"
1780 PRINT
1790 PRINT"                   UP TO BUT"," ","   PERCENT"
1800 PRINT"        FROM     NOT INCLUDING   FREQUENCY","   FREQUENCY"
1810 PRINT
1820 FOR I = 1 TO N1
1830 LET H=INT((1+1E-8)*(X(I)-C(1))/D) + 2
1840 LET F(H)=F(H)+1
1850 LET E(H)=E(H)+X(I)
1860 NEXT I
1870 LET  J = 1
1880 LET J=J+1
1890 LET G(J)=100*F(J)/N1
1900 PRINT C(J-1),C(J),F(J),FNR(G(J))
1910 IF  J=L+2  THEN  1930
1920 GOTO 1880
1930 FOR J = 3 TO L+1
1940 LET F(J)=F(J-1)+F(J)
1950 LET G(J)=100*F(J)/N1
1960 LET E(J)=E(J-1)+E(J)
1970 NEXT J
1980 PRINT
1990 PRINT
2000 PRINT"      C U M U L A T I V E     D I S T R I B U T I O N"
2010 PRINT
2020 PRINT"                  NUMBER LESS    % LESS          VARIATE SUM %"
2030 PRINT"       VALUE      THAN VALUE     THAN VALUE     LESS THAN VALUE"
2040 PRINT
2050 LET  J = 1
2060 LET J=J+1
2070 LET E(J)=100*E(J)/S
2080 PRINT C(J),F(J),FNR(G(J)),FNR(E(J))
2090 IF  J=L+1  THEN  2110
2100 GOTO 2060
2110 PRINT
2120 PRINT
2130 PRINT"O R D E R E D     A R R A Y"
2140 PRINT
2150 LET M=INT(N1/4-.05)+1
2160 LET H=4*(M-1)
2170 LET L=N1-H
2180 FOR I = 1 TO M-1
2190 LET K1=I+M
2200 IF L=1 THEN  2230
2210 LET K2=I+2*M
2220 GOTO 2240
2230 LET K2=I+2*M-1
2240 IF (L-1)*(L-2)=0 THEN  2270
2250 LET K3=I+3*M
2260 GOTO 2280
2270 LET K3=I+3*M-2/L
2280 PRINT X(I),X(K1),X(K2),X(K3)
2290 NEXT I
2300 FOR I = 2 TO L+1
2310 PRINT X((I-1)*M),
2320 NEXT I
2330 PRINT
2340 STOP
2350 REM "SUBROUTINE"
2360 LET G5 = P1*(N1+1)/100
2370 LET  P2 = X(1)
2380 IF G5<1 THEN  2440
2390 LET P2=X(N1)
2400 IF G5>N1 THEN  2440
2410 LET Q5 = INT(G5)
2420 LET  H5=G5-Q5
2430 LET P2=H5*X(Q5+1)+(1-H5)*X(Q5)
2440 RETURN
2450 PRINT
2460 PRINT"INTERVAL IS TOO SMALL.  MAXIMUM IS 50 CLASSES"
2470 PRINT"RESPECIFY L,U";
2480 INPUT W1,W2
2490 GOTO 1640
2510 END
