1 REM MODIFIED FEB 1975
2 PRINT"               L I N R E G"
200 DATA 1E35,1E35
201 PRINT" DO YOU WANT INSTRUCTIONS? (YES=1, NO=0)"
202 INPUT Z9
203 IF Z9 = 1 THEN 650
206 PRINT"WHAT TYPE OF CURVE DO YOU WANT TO FIT:"
210 PRINT "LINEAR=1, EXPONENTIAL=2, POWER=3";
215 INPUT Q1
220 DIM D(250,2)
225 PRINT
230 LET I=0
235 LET I=I+1
240 READ D(I,1),D(I,2)
245 IF D(I,1)<>1E35 THEN 235
250 LET Q2=I-1
255 LET S9=0
260 IF Q1=1 THEN 280
265 IF Q1=2 THEN 335
270 IF Q1=3 THEN 405
275 GOTO 230
280 LET S9=1
285 GOSUB 505
290 PRINT "LINEAR:  Y=A+B*X  WITH  A=";Q8;"AND B=";Q9
295 GOSUB 580
300 FOR J=1 TO Q2
305 LET W7=Q8+Q9*D(J,1)
310 LET Z7=W7-D(J,2)
315 LET Q4=100*Z7/D(J,2)
320 PRINT D(J,1),D(J,2),W7,Z7,Q4
325 NEXT J
330 STOP
335 FOR J=1 TO Q2
340 LET D(J,2)=LOG(D(J,2))
345 NEXT J
350 GOSUB 505
355 PRINT "EXPONENTIAL:  Y=A*EXP(B*X)  WITH  A=";EXP(Q8);"AND B=";Q9
360 GOSUB 580
365 FOR J=1 TO Q2
370 LET W7=EXP(Q8+Q9*D(J,1))
375 LET W8=EXP(D(J,2))
380 LET Z7=W7-W8
385 LET Q4=100*Z7/W8
390 PRINT D(J,1),W8,W7,Z7,Q4
395 NEXT J
400 STOP
405 FOR J=1 TO Q2
410 LET D(J,1)=LOG(D(J,1))
415 LET D(J,2)=LOG(D(J,2))
420 NEXT J
425 GOSUB 505
430 PRINT "POWER:  Y=A*(X^B)  WITH  A=";EXP(Q8);"AND B=";Q9
435 GOSUB 580
440 FOR J=1 TO Q2
445 LET W7=EXP(D(J,1))
450 LET W8=EXP(D(J,2))
455 LET W9=EXP(Q8)*W7^Q9
460 LET Q4=W9/W8-1
465 LET Z7=W9-W8
470 IF Q4<0 THEN 485
475 LET Q4=INT(1000*Q4+0.5)/10
480 GOTO 490
485 LET Q4=INT(1000*Q4-0.5)/10
490 PRINT W7,W8,W9,Z7,Q4
495 NEXT J
500 STOP
505 LET Q3=0
510 LET Q4=0
515 LET Q5=0
520 LET Q6=0
525 LET Q7=0
530 FOR J=1 TO Q2
535 LET Q3=Q3+D(J,1)
540 LET Q4=Q4+D(J,2)
545 LET Q5=Q5+D(J,1)*D(J,2)
550 LET Q6=Q6+(D(J,1))^2
555 LET Q7=Q7+(D(J,2))^2
560 NEXT J
565 LET Q9=(Q2*Q5-Q3*Q4)/(Q2*Q6-Q3^2)
570 LET Q8=(Q4-Q3*Q9)/Q2
575 RETURN
580 LET Q0=(Q2*Q5-Q3*Q4)/SQR((Q2*Q6-Q3^2)*(Q2*Q7-Q4^2))
585 PRINT
590 PRINT "COEFFICIENT OF CORRELATION = ";Q0
595 PRINT "COEFFICIENT OF DETERMINATION = ";Q0^2
600 PRINT
605 PRINT "DO YOU WANT TO SEE A COMPARISON OF THE ACTUAL Y'S AND"
610 PRINT "THE ESTIMATED Y'S. 1=YES, 0=NO";
615 INPUT P9
620 IF P9<>1 THEN 715
625 PRINT
630 PRINT "X-ACTUAL","Y-ACTUAL","Y-ESTIM","DIFFER","PCT-DIFF"
635 PRINT
640 RETURN
645 PRINT
650 PRINT "THIS IS A LINEAR REGRESSION PROGRAM FOR DATA IN TWO "
655 PRINT "VARIABLES, X AND Y.  FROM INPUT POINTS, DESCRIBED BY"
660 PRINT "THEIR X AND Y COORDINATES, AN EQUATION IS PRODUCED THAT"
665 PRINT "BEST FITS THESE POINTS IN THE LEAST-SQUARES SENSE.  TO"
670 PRINT "USE THE PROGRAM, TYPE THE FOLLOWING:"
675 PRINT
680 PRINT "   2 DATA  X(1),Y(1), X(2),Y(2),......, X(N),Y(N)"
685 PRINT "        (WHERE X(1),Y(1) IS THE FIRST POINT, X(2),"
690 PRINT "        Y(2) IS THE SECOND, AND SO ON UNTIL ALL"
695 PRINT "        POINTS HAVE BEEN ENTERED.  ADDITIONAL DATA"
700 PRINT "        STATEMENTS 3-199 MAY BE USED AS NEEDED.)"
705 PRINT
715 END
