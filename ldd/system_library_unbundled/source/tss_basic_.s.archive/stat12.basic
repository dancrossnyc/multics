5 REM ---STAT12---OCT 27, 1969
10   REM  DESCRIPTION: COMPUTES THE CORRELATION MATRIX FOR N 
20   REM  SERIES OF DATA.
40   REM  INSTRUCTIONS: ENTER DATA STARTING IN LINE 900 IN THE 
50   REM  FOLLOWING ORDER:  1) THE NUMBER OF SERIES, 2) THE NUMBER
60   REM  IN EACH SERIES, 3) THEN THE DATA BY GROUP (NOT BY SERIES).
70   REM  THIS PROGRAM IS LIMITED TO 25 SERIES, WITH AN ARBITRARY
75   REM  NUMBER IN EACH SERIES.  TO INCREASE THE MAXIMUM NUMBER OF 
80  REM  SERIES, CHANGE THE DIM STATEMENTS IN LINE 100.
90  READ O0
91  RESET
92  IF O0 <> 999999 THEN 100
93  PRINT "LIST LINES 10 TO 85 FOR INSTRUCTIONS"
94 STOP
100 DIM S(25,25),D(25),X(25)
110  READ N, M
170  LET X(0) = 1
200  FOR K = 1 TO M
210  FOR I = 1 TO N
220  READ X(I)
230  NEXT I
240  FOR I = 0 TO N
250  FOR J = I TO N
260  LET S(I,J) = S(I,J) + X(I)*X(J)
270  NEXT J
280  NEXT I
290  NEXT K
300  REM HAVING COMPUTED THE SUM-OF-SQUARES MATRIX, WE CONTINUE.
305  PRINT TAB(3);"VARIABLE";TAB(23);"MEAN";TAB(34);"VARIANCE";
306  PRINT TAB(48);"STD. DEV."
310  FOR I = 1 TO N
320  LET M1 = S(0,I) / M
330  LET Q = ( M * S(I,I) -S(0,I)*S(0,I) ) / M / (M-1)
340  LET D(I) = SQR(Q)
350  PRINT I, M1, Q, D(I)
360  NEXT I
600  REM NOW WE PRODUCE AND PRINT THE CORRELATION MATRIX...
605  PRINT
610  PRINT "THE CORRELATION MATRIX"
620  PRINT
630  FOR I = 1 TO N
635 IF I = 1 THEN 670
640  FOR J = 1 TO I-1
650 PRINT " ",
660  NEXT J
670  FOR J = I TO N
680  PRINT (M*S(I,J) - S(0,I)*S(0,J))/M/(M-1)/D(I)/D(J),
690  NEXT J
700  PRINT
710  PRINT
720  NEXT I
730  STOP
900 DATA 999999
9999 END
