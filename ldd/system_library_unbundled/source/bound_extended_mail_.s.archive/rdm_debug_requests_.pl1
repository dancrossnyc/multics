/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */

/* format: off */

/* Debugging requests for the read_mail subsystem */

/* Created:  October 1982 by G. Palter */
/* Modified: 13 September 1983 by G. Palter as part of the conversion of read_mail to the new mail system interface */

/* format: on,style4,delnl,insnl,ifthenstmt,ifthen */


rdm_debug_requests_:
     procedure (P_sci_ptr, P_rdm_invocation_ptr);

	put file (rdm_debug_) data;			/* forces a full symbol table ... */

	return;					/* ... but not really an entrypoint */


dcl  P_sci_ptr pointer parameter;
dcl  P_rdm_invocation_ptr pointer parameter;

dcl  sci_ptr pointer;

dcl  argument character (argument_lth) unaligned based (argument_ptr);
dcl  argument_ptr pointer;
dcl  argument_lth fixed binary (21);
dcl  (n_arguments, argument_idx) fixed binary;

dcl  new_debug_mode bit (1) aligned;
dcl  code fixed binary (35);

dcl  rdm_debug_ file stream internal;

dcl  error_table_$bad_arg fixed binary (35) external;
dcl  error_table_$badopt fixed binary (35) external;

dcl  rdm_set_request_tables_ entry (pointer, fixed binary (35));
dcl  ssu_$abort_line entry () options (variable);
dcl  ssu_$arg_count entry (pointer, fixed binary);
dcl  ssu_$arg_ptr entry (pointer, fixed binary, pointer, fixed binary (21));
dcl  ssu_$set_debug_mode entry (pointer, bit (1) aligned);
dcl  probe entry () options (variable);

dcl  index builtin;
%page;
/* The "debug_mode" request: enables/disables read_mail debugging facilities */

debug_mode:
     entry (P_sci_ptr, P_rdm_invocation_ptr);

	sci_ptr = P_sci_ptr;
	rdm_invocation_ptr = P_rdm_invocation_ptr;

	new_debug_mode = "1"b;			/* defaults to turn on debug_mode */

	call ssu_$arg_count (sci_ptr, n_arguments);

	do argument_idx = 1 to n_arguments;
	     call ssu_$arg_ptr (sci_ptr, argument_idx, argument_ptr, argument_lth);
	     if index (argument, "-") = 1 then		/*  a control argument */
		if argument = "-on" then new_debug_mode = "1"b;
		else if argument = "-off" then new_debug_mode = "0"b;
		else call ssu_$abort_line (sci_ptr, error_table_$badopt, """^a""", argument);
	     else call ssu_$abort_line (sci_ptr, error_table_$bad_arg,
		     "This request only accepts control arguments.  ""^a""", argument);
	end;

	rdm_invocation.debug_mode = new_debug_mode;

	call ssu_$set_debug_mode (sci_ptr, (rdm_invocation.debug_mode));
						/* keep ssu_ in step */

	call rdm_set_request_tables_ (rdm_invocation_ptr, code);
	if code ^= 0 then call ssu_$abort_line (sci_ptr, code, "Setting subsystem request tables.");

	return;
%page;
/* The "probe" request: invokes the probe symbolic debugger in a stack frame with all relavent data structure available */

probe:
     entry (P_sci_ptr, P_rdm_invocation_ptr);

	sci_ptr = P_sci_ptr;
	rdm_invocation_ptr = P_rdm_invocation_ptr;

	call ssu_$arg_count (sci_ptr, n_arguments);
	if n_arguments ^= 0 then call ssu_$abort_line (sci_ptr, 0, "No arguments may be supplied.");

	mailbox_ptr = rdm_invocation.mailbox_ptr;	/* make it easy to access the mailbox ... */

	if rdm_invocation.current_message ^= 0 then	/* ... and the current message (if any) */
	     if message_list.messages (rdm_invocation.current_message).message_idx > 0 then
		message_ptr =
		     mailbox.messages (message_list.messages (rdm_invocation.current_message).message_idx)
		     .message_ptr;
	     else message_ptr = null ();
	else message_ptr = null ();

	call probe ();

	return;
%page;
%include rdm_invocation;
%page;
%include rdm_message_list;
%page;
%include rdm_message_chains;
%page;
%include mlsys_mailbox;
%page;
%include mlsys_message;
%page;
%include mlsys_address_list;

     end rdm_debug_requests_;
