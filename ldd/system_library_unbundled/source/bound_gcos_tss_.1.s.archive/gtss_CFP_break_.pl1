/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

gtss_CFP_break_: proc;

/**	Author: Al Dupuis  06/23/79.
	Changed: Al Dupuis	11/10/79 Set break termination code.

   	This module is called to determine if the user has defined
	the special label $*$lbl ..break in his/her *CFP file
	when in command file processing. If the label was defined,
	the next sector to read (gtss_ust.lcfio.sect_in) is set
	to the sector number associated with this label. Otherwise,
	it is set to the cout sector.

**/



	gtss_ust.lcfst.start_term = 8;
	call com_err_$suppress_name (0, "gtss_CFP_break_",
	     "BREAK RECEIVED");
	gtss_starCF_$FILE.Seek_Address,
	     gtss_ust.lcfio.sect_in = gtss_ust.lcfst.initial_sect_out - 1;
	gtss_starCF_$FILE.OP2.Device_Command = read_cmd;
	call gtss_ios_io_ (				/* read the cout sector from *CFP */
	     gtss_starCF_$FILE.cf.aft_indx,
	     addr (select_sequence),
	     addr (select_sequence),
	     fixed (rel (addr (gtss_starCF_$FILE.cf))),
	     status,
	     error_code);
	if status ^= 0
	then do;
	     call ioa_ ("gtss_CFP_break_:"
		|| "Unable to read cout sector status (^i)", status);
	     return;
	end;
	cout_ptr = addr (no_characters);

	do i = 1 to hbound (cout_ptr -> label_table, 1);
	     if break_label = cout_ptr -> label_name (i) then do;
		gtss_ust.lcfio.sect_in = cout_ptr -> label_pos (i);
		return;
	     end;
	end;


dcl break_label bit (54) static int options (constant) init ("333322512521422020"b3);						/* "..BREAK" */
dcl com_err_$suppress_name entry options (variable);
dcl cout_ptr ptr init (null());
dcl error_code fixed bin (35);
dcl i fixed bin (24);
dcl ioa_ entry options (variable);
dcl status fixed bin (24);

%include gtss_deferred_queue;

%include gtss_starCF_;

%include gtss_device_cmds;

%include gtss_ust_ext_;

%include gtss_entry_dcls;
     end;						/* gtss_CFP_break_ */
