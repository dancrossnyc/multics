/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

gtss_abandon_CFP_: proc;

/* Reset to non-command file processing.

   Author: Dave Ward 03/27/79

   Changed: Al Dupuis 05/22/79 To turn off copy_on, etc...bits.
   Changed: Al Dupuis 10/01/79 Added call to timer_manager_.
   Changed: Al Dupuis 10/01/79 To zero out any termination code in start_term.
   Changed: Al Dupuis 11/10/79 End of ..init processing.
*/
	call timer_manager_$reset_cpu_call (gtss_fault_processor_$timer_runout);
	call timer_manager_$reset_cpu_call (gtss_abs_$cpu_runout);
	gtss_ext_$flags.ss_time_limit_set = "0"b;
	gtss_ext_$flags.timer_ranout = "0"b;
	gtss_ust_ext_$ust.gtss_ust.limit = 0;
	gtss_ext_$get_line = iox_$get_line;
	gtss_ext_$put_chars = iox_$put_chars;
	cpos_called = "0"b;
	CFP_in_progress = "0"b;
	dele_on = "0"b;
	copy_on = "0"b;
	talk_on = "0"b;
	trap_off = "0"b;
	lswt2.b6 = "0"b;
	gtss_ust.lcfst.start_term = 0;		/* reset any abort term code */

/* If we are just finishing processing a ..init file thru CRUN, but still
   have to execute DRUN (we are under absentee) prepare things	*/
	if gtss_ext_$process_type = 2
	then do;
	     if ((gtss_ust.lcjid = "0"b)
	     & (gtss_ext_$drun_jid ^= " "))
	     then do;
		gtss_ust.lcfst.start_term = 17;
		call gtss_ascii_bcd_ (addr (gtss_ext_$drun_jid), 5, addr (gtss_ust.lcjid));
		call gtss_aft_$find ("*CFP", fn, code);
		if code ^= 0
		then return;
		call gtss_ios_close_ (fn,
		     addr (fms_status),
		     code);
		if fms_status.bit12 ^= "4000"b3
		then do;
		     call ioa_ ("gtss_abandon_CFP_: "
			|| "*CFP won't close. Status (^w).",
			fms_status);
		     return;
		end;
		call gtss_aft_$delete ("*CFP", fn, code);
	     end;
	end;
	return;

dcl  code				fixed bin (35);
dcl  fn				fixed bin (24);
dcl  ioa_                               entry options (variable);
dcl  iox_$get_line            	entry ext;
dcl  iox_$put_chars           	entry ext;
dcl  timer_manager_$reset_cpu_call	entry (entry);
dcl  1 fms_status aligned,
       2 bit12 bit (12) unaligned,
       2 bit60 bit (60) unaligned;

%include gtss_ext_;

%include gtss_starCF_;

%include gtss_CFP_bits;

%include gtss_ust_ext_;

%include gtss_entry_dcls;
     end						/* gtss_abandon_CFP_ */;
