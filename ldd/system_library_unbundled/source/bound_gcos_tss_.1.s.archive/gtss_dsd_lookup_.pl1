/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

gtss_dsd_lookup_: proc (rt) returns (fixed bin (24));

/**     This function, given a character request type,
        returns the CFP_request_value as defined in the
        include file gtss_CFP_request_types. The value is
        set to zero if the type is not found.

        Author:   Al Dupuis 04/19/79
**/
	do while (length (rt) ^> 3);			/* pad the parameter to make it four characters */
	     rt = rt || " ";
	end;

	string = substr (rt, 1, 4);
	i = 1;
	j = hbound (CFP_request_types, 1);

	do while (i <= j);				/* binary table search */
	     k = divide (i + j, 2, 24);
	     if string = substr (CFP_request_types (k), 1, 4)
	     then return (CFP_request_value (k));	/* hit */
	     if string < substr (CFP_request_types (k), 1, 4)
	     then j = k - 1;
	     else i = k + 1;
	end;

	return (0);				/* miss */
/**

Variables for gtss_dsd_lookup_
**/

dcl rt char (8) varying parameter;
dcl string char (4);
dcl (i,j,k) fixed bin (24);
dcl (hbound, divide, length, substr) builtin;
/** 
**/
%include gtss_CFP_request_types;

     end						/* gtss_dsd_lookup_ */;
