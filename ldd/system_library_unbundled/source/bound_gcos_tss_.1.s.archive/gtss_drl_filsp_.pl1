/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

/* *************************************************************
   *						   *
   * Copyright (c) 1979 by Honeywell Information Systems, Inc. *
   *						   *
   ************************************************************* */
gtss_drl_filsp_: proc (mcpp, increment);

/**	gtss position a linked (sequential) file.

   Authors: Robert J. Grimes	Created
   -	  Albert N. Kepner	  1978
   -	  Robert M. May
   -	  David B. Ward
   Change:  Dave Ward	02/24/79 $db debug switch.
   Change:  Dave Ward	09/17/79 New gtss_ios_position_ usage.
   **/
dcl  mcpp                     ptr parm;
dcl  increment                fixed bin parm;
	mcp = mcpp;

/**

   DRL FILSP, SPACE A LINKED FILE (octal 13)


   8_________1_6_____________________

   DRL      FILSP
   ZERO     L(fileid),L(n)
   ZERO     L(stat),0

   (See TSS SYSTEM PROGRAMMER'S
   REFERENCE MANUAL DD17B, Rev. 0, July 1976
   Page 3-50.1)

*/

	increment = 2 ;				/* Two arguments */
	scup = addr (mc.scu);
	gseg = gtss_ext_$gtss_slave_area_seg (gtss_ext_$stack_level_);
	arg_list_ptr = addrel (gseg, fixed (scu.ilc, 18)+1); /* get addres of arg_list */

/**	Obtain name of file to position.	**/
	if L_fileid >= gtss_ust.lsize.limit then do;	/* name 2 words. */
	     call gtss_abort_subsystem_ (mcp,
		"gtss_drl_filsp_",
		4,
		"FILSP: File name outside memory at (^6o)",
		fixed (scu.ilc, 18));
	     return;
	end;
	afn_ptr = addrel (gseg, L_fileid);

/**	Obtain pointer to return status word.	**/
	if L_stat >= gtss_ust.lsize.limit then do;	/* status 2 words. */
	     call gtss_abort_subsystem_ (mcp,
		"gtss_drl_filsp_",
		4,
		"FILSP: Status word outside memory at (^6o)",
		fixed (scu.ilc, 18));
	     return;
	end;
	status_words_ptr = addrel (gseg, L_stat);
	unspec (status_word1) = "0"b;
	gcs = "4000"b3;				/* => drl successful. */

/**	Obtain (fn) the AFT index to the file.	**/
	call gtss_aft_$find (ascii_file_name, fn, code);
	if code = 1 then do;			/* Unknown file (i.e., not in AFT). */
	     gcs = "4100"b3;			/* Device busy. */
	     return;
	end;

/**	Obtain pointer to n.	**/
	if L_n > gtss_ust.lsize.limit then do;
	     call gtss_abort_subsystem_ (mcp,
		"gtss_drl_filsp_",
		4,
		"FILSP: Space value outside memory at (^6o)",
		fixed (scu.ilc, 18));
	     return;
	end;
	n_ptr = addrel (gseg, L_n);

/**	Reposition the file.	**/
	call gtss_ios_position_ (
	     fn
	     , 0					/* 0 => space forward (n>0) or backward (n<0) */
	     , n					/* Number blocks (320 words) to position. */
	     , status_words.Record_count_residue
	     , status_words_ptr
	     );
	if (gcs ^= "5700"b3) & (gcs ^= "4002"b3) & (gcs ^= "4000"b3) then do;
	     if db_drl_filsp then
		call com_err_ (
		code
		, "gtss_drl_filsp_",
		"File ""^a"" (aft ^i) gtss_ios_position_ status=^w"
		, ascii_file_name
		, fn
		, gcs
		);
	     call gtss_abort_subsystem_ (
		mcp
		, "gtss_drl_filsp_"
		, 0
		, "FILSP: Unable to position"
		);
	end;
	if db_drl_filsp then
	     call ioa_ ("FILSP: file ^a new position = ^o",
	     ascii_file_name, file_position (fn));
	return;

/**	Variables for gtss_drl_filsp_:
   IDENTIFIER		ATTRIBUTES	**/
dcl  addrel                   builtin;
dcl  afn_ptr                  ptr init(null());
dcl  arg_list_ptr             ptr init(null());
dcl  ascii_file_name          char (8)based (afn_ptr);
dcl  code                     fixed bin (35);
dcl  fn                       fixed bin (24);
dcl  gseg                     ptr init(null());
dcl  ioa_                     entry options (variable);
dcl  n                        fixed bin (24)aligned based(n_ptr);
dcl  n_ptr                    ptr init(null());

dcl 1 status_word1 aligned based(status_words_ptr),
      2 gcs bit(12)unal,
      2 fill bit(24)unal;

dcl 1 arg_list aligned based (arg_list_ptr)
     , 2 L_fileid bit (18)unal
     , 2 L_n bit (18)unal
     , 2 L_stat bit (18)unal
     , 2 zero bit (18)unal
     ;

%include gtss_dfd_ext_;

%include gtss_ext_;

%include mc;

%include gtss_ust_ext_;

%include gtss_entry_dcls;

%include gtss_db_names;

%include gtss_io_status_words;
     end						/* gtss_drl_filsp_ */;
