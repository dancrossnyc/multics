/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

gtss_mcfc_empty: proc (rs);

/* Command to set the GCOS simulator's multiple
   caller file control data base "empty".

   Author: Dave Ward	08/08/79
*/
dcl  rs                       char(*)var parm;

	call gtss_mcfc_init_ (r);
	if r = "false" then do;
	     rs = r;
	     return;
	end;

/* Obtain caller's lock id. */
	call get_lock_id_ (lid);

	unspec (mcfc), unspec (names) = "0"b;

/* Pseudo lock all the start list. */
	mcfc.start_list.files.lock, mcfc.start_list.avail.lock = lid;

	names = empty ();

/* Set availibility chains. */
	n = lbound (mcfc.start_list, 1);
	sll = hbound (mcfc.start_list, 1)-lbound (mcfc.start_list, 1)+1;
	do i = lbound (file_entry, 1) to hbound (file_entry, 1);
	     file_entry (i).link = mcfc.start_list (n).avail.first;
	     mcfc.start_list (n).avail.first = i;
	     n = mod (n+1, sll);
	end;

/* Unlock all the start list. */
	mcfc.start_list.files.lock, mcfc.start_list.avail.lock = "0"b;

	rs = "true";
	return;

/* Variables for gtss_mcfc_empty:
   IDENTIFIER		ATTRIBUTES	*/
dcl  empty                    builtin;
dcl  get_lock_id_             entry(bit(36)aligned);
dcl  gtss_mcfc_init_          entry(char(*)var);
dcl  i                        fixed bin;
dcl  lid                      bit(36)aligned;
dcl  n                        fixed bin;
dcl  r                        char(5)var;
dcl  sll                      fixed bin;

%include gtss_mcfc;

%include gtss_ext_;
     end						/* gtss_mcfc_empty */;
