/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

/* *************************************************************
   *						   *
   * Copyright (c) 1979 by Honeywell Information Systems, Inc. *
   *						   *
   ************************************************************* */

gtss_filact_funct11_: proc (mcp_val, high_val, gseg_val, arg_ptr_val, buffer_ptr_val, code);

/**	Derail FILACT function 11 (MF - Modify File).

	All parameters are input parameters except code.

	code returned 0 => Successful.
	code returned 4 => GCOS err4 (see gtss_pnterr structure).
	code returned other => multics error code.

   Authors: Robert J. Grimes	Created
   -	  Albert N. Kepner	  1978
   -	  Robert M. May
   -	  David B. Ward
   Change:  Dave Ward	02/24/79 $db debug switch.
   Change:  Dave Ward	07/13/79 Converted to unsigned fixed bin(18).
			       Corrected options interpretation.
   Change:  Bob Alvarado	09/06/79 changed MF_new_name.name to two words.
   Change:  Dave Ward	09/06/79 Set disk_data for certain file changes.
   Change:  Dave Ward	09/18/79 Return status for dup name.
   Change:  Paul Benjamin     10/01/79 Check status for dup name on same file.
   Change:  Paul Benjamin     12/17/79 ACLs now upper case &
				permissions more closely resemble GCOS.
   Change:  Ron Barstad       02/04/83 fix stringsize error in call to gtss_aft_$find
                                        was trying to pass char(32) in char(8) field
**/
dcl  mcp_val                  ptr parm;
dcl  high_val                 fixed bin(18)unsigned unal parm;
dcl  gseg_val                 ptr parm;
dcl  arg_ptr_val              ptr parm;
dcl  buffer_ptr_val           ptr parm;
dcl  code                     fixed bin(35)parm;
	mcp = mcp_val;
	mem_top = high_val;
	gseg = gseg_val;
	arg_ptr = arg_ptr_val;
	buffer_ptr = buffer_ptr_val;

	code = 0;					/* Successful. */

/**	Obtain modify file arglist. */
	if (MF_args.L_arglist < mem_bottom) |
	((MF_args.L_arglist +3) > mem_top) then do;
return_err4:   ;
	     code = 4;
	     return;
	end;
	arglist_ptr = addrel (gseg, MF_args.L_arglist);

/**	Obtain return status.	**/
	call validate_status (MF_arglist.L_status_return, status_ptr);

	if get_ascii_file_name ("0"b, MF_arglist.L_cat_filedescr, addr (ascii_cat_file)) then do;
could_not_modify_file: ;
	     status_word.status = "400500"b3;		/* Could not access file specified. */
could_not_modify_status_set: ;
	     call bcd_message (
		status2.L_bcd_message
		, status2.message_words
		, buffer_ptr
		, "Could not modify file."||rtrim (gtss_file_values.dname)||">"||rtrim (gtss_file_values.ename)
		);
	     status_word.pd = get_faulty_cat_file_entry ();
ret:	     ;
	     if fcb_ptr ^= null () then
		call msf_manager_$close (fcb_ptr);
	     return;
	end;

	gtss_file_values.version = 1;
	gtss_file_values.new_ename = " ";
	gtss_file_values.change_name = "0"b;
	gtss_file_values.set_switch = "0"b;

	call gtss_expand_pathname_ (
	     addr (ascii_cat_file)
	     , gtss_file_values.dname
	     , gtss_file_values.ename
	     , code
	     );
	if code ^= 0 then goto could_not_modify_file;

/**	Obtain new name (if any).	**/
	if (MF_arglist.L_newname < mem_bottom) |
	(MF_arglist.L_newname+4 > mem_top) then goto return_err4;
	new_name_ptr = addrel (gseg, MF_arglist.L_newname);

	if MF_new_name.newname.left_word ^= (36)"1"b then do; /* New name desired. */
	     gtss_file_values.change_name = "1"b;
	     call gtss_bcd_ascii_$lc (
		addr (MF_new_name.newname)
		, 12
		, addr (gtss_file_values.new_ename)
		);
	end;

/* Obtain pointer to msf file control block
   (i.e., open the file).
*/
	call msf_manager_$open (
	     gtss_file_values.dname
	     , gtss_file_values.ename
	     , fcb_ptr
	     , code
	     );
	if code ^= 0 then goto could_not_modify_file;

	call set_general_permissions;

	call set_options;

	if gtss_file_values.change_name |
	(unspec (gtss_file_values.set_switch) ^= "0"b) then do;
	     call gtss_attributes_mgr_$set (addr (gtss_file_values), code);
	     if code ^= 0 then do;
		if code = error_table_$namedup | code = error_table_$segnamedup then do;
		     status_word.status = "4011"b3;	/* non-unique name. */
		     goto could_not_modify_status_set;
		end;
		goto could_not_modify_file;
	     end;

	     if gtss_file_values.change_name |
	     set_switch.mode_random |
	     set_switch.maxll |
	     set_switch.curll then do;		/* Adjust ios runtime values for the file. */

/* Obtain aft index for the file. */
		aft_name = substr(gtss_file_values.ename,1,8);
		call gtss_aft_$find (
		     aft_name
		     , file_no
		     , ec
		     );
dcl ec fixed bin(35);
		if ec = 0 then do;			/* File name is in aft => change values. */
		     gtss_file_attributes_ptr = gtss_disk (file_no).attributes_ptr;
		     if gtss_file_values.change_name then
			gtss_disk (file_no).entry_name = gtss_file_values.new_ename;
		     if set_switch.mode_random then
			gtss_file_attributes.descriptor.mode,
			gtss_disk (file_no).pat_body.random
			= gtss_file_values.data_flags.mode_random;
		     if set_switch.maxll then
			gtss_file_attributes.max_size
			= gtss_file_values.data_fields.maxll;
		     if set_switch.curll then
			gtss_file_attributes.max_size
			= gtss_file_values.data_fields.curll;
		end;
	     end;
	end;

	status_word.status = "400000"b3;
	goto ret;

set_options: proc;

/* Process options.
*/

	     if (L_options<mem_bottom) |
	     ((L_options+2)>mem_top) then goto return_err4;
	     op = addrel (gseg, L_options);

/* Determine number of specific user permissions. */
	     do n = 0 by 1 while (end_of_list ^= -1);
		if (L_options+3+ (3*n))>mem_top then goto could_not_modify_file;
	     end;

	     if opt (4) then do;			/* New max size. */
		if unspec (max_size) = (18)"1"b then do; /* Set unlimited. */
		     gtss_file_values.set_switch.maxll = "1"b;
		     gtss_file_values.data_fields.maxll = 0;
		end;
		else
		if max_size ^= 0 then do;		/* Set to specified size. */
		     gtss_file_values.set_switch.maxll = "1"b;
		     gtss_file_values.data_fields.maxll = max_size;
		end;
	     end;

	     if opt (8) then do;			/* User attributes. */
		gtss_file_values.set_switch.attr = "1"b;
		gtss_file_values.attributes.attr = substr (user_attributes, 2);
	     end;

	     if opt (9) then do;			/* New mode. */
		gtss_file_values.set_switch.mode_random = "1"b;
		gtss_file_values.data_flags.mode_random = opt (1);
	     end;

	     if n<1 then return;

/* User specific permissions. */
	     acl_count = n;
	     allocate segment_acl set (acl_ptr);

/* Process deletes. */
	     c, d = 0;
	     do k = 1 to n;
		if addr (user (k).specific_permission) -> fb18 = 0 then do;
		     d = d+1;
		     call gtss_bcd_ascii_ (
			addr (user (k).id)
			, 12
			, addr (ascii_id)
			);
		     del_name (d) = "*."||rtrim (ascii_id)||".*";
		end;
	     end;
	     if d>0 then do;			/* There were names to delete. */
		call msf_manager_$acl_delete (
		     fcb_ptr
		     , acl_ptr
		     , d
		     , code
		     );
		if code ^= 0 then do;
		     free segment_acl;
		     goto could_not_modify_file;
		end;

/* Convert acl names not found to be
   add names with null.
*/
		do k = 1 to d;
		     if status_code2 (k) = error_table_$user_not_found then do;
			c = c+1;
			access_name (c) = del_name (k);
			modes (c) = "0"b;
			zero_pad (c) = "0"b;
		     end;
		end;
	     end;

	     if (c>0) | (d<n) then do;

/* Process non-deletes. */
		do k = 1 to n;
		     pp = addr (user (k).specific_permission);
		     if pp -> b36 ^= (36)"1"b then
			if pp -> fb18 ^= 0 then do;
			     c = c+1;
			     call gtss_bcd_ascii_ (
				addr (user (k).id)
				, 12
				, addr (ascii_id)
				);
			     access_name (c) = "*."||rtrim (ascii_id)||".*";
			     modes (c) = get_modes (addr (user (k).specific_permission));
			     if modes (c) = "111111111111111111111111111111111111"b then c = c - 1;
						/* Create permission, forget this one. */
			end;
		end;

		if c>0 then do;
		     call msf_manager_$acl_add (
			fcb_ptr
			, acl_ptr
			, c
			, code
			);
		     if code ^= 0 then do;
			free segment_acl;
			goto could_not_modify_file;
		     end;
		end;
	     end;

	     free segment_acl;
	     return;

dcl  acl_count                fixed bin;
dcl  acl_ptr                  ptr init(null());
dcl  ascii_id                 char(12);
dcl  b36                      bit(36)aligned based;
dcl  c                        fixed bin;
dcl  d                        fixed bin;
dcl  error_table_$user_not_found	fixed bin(35)ext;
dcl  fb18                     fixed bin(18)unsigned unal based;
dcl  k                        fixed bin;
dcl  n                        fixed bin(24);
dcl  op                       ptr;
dcl  pp                       ptr;

dcl 1 optiob_args aligned based(op)
,     2 word1
,       3 opt (0:35)bit(1)unal
,     2 word2
,       3 initial_size fixed bin(18)unsigned unal
,       3 max_size     fixed bin(18)unsigned unal
,     2 user (n)
,       3 id           bit(72)
,       3 specific_permission bit(36)
,     2 end_of_list    fixed bin(35)
,     2 user_attributes bit(36)
;

dcl 1 delete_acl  (acl_count) aligned based(acl_ptr),
      2 del_name char(32),
      2 status_code2 fixed bin(35);

dcl 1 segment_acl (acl_count)aligned based(acl_ptr),
      2 access_name char(32),
      2 modes       bit(36),
      2 zero_pad    bit(36),
      2 status_code fixed bin(35);
	end					/* set_options */;

set_general_permissions: proc;

/* Set general permissions
   => Multics *.*.* access.
*/

/**	Obtain permissions.	**/
	     if MF_arglist.L_permissions = 0 then return;
	     if (MF_arglist.L_permissions < mem_bottom) |
	     (MF_arglist.L_permissions > mem_top) then goto return_err4;
	     pp = addrel (gseg, MF_arglist.L_permissions);
	     if pp -> b36 = (36)"1"b then return;
	     if pp -> fb18 = 0 then do;		/* Delete general permissions. */
		del_name = "*.*.*";
		call msf_manager_$acl_delete (
		     fcb_ptr
		     , addr (delete_acl)
		     , 1
		     , code
		     );
	     end;
	     else do;

/* => Set specific general permissions. */
		access_name = "*.*.*";
		modes = get_modes (pp);
		status_code = 0;
		if modes ^= "111111111111111111111111111111111111"b then do; /* Ignore create */
		     call msf_manager_$acl_add (
			fcb_ptr
			, addr (segment_acl)
			, 1
			, code
			);
		end;
	     end;
	     if code ^= 0 then goto could_not_modify_file;
	     return;

dcl  pp                       ptr;
dcl  fb18                     fixed bin(18)unsigned unal based;
dcl  b36                      bit(36)aligned based;
dcl  bits                     (0:35)bit(1)unal based;

dcl 1 delete_acl,
      2 del_name char(32),
      2 status_code2 fixed bin(35);

dcl 1 segment_acl,
      2 access_name char(32),
      2 modes       bit(36),
      2 zero_pad    bit(36),
      2 status_code fixed bin(35);
	end					/* set_general_permissions */;

get_modes: proc (pp)returns (bit (36));

/* Interpret GCOS permissions as
   Multics accesses.
*/
dcl  pp                       ptr parm;
	     r = "0"b;
dcl  r			bit(36)unal;
dcl  b                        (0:35)bit(1)unal based;
dcl  b0_9		bit(10) based;
	     if pp -> b0_9 = "0000000010"b then return ("111111111111111111111111111111111111"b);
						/* Create permission doesn't apply to files */
	     if pp -> b (17) then return ("0"b);	/* exclude => null */
	     r = "1"b||pp -> b (3)||pp -> b (1);
	     return (r);
	end					/* get_modes */;

/**	(MF) Modify File Declarations.	**/
dcl  new_name_ptr             ptr init(null());

dcl 1 MF_new_name		aligned based(new_name_ptr)
,     3 newname		
,	4 left_word	bit(36)
,	4 right_word	bit(36)
,     3 newpassword		bit(72)
;

dcl 1 MF_args		aligned based(arg_ptr)
,     3 word1
,       4 zero		fixed bin(18)unsigned unal
,       4 L_arglist		fixed bin(18)unsigned unal
,     3 word2
,       4 MF_function_no	fixed bin(18)unsigned unal
,       4 L_buffer		fixed bin(18)unsigned unal
;

dcl 1 MF_arglist		aligned based(arglist_ptr)
,     3 word1
,       4 L_status_return	fixed bin(18)unsigned unal
,       4 zero		fixed bin(18)unsigned unal
,     3 word2
,       4 L_cat_filedescr	fixed bin(18)unsigned unal
,       4 L_permissions	fixed bin(18)unsigned unal
,     3 word3
,       4 L_options		fixed bin(18)unsigned unal
,       4 L_newname		fixed bin(18)unsigned unal
;

%include gtss_filact_intp3x;

%include gtss_filact_intp7x;


%include gtss_filact_intp2x;

%include gtss_filact_intp1x;

/** Declarations for gtss_drl_filact_:
     IDENTIFIER		ATTRIBUTES	**/
dcl  msf_manager_$acl_replace entry(ptr,ptr,fixed bin,bit(1),fixed bin(35));
dcl  msf_manager_$close       entry(ptr);
dcl  msf_manager_$open        entry(char(*),char(*),ptr,fixed bin(35));
dcl  msf_manager_$acl_add     entry(ptr,ptr,fixed bin,fixed bin(35));
dcl  msf_manager_$acl_delete  entry(ptr,ptr,fixed bin,fixed bin(35));
dcl  fcb_ptr                  ptr init(null());
dcl  access_mode              bit (6);
dcl  addr                     builtin;
dcl  addrel                   builtin;
dcl  aft_code                 fixed bin (35);
dcl  aft_name                 char (8);
dcl  altname                  char (8) aligned based (altname_ptr);
dcl  altname_ptr              ptr init(null());
dcl  append_mode              bit (36) static int options(constant)init ("100000000000"b3);
dcl  arglist_ptr              ptr init(null());
dcl  arg_ptr                  ptr init(null());
dcl  attribute_segment_ptr    ptr init(null());
dcl  bit                      builtin;
dcl  bit72                    bit(72)aligned based;
dcl  bit_count                fixed bin (24);
dcl  cat_filedescr_name_offset fixed bin(18)unsigned unal ;
dcl  cdate                    fixed bin (71);
dcl  clim                     float bin;
dcl  crf                      fixed bin(24);
dcl  csp                      float bin;
dcl  delete_$path             entry (char (*), char (*), bit (6), char (*), fixed bin (35));
dcl  descriptor_address       fixed bin(18)unsigned unal ;
dcl  descriptor_ptr           ptr init(null());
dcl  dir_name                 char (168);
dcl  divide                   builtin;
dcl  entry_name               char (12) init (" ");
dcl  error_table_$incorrect_access fixed bin (35) ext;
dcl  error_table_$moderr      fixed bin (35) ext;
dcl  error_table_$namedup     fixed bin (35) ext;
dcl  error_table_$noentry     fixed bin (35) ext;
dcl  error_table_$nomatch     fixed bin (35) ext;
dcl  error_table_$not_seg_type fixed bin(35)ext;
dcl  error_table_$no_dir      fixed bin (35) ext;
dcl  error_table_$segknown    fixed bin (35) ext;
dcl  error_table_$segnamedup  fixed bin (35) ext;
dcl  error_table_$seg_not_found fixed bin (35) ext;
dcl  execute_mode             bit (36) static int options(constant)init ("200000000000"b3);
dcl  file_name_in_ascii       bit(1);
dcl  file_no                  fixed bin (24);
dcl  fixed                    builtin;
dcl  FMS_block_ptr            ptr init(null());
dcl  four_NULS                char (4)static int options (constant)init ((4)" ") /* 4 octal 000's */;
dcl  gseg                     ptr init(null());
dcl  gtss_fail                condition;
dcl  have_file                bit(1);
dcl  hbound                   builtin;
dcl  hcs_$add_acl_entries     entry (char (*), char (*), ptr, fixed bin(24), fixed bin (35));
dcl  hcs_$add_dir_acl_entries entry (char (*), char (*), ptr, fixed bin, fixed bin (35));
dcl  hcs_$add_inacl_entries   entry (char (*), char (*), ptr, fixed bin, fixed bin (3), fixed bin (35));
dcl  hcs_$append_branchx      entry (char (*), char (*), fixed bin (5), (3) fixed bin (3), char (*), fixed bin (1), fixed bin (1), fixed bin (24), fixed bin (35));
dcl  hcs_$delentry_file       entry(char(*),char(*),fixed bin(35));
dcl  hcs_$initiate            entry (char (*), char (*), char (*), fixed bin (1), fixed bin (2), ptr, fixed bin (35));
dcl  hcs_$make_seg            entry (char (*), char (*), char (*), fixed bin (5), ptr, fixed bin (35));
dcl  hcs_$quota_read          entry (char (*), fixed bin (18), fixed bin (71), bit (36) aligned, bit (36), fixed bin (1), fixed bin (18), fixed bin (35));
dcl  hcs_$status_long         entry (char (*), char (*), fixed bin (1), ptr,ptr, fixed bin (35));
dcl  mem_top                  fixed bin(18)unsigned aligned;
dcl  i                        fixed bin(24);
dcl  increment                fixed bin(24);
dcl  ioa_                     entry options (variable);
dcl  k                        fixed bin(24);
dcl  l                        fixed bin(24);
dcl  lbound                   builtin;
dcl  mem_bottom               fixed bin(18)unsigned aligned static int options(constant)init(103);
dcl  max_options              fixed bin static int options(constant)init (100);
dcl  max_resources            fixed bin static int options(constant)init (99999);
dcl  minus_one                bit (36) static int options(constant)init ((36)"1"b);
dcl  mlim                     float bin;
dcl  mod                      builtin;
dcl  modify_mode              bit (36) static int options(constant)init ("200000000000"b3);
dcl  msp                      float bin;
dcl  multics_access_mode      fixed bin(5);
dcl  nic                      fixed bin(24);
dcl  p                        ptr init(null());
dcl  path_name                char (168) varying;
dcl  permission_word          bit(36)aligned based;
dcl  person_id                char (22);
dcl  quota                    fixed bin (18);
dcl  read_mode                bit (36) static int options(constant)init ("400000000000"b3);
dcl  rel                      builtin;
dcl  rings                    (3) fixed bin (3) static int options(constant)init (4, 4, 4);
dcl  shlim                    (0:7) float bin;
dcl  shsp                     (0:7) float bin;
dcl  size                     builtin;
dcl  smc_entry_ptr            ptr init(null());
dcl  sons_lvid                bit (36);
dcl  status                   fixed bin (24);
dcl  status_mode              bit (36) static int options(constant)init ("400000000000"b3);
dcl  status_ptr               ptr init(null());
dcl  substr                   builtin;
dcl  switches                 bit (6) static int options(constant)init ("37"b3);
dcl  tacc_sw                  fixed bin (1);
dcl  trp                      fixed bin (71);
dcl  tup                      bit (36) aligned;
dcl  two_words                bit (72) based;
dcl  type                     fixed bin (2);
dcl  used                     fixed bin (18);
dcl  user_attribute_word      bit (35);
dcl  user_info_$limits        entry (float bin, float bin, fixed bin (71), fixed bin(24), (0:7) float bin, float bin, float bin, (0:7) float bin);
dcl  words380                 bit(13680)aligned based;
dcl  write_mode               bit (36) static int options(constant)init ("100000000000"b3);

dcl 1 status2		aligned based(status_ptr)
,     3 word1
,       4 status_code	bit(12)unal
,       4 zero1		bit(06)unal
,       4 zero2		fixed bin(18)unsigned unal
,     3 word2
,       4 L_bcd_message	bit(18) unal
,       4 message_words	fixed bin(18)unsigned unal
;

dcl 1 bcdname aligned based(p),
    2 first8 bit(48)unal,
    2 last4  bit(24)unal;


dcl 1 status_word aligned based (status_ptr),
    2 status bit(18) unal,
    2 pd fixed bin(18)unsigned unal,
    2 null_bit bit (1) unal,
    2 user_attributes bit (35) unal;

%include gtss_ust_ext_;

%include gtss_ext_;

%include gtss_filact_status;

%include gtss_pnterr;

%include mc;

%include gtss_entry_dcls;

%include gtss_ascii_file_names;

%include gtss_file_values;

%include gtss_FMS_catalog;

%include status_info;

%include gtss_filact_options;

%include gse_ext_;

%include gtss_db_names;

%include gtss_dfd_ext_;

%include gtss_file_attributes;
     end						/* gtss_filact_funct11_ */;
