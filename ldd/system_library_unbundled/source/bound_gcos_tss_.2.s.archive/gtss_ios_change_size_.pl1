/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

gtss_ios_change_size_: proc (file_no, size_change, link_indicator, status, code);

/*
   "			 This entry point is used to grow a permanent file up to  its
   "		maximum  size  limit  and to change the size of a temporary file.
   "		The caller should ensure that this entry point is not  called  to
   "		reduce the size of a permanent file.

   Author: Dave Ward	03/04/80 (derived from gtss_ios_)
*/
dcl  code                     fixed bin (35) parm;
dcl  file_no                  fixed bin (24) parm;
dcl  link_indicator           bit (1) parm;
dcl  size_change              fixed bin (24) parm;
dcl  status                   fixed bin (24) parm;

	fn = file_no;				/* Use local variables */
	sc = size_change;
	li = link_indicator;
	ap = gtss_disk.attributes_ptr (fn);
	fcb_ptr = gtss_disk (fn).fcb_ptr;

	status = 0;				/* Initialize status to indicate no error */
	code = 0;

	if bad_file_status (fn, gsc) then do;
/* FIX ** 	     if gsc = 15 then gcos_status = "4042"b3; */	/* Bad file number. */
/* FIX ** 	     else */					/* => 14. */
/* FIX ** 	     gcos_status = "4025"b3; */			/* File not open. */
status=gsc;
	     return;
	end;

	if li then sc = sc * 12;			/* convert to llinks */

	if sc = 0 then do;				/* Grow file by unspecified amount */
	     if pat_body.perm (fn) then do;		/* Perm file */

/* Grow a perm file by approx. 1/8 of its current size. */
		sc = divide (ap -> current_size, 8, 24, 0)+1;
		if ap -> max_size > 0
		then sc = min (sc, ap -> max_size - ap -> current_size);
	     end;
	     else do;				/* Temp file */
		sc = min (ap -> current_size, 1365*12-ap -> current_size);
	     end;
	     if sc <= 0 then do;
		status = 1;			/* file size is unchanged */
		sc = 0;
	     end;
	end;
	else do;					/* change file size by specified amount */
	     if pat_body.perm (fn) then do;		/* Perm File */
		if ap -> max_size > 0 then do;
		     max_change = ap -> max_size - ap -> current_size;
		     if sc > max_change then do;
			sc = max_change;
			if sc <= 0
			then do;
			     status = 1;		/* file size is unchanged */
			     sc = 0;
			end;
			else status = 2;		/* file size increased by
						   less than requested amount */
		     end;
		end;
	     end;
	     else do;				/* Temp File */

/* Make sure the size change is a multiple of a full link (12 llinks) */
		if mod (sc, 12) ^= 0 then do;
		     if sc > 0 then sc = sc + 12 - mod (sc, 12);
		     else sc = sc - mod (sc, 12);
		end;
	     end;
	end;					/* End of change file size by
						   specified amount. */

/* Change size of file */
	ap -> current_size = ap -> current_size + sc;

/* Do not exceed implementation restriction of 500 components per file */
	largest_file = divide (sys_info$max_seg_size*500, 320, 24, 0); /* max number of llinks */
	if ap -> current_size > largest_file then do;
	     status = 3;				/* implementation restriction of 500 components per file */
	     ap -> current_size = largest_file;
	end;

/* Make sure a temp file is not reduced below one link */
	if ^pat_body.perm (fn) then
	     ap -> current_size = max (12, ap -> current_size);

/* Call an internal procedure to set file_size, no_components, and the msf flag. */
/* Remember if the file was an msf before the size change. */
	msf_save = msf (fn);
	no_components_save = no_components (fn);
	call fix_size (status);

/*   If a temp file is reduced from an msf to
   a single segment, free the array of component pointers. */
	if sc < 0
	then do;
	     if msf_save & ^msf (fn) then do;
		single_segment_ptr (fn) = msf_array_ptr (fn) -> msf_components (0);
		free msf_array_ptr (fn) -> msf_components;
	     end;

/* If the number of components in a file is reduced,
   set the unused pointers to null */
	     else
	     if no_components (fn) < no_components_save then
		do i = no_components (fn) to no_components_save - 1;
		msf_array_ptr (fn) -> msf_components (i) = null ();
	     end;
	end;

/* If the file has just been grown from a single segment to an msf
   then allocate an array of component pointers. */
	if ^msf_save & msf (fn) then do;
	     allocate msf_components in (work_area) set (msf_array_ptr (fn));
	     msf_array_ptr (fn) -> msf_components (0) = single_segment_ptr (fn);
	     single_segment_ptr (fn) = null ();
	end;

/* If the number of components in a file is increased
   then get pointers to the new components */
	if no_components (fn) > no_components_save then
	     do i = no_components_save to no_components (fn) - 1;
	     call msf_manager_$get_ptr (fcb_ptr, (i), "1"b,
		msf_array_ptr (fn) -> msf_components (i), bc, code);
	     if code ^= 0 then do;
		status = 5;			/* error detected by msf_manager_$get_ptr */
		free msf_array_ptr (fn) -> msf_components;
		call msf_manager_$close (gtss_disk (fn).fcb_ptr);
		return;
	     end;
	end;

/* Call msf_manager_$adjust to set the bit count
   and free any excess file space. */
	bit_count = 36 * (mod (file_size (fn)-1, sys_info$max_seg_size)+1);

	on cond (record_quota_overflow) begin;
	     gtss_disk (fn) = gtss_disk (41);		/* Restore file fn to state before change_size. */
	     if db_ios then
		call ioa_ ("gtss_ios_change_size_: record quota overflow.");
	     status = 4;				/* ?? */
	     code = error_table_$rqover;
	     goto fin_change_size;
	end;
	gtss_disk (41) = gtss_disk (fn);		/* Save state of file fn. */

	call msf_manager_$adjust (fcb_ptr, no_components (fn)-1, bit_count, "110"b, code);
	if code ^= 0 & status = 0
	then status = 4;				/* error detected by msf_manager_$adjust */

	if pat_body.perm (fn) then do;		/* Perm File */
	     gtss_file_attributes_ptr = gtss_dfd_ext_$disk_file_data (fn).gtss_disk.attributes_ptr;
	     gtss_file_values.version = 1;
	     gtss_file_values.change_name = "0"b;
	     gtss_file_values.dname = gtss_dfd_ext_$disk_file_data (fn).gtss_disk.dir_name;
	     gtss_file_values.ename = gtss_dfd_ext_$disk_file_data (fn).gtss_disk.entry_name;
	     gtss_file_values.new_ename = " ";		/* Set values for current entry. */
	     string (gtss_file_values.set_switch) = "0"b;
	     gtss_file_values.set_switch.curll = "1"b;
	     gtss_file_values.data_fields.curll = gtss_file_attributes.current_size;

	     call gtss_attributes_mgr_$set (addr (gtss_file_values), code);
	     if code ^= 0 then status = 6;		/* $set failed. */
	end;

fin_change_size: ;

	return;					/* End of gtss_ios_change_size_ */


/* INTERNAL PROCEDURES */

bad_file_status: proc (fn, status) returns (bit (1));

/* This routine is called to verify that
   the file number input parameter corresponds
   to a valid open file.  If so, "0"b is returned.
   Otherwise , "1"b is returned. */

dcl  fn                       fixed bin (24) parm;
dcl  status                   fixed bin (24) parm;

	     if fn < lbound (gtss_disk, 1) | fn >= hbound (gtss_disk, 1) then do;
		status = 15;			/* Bad file number */
		return ("1"b);
	     end;
	     if gtss_disk.fcb_ptr (fn) = null () then do;
		status = 14;			/* File not open  */
		return ("1"b);
	     end;
	     return ("0"b);
	end bad_file_status;

fix_size:	proc (status);

/* This routine is called from within the open and change_size entry points
   to set file_size, no_components, and the msf flag. */
dcl  status                   fixed bin(24)parm;

	     gtss_disk.file_size (fn) = 320 * ap -> gtss_file_attributes.current_size;
	     gtss_disk.no_components (fn) =
		divide (file_size (fn)-1, sys_info$max_seg_size, 24, 0)+1;
	     gtss_disk.msf (fn) = (no_components (fn) > 1);
	     if no_components (fn) > 500 then do;
		no_components (fn) = 500;
		file_size (fn) = 500*1024*255;
		status = 3;			/* Size of file requested exceeds 500 components */
	     end;
	end fix_size;

/* VARIABLES FOR GTSS_IOS_ */
dcl  hcs_$fs_get_path_name    entry(ptr,char(*),fixed bin,char(*),fixed bin(35));
dcl file_dir char(168);
dcl file_dir_len fixed bin;
dcl file_ent char(32);
dcl  gcos_status              bit(12)aligned based(gsp);
dcl  bit72                    bit(72)aligned based;
dcl  gsp                      ptr;

dcl  acl_ptr                  ptr	/* pointer to segment_acl passed
				as a parameter to msf_manager_$acl_list */;
dcl  ap                       ptr	/* Pointer to the attributes structure for the current file */;
dcl  bc                       fixed bin (24)	/* Used as a sink for bit counts returned by
				msf_manager_$get_ptr */;
dcl  bit_count                fixed bin (24)	/* The bit count of the last segment of an msf.
				Passed as a parameter to msf_manager_$adjust. */;
dcl  bksp_sw                  bit (1)	/* Distinguishes between backspacing
				"1"b and forward spacing "0"b a linked file. */;
dcl  cmd_word                 bit (36) aligned	/* temp */;
dcl  count                    fixed bin (24)	/* Counts io commands processed
				for current io select sequence */;
dcl  da_residue               fixed bin	/* Address of last memory location
				accessed during I/O.  Used in building
				return status words for I/O. */;
dcl  data_moved               fixed bin (24)	/* number of words moved or skipped over by
				the current read or write operation. */;
dcl  dcw_number               fixed bin (24)	/* Used to count DCW's in
				the current select sequence */;
dcl  dcw_offset               fixed bin (24)	/* slave offset of current dcw */;
dcl  dcw_ptr                  ptr	/* Multics pointer to the current dcw */;
dcl  disconnect               bit (1)	/* Indicator that the last DCW has been encountered. */;
dcl  error_table_$namedup     fixed bin (35) ext;
dcl  error_table_$rqover      fixed bin(35)ext;
dcl  error_table_$segknown    fixed bin (35) ext;
dcl  fcb_ptr                  ptr init (null ())	/* pointer to file control block
				used by msf_manager_ */;
dcl  file_ptr                 ptr	/* pointer to current positoion in file */;
dcl  fn                       fixed bin (24)	/* Index in gtss_ext_$disk_file_data of
				information about the file to be processed. */;
dcl  get_mode                 fixed bin (5)	/* User's access mode to segment as
				returned by hcs_$fs_get_mode */;
dcl  gsc                      fixed bin(24);
dcl  i                        fixed bin (24);
dcl  idptr                    ptr	/* Pointer to the current id
				word of the I/O select sequence. */;
dcl  j                        fixed bin (24);
dcl  l                        fixed bin (24)	/* The length in words of the current
				piece of data to be moved to or from the file */;
dcl  largest_file             fixed bin (24)	/* Max no. of llinks a file can grow to */;
dcl  last_component           bit (1)	/* When on, indicates that the
				current component is the last component of the file */;
dcl  li                       bit (1)	/* variable for the parameter link_indicator */;
dcl  M                        char (l*4) based	/* A template used for moving data
				to or from the file. */;
dcl  max_change               fixed bin (24)	/* The maximum amount that
				the size of a file can be increased
				expressed in llinks (320 word blocks) */;
dcl  msf_components           (0:499) ptr based	/* An array of pointers for each msf.
				Each component which has been accessed has a
				corresponding initialized pointer. */;

dcl  msf_save                 bit (1)	/* A flag used to remember whether
				the file was a msf before its size
				was changed. */;
dcl  no_components_save       fixed bin (24)	/* Used to remember the number
				of components a file had before its
				size was changed */;
dcl  pat_body_overlay         bit (180) based	/* used for initializing the
				pat body to all zeros */;
dcl  opptr                    ptr	/* Pointer to the current operation
				word of the I/O select sequence. */;
dcl  rec_ct_residue           fixed bin (24)	/* holds no of unskipped records */;
dcl  record_quota_overflow    condition ext;
dcl  sc                       fixed bin (24)	/* local variable for the parameter size_change */;
dcl  scratch_status           bit (72) aligned	/* temp */;
dcl  seek_address             fixed bin (24) based	/* user seek address for disk or drum */;
dcl  seek_ptr                 ptr	/* Pointer to the word containing
				the io seek address */;
dcl  seeksw                   bit (1)	/* sw controlling disk or drum seeks */;
dcl  seg_length               fixed bin (24)	/* length in words of current component of file */;
dcl  select_seg_ptr           ptr	/* Pointer to beginning of
				segment containing select sequence */;
dcl  select_seq_in_memory     bit (1)	/* 1 => the select sequence is in the
				same segment that is used for Gcos memory.
				In this case the addresses of DCW's, seek address data word, and
				status return words will be checked against the
				memory_limit parameter. */;
dcl  slave_status             bit (36) aligned;
dcl  sp                       ptr	/* Pointer to user's select sequence for this I/O */;
dcl  storlimit                fixed bin (24)	/* slave core boundary */;
dcl  sptr                     ptr	/* pointer to return word of select sequence */;
dcl  swptr                    ptr	/* Pointer to status return words */;
dcl  sys_info$max_seg_size    fixed bin (35) ext;
dcl  ta_offset                fixed bin (24)	/* Offset in the user's slave memory
				of the transmission area for the current DCW. */;
dcl  ta_ptr                   ptr	/* pointer to the transmission area for
				the current DCW. */;
dcl  ta_seg_ptr               ptr	/* pointer to the user's slave
				memory segment */;
dcl  tdcw_previous            bit (1)	/* Indicator that the last DCW processed was a TDCW */;
dcl  tfp                      fixed bin (71);
dcl  wc_residue               fixed bin (24)	/* Number of words remaining to be transferred
				in the current dcw when eof or
				memory fault occurs. Used in building
				return status words for I/O. */;
dcl  work_area                area (sys_info$max_seg_size) aligned
		     based (gtss_ext_$work_area_ptr)	/* Area used to store arrays of pointers to
			components of msf's. */;
dcl  delete_$path             entry (char (*), char (*), bit (6), char (*), fixed bin (35));
dcl  hcs_$fs_get_mode         entry (ptr, fixed bin (5), fixed bin (35));
dcl  hcs_$make_seg            entry (char (*), char (*), char (*), fixed bin (5), ptr, fixed bin (35));
dcl  ioa_                     entry options (variable);
dcl  msf_manager_$adjust      entry (ptr, fixed bin, fixed bin (24), bit (3), fixed bin (35));
dcl  msf_manager_$close       entry (ptr);
dcl  msf_manager_$get_ptr     entry (ptr, fixed bin, bit (1), ptr, fixed bin (24), fixed bin (35));
dcl  msf_manager_$open        entry (char (*), char (*), ptr, fixed bin (35));

/* STRUCTURES */

dcl 1 dcw aligned based (dcw_ptr),			/* dcw model */
    2 data_addr bit (18) unaligned,			/* data address */
    2 zero bit (3) unaligned,				/* fill */
    2 action bit (3) unaligned,			/* action */
    2 count bit (12) unaligned;						/* word count for transfer */

dcl 1 id_word aligned based (idptr),			/* model of identification word */
    2 filep bit (18) unaligned,			/* file control block pointer */
    2 dcwp bit (18) unaligned;						/* dcw list pointer */


dcl 1 op_word aligned based (opptr),			/* model of operation word */
    2 dev_com bit (6) unaligned,			/* device command */
    2 zero1 bit (12) unaligned,			/* zeros */
    2 ioc_com bit (5) unaligned,			/* ioc command */
    2 zero2 bit (1) unaligned,			/* zero */
    2 control bit (6) unaligned,			/* control */
    2 count bit (6) unaligned;						/* count */

dcl 1 return_word aligned based (sptr),			/* model of status return word */
    2 status_return bit (18) unaligned,			/* pointer to return words */
    2 courtesy_call bit (18) unaligned;						/* pointer to courtesy call rtn */

dcl 1 stat_words aligned based (swptr),			/* model of status words */
    2 sw1 bit (36) aligned,				/* word 1 */
    2 sw2 bit (36) aligned;						/* word 2 */



dcl  decode_mode              (0:63) bit (3) aligned			/* Permissions are read, execute, write */
     static init (					/* OCTAL */
     "100"b,					/* 0 -- Zero access mode maps to Query permission */
     "100"b,					/* 1 */
    (2) (1)"000"b,					/* 2-3 */
     "110"b,					/* 4 */
    (3) (1)"000"b,					/* 5-7 */
     "100"b,					/* 10 */
    (7) (1)"000"b,					/* 11-17 */
    (2) (1)"101"b,					/* 20-21 */
    (2) (1) "000"b,					/* 22-23 */
     "101"b,					/* 24 */
    (11) (1)"000"b,					/* 25-37 */
    (2) (1) "100"b,					/* 40-41 */
    (6) (1)"000"b,					/* 42-47 */
     "100"b,					/* 50 */
    (3) (1)"000"b,					/* 51-53 */
     "100"b,					/* 54 */
    (3) (1)"000"b,					/* 55-57 */
    (2) (1)"101"b,					/* 60-61 */
    (10) (1)"000"b,					/* 62-73 */
     "101"b,					/* 74 */
    (3) (1)"000"b);						/* 75-77 */

dcl  io_commands              (8) bit (36) internal static aligned init (


/* 	Disk Command Table						 */

     "340000000002"b3,				/* 34 - seek disk address */
     "250000002400"b3,				/* 25 - read disk continuous */
     "310000002400"b3,				/* 31 - write disk continuous */
     "700000020001"b3,				/* 70 - rewind */
     "460000020001"b3,				/* 46 - backspace record(s) */
     "440000020001"b3,				/* 44 - forward space record(s) */
     "400000020001"b3,				/* 40 - reset status */
     "000000020001"b3);						/* 00 - request status */

%include gtss_dfd_ext_;

%include gtss_file_attributes;

%include gtss_ext_;

%include gtss_file_values;

%include gtss_entry_dcls;

%include gtss_db_names;
     end						/* gtss_ios_change_size_ */;
