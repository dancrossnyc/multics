/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


/* format: style4,delnl,insnl,ifthen */
tape_mult_open_:
     procedure;

/*	This program is part of the Multics standard tape I/O module, tape_mult_.
   *
   *	Created on 10/28/74 by Bill Silver.
   *	Modified 4/79 by R.J.C. Kissel to handle 6250 bpi.
   *	Modified 1/12/81 by J. A. Bush for bootable tape labels
   *      Modified 8/82 by S. Krupp for change from tdcm_ to tape_ioi_ interface.
   *	This program implements the iox_$open entry point.
*/


/*			ARGUMENT  DATA	*/

dcl  arg_error_code fixed bin (35),			/* (O) Standard error code. */
     arg_extend_flag bit (1),				/* (I) ON => extend file - illegal for tape_mult_ */
     arg_iocb_ptr ptr,				/* (I) Pointer to I/O control block. */
     arg_open_mode fixed bin;				/* (I) Index that => opening mode. */


/*			AUTOMATIC  DATA	*/

dcl  density fixed bin,				/* Density of tape: 800, 1600, or 6250. */
     error_code fixed bin (35),			/* Standard system error code. */
     iocb_ptr ptr,					/* Pointer to I/O control block. */
     open_mode fixed bin,				/* Index that => opening mode. */
     rdy_status bit(36) aligned,			/* Rdy status returned here. */
     tioi_id bit(36) aligned,				/* Id for this tape_ioi_ activation. */
     volid char (16);				/* Tape reel ID. */


/*		INTERNAL STATIC DATA	*/

dcl  stream_input_mode fixed bin init (1) internal static,
     stream_output_mode fixed bin init (2) internal static;


/*		EXTERNAL  ENTRIES		*/

dcl  (addr, null) builtin;

dcl  (
     error_table_$bad_arg,
     error_table_$bad_density,
     error_table_$bad_mode
     ) external fixed bin (35);

dcl  iox_$err_not_closed entry options (variable),
     iox_$propagate entry (ptr),
     tape_mult_close_$close entry options (variable),
     tape_mult_labeler_$read entry (ptr, char (*), fixed bin (35)),
     tape_mult_labeler_$write entry (ptr, fixed bin (35)),
     tape_mult_read_$get_chars entry options (variable),
     tape_mult_read_$position entry options (variable),
     tape_mult_write_$put_chars entry options (variable);
%page;
%include tmdb;
%page;
%include mstr;
%page;
%include tape_ioi_dcls;
%page;
%include iocb;
%page;
open:
     entry (arg_iocb_ptr, arg_open_mode, arg_extend_flag, arg_error_code);

	iocb_ptr = arg_iocb_ptr;			/* Copy arguments. */
	open_mode = arg_open_mode;

	iocb_ptr = iocb_ptr -> iocb.actual_iocb_ptr;	/* Initialize pointers and work variables. */
	tmdb_ptr = iocb_ptr -> iocb.attach_data_ptr;
	tioi_id = tmdb.work.tioi_id;

	tmdb.open.length = 18;			/* Set up default open mode. */
	tmdb.open.description = "uninitialized_mode";

	if arg_extend_flag				/* tape_mult_ does not support extend. */
	then do;					/* We will not allow it to be specified. */
	     error_code = error_table_$bad_arg;
	     goto OPEN_ERROR;
	end;

	if open_mode = stream_input_mode		/* Are we opening for input or output? */
	then do;					/* Input. */
	     tmdb.open.length = 12;
	     tmdb.open.description = "stream_input";
	     goto SETUP_REEL;
	end;

	if open_mode = stream_output_mode		/* If not input it must be output. */
	then do;
	     if ^tmdb.opt.flags.ring			/* Is write ring in? */
	     then goto BAD_MODE;			/* No, we must abort the open. */
	     tmdb.open.length = 13;
	     tmdb.open.description = "stream_output";
	     goto SETUP_REEL;
	end;

BAD_MODE:						/* We cannot open with this mode. */
	error_code = error_table_$bad_mode;
	goto OPEN_ERROR;

/*	Now we must get the tape reel ready for I/O processing.  We must set
   *	the density and process the tape label.
*/
SETUP_REEL:
	density = tmdb.opt.density;			/* Get user specified density. */

	if open_mode = stream_output_mode		/* Are we writing or reading? */
	then do;					/* Writing. */
	     call REWIND(error_code);			/* rewind before setting density */
	     if error_code ^= 0
	     then goto OPEN_ERROR;
	     call tape_ioi_$order(tioi_id, "den", 1, addr(density), (0), (0), error_code);
	     if error_code ^= 0
	     then goto OPEN_ERROR;
	     tmdb.opt.write_sw = "1"b;
	     call tape_mult_labeler_$write(tmdb_ptr, error_code);
	end;
	else do;					/* Reading. */
	     tmdb.opt.write_sw = "0"b;
	     call OPEN_FOR_READING;			/* There is more to do for reading. */
	end;
	if error_code ^= 0 then
	     goto OPEN_ERROR;

/*	Now we must fill in the iocb.  It will be set up so an error will occur
   *	if any attempt is made to open the switch again or to detach it before
   *	it is closed.  Depending upon the opening mode we will set up the
   *	put_chars or get_chars entry point.
*/
	iocb_ptr -> iocb.open_descrip_ptr = addr (tmdb.open);
	iocb_ptr -> iocb.open = iox_$err_not_closed;
	iocb_ptr -> iocb.close = tape_mult_close_$close;
	iocb_ptr -> iocb.detach_iocb = iox_$err_not_closed;

/*	The I/O operations supported by tape_mult_ depends upon the opening mode.
   *	     stream_input:
   *		get_chars
   *		position
   *	     stream_output:
   *		put_chars
   *		control (error_count order)
*/
	if open_mode = stream_output_mode		/* Set up I/O entry and tseg write switch. */
	then iocb_ptr -> iocb.put_chars = tape_mult_write_$put_chars;
						/* Writing. */
	else do;					/* Reading. */
	     iocb_ptr -> iocb.get_chars = tape_mult_read_$get_chars;
	     iocb_ptr -> iocb.position = tape_mult_read_$position;
	end;

	call iox_$propagate (iocb_ptr);

	arg_error_code = 0;				/* Open completed successfully. */
	return;

OPEN_ERROR:					/* Fatal error during opening. */
	arg_error_code = error_code;
	return;
%page;
OPEN_FOR_READING:
     procedure;

/*	This procedure is called to open the tape for reading.  We will try to
   *	read the tape reel label.  The first density tried will be
   *	the user specified value.  However, if we have trouble reading the tape
   *	label at this density we will try another density.  If it fails at
   *	800 bpi, 1600 bpi, or 6250 bpi, then we will give up.
*/

dcl  user_den_idx fixed bin;
dcl  den_to_try (3, 3) fixed bin internal static options (constant)
	init (800, 1600, 6250, 1600, 800, 6250, 6250, 800, 1600);
dcl  try_idx fixed bin;
dcl  lbound builtin;
dcl  hbound builtin;

/* Get the index of the user specified density. */

	do user_den_idx = lbound (den_to_try, 1) to hbound (den_to_try, 1)
	     while (density ^= den_to_try (user_den_idx, 1));
	end;

/* Try all the densities. */

	do try_idx = lbound (den_to_try, 2) to hbound (den_to_try, 2);
	     call REWIND(error_code);
	     if error_code ^= 0 then
		return;				/* Nothing to be done about this. */

	     density = den_to_try (user_den_idx, try_idx);/* What the user said the first time. */

	     call tape_ioi_$order(tioi_id, "den", 1, addr(density), (0), (0), error_code);
	     if error_code = 0 then do;
		call tape_mult_labeler_$read (tmdb_ptr, volid, error_code);
		if error_code ^= error_table_$bad_density
		then return;			/* SUCCESS, or a bad error. */
	     end;
	end;

     end OPEN_FOR_READING;
%page;
REWIND: proc(code);

/*        This procedure signals the tape rewind order and the waits until
   *      the tape is actually rewound (device is ready for processing).
*/

          dcl code fixed bin(35);

	call tape_ioi_$order(tioi_id, "rdy", 1, addr(rdy_status), (0), (0), code);
						/* Wait til device is ready. */
	if code ^= 0
	then return;
          call tape_ioi_$order(tioi_id, "rew", 1, (null), (0), (0), code);
						/* Rewind tape. */
	if code ^= 0
	then return;
	call tape_ioi_$order(tioi_id, "rdy", 1, addr(rdy_status), (0), (0), code);
						/* Wait til rewound. */

     end REWIND;

     end tape_mult_open_;
