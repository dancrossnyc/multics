/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */
/* PARSE_TAPE_REEL_NAME_ - Produce IOX Attach Description from Tape Reel Name
   coded 9/16/77 by Noel I. Morris
   modified 4/79 by R.J.C. Kissel to recognize 6250 bpi.
   modified 2/80 by Michael R. Jordan to lengthen the descrip variable.
*/

/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */



parse_tape_reel_name_:
     proc (reelname, attach_descrip);

/* Private version for backup handles long comment strings */
/* S. Herbst 05/15/81 */

dcl  reelname char (*),				/* tape reel name */
     attach_descrip char (*);				/* IOX attach description */

dcl (comment, descrip) char (256) var,
    (i, j) fixed bin,
     atom char (32) var;

dcl  requote_string_ entry (char (*)) returns (char (*));

dcl (index, length, rtrim, substr) builtin;


	comment, descrip = "";

	i = index (reelname, ",");
	if i = 0
	then descrip = reelname;
	else
	do;
	     descrip = substr (reelname, 1, i - 1);
	     do while (i ^= 0);
		j = index (substr (reelname, i + 1), ",");
		if j = 0
		then atom = substr (reelname, i + 1);
		else
		do;
		     j = j + i;
		     atom = substr (reelname, i + 1, j - i - 1);
		end;

		if atom = "7track"
		then descrip = descrip || " -tk 7";
		else if atom = "9track"
		then descrip = descrip || " -tk 9";
		else if index (atom, "=800") ^= 0
		then descrip = descrip || " -den 800";
		else if index (atom, "=1600") ^= 0
		then descrip = descrip || " -den 1600";
		else if index (atom, "=6250") ^= 0
		then descrip = descrip || " -den 6250";
		else if atom = "800"
		then descrip = descrip || " -den 800";
		else if atom = "1600"
		then descrip = descrip || " -den 1600";
		else if atom = "6250"
		then descrip = descrip || " -den 6250";
		else if atom = "sys"
		then descrip = descrip || " -sys";

		else comment = comment || atom || " ";
		i = j;
	     end;
	end;

	if comment ^= "" then do;
	     comment = rtrim (comment);
	     descrip = descrip || " -com ";
	     if length (descrip) + length (comment) <= length (attach_descrip) then /* fits in caller's arg */
		descrip = descrip || requote_string_ ((comment));
	     else descrip = descrip || requote_string_ (substr (comment, 1, length (attach_descrip) - length (descrip)));
	end;

	attach_descrip = descrip;

	return;


     end;
