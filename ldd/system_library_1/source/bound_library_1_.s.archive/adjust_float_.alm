" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Bull Inc., 1987                *
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1983 *
" *                                                         *
" ***********************************************************
	bool	digit_0,060
"	this code is used to adjust a float decimal
"	number preparatory to the edit step.  the float decimal number is
"	normalized to contain no leading zeros and the PL/I exponent is
"	returned as a decimal number.  The PL/I exponent is
"		original_exponent  - number leading zeros + q - sf
"	where q is scale (number of digits after v in picture),
"	and sf is picture scale factor.
"		call adjust_float_(pt,pp,exp)
"	where 	pt ptr		points at number
"		pp ptr		points at picture_image block
"		exp fixed dec(3)	is set to exponent to use in edit
"
"	this code runs in the stack frame of its caller.
"
	segdef	adjust_float_
"
	equ	prec,56
	equ	scale,57
	equ	t,58
	equ	t1,59
"
adjust_float_:
	epp2	ap|2,*		get ptr to float dec number
	epp2	2|0,*
	epp3	ap|6,*		get ptr to exponent
	epp4	ap|4,*		get ptr to picture info block
	epp4	4|0,*
	ldq	4|0		get packed prec & scale
	lls	18
	ana	=o777,dl		get precision
	eax7	0,al
	tct	(pr,rl)		count leading zeros
	desc9a	2|0(1),x7
	arg	zero_table-12
	arg	sp|t
	ttn	float_zero	skip if all zero
	sta	sp|prec		save precision
	lls	9		isolate scale
	ana	=o777,dl
	sta	sp|scale		and save
	ldq	=o177,dl		isolate number leading digits
	ansq	sp|t
	mlr	(pr,x7),(pr)	extract exponent
	desc9a	2|0(1),1
	desc9a	sp|t1,1
	ldq	sp|t1		get exponent
	qls	1
	qrs	28
	lxl6	sp|t		get number leading zeros
	tze	l1		skip if none
	lda	sp|prec		get back precision
	sba	sp|t		compute number of digits to move
	mlr	(pr,rl,x6),(pr,rl),fill(digit_0)	move digits over to normalize
	desc9a	2|0(1),al
	desc9a	2|0(1),x7
	sbq	sp|t		correct exponent
l1:	asq	sp|scale		set scale = exponent
	lda	4|1		get picture scale factor
	als	9
	ars	27
	neg	0
	asa	sp|scale		get final exponent
	ldq	4|1		get length of exponent field
	lls	27
	ana	=o777,dl
	ada	1,dl
	btd	(pr),(pr,rl)
	desc9a	sp|scale,4
	desc9ls	3|0,al
	short_return
"
float_zero:
	mlr	(0),(pr)		move zero into exponent
	desc9a	zero,4
	desc9a	3|0,4
	short_return
"
zero:	aci	"+000"
zero_table:
	oct	000001002003,004005006007,011012000000
	end
