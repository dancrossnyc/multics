" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Bull Inc., 1987                *
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

" FV_IV_TEMPLATE - Catch Unexpected Faults when Starting a Processor.
"	coded 2/76 by S. H. Webber
"	last modified 4/7/76 by Noel I. Morris
"	Converted to ALM macros (this was the last mexp program
"	  in all of hardcore!), 04/20/80 W. Olin Sibert


	name	fv_iv_template

	segdef	fv_iv_template
	segdef	rsw_data


"	This 2K segment is placed at the base of every system controller.
"	It will catch attempts to start up a processor with incorrect
"	address assignment switch settings.
"



	macro	fv_iv
	dup	992		"992=32*(15+16)
	tra	start-fv_iv_template
	nop
	dupend
	&end


" 

	inhibit	on	<+><+><+><+><+><+><+><+><+><+><+><+>

fv_iv_template:
	fv_iv

start:	eax0	3
	rsw	1,0
	sta	rsw_data-fv_iv_template,0
	eax0	-1,0
	tpl	-3,ic

	inhibit	on	<+><+><+><+><+><+><+><+><+><+><+><+>
	dis	*
	inhibit	off	<-><-><-><-><-><-><-><-><-><-><-><->
	tra	-1,ic

	org	2048-32
rsw_data:	bss	,32

	end
