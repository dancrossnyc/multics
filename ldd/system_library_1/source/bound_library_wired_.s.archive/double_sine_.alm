" ******************************************
" *                                        *
" * Copyright, (C) Honeywell Limited, 1985 *
" *                                        *
" ******************************************
	name	double_sine_
" Modification history:
"	Written by H. Hoover, M. Mabey, and B. Wong, April 1985,
"	based on the GCOS routine '7nat'.
"
" Function:  Approximate to double precision the sine or cosine of an
"	angle given in degrees or radians.
"
" Entry:	through the appropriately named entry point with:
" 	EAQ = the angle whose sine or cosine is desired.
"	PR2 = the address of a 14 word, even-word aligned scratch area.
"	      4 words are used in this program and 14 are used by the
"	      routine "double_principal_angle_".  The storage for
"	      double_sine_ and double_principal_angle_ overlap.
"	PR3 = the return address.
"
" Exit:	EAQ = the desired sine or cosine.
"
" Uses:	X0, X1, X2.
"	X0 = saves a return address from double_principal_angle_ routines
"	X1 = shift (returned by double_principal_angle_ routines)
"	X2 = indicates BFP or HFP mode - all of the floating point math
"	      math routines use this register for the same thing.
"	The double_principal_angle_ routines use registers X1 and X2.


	segref	math_constants_,half_pi,one_degree,pi
	segref	double_principal_angle_,double_principal_radians_,double_principal_degrees_

	equ	BFP,0
	equ	HFP,2
	equ	x,0
	equ	xx,2

	segdef	double_cosine_degrees_,hfp_double_cosine_degrees_
	segdef	double_cosine_radians_,hfp_double_cosine_radians_
	segdef	double_sine_degrees_,hfp_double_sine_degrees_
	segdef	double_sine_radians_,hfp_double_sine_radians_


hfp_double_cosine_degrees_:
	eax2	HFP		" 2 word offset for HFP constants
	tra	double_cosine_degrees

double_cosine_degrees_:
	eax2	BFP		" no offset for BFP constants

double_cosine_degrees:
	fad	=0.0,du		" normalize input
	dfcmg	one_eighty,x2	" if abs_angle <= 180:
	tmi	case1_degrees	" then no angle reduction is necessary
	tsx0	double_principal_degrees_
	tra	case_degrees+1,x1	" select appropriate case


hfp_double_cosine_radians_:
	eax2	HFP		" 2 word offset for HFP constants
	tra	double_cosine_radians

double_cosine_radians_:
	eax2	BFP		" no offset for BFP constants

double_cosine_radians:
	fad	=0.0,du		" normalize input and set indicators
	dfcmg	pi,x2		" if abs (angle) <= pi
	tmi	case1_radians	" then no angle reduction is necessary
	tsx0	double_principal_radians_
	tra	case_radians+1,x1	" select appropriate case
				

hfp_double_sine_degrees_:
	eax2	HFP		" 2 word offset for HFP constants
	tra	double_sine_degrees

double_sine_degrees_:
	eax2	BFP		" no offset for BFP constants

double_sine_degrees:
	fad	=0.0,du		" normalize input
	dfcmg	ninety,x2		" if abs (angle) < pi/2
	tmi	case0_degrees	" then no angle reduction is necessary
	tsx0	double_principal_degrees_
	tra	case_degrees,x1	" select appropriate case


hfp_double_sine_radians_:
	eax2	HFP		" 2 word offset for HFP constants
	tra	double_sine_radians

double_sine_radians_:
	eax2	BFP		" no offset for BFP constants

double_sine_radians:
	fad	=0.0,du		" normalize input
	dfcmg	half_pi,x2	" if abs (angle) <= pi/2
	tmoz	case0_radians	" then no angle reduction is necessary
	tsx0	double_principal_radians_
	tra	case_radians,x1	" Case select appropriate case_radians

case_radians:
	tra	case0_radians
	tra	case1_radians
	tra	case2_radians
	tra	case3_radians
	tra	case0_radians

case1_radians:
	fad	=0.0,du		" set indicators
	tmi	2,ic		" EAQ = - abs (EAQ)
	  negl	0		" fneg underflows at o400400000000

	dfad	half_pi1,x2
	dfad	half_pi2,x2
	tra	part_sine_radians

case2_radians:
	fneg	0
	tra	part_sine_radians

case3_radians:
	fad	=0.0,du		" set indicators
	tpl	2,ic		" EAQ = abs (EAQ)
	  fneg	0

	dfsb	half_pi1,x2
	dfsb	half_pi2,x2
	tra	part_sine_radians

case_degrees:
	tra	case0_degrees
	tra	case1_degrees
	tra	case2_degrees
	tra	case3_degrees
	tra	case0_degrees

case1_degrees:
	fad	=0.0,du		" set indicators
	tmi	2,ic		" EAQ = - abs (EAQ)
	  negl	0		" fneg underflows at o400400000000

	fad	ninety,x2
	tra	part_sine_degrees

case2_degrees:
	fneg	0
	tra	part_sine_degrees

case3_degrees:
	fad	=0.0,du		" set indicators
	tpl	2,ic		" EAQ = abs (EAQ)
	fneg

	fsb	ninety,x2
"	tra	part_sine_degrees

case0_degrees:			" case0_degrees is just part_sine_degrees

part_sine_degrees:
	dfcmg	eps2,x2		" if conversion to radians underflows
	tpl	2,ic
	  fld	=0.0,du		" then use zero
	dfmp	one_degree,x2	" convert to radians.
"	tra	part_sine_radians

case0_radians:			" case0_radians is just part_sine_radians


" Procedure part_sine (x) calculates 'sin(x)' for 'x' in the range
" [-pi/2, pi/2] given 'x' in the EAQ.

part_sine_radians:
	dfrd	0
	dfcmg	eps3,x2		"if abs (x) < 5e-10:
	tmi	pr3|0		"    sine is x for small x
	dfst	pr2|x
	dfmp	pr2|x		" calculate xx = x*x
	dfstr	pr2|xx
	dfmp	p9,x2		" calculate p(xx)
	dfad	p8,x2
	dfmp	pr2|xx
	dfad	p7,x2
	dfmp	pr2|xx
	dfad	p6,x2
	dfmp	pr2|xx
	dfad	p5,x2
	dfmp	pr2|xx
	dfad	p4,x2
	dfmp	pr2|xx
	dfad	p3,x2
	dfmp	pr2|xx
	dfad	p2,x2
	dfmp	pr2|xx
	dfad	p1,x2
	dfmp	pr2|xx
	fad	p0,x2
	dfmp	pr2|x		" return x*p(xx)
	dfrd	0
	tra	pr3|0


" Constants:

	even
eps1:	dec	1.886591d-8
	oct	764242035115,000000000000
eps2:	dec	8.418858142948452884d-38
	oct	402162456701,514360373670	" 2.670821537926801391d-154
eps3:	dec	5.0d-10
	oct	762104560276,404665512263
half_pi1:	oct	002622077325,042055060432	" 1.570796326794896619d0
	oct	002062207732,504205506043	" 1.570796326794896619d0
half_pi2:	oct	602611431424,270033407150	" 8.333742918520878328d-20
	oct	742461143142,427003340714	" 5.170182981794105568d-19
ninety:	dec	90.0d0
	oct	004264000000,000000000000
one_eighty:
	dec	180.0d0
	oct	004550000000,000000000000
p0:	dec	 9.9999999999999999998d-1	" this rounds to 1.0d0
	oct	002040000000,000000000000
p1:	dec	-1.6666666666666666664d-1
	oct	001652525252,525252525253
p2:	dec	 8.333333333333332952d-3
	oct	776104210421,042104210331
p3:	dec	-1.9841269841269648946d-4
	oct	773137713771,377140131713
p4:	dec	 2.7557319223936401884d-6
	oct	770134357072,252646307133
p5:	dec	-2.5052108378101760587d-8
	oct	765450633523,013112232534
p6:	dec	 1.60590431721336921d-10
	oct	760541110601,052315030325
p7:	dec	-7.647126379076958d-13
	oct	755121402455,333370604367
p8:	dec	 2.8101852815318d-15
	oct	750624773046,213725310300
p9:	dec	-7.9798971356d-18
	oct	745331460002,411662206514

	end
