" ******************************************
" *                                        *
" * Copyright, (C) Honeywell Limited, 1983 *
" *                                        *
" ******************************************

"	Function:	Convert a Binary Floating Point number to the
"		nearest Hexadecimal Floating Point number.
"
"	Entry:	EAQ = BFP number to convert.
"		PR2 = address of 2 word work area.
"		PR3 = return address.
"
"	Exit:	EAQ = HFP equivalent of original BFP number.
"		X1 = number of bits of precision lost (0 to 3).

"	Written 20 Dec 83 by HH.

	segdef	bfp_to_hfp_

	equ	accum,0
	equ	exponent,1

bfp_to_hfp_:
	sta	pr2|accum		save A
	ste	pr2|exponent	store exponent
	lda	pr2|exponent	A := 8/exponent,28/0
	ars	2		A := 10/exponent,26/0
	ada	=o001400,du	A := 10/exponent+3,26/0
	sta	pr2|exponent
	lde	pr2|exponent	E := floor((exponent+3)/4)
	ars	26		A := exponent+3
	ana	3,dl		A := mod(exponent+3, 4)
	neg	0		A := -mod(exponent+3, 4)
	eax1	3,al		X1 := 3 - mod(exponent+3, 4)
	lda	pr2|accum		restore A
	lrs	0,x1		normalize (discards 0 to 3 bits)
	tnz	return		done if mantissa is nonzero
	fld	=0.0		load "normalized" floating zero

return:
	tra	pr3|0		return

	end
