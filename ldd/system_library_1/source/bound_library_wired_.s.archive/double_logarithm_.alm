" ******************************************
" *                                        *
" * Copyright, (C) Honeywell Limited, 1985 *
" *                                        *
" ******************************************
	name	double_logarithm_
" Modification history:
"	Written by H. Hoover, M. Mabey, and B. Wong, April 1985,
"	based on GCOS routine '7nar'.
"
" Function:  Calculates the logarithm functions log_base_e(x), log_base_2(x),
"	and log_base_10(x) to double precision accuracy in either BFP or
"	HFP mode.
"
" Entry:	through the appropriately named entry point with:
" 	EAQ = the argument x.
"	PR2 = the address of a 16 word, even-word aligned scratch area.
"	PR3 = the return address.
"
" Exit:	EAQ = the desired logarithm
"
" Uses:	X0, X1, X2, X3
"	X0 = saves a return address from call_math_error_
"	     or saves a return address from log2
"	X1 = saves a return address from part_log2_of_ratio
"	X2 = indicates BFP or HFP mode - all the floating point math
"	     routines use this register for the same purpose.
"	X3 = address of second argument for part_log2_of_ratio

	segref	math_constants_,hfp_log_10_of_2,hfp_log_e_of_2,log_10_of_2,log_e_of_2,max_value

	equ	BFP,0
	equ	HFP,2
	equ	xe,0
	equ	xm,2
	equ	bias,4
	equ	shift,6
	equ	x_plus_y,8
	equ	z,10
	equ	zz,12
	equ	zp,14

	segdef	double_log_base_10_,hfp_double_log_base_10_
	segdef	double_log_base_2_,hfp_double_log_base_2_
	segdef	double_log_base_e_,hfp_double_log_base_e_


double_log_base_10_:
	tsx0	log2		" calculate log2 (x)
	dfmp	log_10_of_2	" EAQ := log_10_of_2 * log2 (x)
	dfrd	0
	tra	pr3|0		" return to caller

double_log_base_2_:
	tsx0	log2		" calculate log2 (x)
	dfrd	0
	tra	pr3|0		" return to caller

double_log_base_e_:
	tsx0	log2		" calculate log2 (x)
	dfmp	log_e_of_2	" EAQ := log_e_of_2 * log2 (x)
	dfrd	0
	tra	pr3|0		" return to caller

hfp_double_log_base_10_:
	tsx0	hfp_log2		" calculate log2 (x)
	dfmp	hfp_log_10_of_2	" EAQ := hfp_log_10_of_2 * log2 (x)
	dfrd	0
	tra	pr3|0		" return to caller

hfp_double_log_base_2_:
	tsx0	hfp_log2		" calculate log2 (x)
	dfrd	0
	tra	pr3|0		" return to caller

hfp_double_log_base_e_:
	tsx0	hfp_log2		" calculate log2 (x)
	dfmp	hfp_log_e_of_2	" EAQ := hfp_log_e_of_2 * log2 (x)
	dfrd	0
	tra	pr3|0		" return to caller

log_of_negative:
	ldq	21,dl
	tsx0	<call_math_error_>|[call_math_error_]
	dfld	max_value
	fneg	0
	tra	pr3|0

log_of_zero:
	ldq	20,dl
	tsx0	<call_math_error_>|[call_math_error_]
	dfld	max_value
	fneg	0
	tra	pr3|0

log2:
	eax2	BFP		" no offset for BFP constants
	fad	=0.0,du		" normalize input and set indicators
	tmi	log_of_negative
	tze	log_of_zero

	dfcmp	square_root_two	" check for x in the range [.707,1.414]
	tpl	6,ic
	  dfcmp	square_root_half
	  tmi	4,ic		" if square_root_half >= x & x <= square_root_two
	    eax3	one		"   X3 := addr (1.0)
	    eax1	0,x0		"   copy return address
	    tra	part_log2_of_ratio	"   result = part_log2_of_ratio (x, 1)
				" else
	ste	pr2|xe		"   store addr (x) -> expon in xe 
	lde	=0,du		"   addr (xm) -> expon = 0
	dfst	pr2|xm
	lda	pr2|xe		"   A := 8/xe,10/0,18/garbage
	lrs	72-18		"   AQ := 62/xe,10/0
	lde	=61b25,du		"   EAQ := unnormalized float(xe)
	fsb	=0.5,du		"   EAQ := float(xe) - 0.5
	fst	pr2|bias
	dfld	pr2|xm
	eax3	square_root_half	"   X3 := addr (square_root_half)
	tsx1	part_log2_of_ratio	"   EAQ := part_log2_of_ratio (x, square_root_half)
          fad       pr2|bias            "   EAQ := part_log2_of_ratio (x, square_root_half) + bias  (= log2(x))
	tra	0,x0		"   return result


hfp_log2:
	eax2	HFP		" 2 word offset for HFP constants
	fad	=0.0,du		" normalize input and set indicators
	tmi	log_of_negative
	tze	log_of_zero

	dfcmp	hfp_square_root_two	" check for x in the range [.707,1.414]
	tpl	6,ic
	  dfcmp	hfp_square_root_half
	  tmi	4,ic		" if square_root_half >= x & x <= square_root_two
	    eax3	hfp_one		"   X3 := addr (1.0)
	    eax1	0,x0		"   copy return address
	    tra	part_log2_of_ratio
				"   result = part_log2_of_ratio (x, 1)
				" else
	ste	pr2|xe		"   store addr (x) -> expon in xe
	lde	=0,du		"   addr (xm) -> expon = 0
				"   EAQ := xm
	stz	pr2|shift		"   shift := 0

	even
do_while:				"   do while (xm < 0.5)
	dfcmp	=0.5d0
	tpl	end_do_while
	lls	1		"      xm = 2*xm
          aos	pr2|shift		"      shift := shift + 1
	tra	do_while		"   end do_while
end_do_while:

	dfst	pr2|xm
	lda	pr2|xe		"   A := 8/xe,10/0,18/garbage
	lrs	36-10		"   AQ := 36/4*xe,8/0,28/garbage
	sba	pr2|shift		"   AQ := 36/4*xe-shift,8/0,28/garbage
	lrs	29		"   AQ := 65/4*xe-shift,7/0
	lde	=16b25,du		"   EAQ := unnormalized float(4*xe-shift)
	fsb	=0.5,du		"   EAQ := float(4*xe-shift)-0.5
	fst	pr2|bias
	dfld	pr2|xm
	eax3	hfp_square_root_half
				"   X3 := addr (square_root_half)
	tsx1	part_log2_of_ratio
				"   EAQ := part_log2_of_ratio (x, square_root_half)
	fad	pr2|bias		"   EAQ := part_log2_of_ratio (x, square_root_half) + bias
	tra	0,x0		"   return result


" part_log2_of_ratio (x, y) calculates log2(x/y), where x/y is in the
" range [0.5*2**0.5, 2**0.5], given x in the EAQ and the address of y in X3.

part_log2_of_ratio:

	dfad	0,x3		" EAQ := x + y
	dfst	pr2|x_plus_y
	dfsb	0,x3		" EAQ := x
	dfsb	0,x3		" EAQ := x - y
	dfdv	pr2|x_plus_y	" calculate z = (x - y) / (x + y)
	fcmg	eps,x2
	tpnz	4,ic		" if abs(z) < 1.27420168d-11
	  dfmp	p0,x2		"   EAQ = z * p0
	  dfdv	q0,x2		"   EAQ = z * p0 / q0
	  tra	0,x1		"   return to caller
	dfstr	pr2|z
	dfmp	pr2|z		" calculate zz = z*z
	dfstr	pr2|zz		" calculate p(zz)
	dfmp	p3,x2
	dfad	p2,x2
	dfmp	pr2|zz
	dfad	p1,x2
	dfmp	pr2|zz
	dfad	p0,x2
	dfmp	pr2|z		" calculate z*p(zz)
	dfstr	pr2|zp
	dfld	pr2|zz		" calculate q(zz)
	dfad	q3,x2
	dfmp	pr2|zz
	dfad	q2,x2
	dfmp	pr2|zz
	dfad	q1,x2
	dfmp	pr2|zz
	dfad	q0,x2
	dfdi	pr2|zp		" calculate z*p(zz)/q(zz)

	tra	0,x1		" return to caller

	even
eps:	dec	1.27420168d-11
	oct	756700243611,000000000000
one:	dec	1.0d0
hfp_one:	oct	002040000000,000000000000
p0:	dec	.51390458864923992069d03
	oct	006100171711,437121505724
p1:	dec	-.79210250577344319906d03
	oct	007634771341,056376644076
p2:	dec	.34070763364903118663d03
	oct	006052455223,572450215316
p3:	dec	-.35419160305337449948d02
	oct	005671122617,220231325351
q0:	dec	.17810575834951956203d03
	oct	004544154227,652616712022
q1:	dec	-.33389039541217149928d03
	oct	007726207007,413660334102
q2:	dec	.19375591463035879517d03
	oct	004603406034,760376537401
q3:	dec	-.35526251110400238735d02
	oct	005670745074,667153071771
square_root_half:
	dec	7.071067811865475244008d-01
hfp_square_root_half:
	oct	000552023631,477473631102
square_root_two:
	dec	1.414213562373095048801d+00
hfp_square_root_two:
	oct	002055202363,147747363110

	end
