" ******************************************
" *                                        *
" * Copyright, (C) Honeywell Limited, 1985 *
" *                                        *
" ******************************************
	name	square_root_
" Modification history:
"	Written by H. Hoover, M. Mabey, and B. Wong, April 1985,
"	based on the GCOS routine '7nbb'.
"
" Function:  Approximate to single precision the square root of a number.
"
" Entry:	through the appropriately named entry point with:
" 	EAQ = the number whose square root is desired.
"	PR2 = the address of an 8 word, even-word aligned scratch area.
"	PR3 = the return address.
"
" Exit:	EAQ = the desired square root.
"
" Uses:	X0, X1
"	X0 = temporary storage for exponent of input argument
"	     and saves a return address from call_math_error_
"	X1 = index to scale table

	equ	BFP,0
	equ	HFP,2
	equ	root_m,0
	equ	x,2
	equ	m,4
	equ	e,6

	bool	P0.25H,000200	" yields HFP +0.25 under 'du' modification
	bool	P4.0H,002200	" yields HFP +4.0 under 'du' modification

	segdef	square_root_,hfp_square_root_


square_root_:
	fad	=0.0,du		" normalize input arg
	tze	pr3|0		" if x = 0 return (0)
	tpl	calc_square_root	" if x < 0:
	  fneg	0		"   x = -x
	  fst	pr2|x
	  ldq	13,dl
	  tsx0	<call_math_error_>|[call_math_error_]
	  fld	pr2|x		"   calculate sqrt (abs(x))

calc_square_root:
	fst	pr2|x		" store EA := input arg
	ldx0	pr2|x		" X0 := addr (x) -> expon
				" m = x
	lde	=0b25,du		" addr (m) -> expon = 0
	canx0	=1b25,du		" calculate mod (e, 2)
	tze	2,ic		" if mod (e, 2) = 1:
	  lde	=-1b25,du		"   EA := m = .5*m
	ldq	pr2|x		" Q := 8/expon,28/garbage
	qrs	28		" Q := 28/0,8/expon
	adq	=1,dl		" calculate e+1
	qrs	1		" calculate divide (e+1, 2, 7)
	qls	28		" position result in exponent field
	stq	pr2|e		" store Q := e = divide (e+1, 2, 7)
	ldq	=0		" clear Q
	dfst	pr2|m		" store EAQ := m
	fmp	p2		" calculate root_m = p(m)
	fad	p1
	fmp	pr2|m
	fad	p0

	fst	pr2|root_m
	fdi	pr2|m		" calculate root_m = .5 * (root_m + m/root_m)
	fad	pr2|root_m
	fmp	=0.5,du

	dfst	pr2|root_m	" calculate root_m + float (m, 63)/root_m
	dfdi	pr2|m
	dfad	pr2|root_m
	ade	=-1b25,du		" root_m = .5 * (root_m + float (m, 63)/root_m)
				" root_x = root_m
	ade	pr2|e		" calculate addr (root_x) -> expon =
				"   addr (root_x) -> expon + divide (e+1, 2, 7)
	frd	0
	tra	pr3|0		" return (root_x)

hfp_square_root_:
	fad	=0.0,du		" normalize input arg
	tze	pr3|0		" if x = 0 return (0)
	tpl	hfp_calc_square_root
				" if x < 0:
	  fneg	0		"   x = -x
	  fst	pr2|x
	  ldq	13,dl
	  tsx0	<call_math_error_>|[call_math_error_]
	  fld	pr2|x		"   calculate sqrt (abs(x))

hfp_calc_square_root:
	fst	pr2|x		" store EA := input arg
	ldx0	pr2|x		" X0 := addr (x) -> expon
				" m = x
	lde	=0b25,du		" addr (m) -> expon = 0
	eax1	0		" scale = 0.5
	fcmp	P0.25H,du
	tpl	3,ic		" if m >= .25:  scale = 0.5
	  eax1	2		" else:         scale = 0.25
	  fmp	P4.0H,du		"               EA := m = 4*m
	canx0	=1b25,du		" calculate mod (e, 2)
	tze	2,ic		" if mod (e, 2) = 1:
	  adx1	=1,du		"   scale = 0.25*scale
	ldq	pr2|x		" Q := 8/expon,28/garbage
	qrs	28		" Q := 28/0,8/expon
	adq	=1,dl		" calculate e+1
	qrs	1		" calculate divide (e+1, 2, 7)
	qls	28		" position result in exponent field
	stq	pr2|e		" store Q := e = divide (e+1, 2, 7)
	ldq	=0		" clear Q
	dfst	pr2|m		" store EAQ := m
	fmp	hfp_p2		" calculate root_m = p(m)
	fad	hfp_p1
	fmp	pr2|m
	fad	hfp_p0

	fst	pr2|root_m
	fdi	pr2|m		" calculate root_m = .5 * (root_m + m/root_m)
	fad	pr2|root_m
	fmp	=0.5,du

	dfst	pr2|root_m	" calculate root_m + float (m, 63)/root_m
	dfdi	pr2|m
	dfad	pr2|root_m
	fmp	scale,x1		" root_m = scale * (root_m + float (m, 63)/root_m)
				" root_x = root_m
	ade	pr2|e		" calculate addr (root_x) -> expon =
				"   addr (root_x) -> expon + divide (e+1, 2, 7)
	frd	0
	tra	pr3|0		" return (root_x)

	even
p0:	dec	2.5927688d-1
hfp_p0:	oct	000204577702,000000000000
p1:	dec	1.0521212d0
hfp_p1:	oct	002041525750,000000000000
p2:	dec	-3.1632214d-1
hfp_p2:	oct	001536026031,000000000000
scale:	oct	000400000000	" 0.5
	oct	000100000000	" 0.25*0.5 = 0.125
	oct	000200000000	" 0.25
	oct	000040000000	" 0.25*0.25 = 0.0625

	end
