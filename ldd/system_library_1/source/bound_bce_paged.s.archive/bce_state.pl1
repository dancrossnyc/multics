/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1984 *
        *                                                         *
        *********************************************************** */
bce_state: proc (P_ss_info_ptr);

/* Return state (collection_1_phase) for bce.  Keith Loepere, May 1984. */

/* format: style4,indattr,ifthenstmt,ifthen,idind33,^indcomtxt */

dcl  P_ss_info_ptr		        ptr parameter;
dcl  active_fnc_err_	        entry options (variable);
dcl  active_function	        bit (1) aligned;	/* as opposed to command usage */
dcl  af_return		        char (af_return_len) based (af_return_ptr) var; /* return for active function */
dcl  af_return_len		        fixed bin (21);
dcl  af_return_ptr		        ptr;
dcl  arg_count		        fixed bin;
dcl  code			        fixed bin (35);
dcl  com_err_		        entry options (variable);
dcl  cu_$af_return_arg_rel	        entry (fixed bin, ptr, fixed bin (21), fixed bin (35), ptr);
dcl  error		        variable entry options (variable);
dcl  error_table_$not_act_fnc	        fixed bin (35) ext;
dcl  ioa_			        entry options (variable);
dcl  me			        char (9) init ("bce_state") static options (constant);/* program name */

	ss_info_ptr = P_ss_info_ptr;
	arg_list_ptr = ss_info.arg_list_ptr;
	call cu_$af_return_arg_rel (arg_count, af_return_ptr, af_return_len, code, arg_list_ptr);
	active_function = (code = 0);
	if active_function then error = active_fnc_err_;
	else error = com_err_;
	if arg_count ^= 0 then do;
	     call error (0, me, "Usage is: bce_state");
	     return;
	end;

	if active_function then af_return = rtrim (COLLECTION_1_PHASE_NAMES (
sys_info$collection_1_phase));
	else call ioa_ ("^a", COLLECTION_1_PHASE_NAMES (sys_info$collection_1_phase));
	return;
%page; %include bce_subsystem_info_;
%page; %include collection_1_phases;
     end;
