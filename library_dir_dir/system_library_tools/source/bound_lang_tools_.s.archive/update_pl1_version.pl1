/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


/* A tool to update pl1_version information in bound_pl1_ directly */

update_pl1_version:
	procedure;

/* Written:  12 Dec 1979 by Peter Krupp */

/* external entries */

dcl  ioa_ entry options(variable);
dcl  cu_$arg_ptr entry (fixed bin, ptr, fixed bin, fixed bin (35));
dcl  cu_$arg_count entry() returns (fixed bin);
dcl  expand_pathname_ entry (char (*), char (*), char (*), fixed bin (35));
dcl  com_err_ entry options (variable);
dcl  com_err_$suppress_name entry options(variable);
dcl  hcs_$initiate entry (char (*), char (*), char (*), fixed bin (1), fixed bin (2), ptr, fixed bin (35));
dcl  component_info_$name entry (ptr, char(32), ptr, fixed bin(35));
dcl  gen_pl1_version_ entry (1 structure, 2 char(256) var aligned, 2 char(3) var aligned, char(3) var, fixed bin(35));
dcl  get_group_id_ entry () returns (char (32));
dcl  hcs_$add_acl_entries entry (char (*), char (*), ptr, fixed bin, fixed bin (35));
dcl  hcs_$delete_acl_entries entry (char (*), char (*), ptr, fixed bin, fixed bin (35));
dcl  hcs_$list_acl entry (char (*), char (*), ptr, ptr, ptr, fixed bin, fixed bin (35));
dcl  hcs_$terminate_noname entry (ptr, fixed bin(35));

/* automatic */

dcl  ACCESS_NAME char(32) aligned;
dcl  release char(3) var;
dcl  release_arg_length fixed bin;
dcl  release_arg_ptr ptr;
dcl  path_arg_length fixed bin;
dcl  path_arg_ptr ptr;
dcl  object_dir char(168);
dcl  object_entry char(32);
dcl  nargs fixed bin;
dcl  code fixed bin(35);
dcl  null_ref_name char(0);
dcl  bound_seg_ptr ptr init(null());
dcl  area_ret_ptr ptr;
dcl  cleanup_access bit(1) aligned initial("0"b);
dcl  call_restore_acl_and_terminate bit(1) aligned initial("0"b);

dcl  1 old_acl_entry aligned,
	2 access_name char(32),
	2 modes bit(36),
	2 zero_pad bit(36),
	2 status_code fixed bin(35);
dcl  1 new_acl_entry like old_acl_entry aligned;

/* internal static */

dcl (my_name char(18) initial("update_pl1_version"),
     release_arg_no fixed bin init(2),
     path_arg_no fixed bin init(1),
     copy_sw fixed bin(2) init(0),
     seg_sw fixed bin(1) init(0),
     acl_count fixed bin init(1),
     component_name char(32) init("pl1_version")) int static options(constant);

/* external static */

dcl  error_table_$user_not_found fixed bin(35) ext;

/* based */

dcl  path_arg char(path_arg_length) based(path_arg_ptr);
dcl  release_arg char(release_arg_length) based(release_arg_ptr);
dcl  1 pl1_version_struc based,
	2 pl1_version char(256) var,
	2 pl1_release char(3) var;

/* builtin */

dcl  (addr,null,length) builtin;

/* condition */

dcl  cleanup condition;

/* include files */

%include component_info;

/* program */

	on cleanup
	begin;
	     if call_restore_acl_and_terminate
	     then call restore_acl_and_terminate;
	end;


/* Determine number of arguments in command invocation */

	nargs = cu_$arg_count();
	if nargs ^= 2
	then do;
		call com_err_$suppress_name(0,my_name,"Usage: update pl1_version <path> {<release> | EXL}");
		return;
	     end;

/* Get the pathname and validate it */

	call cu_$arg_ptr(path_arg_no,path_arg_ptr,path_arg_length,code);
	call expand_pathname_(path_arg,object_dir,object_entry,code);
	if code ^= 0
	then go to ERROR;

/* Get the release and validate its length */

	call cu_$arg_ptr(release_arg_no,release_arg_ptr,release_arg_length,code);
	release = release_arg;

	if length(release) > 3
	then do;
	          call com_err_(0,my_name,"The length of release may not be greater than 3");
		return;
	     end;

/* Get current acl of bound segment if it exists */

	ACCESS_NAME = get_group_id_();
	old_acl_entry.access_name = ACCESS_NAME;
	call hcs_$list_acl(object_dir,object_entry,null(),area_ret_ptr,addr(old_acl_entry),acl_count,code);
	
	if code ^= 0
	then go to ERROR;

	if old_acl_entry.status_code ^= error_table_$user_not_found
	then cleanup_access = "1"b;

/* Get pointer to segment and component */

	call_restore_acl_and_terminate = "1"b;
	call hcs_$initiate(object_dir,object_entry,null_ref_name,seg_sw,copy_sw,bound_seg_ptr,code);

	if bound_seg_ptr = null()
	then go to ERROR;

	ci.dcl_version = 1;
	call component_info_$name(bound_seg_ptr,component_name,addr(ci),code);
	if code ^= 0
	then go to ERROR;

/* Now try to get rw access to bound segment */

	new_acl_entry.access_name = ACCESS_NAME;
	new_acl_entry.modes = "101"b; /* rw */
	new_acl_entry.zero_pad = "0"b;
	call hcs_$add_acl_entries(object_dir,object_entry,addr(new_acl_entry),acl_count,code);

	if code ^= 0
	then go to ERROR;

/* At this point update_pl1_version has rw access to the bound segment (usually bound_pl1_) */
/* and may alter it. */

	call gen_pl1_version_(ci.text_start -> pl1_version_struc,release,code);

	if code ^= 0
	then go to ERROR;

	call ioa_("^a: pl1_version=""^a""",my_name,ci.text_start -> pl1_version);
	call ioa_("^a: pl1_release=""^a""",my_name,ci.text_start -> pl1_release);

/* Restore acl to original state */

	call restore_acl_and_terminate;
	return;

/* Error exit: print a message and quit */

ERROR:
	call com_err_(code,my_name);

	if call_restore_acl_and_terminate
	then call restore_acl_and_terminate;

	return;

%page;
/* Delete new acl entry and restore the old acl entry if one existed */

restore_acl_and_terminate:
	procedure;

/* automatic */

dcl  1 delete_acl aligned,
	2 access_name char(32),
	2 status_code fixed bin(35);

/* program */

	delete_acl.access_name = ACCESS_NAME;
	call hcs_$delete_acl_entries(object_dir,object_entry,addr(delete_acl),acl_count,code);

	if cleanup_access
	then call hcs_$add_acl_entries(object_dir,object_entry,
		addr(old_acl_entry),acl_count,code);

	if bound_seg_ptr ^= null()
	then call hcs_$terminate_noname(bound_seg_ptr,code);

	end /* restore_acl_and_terminate */;

	end /* update_pl1_version */;

