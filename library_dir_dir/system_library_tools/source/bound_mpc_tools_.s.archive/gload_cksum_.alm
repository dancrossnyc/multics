" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************


" " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " "
" Program to compute a GCOS stye checksum word, adding with carry except
" the last word. The calling sequence is:
"
"	dcl gload_cksum_ entry (ptr, fixed bin (18), bit (36));
"        call gload_cksum_ (start_ptr, length, cksum_word);
"
"	where: start_ptr
"	          is a ptr to the first word to be added to the inputed 
"	          checksum word. (INPUT)
"	       length
"	          is the number of words to be added to form the completed
"	          checksum word. (INPUT)  
"	       cksum_word
"	          is the resultant checksum word. (INPUT/OUTPUT)
"
"	Written by J. A. Bush 8/31/81
"
" " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " "
	name	gload_cksum_
	entry	gload_cksum_

gload_cksum_:
	eppbp	ap|2,*		get array ptr
	eppbp	bp|0,*
	lxl5	ap|4,*		load length
	eax4	0		start at 0
	lda	ap|6,*		load the starting addend
	ldi	4000,dl		set overflow mask
cks_loop:	awca	bp|0,4		add next array element
	eax4	1,4		increment index
	eax5	-1,5		decrement count
	tpnz	cks_loop		transfer if more to do
	sta	ap|6,*		return checksum word
	ldi	0,dl		reset overflow mask
	short_return		" and return
	end
