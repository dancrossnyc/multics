
/****^  HISTORY COMMENTS:
  1) change(2020-02-02,GDixon), approve(2020-02-02,MCR10086),
     audit(2021-03-31,Swenson), install(2021-03-31,MR12.6g-0054):
      - Starting from code for mbuild_compare_.pl1 as a template:
         - Added vi_request entrypoint.
                                                   END HISTORY COMMENTS */

mbuild_info_checks_:
     proc (AsciP, Abuild_dataP) returns (bit(1) aligned);

  dcl  PROC char(20) int static options(constant) init("mbuild_info_checks_");

     return (check_parms (mbuild_data_version_3));
/* -------------------- */

  dcl  check_star_name_$entry entry (char(*), fixed bin(35));
  dcl  ioa_$rsnnl entry() options(variable);
  dcl  pathname_$component entry (char(*), char(*), char(*)) returns(char(194));
  dcl  ssu_$arg_list_ptr entry (ptr, ptr);

  dcl  mbuild_$banner entry (ptr, char(*), bit(1) aligned) options(variable);
  dcl  mbuild_$general_request entry (ptr, char(*), bit(1) aligned, ptr, fixed bin);

  dcl  mbuild_Tlist_$empty_request_list entry (ptr);
  dcl  mbuild_Tlist_$request_Seg_match_star entry (ptr, char(*), fixed bin(35)) returns(ptr);

  dcl  mbuild_data_$exists_Seg entry (ptr, ptr) returns(bit(1) aligned);
  dcl  mbuild_data_$get_build_progress entry (ptr) returns(bit(*) aligned);

  dcl  mbuild_library_$library_directories entry (char(*) var, (*) char(200) var, fixed bin, fixed bin(35));

  dcl (addr, length, maxlength, null, string) builtin;

  dcl  cleanup condition;

  dcl (error_table_$bigarg,
       error_table_$nomatch,
       mbuild_et_$inexact_library,
       mbuild_et_$source_missing
        )fixed bin(35) ext static;

%page;
/* ======================================================================
   REQUEST:  vi -rules AREA
		Invoke verify_info to display info segment rules
		and guidelines for AREA.  AREA may be any of the
		keywords supported by verify_info -rules AREA control
		argument.  For details, type:  help verify_info -brief

	   vi {-vi_control_args_except_-rules}
		Invoke verify_info (vi) command on all Info
		original-content segments in the install directory.
           
             vi SEG_NAME {-vi_control_args_except_-rules}
                    Invoke verify_info (vi) on just the given Info
                    segment(s) in the install directory.  The .info 
		suffix must included.  The star convention is permitted.  

   Checks format of original-content Info segments.
/* ========================================================================================== */

vi_request:
     entry(AsciP, Abuild_dataP);

  dcl 1 C aligned,
      2 sciP ptr,
      2 seg_name char(32) unal,
      2 seg_starness fixed bin(35),
      2 first_control_arg fixed bin,
      2 rulesS bit(1) aligned,
      2 rules_area char(32) var;

     C.sciP = AsciP;
     C.seg_name = "";
     C.seg_starness = STAR_TYPE_MATCHES_EVERYTHING;

     C.first_control_arg = 1;
     C.rulesS = F;
     C.rules_area = "";


     call arg_setup (AsciP);

     if args_remain() then				/* User asks for a options via -Control_args	        */
	call controlArgs(C);

  dcl  arg_listP ptr;				/* Pointer to this request's argument list.	        */
     call ssu_$arg_list_ptr (C.sciP, arg_listP);

     if  C.rulesS  then do;				/* If just displaying verify_info -rules AREA, do it now  */
	call mbuild_$general_request (C.sciP, "^2/verify_info -rules", T, arg_listP, C.first_control_arg);
	return;
	end;
     

/* ----------------------------------------------------------------------
    - Ensure install directory has been scanned.
   ---------------------------------------------------------------------- */

     string(bld_progress) = mbuild_data_$get_build_progress (addr(bld));
     if ^bld_progress.scannedS then do;
	call ssu_$abort_line (AsciP, 0,
	     "Please obtain data for vi task with request:  scan  or  read");
	end;					/*  Need data from scan to perform vi tasks.	        */
     

/* ----------------------------------------------------------------------
    - List original content Info Segs from thread:  scan_Tb 
   ---------------------------------------------------------------------- */

  dcl 1 req aligned like request;			/* Provide storage for request structure.	        */
     requestP = addr(req);				/*  - per-request threaded lists are valid only during    */
     request.info = "";				/*    execution of that one request.  At end of request,  */
     request.struct = "req";				/*    the list must be emptied.		        */
     request.name = ssu_$get_request_name(C.sciP);
     call Tlist_initialize (addr(request.request_Tb), addr(request));
						/* request_Tb is a list for Seg structures.	        */

     on cleanup call vi_janitor(addr(request));

     do SegP = Tlist_first_item (addr(bld.scan_Tb)) 
        repeat Tlist_next_item  (addr(Seg.scan_Td)) while (SegP ^= null() );
	if  Seg.type = "Info"  &  Seg.operation ^= "DELETE"  then
	     call Tlist_add_to_tail (addr(request.request_Tb), addr(Seg), addr(Seg.request_Td));
	end;

     if  Tlist_is_empty (addr(request.request_Tb))  then 
	return;					/* Nothing to verify!			        */


/* ----------------------------------------------------------------------
    - Do requested vis operations.
   ---------------------------------------------------------------------- */

  dcl  segs_done fixed bin init(0);

     SegP = mbuild_Tlist_$request_Seg_match_star (addr(request), C.seg_name, C.seg_starness);
						/* This call removes Seg and all prior items from the     */
						/*  request list.				        */
     do while ( SegP ^= null() );			/* When this loop ends, entire request list is empty      */
	call do_vi (SegP, C);
	SegP = mbuild_Tlist_$request_Seg_match_star (addr(request), C.seg_name, C.seg_starness);
	end;

     if segs_done = 0 then
	call ssu_$print_message (C.sciP, error_table_$nomatch, 
	     "No Info segments were processed.");
     return;

vi_janitor:					/* Clean up request list if subsystem request is aborted. */
     proc (ArequestP);

  dcl  ArequestP ptr;
  dcl 1 Arequest aligned like request based(ArequestP);

     call mbuild_Tlist_$empty_request_list (ArequestP);

     end vi_janitor;
%page;
/* ----------------------------------------------------------------------
    - Compare one source file.
   ---------------------------------------------------------------------- */
do_vi:
     proc (aSegP, c);

  dcl  aSegP ptr,
      1 aSeg aligned like Seg based (aSegP);
  dcl 1 c aligned like C;
     
     segs_done = segs_done + 1;


/* ----------------------------------------------------------------------
    - Make sure source file exists in install directory.
   ---------------------------------------------------------------------- */

     if ^mbuild_data_$exists_Seg (addr(bld), aSegP) then do;
						/* File represented by Seg(Info) not in build dir.        */
	call mbuild_$banner (c.sciP, "^2/ " || Seg.name, T);
	call ssu_$print_message (c.sciP, mbuild_et_$source_missing, 
	     "Missing info file corresponding to ^a: ^a", aSeg.type, aSeg.name);
	return;
	end;

/* ----------------------------------------------------------------------
    - Validate Info segment, passing any installer-specified control arguments.
   ---------------------------------------------------------------------- */

  dcl  request_line char(500);

     call mbuild_$banner (c.sciP, "^2/ " || Seg.name, T);
     call ioa_$rsnnl ("verify_info ^a", request_line, 0, aSeg.name);
     call ssu_$arg_list_ptr (c.sciP, arg_listP);
	
     call mbuild_$general_request (c.sciP, request_line, T, arg_listP, c.first_control_arg);

     end do_vi;
%page;
/* ------------------------------------------------------------
 * Process print -control_arg arguments.
 * ------------------------------------------------------------ */

controlArgs:
     proc (c);

  dcl 1 c aligned like C;				/*  control arg specification data                   (in) */

     argI = 0;

     do while (args_remain());
	call ssu_$arg_ptr (c.sciP, argI+1, argP, argL);	/* Conditionally read next arg to command/af/request      */
					
	if isControlArg(arg) then do;

	     if  arg = "-rules"      then do;  argI = argI + 1;
				         c.rulesS = T;
				         end;	/* rulesS takes place of -rules control arg.	        */
						/*  - next argument (if any) is AREA operand of -rules    */

	     c.first_control_arg = argI + 1;		/* Found first vis control argument.		        */
	     return;
	     end;
	
	else if c.seg_name = "" then do;		/* If arg not a -control_arg			        */
	     c.seg_name = arg;
	     if length(arg) > maxlength(c.seg_name) then
		call ssu_$print_message (c.sciP, error_table_$bigarg, "^a may not be longer than ^d characters.",
		     arg, maxlength(c.seg_name));
	     call check_star_name_$entry (c.seg_name, c.seg_starness);
	     if ^(c.seg_starness = STAR_TYPE_USE_PL1_COMPARE | 
	          c.seg_starness = STAR_TYPE_USE_MATCH_PROCEDURE | 
		c.seg_starness = STAR_TYPE_MATCHES_EVERYTHING)  then
		call ssu_$abort_line (c.sciP, c.seg_starness, "^a", c.seg_name);
	     c.first_control_arg = argI + 2;
	     end;

	argI = argI + 1;				/* Record that we processed the arg just examined above.  */
	end;

     end controlArgs;

%page;
%include ssu_request_dcls_;
%page;
%include mbuild_request_parms_;
%page;
%include mbuild_info_;
%page;
%include mbuild_Tlist_;
%page;
%include mbuild_data_;
%page;
%include check_star_name;

     end mbuild_info_checks_;
