/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */
convert_word_: proc (word, hyphens, word_length, expanded_word, hyphenated_word);
dcl  word (0:*) fixed bin;
dcl  hyphens (0:*) bit (1) aligned;
dcl  word_length fixed bin;
dcl  expanded_word char (*);
dcl  hyphenated_word char (*);
dcl  i fixed bin;
dcl  no_hyphens bit (1) aligned;
dcl  word_index fixed bin init (1);
dcl  hyphenated_index fixed bin init (1);
	%include digram_structure;

	no_hyphens = ""b;

convert_word:
	do i = 1 to word_length;
	     if substr (letters (word (i)), 2, 1) = " "
	     then
		do;
		substr (expanded_word, word_index, 1) = substr (letters (word (i)), 1, 1);
		if ^no_hyphens then
		     do;
		     substr (hyphenated_word, hyphenated_index, 1) = substr (letters (word (i)), 1, 1);
		     hyphenated_index = hyphenated_index + 1;
		end;
		word_index = word_index + 1;
	     end;
	     else
	     do;
		substr (expanded_word, word_index, 2) = letters (word (i));
		if ^no_hyphens then
		     do;
		     substr (hyphenated_word, hyphenated_index, 2) = letters (word (i));
		     hyphenated_index = hyphenated_index + 2;
		end;
		word_index = word_index + 2;
	     end;
	     if ^no_hyphens
	     then
		if hyphens (i)
		then
		     do;
		     substr (hyphenated_word, hyphenated_index, 1) = "-";
		     hyphenated_index = hyphenated_index + 1;
		end;
	end;

	if ^no_hyphens then if hyphenated_index <= length (hyphenated_word) then substr (hyphenated_word, hyphenated_index) = "";
	if word_index <= length (expanded_word) then substr (expanded_word, word_index) = ""; /* fill out with spaces */
	return;

convert_word_$no_hyphens: entry (word, word_length, expanded_word);
	no_hyphens = "1"b;
	goto convert_word;

     end;
