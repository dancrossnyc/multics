/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */


backup_preattach: bpa: proc ();

/* Hack to preattach bk_ss_$data_iocb */

/* 11/18/80, WOS */

dcl  ap pointer;
dcl  al fixed bin (21);
dcl  arg char (al) based (ap);
dcl (nargs, argno) fixed bin;
dcl  code fixed bin (35);
dcl  open_mode fixed bin;
dcl  attach_desc char (512) varying;
dcl  iocbp pointer;
dcl  stream_name char (32);

dcl  com_err_ entry options (variable);
dcl  cu_$arg_count entry (fixed bin, fixed bin (35));
dcl  cu_$arg_ptr entry (fixed bin, pointer, fixed bin (21), fixed bin (35));
dcl  iox_$attach_name entry (char (*), pointer, char (*), pointer, fixed bin (35));
dcl  iox_$open entry (pointer, fixed bin, bit (1) aligned, fixed bin (35));
dcl  iox_$close entry (pointer, fixed bin (35));
dcl  iox_$detach_iocb entry (pointer, fixed bin (35));
dcl  iox_$destroy_iocb entry (pointer, fixed bin (35));
dcl  unique_chars_ entry (bit (*)) returns (char (15));

dcl  bk_ss_$data_iocb pointer external static;
dcl  bk_ss_$preattached bit (1) aligned external static;

dcl (error_table_$badopt,
     error_table_$noarg) fixed bin (35) external static;

dcl  WHOAMI char (32) internal static options (constant) init ("backup_preattach");

dcl (addr, substr, null) builtin;

/*  */

	call cu_$arg_count (nargs, code);
	if code ^= 0 then do;
	     call com_err_ (code, WHOAMI);
MAIN_RETURN:   return;
	     end;

	if nargs = 0 then do;
	     call com_err_ (error_table_$noarg, WHOAMI,
		"^/Usage:^-^a open_mode attach_desc^/^2x(or)^-^a -detach",
		WHOAMI, WHOAMI);
	     goto MAIN_RETURN;
	     end;

	if nargs = 1 then do;			/* Close, or something like that */
	     call cu_$arg_ptr (1, ap, al, (0));
	     if (arg = "-close") | (arg = "-detach") then do;
		if bk_ss_$preattached = "0"b then do;
NOT_PREATTACHED:	     call com_err_ (0, WHOAMI, "Backup I/O is not preattached.");
		     goto MAIN_RETURN;
		     end;

		bk_ss_$preattached = "0"b;
		if bk_ss_$data_iocb = null () then
		     goto NOT_PREATTACHED;

		call iox_$close (bk_ss_$data_iocb, (0));
		call iox_$detach_iocb (bk_ss_$data_iocb, (0));
		call iox_$destroy_iocb (bk_ss_$data_iocb, (0));
		goto MAIN_RETURN;
		end;

	     else do;
		call com_err_ (0, WHOAMI, "Unknown control function ^a.", arg);
		goto MAIN_RETURN;
		end;
	     end;

	call cu_$arg_ptr (1, ap, al, (0));		/* Get the opening mode */

	if bk_ss_$preattached then do;
ALREADY_PREATTACHED:
	     call com_err_ (0, WHOAMI, "Backup I/O is already preattached. Use ^a -detach first.", WHOAMI);
	     goto MAIN_RETURN;
	     end;

	if arg = "input" then
	     open_mode = Stream_input;
	else if arg = "output" then
	     open_mode = Stream_output;
	else do;
	     call com_err_ (0, WHOAMI, "Invalid opening mode ^a. Must be either ""input"" or ""output"".", arg);
	     goto MAIN_RETURN;
	     end;

	attach_desc = "";
	do argno = 2 to nargs;
	     call cu_$arg_ptr (argno, ap, al, (0));
	     if length (attach_desc) > 0 then
		attach_desc = attach_desc || " ";
	     attach_desc = attach_desc || arg;
	     end;

	stream_name = "backup." || unique_chars_ (""b);

	call iox_$attach_name (stream_name, iocbp, (attach_desc), codeptr (backup_preattach), code);
	if code ^= 0 then do;
	     call com_err_ (code, WHOAMI, "Cannot attach stream.");
	     goto MAIN_RETURN;
	     end;

	call iox_$open (iocbp, open_mode, "0"b, code);
	if code ^= 0 then do;
	     call com_err_ (code, WHOAMI, "Cannot open stream.");
	     return;
	     end;

	bk_ss_$preattached = "1"b;
	bk_ss_$data_iocb = iocbp;

	return;

%page;
%include iox_modes;

	end backup_preattach;
