" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1983 *
" *                                                         *
" ***********************************************************
" procedure to return array bounds|dimension to pl/1 program
"
" Barry Wolman, 20 January 1970
"
" Usage:
"	n = bound_(array,dim,n_dims,code);
"
" where:
"	array is the array about which info is desired
"	dim is the dimension in question
"	n_dims is the total number of dimensions
"	code is 	1 for lbound
"		2 for hbound
"		3 for dim
"
	entry	bound_
"
	equ	array,2
	equ	dim,4
	equ	number,6
	equ	code,8
	equ	bound,10
	equ	array_desc,12
"
bound_:	lxl0	ap|code,*		get what to do
	ldq	ap|number,*	get number of dimensions
	sbq	ap|dim,*		get dimension desired
	mpy	3,dl		multiply by info size
	eppbp	ap|array_desc,*	get ptr to array descriptor
	xec	table-1,0		execute instruction to fetch info
done:	sta	ap|bound,*	store answer
	short_return
"
table:	lda	bp|1,ql		1, get lower bound
	lda	bp|2,ql		2, get upper bound
	tra	*+1		3, get dimension
	lda	bp|2,ql		which is
	sba	bp|1,ql
	ada	1,dl		hb-lb+1
	tra	done
"
	end
