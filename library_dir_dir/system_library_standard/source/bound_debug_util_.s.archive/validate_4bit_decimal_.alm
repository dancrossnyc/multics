"  ******************************************************
"  *                                                    *
"  *                                                    *
"  * Copyright (c) 1972 by Massachusetts Institute of   *
"  * Technology and Honeywell Information Systems, Inc. *
"  *                                                    *
"  *                                                    *
"  ******************************************************

" Program to assist valid_decimal_....
" Written 780815 by PG and JRD.
"
	name	validate_4bit_decimal_
	segdef	sign
	segdef	digits
"
	equ	value_ptr,2
	equ	sign_offset,4
	equ	sign_result,6
"
" Entry: validate_4bit_decimal_$sign (value_ptr, sign_offset) returns (bit (1) aligned);
"
sign:
	epp2	ap|value_ptr,*   		pr2 -> arg1
	epp2	pr2|0,*  			pr2 -> data
	lxl1	ap|sign_offset,*
	epp3	ap|sign_result,*    	pr3 -> place to return bit 
	cmpc	(pr,x1),(),fill(12)		first sign is 12(8)
	desc4a	pr2|0,1
	zero
	tnc	fail			char < 12, i.e. not a sign char
	cmpc 	(pr,x1),(),fill(17)		last sign is 17(8)
	desc4a	pr2|0,1
	zero
	tze	ok   			-17 is OK
	tnc	ok   			<17 is OK, too
"
fail:
	stz	pr3|0   			return ("0"b)
	short_return
"
ok:
	lda	=o400000,du
	sta	pr3|0   return ("1"b)
	short_return
"
" Entry: validate_4bit_decimal_$digits (value_ptr, digits_offset, digits_length) returns (bit (1) aligned);
"
	equ	digits_offset,4
	equ	digits_length,6
	equ	digits_result,8
"
digits:
	epp2	ap|value_ptr,*
	epp2	pr2|0,*
	lxl1	ap|digits_offset,*
	lxl2	ap|digits_length,*
	epp3	ap|digits_result,*
	tct	(pr,rl,x1)
	desc4a	pr2|0,x2
	arg	table
	arg	pr3|0			use return value as a temp!
	ttn	ok
	tra	fail
"
table:
	vfd	9/0,9/0,9/0,9/0
	vfd	9/0,9/0,9/0,9/0
	vfd	9/0,9/0,9/1,9/1
	vfd	9/1,9/1,9/1,9/1
	end
