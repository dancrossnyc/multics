/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */

find_nonobject_info_: proc (eptr, ename, owner, section, adj_offset, code);



/* This procedure takes the address of an entry point in the linkage section and
   attempts to return its name */


dcl  eptr pointer;					/* Pointer to the entry point. */
dcl  ename char (*);				/* Entry name output */
dcl  owner fixed bin(18);				/* seg no. of text */
dcl  section char(8) aligned;				/* name of section */
dcl  adj_offset fixed bin(18);			/* offset relative to section */
dcl  code fixed bin (35);				/* Standard File System Error Code. Returned. */


/* Automatic Storage */


dcl  pls pointer;					/* Pointer to the linkage section */
dcl  offset bit (18) aligned;				/* Offset of the entry in the linkage section */
dcl  section_offset fixed bin(18);			/* offset of section within cls */
dcl  (i, class) fixed bin;

/* Externals */


dcl  error_table_$name_not_found ext fixed bin (35);
dcl  get_def_name_ entry (ptr, ptr, bit (18) aligned, fixed bin, char (*), fixed bin (35));
dcl  hcs_$get_lp entry (ptr, ptr);
dcl  find_owner_ entry (ptr, fixed bin(18), fixed bin(18), char(8) aligned, fixed bin, ptr);
dcl (addrel, bit, fixed, null, rel, bin, baseno, ptr, baseptr) builtin;


/* this procedure should be called only for non-object segments */

program_begins_here:

	ename = "";
	section = "text";				/* initialize in case of error */

	if eptr = null then goto error;

	/* be sure this is a linkage section before we go looking for an entry sequence */
	call find_owner_ (eptr, owner, section_offset, section, class, pls);
	if owner = -1 then do;			/* eptr doesn't point to a linkage section,
						   but maybe it points to a seg that has one,
						   as for example a seg created by datmk_ or type 6 link */
	     owner = bin (baseno (eptr), 18);		/* we have ptr to seg itself */
	     offset = rel (eptr);			/* so use offset directly from ptr */
	     class = 0;				/* it's text if anything */
	     section = "text";
	     call hcs_$get_lp (ptr (eptr, 0), pls);
	     if pls = null then do;			/* may have a ring 0 seg (they're
						   not all complete object segs); but
						   must go into r0 to get pls */
		call get_def_name_(null, ptr(eptr, 0), offset, -1, ename, code);
		go to return;
	     end;
	     go to get_name;			/* and forget about entry sequence */
	end;
	offset = bit (fixed (fixed (rel (eptr), 18)-section_offset, 18), 18); /* Offset to entry */

get_name: call get_def_name_ (pls, null, offset, class, ename, code); /* Get name */

return:	adj_offset = bin(offset, 18);
	return;

error:	code = error_table_$name_not_found;
     end;
