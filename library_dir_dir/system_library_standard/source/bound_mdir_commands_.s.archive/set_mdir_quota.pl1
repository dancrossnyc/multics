/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   * Copyright (c) 1972 by Massachusetts Institute of        *
   * Technology and Honeywell Information Systems, Inc.      *
   *                                                         *
   *********************************************************** */

/* format: style2,indcomtxt */

/* SET_MDIR_QUOTA: Command to set quota on a master directory. */

/* Written March 1976 by Larry Johnson */
/* Modified 83-12-07 BIM for correct quota precision */

set_mdir_quota:
smdq:
     procedure options (variable);

	dcl     name		 char (14) int static options (constant) init ("set_mdir_quota");
	dcl     code		 fixed bin (35);
	dcl     arg_ptr		 ptr;
	dcl     arg_len		 fixed bin;
	dcl     arg		 char (arg_len) based (arg_ptr);
	dcl     dir		 char (168);
	dcl     ename		 char (32);
	dcl     quota		 fixed bin (18);
	dcl     i			 fixed bin;
	dcl     sw		 bit (1) aligned;
	dcl     nargs		 fixed bin;
	dcl     j			 fixed bin;

	dcl     cu_$arg_ptr		 entry (fixed bin, ptr, fixed bin, fixed bin (35));
	dcl     ioa_		 entry options (variable);
	dcl     com_err_		 entry options (variable);
	dcl     expand_path_	 entry (ptr, fixed bin, ptr, ptr, fixed bin (35));
	dcl     mdc_$set_mdir_quota	 entry (char (*), char (*), bit (1) aligned, fixed bin (18), fixed bin (35));
	dcl     cv_dec_check_	 entry (char (*), fixed bin) returns (fixed bin (35));
	dcl     cu_$arg_count	 entry (fixed bin);
	dcl     pathname_		 entry (character (*), character (*)) returns (character (168));

	dcl     (addr, bin, substr)	 builtin;

	call cu_$arg_count (nargs);
	if nargs = 0
	then do;
		call ioa_ ("Usage: ^a path quota", name);
		return;
	     end;

	do j = 1 to nargs by 2;
	     call cu_$arg_ptr (j, arg_ptr, arg_len, code);/* Path name */
	     if code ^= 0
	     then do;
		     call com_err_ (code, name);	/* Should not happen */
		     return;
		end;
	     call expand_path_ (arg_ptr, arg_len, addr (dir), addr (ename), code);
	     if code ^= 0
	     then do;
		     call com_err_ (code, name, "^a", arg);
		     go to next;
		end;

	     call cu_$arg_ptr (j + 1, arg_ptr, arg_len, code);
						/* Get new quota */
	     if code ^= 0
	     then do;
		     call com_err_ (code, name, "Quota for ^a.", pathname_ (dir, ename));
		     return;
		end;
	     quota = cv_dec_check_ (arg, i);
	     if i ^= 0
	     then do;
		     call com_err_ (0, name, "Quota for ^a must be numeric: ^a", pathname_ (dir, ename), arg);
		     go to next;
		end;

	     if substr (arg, 1, 1) = "+" | substr (arg, 1, 1) = "-"
	     then sw = "1"b;
	     else sw = "0"b;

	     call mdc_$set_mdir_quota (dir, ename, sw, quota, code);
	     if code ^= 0
	     then call com_err_ (code, name, "^a", pathname_ (dir, ename));
next:
	end;
	return;

     end set_mdir_quota;
