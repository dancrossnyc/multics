" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

"fast_put: proc(iocb_ptr,buff_ptr,buff_len,code);
	name	fast_put
	segdef	fast_put
fast_put:	epp1	pr0|2,*		pr1 gets addr of first arg
	epp1	pr1|0,*		pr1->iocb
	epp1	pr1|open_data_ptr,*	pr1->cb_uns_file

"len=max(buff_len,0);
	ldq	pr0|6,*		q gets arg2
	tpl	2,ic		done if buff_len is > 0
	ldq	0,dl		else set len to zero

"cb_uns_file.write_pos=cb_uns_file.write_pos+len;
	lda	pr1|write_pos	save old value of write pos
	sta	pr0|8,*		use last arg for temp
	asq	pr1|write_pos	advance write position

"if write_pos<=cb_uns_file.write_limit then do;
	lda	pr1|write_pos	a gets new write pos
	cmpa	pr1|write_limit	write_pos::write_limit
	tpnz	slow_put		exception--make external call

     "record_write=buffer;
	epp2	pr0|4,*		pr2 gets addr of buff_ptr arg
	epp2	pr2|0,*		pr2->buffer
	lda	pr0|8,*		get saved write position
	epp3	pr1|seg_ptr,*	pr3->segment base
	mlr	(pr,rl),(pr,rl,al),fill(040)  copy buffer into the file
	desc9a	pr2|0,ql		output buffer descrip
	desc9a	pr3|-1(3),ql	file position descrip

     "code=0. return, end;
	stz	pr0|8,*		arg4<--0
	short_return		done with put_chars operation

"write_pos=write_pos-len;
slow_put:	lda	pr0|8,*		a<--saved write position
	sta	pr1|write_pos	restores write pos to former value

"call open_uns_file$put_chars_uns_file(iocb_ptr,buff_ptr,buff_len,code);
	fld	4*2048,dl		set up arg count for external call
	callsp	<open_uns_file>|[put_chars_uns_file] handles exceptional cases
	short_return

"declarations:
	equ	open_data_ptr,18	in iocb
	equ	write_pos,10	in cb_uns_file
	equ	write_limit,2	     "
	equ	seg_ptr,0		     "
	end
