/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */

/* Modified by A. Downing to use round builtin function 07/16/73 */

asinh_: procedure (number) returns (float binary (27));

/*      compute the hyperbolic arcsine or arccosine of a single-precision floating-point number      */
declare (number, r) float binary (27),
        (f, n, p) float binary (63);
dcl  code_ ext entry (fixed bin),
     (abs, log, round, sqrt) builtin;
	p = 1.e0;
	n = number;
	f = abs (n);
	r = f;
	if f < 5.e-5 then go to negate;
asinhs:	if f >= 11586.e0 then p = f;
	else p = sqrt (f*f + p);
	p = p + f - 1.e0;
	r = round (p, 28);
	r = log (r+1);
negate:	if n < 0.0e0 then r = -r;
	return (r);
acosh_: entry (number) returns (float binary (27));
	p = -1.e0;
	n, f = abs (number);
	if f >= 1.e0 then go to asinhs;
	call code_ (34);
	return (0.0e0);
     end asinh_;
