/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */

code_: proc (n);

dcl  n fixed bin;					/* error message number */

dcl (p, q, called_pt, caller_pt) ptr,
    (called_size, caller_size) fixed bin,
     bn bit (18);

dcl  cu_$stack_frame_ptr entry returns (ptr),
     pl1_frame_$name entry (ptr, ptr, fixed bin),
     math_error_ entry (fixed bin, char (*) aligned, char (*) aligned, ptr);

dcl 1 frame based,
    2 skip (8) ptr,
    2 back ptr,
    2 forward ptr,
    2 return ptr;

dcl  called char (called_size) aligned based (called_pt),
     caller char (caller_size) aligned based (caller_pt);

	p = cu_$stack_frame_ptr() -> frame.back;
	bn = baseno (p -> frame.return);

	do while (baseno (p -> frame.return) = bn);
	     p = p -> frame.back;
	end;

	q = p -> frame.forward;

	call pl1_frame_$name (p, caller_pt, caller_size);
	call pl1_frame_$name (q, called_pt, called_size);

	call math_error_ (n, (caller), called, p -> frame.return);

     end;
