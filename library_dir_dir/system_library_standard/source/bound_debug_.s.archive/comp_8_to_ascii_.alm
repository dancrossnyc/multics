" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

"  This procedure returns a character string representing the input
"  bit string as comp-8 format.
"
"	dcl comp_8_to_ascii_ entry (bit (*), char (*));
"	call comp_8_to_ascii_ (bit_string, char_string);
"
"	     bit_string	bit string of comp-8 (INPUT)
"	     char_string	ASCII representation of comp-8 (OUTPUT)
"
	name	comp_8_to_ascii_
	segdef	comp_8_to_ascii_


comp_8_to_ascii_:
	epp2	ap|2,*		pr2 = address of bit string
	epp3	ap|4,*		pr3 = address of character string
	ldx3	0,du		compute address of descriptors
	lxl2	ap|0		  get arg list header word
	canx2	=o4,du		
	tnz	*+2		  no display ptr
	ldx3	2,du		  add 2 word offset if display ptr
	ldq	ap|6,x3*		q = length of bit string
	anq	=o000077777777	  get size portion
	qls	1		  convert bits to digits
	div	9,dl		  length*2/9
	lda	ap|8,x3*		a = length of character string
	ana	=o000077777777	  get size portion of descriptor

	mvt	(pr,rl),(pr,rl),fill(17)
	desc4a	pr2|0,ql
	desc9a	pr3|0,al
	arg	table

	short_return

table:	aci	"0123456789x+x-xx"

	end
