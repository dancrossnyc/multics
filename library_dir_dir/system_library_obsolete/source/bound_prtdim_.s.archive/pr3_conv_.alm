" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" ***********************************************************
" PR3_CONV_ - Conversion for PRT300 and PRU1200 series printer
"	coded 10/24/74 by Noel I. Morris


"  ******************************************************
"  *                                                    *
"  *                                                    *
"  * Copyright (c) 1972 by Massachusetts Institute of   *
"  * Technology and Honeywell Information Systems, Inc. *
"  *                                                    *
"  *                                                    *
"  ******************************************************


" This module perform the necessary conversion for printing
" on the PRT300 and PRU1200 line printers.
"
" The manner in which this procedure is utilized is described in detail
" in the listing of prt_conv_.


	name	pr3_conv_

	segdef	pr3_conv_


	temp	char



pr3_conv_:
	tra	sb|0
	tra	pr3_send_chars
	tra	pr3_send_slew_pattern
	tra	pr3_send_slew_count

" 

pr3_send_chars:

spaceloop:
	eaq	0,2		white space count in QU
	tmoz	nospace		skip if no white space
	sbx2	127,du		can take only 127 at a time
	tmoz	*+2		if > 127
	ldq	127,du		take only 127
	orq	hs_char		insert skip character
	stq	char		and save

	mlr	(pr),(pr)		move to output string
	desc9a	char,2		..
	desc9a	bb|0,2		..

	ldq	2,dl		step output pointer
	a9bd	bb|0,ql		..
	tra	spaceloop		loop

nospace:	mlr	(pr,rl),(pr,rl)	copy characters into output
	desc9a	bp|0,au		..
	desc9a	bb|0,au		..

	a9bd	bp|0,au		step input and output pointers
	a9bd	bb|0,au		..

	eax2	0		make sure X2 now zero
	tra	sb|0		return to caller

" 

pr3_send_slew_pattern:
	als	18		move pattern to AU
	ora	ff_char		insert FF character
	sta	char		save characters

send_slew:
	mlr	(pr),(pr),fill(0)	pad last word with zeroes
	desc9a	char,2		..
	desc9a	bb|0,2+3		..

	ldq	2,dl		increment output pointer
	a9bd	bb|0,ql		..

	tra	sb|0		return to caller



pr3_send_slew_count:
	eaq	0,al		line count in QU
	sbla	127,dl		can only do 127 at a time
	tmoz	*+2		if > 127,
	ldq	127,du		do only 127 this time
	orq	vt_char		insert VT character
	stq	char		and save characters
	tra	send_slew		join common code

" 

vt_char:	vfd	o9/013
ff_char:	vfd	o9/014
hs_char:	vfd	o9/037




	end
