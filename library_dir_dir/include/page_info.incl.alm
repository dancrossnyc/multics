"	BEGIN INCLUDE FILE page_info.incl.alm  --  6/72
"
" Modified 02/22/81, W. Olin Sibert, for ADP conversion
" Modified 06/23/82, E. N. Kittlitz, to move core map.
"
	equ	page_power,10		" 2**10 = size of page
	bool	df1,000001		" directed fault 1 on page fault

	bool	address_mask,777777740000	" Mask for PTW add, add_type compares
					" Use as =v36/address_mask

	bool	ptw.nulled,400000		" flag for testing addresses

	bool	int,100000		" Disk dim flags
	bool	pri,000020
	bool	no_post,000004

	equ	sst,3			" Standard pointer and index
	equ	pdm,sst			" register usage
	equ	ast,sst
	equ	ptw,2

"	Various AST masks which are not
"	expressible in standard notation
"
	bool	aste.csl_mask_inner,777000
	bool	aste.records_mask_inner,777000

	equ	fault_entry,0		" Flags indicating how we
	equ	read_entry,1		" were entered
	equ	write_entry,2
	equ	pre_page_entry,3
	equ	post_purge_entry,4
	equ	pd_flush_entry,5
	equ	evict_entry,6
	equ	abs_wire_entry,7
	equ	cleanup_entry,8

" Constants for manipulating core addresses and PTWs. The core address
" is a fixed bin (35) aligned, and must be moved to/from the appropriate
" field in the SDW or PTW.
"
	iftarget	l68
	   bool	ptw_add_mask,777760 " Page number
	   bool	sdw_add_mask,777760 " Page number
	   bool	coreadd_mask,000077777777 " Mask for coreadd in AL

	   equ	coreadd_to_ptw.ls,12
	   equ	coreadd_to_sdw.ls,12
	   equ	ptw_to_coreadd.rl,12
	   equ	sdw_to_coreadd.rl,12
	   equ	cmep_to_ptw.ls,2
	   equ	cmep_to_sdw.ls,2
	   equ	ptw_to_cmep.rl,2
	   equ	ptw_to_cmep_lower.rl,20
	   equ	sdw_to_cmep.rl,2
	ifend

	iftarget	adp
	   bool	ptw_add_mask,177777 " Page number
	   bool	sdw_add_mask,777774 " Page number
	   bool	coreadd_mask,000377777777 " Mask for coreadd in AL

	   equ	coreadd_to_ptw.ls,8
	   equ	coreadd_to_sdw.ls,10
	   equ	ptw_to_coreadd.rl,8
	   equ	sdw_to_coreadd.rl,10
	   equ	cmep_to_ptw.rl,2	" Must shift backwards
	   equ	cmep_to_sdw.ls,0
	   equ	ptw_to_cmep.ls,2	" Must shift backwards
	   equ	ptw_to_cmep_lower.rl,16
	   equ	sdw_to_cmep.rl,0
	ifend

	equ	cmep_to_coreadd.rl,10	.cmep to coreadd
	equ	coreadd_to_cmep.ls,10	coreadd to .cmep

" 
"
" Assorted general purpose macros used in PC. These will be changed
" when address types are changed for 20 bit addresses. Additionally,
" for more accurate metering on the ADP, the metering macros should
" modified to update the metering cells uninterruptably.
"

	macro	missing
	ife	&2,()
	  warn	(&1: Argument missing.)
	ifend
	&end
"
" staddra and staddrq store the address portion (upper 22 bits) of the A or Q
" into the specified location.
"
	macro	staddra
	missing	staddra,&1
	era	&F1
	ana	=v36/address_mask
	ersa	&F1
	&end

	macro	staddrq
	missing	staddrq,&1
	erq	&F1
	anq	=v36/address_mask
	ersq	&F1
	&end

"	Macros for metering 6/23/80

	macro	read_clock
	iftarget	l68
	  rccl	sys_info$clock_,*
	ifend
	iftarget adp
	  rccl	0
	ifend
	&end

	macro	increment
	missing	increment,&1
	aos	&1
	&end

	macro	store_clock
	missing	store_clock,&1
	read_clock
	staq	&1
	&end

	macro	meter_time
	missing	meter_time,&3
	increment &3
	read_clock
	sbaq	&1
	adaq	&2
	staq	&2
	&end

"	END INCLUDE FILE  page_info.incl.alm
