/* BEGIN INCLUDE FILE ... answer_table.incl.pl1 */

/* *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  * */
/*							       */
/* The answer table has one entry per "login instance" whether completed or  */
/* not.  It is connected to the Channel Definition Table by the pointer      */
/* "channel".  The header is used mostly by dialup_.		       */
/*							       */
/* Programs which use this file must also include user_table_header.incl.pl1 */
/*							       */
/* *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  * */

/****^  HISTORY COMMENTS:
  1) change(81-09-10,Kittlitz), approve(), audit(), install():
     Replace anstbl.incl.pl1.
  2) change(85-01-15,Swenson), approve(), audit(), install():
     Add anstbl.session_uid_counter.
  3) change(85-08-21,Coren), approve(), audit(), install():
     Add anstbl.message_update_time and named constants for values of
     anstbl.session, and to make all padding explicit.
  4) change(85-08-23,Coren), approve(), audit(), install():
     Change "entry" to a placeholder so as not to require
     user_table_entry.incl.pl1.
  5) change(86-06-29,Swenson), approve(87-07-13,MCR7741),
     audit(87-04-14,GDixon), install(87-08-04,MR12.1-1056):
     Added the flag login_server_present which indicates whether a login
     server request has been received.  This is used to determine whether
     dial_ctl_ should call uc_cleanup_network_dials_ (and thus
     hpriv_connection_list_, which only exists with the MNA RPQ software).
  6) change(87-04-14,GDixon), approve(87-07-13,MCR7741),
     audit(87-07-16,Brunelle), install(87-08-04,MR12.1-1056):
      A) Moved constants for ute.pw_flags.mask_ctl into
         user_table_entry.incl.pl1.
      B) Added common table header to all user tables.
                                                   END HISTORY COMMENTS */

/* *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  * */
/*							       */
/* The anstbl structure below is divided into sections.  Each section begins */
/* with a comment describing the elements in the section.  Elements are      */
/* placed within a section, based upon their function or the programs that   */
/* use them.  Each section begins on a double word and is an even number of  */
/* words long.						       */
/*							       */
/* *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  * */



/* format: style4 */

dcl  ANSTBL_version_4 static options (constant) init (4);	/* version of this declaration */

dcl  ansp ptr automatic init (null);

dcl  1 anstbl based (ansp) aligned,			/* Structure of answer table */
       2 header like ut_header,			/* Header common to all user tables. */

/* Counter elements. */
       2 nlin fixed bin,				/* number of active lines */
       2 mxlin fixed bin,				/* maximum number of active lines */
       2 n_users fixed bin,				/* number of logged-in users */
       2 max_users fixed bin,				/* maximum number of users allowed */
       2 n_units fixed bin,				/* number of logged in units */
       2 max_units fixed bin,				/* maximum number of units */
       2 n_sessions fixed bin,			/* number of Multics sessions */
       2 n_pad fixed bin,

/* Name elements. */
       2 sysdir char (64) unal,			/* name of main system control directory */
       2 as_tty char (8) unal,			/* name of main answering service device. */

/* Login elements. */
       2 login_word char (8) unal,			/* login word if special_session=1  */
       2 session char (8) unal,			/* session indicator */
       2 special_message char (128) unal,		/* message to be printed for special session */
       2 message_update_time fixed bin (71),		/* time at which special_message was last updated */
       2 message_lng fixed bin,			/* length of special message */
       2 login_pad fixed bin,

/* Table update elements. */
       2 lock_count fixed bin,			/* global lock for all system control tables */
       2 update_pending bit (1) aligned,		/* flag indicating that update is required */
       2 update_channel fixed binary (71),		/* event channel of update procedure */
       2 acct_update_chn fixed bin (71) aligned,		/* Timer IPC channel */
       2 acct_last_update_time fixed bin (71) aligned,	/* Time of last accounting update */
       2 acct_alarm_fail fixed bin,			/* non-zero if alarm has failed */
       2 update_pad fixed bin,

/* dialup_ data values. */
       2 current_time fixed bin (71),			/* Time of last transaction */
       2 processid_index fixed bin (18),		/* unique index for process id generation */
       2 session_uid_counter fixed bin (35),		/* current session_uid */

/* load_ctl_ elements. */
       2 shift fixed bin,				/* Shift, set by act_ctl_ */
       2 auto_maxu fixed bin,				/* 0 = manual, 1 = config, 2 = load-level */
       2 extra_units fixed bin,			/* Total daemon and abs units. */
						/* load_ctl_ load leveling desired response range: */
       2 response_high fixed bin,			/* bad if user response time slower than this */
       2 response_low fixed bin,			/* bad if user response time faster than this */
       2 load_ctl_pad fixed bin,

/* Login server request server data. */
       2 ls_request_server_event_channel fixed bin (71),	/* channel for login server requests */
       2 ls_request_server_process_id bit (36) aligned,	/* process serving login server requests */
       2 login_server_present bit (1) aligned,		/* On IFF a login server request has been processed */

       2 entry_pad (28) fixed bin,			/* pad to 128 words */
       2 entry (0 refer (anstbl.current_size)),		/* user entries */
         3 contents (UTE_SIZE) fixed bin;

/* constants */

/* values for anstbl.session */

dcl  (AT_NORMAL init ("normal  "),
     AT_SPECIAL init ("special "),
     AT_SHUTDOWN init ("shutdown")) char (8) internal static options (constant);

dcl  UTE_SIZE fixed bin internal static initial (300);

/* END INCLUDE FILE ... answer_table.incl.pl1 */
