/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1982 *
        *                                                         *
        *********************************************************** */

/****^  HISTORY COMMENTS:
  1) change(85-09-09,Farley), approve(85-09-09,MCR6979),
     audit(85-12-17,CLJones), install(86-03-21,MR12.0-1033):
     FIPS support by
     checking for device zero, add tri-density for STC drives, and survey to
     the DRIVE instead of controller for FIPS.
  2) change(86-04-11,Farley), approve(86-04-11,MCR6979),
     audit(86-04-11,Fawcett), install(86-04-14,MR12.0-1039):
     Post Bug Fix (PBF) to change FIPS attachment to attach the psuedo IOI
     controller entry, instead of the FIPS device. This was needed to correct a
     problem with adding a device back on after it had been deleted during
     startup.
                                                   END HISTORY COMMENTS */

/* format: style4,delnl,insnl,ifthenstmt,indnoniterend */
rcp_tape_survey_:
     procedure (Device, Drive, Fips, Operational, Qualifiers, Code);

/*	Perform survey of all tape devices.
   *	Created on 11/13/74 by Bill Silver.
   *	Modified on 1/20/77 by Noel I. Morris for multiple tape controllers.
   *	Modified 31 July 1981 to avoid looping forever on a dead tape controller, W. Olin Sibert
   *	Modified September 1981 by J. Bongiovanni to set mask to open-level
   *	Modified 9/81 to identify itself in all syserr messages, M.R. Jordan
   *	Modified June 1982 by C. Hornig to run in ring 1.
   *	Modified 4/10/84 by GA Texada to support FIPS.
   *	Modified 8/01/84 by Paul Farley to add check for zero
   *	survey_data.handler data, so device 0 checking would work properly.
   *	Modified 1/85 by Paul Farley for tri-density STC tape drives.
   *
   *	This is a system initialization procedure.  It is called by rcp_init.
   *	It will use IOI to perform a survey_devices command to the tape controller.
*/

/*		ARGUMENT  DATA		*/

dcl  Device char (*) parameter;
dcl  Drive fixed bin parameter;
dcl  Fips bit (1) aligned parameter;
dcl  Operational bit (1) aligned parameter;
dcl  Qualifiers (4) fixed bin (35) parameter;
dcl  Code fixed bin (35) parameter;


/*		AUTOMATIC  DATA		*/

dcl  ecode fixed bin (35);				/* error_table_ code. */
dcl  ioi_index fixed bin;				/* IOI device index. */
dcl  i fixed bin;
dcl  ws_ptr ptr;					/* Pointer to IOI workspace segment. */
dcl  1 wait_list aligned,				/* for ipc_$block */
       2 count fixed bin init (1),
       2 pad fixed bin,
       2 ev_chn (1) fixed bin (71);
dcl  1 ev_msg aligned like event_wait_info;

/*		BASED  DATA		*/

dcl  1 ws aligned based (ws_ptr),			/* IOI workspace. */
       2 idcw bit (36),				/* Survey_devices tape command. */
       2 dcw bit (36),				/* DCW for survey_devices command. */
       2 survey_data,
         3 handler (16) unaligned,
	 4 pad1 bit (1),
	 4 reserved bit (1),
	 4 operational bit (1),
	 4 ready bit (1),
	 4 number uns fixed bin (5),
	 4 pad2 bit (1),
	 4 speed uns fixed bin (3),
	 4 nine_track bit (1),
	 4 density uns fixed bin (4);

dcl  (addr, binary, bit, rel, size, unspec) builtin;

/* CONSTANTS */

dcl  density_table (0:15) bit (5) static options (constant) /* Used to set density characteristic. */
	init ("00010"b,				/* 1600 */
	"11100"b,					/* 200 , 556 , 800 */
	"00000"b,					/* Invalid */
	"00000"b,					/* Invalid */
	"11110"b,					/* 200, 556, 800, 1600 */
	"01110"b,					/* 556, 800, 1600 */
	"00000"b,					/* Invalid */
	"00111"b,					/* 800, 1600, 6250 */
	"00110"b,					/* 800, 1600 */
	"01100"b,					/* 556, 800 */
	"00000"b,					/* Invalid */
	"00011"b,					/* 1600, 6250 */
	"00000"b,					/* Invalid */
	"00000"b,					/* Invalid */
	"00000"b,					/* Invalid */
	"00000"b);				/* Invalid */

dcl  speed_table (0:7) bit (3) static options (constant)	/* Used to set speed characteristics. */
	init ("000"b,				/* Invalid */
	"100"b,					/* 75 ips */
	"010"b,					/* 125 ips */
	"000"b,					/* Invalid */
	"001"b,					/* 200 ips */
	"000"b,					/* Invalid */
	"000"b,					/* Invalid */
	"000"b);					/* Invalid */

/* EXTERNAL */

dcl  admin_gate_$ioi_attach entry (fixed bin, char (*), fixed bin (71), bit (1) aligned, fixed bin (35));
dcl  admin_gate_$ioi_detach entry (fixed bin, fixed bin (35));
dcl  admin_gate_$syserr entry options (variable);
dcl  ipc_$block entry (ptr, ptr, fixed bin (35));
dcl  ipc_$create_ev_chn entry (fixed bin (71), fixed bin (35));
dcl  ipc_$delete_ev_chn entry (fixed bin (71), fixed bin (35));
dcl  ioi_$connect entry (fixed bin, fixed bin, fixed bin (35));
dcl  ioi_$timeout entry (fixed bin, fixed bin (71), fixed bin (35));
dcl  ioi_$workspace entry (fixed bin, ptr, fixed bin, fixed bin (35));
%page;
	Qualifiers (*) = 0;
	Operational = "0"b;
	wait_list.ev_chn (1) = 0;
	ioi_index = 0;

	call ipc_$create_ev_chn (wait_list.ev_chn (1), Code);
	if Code ^= 0 then goto return_to_caller;

	if Fips
	then call admin_gate_$ioi_attach (ioi_index, Device, wait_list.ev_chn (1), "1"b, Code);
	else call admin_gate_$ioi_attach (ioi_index, Device || "_00", wait_list.ev_chn (1), "1"b, Code);
	if Code ^= 0 then goto return_to_caller;

	call ioi_$workspace (ioi_index, ws_ptr, size (ws), Code);
	if Code ^= 0 then goto return_to_caller;

	call ioi_$timeout (ioi_index, 1000000 /* 1 sec */, Code);
	if Code ^= 0 then goto return_to_caller;

	ws.idcw = "570000700000"b3;			/* Set up survey_devices tape command. */
	ws.dcw = ""b;				/* Set up DCW for survey_devices command. */
	dcwp = addr (ws.dcw);
	dcw.address = rel (addr (ws.survey_data));	/* Put survey_device info in workspace survey_data. */
	dcw.tally = bit (binary (8, 12));		/* Should return 8 words at most. */

	call ioi_$connect (ioi_index, binary (rel (addr (ws.idcw)), 18), Code);
	if Code ^= 0 then goto return_to_caller;

	call ipc_$block (addr (wait_list), addr (ev_msg), Code);
	if Code ^= 0 then goto return_to_caller;

	imp = addr (ev_msg.message);
	if imess.er
	then call admin_gate_$syserr (BEEP, "rcp_tape_survey_: Error surveying controller ^a: ^w", Device, imess.status)
		;

	else if imess.time_out
	then call admin_gate_$syserr (BEEP, "rcp_tape_survey_: Tape controller ^a did not respond within one second.",
		Device);

	do i = 1 to 16;
	     if unspec (ws.survey_data.handler (i)) ^= ""b then do;
		if ws.survey_data.handler (i).number = Drive then do;
		     if Operational then do;
			call admin_gate_$syserr (BEEP, "rcp_tape_survey_: Multiple device ^d's found on ^a.", Drive,
			     Device);
			Operational = "0"b;
			goto return_to_caller;
			end;

		     if ws.survey_data.handler (i).operational then do;
			Operational = "1"b;
			if ws.survey_data.handler (i).nine_track
			then Qualifiers (1) = 9;
			else Qualifiers (1) = 7;
			unspec (Qualifiers (2)) = density_table (ws.survey_data.handler (i).density);
			unspec (Qualifiers (3)) = speed_table (ws.survey_data.handler (i).speed);
			end;
		     end;
		end;
	end;

return_to_caller:
	if ioi_index ^= 0 then call admin_gate_$ioi_detach (ioi_index, ecode);
	if wait_list.ev_chn (1) ^= 0 then call ipc_$delete_ev_chn (wait_list.ev_chn (1), ecode);

	return;
%page;
%include event_wait_info;
%include ioi_stat;
%include iom_dcw;
%include iom_pcw;
%include syserr_constants;
%page;
/* BEGIN MESSAGE DOCUMENTATION

   Message:
   rcp_tape_survey_: Error surveying controller CONTROLLER: ERROR_STATUS

   S: $beep

   T: $init

   M: An error was received from IOI while attempting to survey the tape controller
   DEVICE.
   $err

   A: $inform
   $recover

   Message:
   rcp_tape_survey_: Tape controller CONTROLLER did not respond within one second.

   S: $beep

   T: $init

   M: The tape controller or IOM failed to respond to the survey-devices
   command by sending an interrupt within one second.

   A: Contact field engineering personnel.

   Message:
   rcp_tape_survey_: Multiple device NUMBER's found on CONTROLLER.

   S: $beep

   T: $init

   M: The survey information indicates that there are multiple devices
   with the same device number.

   A: Contact field engineering personnel.

   END MESSAGE DOCUMENTATION */

     end rcp_tape_survey_;
