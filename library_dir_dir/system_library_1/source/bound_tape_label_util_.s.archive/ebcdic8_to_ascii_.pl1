/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1982 *
        *                                                         *
        * Copyright (c) 1972 by Massachusetts Institute of        *
        * Technology and Honeywell Information Systems, Inc.      *
        *                                                         *
        *********************************************************** */


ebcdic8_to_ascii_: proc (input, output);

/* This subroutine converts from packed 8 bit EBCDIC to 9 bit ASCII.

   Usage:
   dcl ebcdic8_to_ascii_ entry(bit(*),char(*))
   call ebcdic8_to_ascii_(input bit string, output char string)

   Note:
   The input is converted in 8 bit groups, any leftover bits in the input string are
   ignored, the number of characters converted will be:  divide(length(input), 8, 24).

   The conversion table defined in ebcdic_to_ascii is used for the tranlation.

   Written by:  R.J.C. Kissel 11/09/76
*/

/* Formal parameters */

dcl  input bit (*);
dcl  output char (*);

/* External variables */

dcl  ebcdic_to_ascii_$ea_table char (256) external static;

/* Automatic */

dcl  char_code fixed bin;
dcl  char_limit fixed bin;
dcl  input_chars (1:divide (length (input), 8, 24)) bit (8) based (addr (input));
						/* divide the input string into 8 bit chars */
dcl  char_index fixed bin;
dcl (addr, divide, fixed, hbound, lbound, length, substr, min) builtin;

	output = "";

	char_limit = min (hbound (input_chars, 1), length (output)); /* only output as much as posible */
	do char_index = lbound (input_chars, 1) to char_limit by 1;
	     char_code = fixed (input_chars (char_index), 17);
	     substr (output, char_index, 1) = substr (ebcdic_to_ascii_$ea_table, char_code+1, 1);
	end;

     end ebcdic8_to_ascii_;
