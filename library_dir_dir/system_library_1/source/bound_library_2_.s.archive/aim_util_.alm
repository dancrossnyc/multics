" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Bull Inc., 1987                *
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1984 *
" *                                                         *
" ***********************************************************

" aim_util_ -- utilities that construct and deconstruct access classes
"              and authorizations.
"
"  BIM 84-04-26
"  Modified 1985-05-02, BIM: don't assume double-word alignment.
"
	name	aim_util_
	entry	get_access_class
	entry	get_level
	entry	get_categories
	entry	get_privileges
	entry	make_access_class

	include	aim_template

" 

"	CONVENTIONS
"
"	ap	pr0 (argument list)


" GET_ACCESS_CLASS

	set	auth,2	" arg offset
	set	acc,4	" arg offset

get_access_class:
	epp1	ap|auth,*
	lda	pr1|0		" get the access class
	ldq	pr1|1
         	anaq	access_class_mask   " remove privileges
	epp1	ap|acc,*
	sta	pr1|0
	stq	pr1|1
	short_return

" GET_LEVEL

	set	acc,2		" arguments are different
	set	level,4

get_level:
	epp1	ap|acc,*
	lda	pr1|0		" get the access class
	ldq	pr1|1
	qrl	18		" level DU in Q
	stq	ap|level,*
	short_return

" GET_CATEGORIES

	set	acc,2	" access class
	set	cats,4 	" categories

get_categories:
	lda	ap|acc,*	" First word is categories
	sta	ap|cats,* " simple, no?
	short_return

" GET_PRIVILEGES

	set	auth,2
	set	privs,4

get_privileges:
	epp1	ap|auth,*
	ldq	pr1|1		" just this word
	anq	=o777777,dl	" privs are low half of second word
	qls	18                  " return bit (36) aligned
	stq	ap|privs,*
	short_return

" MAKE_ACCESS_CLASS

	set	level,2
	set	cats,4
	set	acc,6

make_access_class:
	lda	ap|cats,*
	ldq	ap|level,*
	qls	18		" DU
	anaq	access_class_mask	" in case of junk
	epp1	ap|acc,*
	sta	pr1|0
	stq	pr1|1
	short_return

	even
access_class_mask:
	vfd	18/-1,18/0,15/0,3/7,18/0
	end
