/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1986 *
        *                                                         *
        *********************************************************** */

/* format: style4,declareind10,dclind10,idind20,indattr,delnl,insnl,tree,ifthenstmt */

bce_ioi_post:
     proc (ioi_event_channel, ioi_message);

/****^  HISTORY COMMENTS:
  1) change(86-09-05,Farley), approve(86-07-18,MCR7439),
     audit(86-09-24,Fawcett), install(86-10-20,MR12.0-1189):
     Program to post an I/O complete for IOI at BCE.
                                                   END HISTORY COMMENTS */

	if sys_info$service_system			/* shouldn't be here */
	then call syserr (CRASH, "bce_ioi_post: System not at correct level to post I/O completion.");
	ioi_post_area_ptr = addr (bce_ioi_post_seg$);
	imp = addr (ioi_message);
	do bufx = 1 to ioi_post_area.number;
	     if ioi_post_area.buffer (bufx).ev_chn = ioi_event_channel then do;
		io_post_buffer_ptr = addr (ioi_post_area.buffer (bufx));
		if imess.level = "7"b3 then do;
		     if io_post_buffer.state ^= WAITING_SPECIAL then goto next_post_buffer;
		     io_post_buffer.message = ioi_message;
		     io_post_buffer.state = SPECIAL_ARRIVED;
		end;
		else if io_post_buffer.state ^= IO_OUTSTANDING
		     then goto next_post_buffer;
		     else do;
			io_post_buffer.message = ioi_message;
			io_post_buffer.state = IO_COMPLETE;
		     end;
		return;
	     end;
next_post_buffer:
	end;
crash:
	if imess.level = "1"b3
	then call syserr (ANNOUNCE,
		"bce_ioi_post: Unexpected System Fault.^/^14xioi_event_channel: ^24.3b^/^14xioi_message: ^24.3b",
		unspec (ioi_event_channel), unspec (ioi_message));
	else if imess.level ^= "7"b3			/* ignore unwanted specials, but crash on others.. */
	     then call syserr (CRASH,
		     "bce_ioi_post: Error posting I/O event.^/^14xioi_event_channel: ^24.3b^/^14xioi_message: ^24.3b",
		     unspec (ioi_event_channel), unspec (ioi_message));
	return;
%page;
dcl	based_bin		fixed bin based;
dcl	bufx		fixed bin;
dcl	ioi_event_channel	fixed bin (71) parameter;
dcl	ioi_message	fixed bin (71) parameter;
dcl	sys_info$service_system
			bit (1) aligned external static;
dcl	syserr		entry options (variable);
%page;
%include bce_ioi_post_area;
%page;
%include ioi_stat;
%page;
%include syserr_constants;
%page;
/* BEGIN MESSAGE DOCUMENTATION

   Message:
   bce_ioi_post: System not at correct level to post I/O completion.

   S: $crash

   T: $init

   M: The flag sys_info$service_system indicates that the system is up and
   should be posting I/O completions via pxss$io_wakeup, not this program.
   $err

   A: $notify

   Message:
   bce_ioi_post: Unexpected System Fault.
   ioi_event_channel: CCCCCCCCCCCC ioi_message: MMMMMMMMMMMM

   S: $crash

   T: $init

   M: $err

   A: $notify

   Message:
   bce_ioi_post: Error posting i/o completion.
   ioi_event_channel: CCCCCCCCCCCC ioi_message: MMMMMMMMMMMM

   S: $crash

   T: $init

   M: No posting buffer was found in an "I/O outstanding" state for the
   above event channel.
   $err

   A: $notify

   END MESSAGE DOCUMENTATION */

     end bce_ioi_post;
