" ******************************************
" *                                        *
" * Copyright, (C) Honeywell Limited, 1985 *
" *                                        *
" ******************************************

" HISTORY COMMENTS:
"  1) change(86-07-14,BWong), approve(86-07-14,MCR7413),
"     audit(86-07-16,Ginter), install(86-07-28,MR12.0-1104):
"     Make code more efficient.
"                                                      END HISTORY COMMENTS


name	double_arc_tangent_
" Modification history:
"	Written by H. Hoover, M. Mabey, and B. Wong, April 1985,
"	based on the GCOS routine '7nan'.
"
" Function:  Approximate to double precision the principal value, in radians
"	or degrees, of the arctangent of (x, y) or z where z=x/y for any
"         valid input argument(s).  For atan(z) the answer is in quadrant 1
"	or 4 (-pi/2<=atan<=pi/2, -90<=atan<=90).  For atan(x,y) the answer
"	will be in the correct quadrant (-pi<=atan2<=pi, -180<=atan2<=180).
"
"	Modified: March 18, 1986 by B. Wong - Make code more efficient by
"		replacing 
"
"		  range_0_to_1:	fcmg	tan_pi_by_32,x2
"				tmi	range_0
"		  range_1:	tra	calculate_for_range_1_to_7
"		  range_0:
"
"		with
"
"		  range_0_to_1:	fcmg	tan_pi_by_32,x2
"		  range_1:	tpl	calculate_for_range_1_to_7
"		  range_0:
"
" Entry:	through the appropriately named entry point with:
" 	EAQ = the first argument (z or x).
"	PR1 = the address of the second argument (y).
"	PR2 = the address of a 14 word, even-word aligned scratch area.
"	PR3 = the return address.
"
" Exit:	EAQ = the desired arctangent in radians or degrees.
"
" Uses:	X0, X1, X2, X3, X4
"	X0 = saves a return address from arctan
"	X1 = saves a return address from arctan2
"	X2 = indicates BFP or HFP mode - all the floating point math
"	     routines use this register for the same purpose.
"	X3 = saves a return address from part_arctan
"	X4 = index to tables


	segref	math_constants_,half_pi,hfp_one_radian,one_radian,pi

	equ	BFP,0
	equ	HFP,2
	equ	z,0
	equ	zz,2
	equ	arctan_z,4
	equ	x,6
	equ	y,8
	equ	indicators,10
	equ	abs_z_minus_u,12

	segdef	double_arc_tan_degrees_,double_arc_tangent_degrees_
	segdef	double_arc_tan_degrees_2_,double_arc_tangent_degrees_2_
	segdef	double_arc_tan_radians_,double_arc_tangent_radians_
	segdef	double_arc_tan_radians_2_,double_arc_tangent_radians_2_
	segdef	hfp_double_arc_tan_degrees_
	segdef	hfp_double_arc_tan_degrees_2_
	segdef	hfp_double_arc_tan_radians_
	segdef	hfp_double_arc_tan_radians_2_

double_arc_tangent_degrees_:
double_arc_tan_degrees_:
	eax2      BFP		" no offset for BFP constants
	tsx0      arctan		" EAQ := arctan (x)
	dfmp      one_radian	" convert radians to degrees
	dfrd	0
	tra       pr3|0		" return

double_arc_tangent_degrees_2_:
double_arc_tan_degrees_2_:
	eax2      BFP		" no offset for BFP constants
	tsx1      arctan2		" EAQ := arctan2 (x,y)
	dfmp      one_radian	" convert radians to degrees
	dfrd	0
	tra       pr3|0		" return

double_arc_tangent_radians_:
double_arc_tan_radians_:
	eax2      BFP		" no offset for BFP constants
	tsx0      arctan		" EAQ := arctan (x)
	dfrd	0
	tra       pr3|0		" return

double_arc_tangent_radians_2_:
double_arc_tan_radians_2_:
	eax2      BFP		" no offset for BFP constants
	tsx1      arctan2		" EAQ := arctan2 (x,y)
	dfrd	0
	tra       pr3|0		" return

hfp_double_arc_tan_degrees_:
	eax2      HFP		" 2 word offset for HFP constants
	tsx0      arctan		" EAQ := arctan (x)
	dfmp      hfp_one_radian	" convert radians to degrees
	dfrd	0
	tra       pr3|0		" return

hfp_double_arc_tan_degrees_2_:
	eax2      HFP		" 2 word offset for HFP constants
	tsx1      arctan2		" EAQ := arctan2 (x,y)
	dfmp      hfp_one_radian	" convert radians to degrees
	dfrd	0
	tra       pr3|0		" return

hfp_double_arc_tan_radians_:
	eax2      HFP		" 2 word offset for HFP constants
	tsx0      arctan		" EAQ := arctan (x)
	dfrd	0
	tra       pr3|0		" return

hfp_double_arc_tan_radians_2_:
	eax2      HFP		" 2 word offset for HFP constants
	tsx1      arctan2		" EAQ := arctan2 (x,y)
	dfrd	0
	tra       pr3|0		" return

arctan:
	fad	=0.0,du		" normalize input and set indicators
	dfst	pr2|arctan_z	" store argument z

" Find which of the 9 ranges abs(z) lies in using a binary search.

" Set X4 as the range indicator.  X4 is set to X4+4*(range-1) since double
" precision tables with decimal BFP and octal HFP values are used.

	eax4	0,x2		" initialize the table index with BFP or HFP offset

	fcmg	tan_7_pi_by_32,x2
	tmi	range_0_to_3

	fcmg	tan_13_pi_by_32,x2
	tmi	range_4_to_6

	fcmg	tan_15_pi_by_32,x2
	tmi	range_7

range_8:
				" range = 8, abs (z) >= tan_15_pi_by_32
	dfcmg	eps1,x2
	tmi	3,ic		" if abs (z) < 1e71b:
	  dfld	half_pi,x2	"   EAQ := radians = half_pi
	  tra	set_to_quadrant_1_or_4
				" else:
	fad	=0.0,du
	tpl	2,ic
	  fneg	0		"   EAQ := abs (z)
	dfdi	=-1.0d0		"   EAQ := -1/abs_z
	tsx3	part_arctan	"   calculate part_arctan (-1/abs_z)
				"     which is equivalent to - (part_arctan (1/abs_z))
	dfad	half_pi,x2	"   EAQ := radians = half_pi - part_arctan (1/abs_z)
	tra	set_to_quadrant_1_or_4

range_7:
	adx4	=24,du		" range = 7, tan_13_pi_by_32 <= abs (z) < tan_15_pi_by_32
	tra	calculate_for_range_1_to_7

range_4_to_6:
	fcmg	tan_11_pi_by_32,x2
	tmi	range_4_to_5

range_6:
	adx4	=20,du		" range = 6, tan_11_pi_by_32 <= abs (z) < tan_13_pi_by_32
	tra	calculate_for_range_1_to_7

range_4_to_5:
	fcmg	tan_9_pi_by_32,x2
	tmi	range_4

range_5:
	adx4	=16,du		" range = 5, tan_9_pi_by_32 <= abs (z) < tan_11_pi_by_32
	tra	calculate_for_range_1_to_7

range_4:
	adx4	=12,du		" range = 4, tan_7_pi_by_32 <= abs (z) < tan_9_pi_by_32
	tra	calculate_for_range_1_to_7

range_0_to_3:
	fcmg	tan_3_pi_by_32,x2
	tmi	range_0_to_1

	fcmg	tan_5_pi_by_32,x2
	tmi	range_2

range_3:
	adx4	=8,du		" range = 3, tan_5_pi_by_32 <= abs (z) < tan_7_pi_by_32
	tra	calculate_for_range_1_to_7

range_2:
	adx4	=4,du		" range = 2, tan_3_pi_by_32 <= abs (z) < tan_5_pi_by_32
	tra	calculate_for_range_1_to_7

range_0_to_1:
	fcmg	tan_pi_by_32,x2

range_1:
				" range = 1, tan_pi_by_32 <= abs (z) < tan_3_pi_by_32
	tpl	calculate_for_range_1_to_7

range_0:
				" range = 0, abs (z) < tan_pi_by_32
	fad	=0.0,du
	tpl	2,ic
	  fneg	0		" EAQ := abs (z)
	tsx3	part_arctan	" EAQ := part_arctan (abs_z)
	tra	set_to_quadrant_1_or_4

calculate_for_range_1_to_7:
	fad	=0.0,du
	tpl	2,ic
	  fneg	0		" EAQ := abs (z)

	dfsb	u,x4		" EAQ := abs_z - u(range)
	dfst	pr2|abs_z_minus_u
	dfad	u,x4		" EAQ := abs_z
	dfmp	u,x4		" EAQ := abs_z * u(range)
	fad	one,x2		" EAQ := 1 + abs_z*u(range)
	dfdi	pr2|abs_z_minus_u	" EAQ := t

	tsx3	part_arctan	" EAQ := part_arctan (t)
	dfad	arctan_of_u,x4	" EAQ := radians = part_arctan (t) + arctan(u(range))

set_to_quadrant_1_or_4:
	fszn	pr2|arctan_z	" set indicators
	tpl	0,x0		" if z >= 0 then return (radians)
	  fneg	0		" else return (-radians)
	  tra	0,x0

part_arctan:
				" EAQ contains z arg
	fcmg	eps2,x2		" if abs (z) < 5.7031627e10
	tmi	0,x3		" then return (z)

	dfstr	pr2|z
	dfmp	pr2|z		" calculate zz = z*z
	dfstr	pr2|zz
	dfmp	p7,x2		" calculate p(zz)
	dfad	p6,x2
	dfmp	pr2|zz
	dfad	p5,x2
	dfmp	pr2|zz
	dfad	p4,x2
	dfmp	pr2|zz
	dfad	p3,x2
	dfmp	pr2|zz
	dfad	p2,x2
	dfmp	pr2|zz
	dfad	p1,x2
	dfmp	pr2|zz
	dfad	one,x2
	dfmp	pr2|z		" calculate z*p(zz)
	tra	0,x3		" return

arctan2:
	fad	=0.0,du		" normalize x
	dfst	pr2|x		" save normalized x for quadrant check
	dfld	pr1|0		" load y
	fad	=0.0,du		" normalize y
	dfst	pr2|y		" save normalized y for quadrant check
	tnz	y_not_zero
	fszn	pr2|x		" test if x = 0 also
	tze	arctan2_domain_err	" 0/0 is error
	dfld	half_pi,x2	" atan(x/0) =  + or - (half_pi)
	fszn	pr2|x
	tpl	0,x1		" if x >= 0 then return (radians)
	  fneg	0		" else return (-radians)
	  tra	0,x1


y_not_zero:
	sti	pr2|indicators	" save indicators
	ldi	no_overflow,x2
	dfdi	pr2|x		" EAQ := x/y
	teo	quotient_too_large	" if overflow, atan(x,y) = pi/2 or -pi/2
	teu	quotient_too_small	" if underflow, atan(x,y) = 0
	ldi	pr2|indicators	" restore previous indicators
	fad	=0.0,du		" set indicators
	tpl	2,ic		" calculate z = abs (x,y)
	  fneg	0
	tsx0	arctan		" EAQ := arctan(z)

set_quadrant:
	fszn	pr2|y		" set the quadrant
	tpl	3,ic		" if y < 0 then
	  fneg	0		"   radians = pi-radians
	  dfad	pi,x2
	fszn	pr2|x
	tpl	0,x1		" if x >= 0 then return (radians)
	  fneg	0		" else return (-radians)
	  tra	0,x1

				" error when x=0 and y=0
arctan2_domain_err:
	ldq	24,dl
	tsx0	<call_math_error_>|[call_math_error_]
	fld	=0.0,du
	tra	pr3|0		" return to caller

quotient_too_small:
	ldi	pr2|indicators	" restore indicators
	fld	=0.0,du		" radians = 0.0
	tra	set_quadrant

quotient_too_large:
	ldi	pr2|indicators	" restore indicators
	dfld	half_pi,x2	" radians = half_pi
	tra	set_quadrant

	even

eps1:	oct	220400000000,000000000000	" 2**71 = 2.36e21
	oct	044400000000,000000000000
eps2:	dec	5.7031627d-10
	oct	762116304341,000000000000
no_overflow:
	oct	000000004000,000000000000	" bit 25 is the overflow mask
	oct	000000004010,000000000000	" bit 33 is the hex indicator

" This is the table of ranges.

tan_pi_by_32:
	dec	.98491403d-1		" tan(pi/32)
	oct	000062332734,000000000000
tan_3_pi_by_32:
	dec	.30334668d00		" tan(3*pi/32)
	oct	000233240406,000000000000
tan_5_pi_by_32:
	dec	.53451114d00		" tan(5*pi/32)
	oct	000421526707,000000000000
tan_7_pi_by_32:
	dec	.82067879d00		" tan(7*pi/32)
	oct	000644140013,000000000000
tan_9_pi_by_32:
	dec	1.2185035d00		" tan(9*pi/32)
	oct	002046773754,000000000000
tan_11_pi_by_32:
	dec	1.8708684d00		" tan(11*pi/32)
	oct	002073674236,000000000000
tan_13_pi_by_32:
	dec	3.2965582d00		" tan(13*pi/32)
	oct	002151372636,000000000000
tan_15_pi_by_32:
	dec	10.153170d00		" tan(15*pi/32)
	oct	002504715423,000000000000

" This table is values of u, where u(i)=...

u:	dec	1.98912367379658006912d-01    " tan(pi/16)
	oct	000145657536,012514254010
	dec	4.14213562373095048802d-01	" tan(2*pi/16)
	oct	000324047463,177167462204
	dec	6.68178637919298919998d-01	" tan(3*pi/16)
	oct	000526067012,533771440573
	dec	1.0d0			" tan(4*pi/16)
	oct	002040000000,000000000000
	dec	1.49660576266548901760d00	" tan(5*pi/16)
	oct	002057710307,045516430250
	dec	2.41421356237309504880d00	" tan(6*pi/16)
	oct	002115202363,147747363110
	dec	5.02733949212584810451d00	" tan(7*pi/16)
	oct	002240677734,220443561021

" This table is values of arctan(u(i)).

arctan_of_u:
	dec	.19634954084936207740d00	" pi/16
	oct	000144417665,210413214107
	dec	.39269908169872415481d00	" 2*pi/16
	oct	000311037552,421026430215
	dec	.58904862254808623221d00	" 3*pi/16
	oct	000455457437,631441644324
	dec	.78539816339744830962d00	" 4*pi/16
	oct	000622077325,042055060432
	dec	.98174770424681038702d00	" 5*pi/16
	oct	000766517212,252470274541
	dec	1.17809724509617246442d00	" 6*pi/16
	oct	002045545743,763144164432
	dec	1.37444678594553454182d00	" 7*pi/16
	oct	002053766737,233564735237

" These constants are used to approximate atan over the range [0,tan(pi/32)].

one:      dec	1.0d0
	oct	002040000000,000000000000
p1:	dec	-.33333333333333333154d00
	oct	001525252525,252525252546
p2:	dec	+.19999999999999612046d00
	oct	000146314631,463146206723
p3:	dec	-.14285714285394000547d00
	oct	001666666666,667047430233
p4:	dec	+.1111111098121285609d00
	oct	000070707070,555661001627
p5:	dec	-.9090880462996335d-01
	oct	001721350632,623104337257
p6:	dec	+.76888077127566d-01
	oct	000047273577,013343060615
p7:	dec	-.64430854376d-01
	oct	001737005655,071501356653

	end
