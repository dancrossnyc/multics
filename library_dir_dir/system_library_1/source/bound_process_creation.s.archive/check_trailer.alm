" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Bull Inc., 1987                *
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

"
"	check_trailer
"
"	This program loops through the entire trailer segment searching
"	for any trailers which belong to a process which has been (is being)
"	destroyed.  It is a debugging temporary program only.
"
" Modified 04/16/81, W. Olin Sibert, to get count of trailers from sst$n_trailers
"

	name	check_trailer
	entry	check_trailer

check_trailer:

"
"	Call is:
"
"	error = check_trailer(rel_dstep)
"
"	dcl error bit (1) aligned, rel_dstep dixed bin (18)
"
	lda	pr0|2,*		pick up the pointer to the DST entry
	stz	pr0|4,*		zero out return error code

	ldq	sst$n_trailers	get count of trailers in system
	qls	1		multiply by two (size of a trailer)
	eax0	0,ql		and move to X0

	ldq	-1,du		use mask for right half of word 1 of trailer entry
	epp2	str_seg$+0	get pointer to trailer segment itself

loop:	eax0	-2,x0		go to next trailer
	tmi	ok		if haven't found bad one by now we're OK
	cmk	pr2|1,x0		compare a with rel(dstep) in trailer
	tze	trouble		the same, trouble. return "1"b
	tra	loop

ok:	short_return

trouble:	lda	=o400000,du	return "1"b if error
	sta	pr0|4,*
	short_return

	end
