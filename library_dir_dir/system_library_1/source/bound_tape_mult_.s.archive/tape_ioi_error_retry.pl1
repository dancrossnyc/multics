/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1982 *
        *                                                         *
        *********************************************************** */



/****^  HISTORY COMMENTS:
  1) change(87-07-06,Hartogs), approve(87-07-06,MCR7726),
     audit(87-08-27,GWMay), install(87-08-27,MR12.1-1094):
     A) Error recovery changed to rewind on error at first label instead of a
        backspace and erase.
     B) Added SET_DENSITY routine.
     C) Changed dcw commands to use descriptive variables.
                                                   END HISTORY COMMENTS */


/* This is where all the tape error recovery is done.  */

/* Written Aug 1982 by Sherman D. Sprague. */
/* Modified October 1983 by Chris Jones to add eof entry. */

/* format: style4,delnl,insnl,indattr,ifthen,dclind9 */
tape_ioi_error_retry:
     proc (arg_wksp_ptr, arg_buf_ptr, arg_status_ptr, arg_code);

/* Parameters */

dcl      arg_buf_ptr	  ptr parameter;		/* (I/O) pointer to  the data to be or just processed */
dcl      arg_code		  fixed bin (35) parameter;	/* (O) system status code */
dcl      arg_lost_status_cnt	  fixed bin parameter;	/* (I) count of number of statuses lost */
dcl      arg_wksp_ptr	  ptr parameter;		/* (O) pointer to the workspace */
dcl      arg_status_ptr	  ptr parameter;

/* Automatic variables */

dcl      backspace_cnt	  fixed bin;
dcl      block_count            fixed bin;
dcl      code		  fixed bin (35);
dcl      deadline		  fixed bin (71);
dcl      status_present	  bit (1) aligned init ("0"b);
dcl      status_special	  bit (36) aligned;
dcl      tries		  fixed bin;
dcl      write_sw		  bit (1) aligned;

/* Entries */

dcl      ioi_$connect	  entry (fixed bin, fixed bin (18), fixed bin (35));
dcl      ioi_$get_special_status
			  entry (fixed bin, bit (1) aligned, bit (36) aligned, fixed bin (35));
dcl      ipc_$block		  entry (ptr, ptr, fixed bin (35));
dcl      tape_ioi_utils$get_status
			  entry (ptr) returns (ptr);

dcl      timer_manager_$alarm_wakeup
			  entry (fixed bin (71), bit (2), fixed bin (71));
dcl      timer_manager_$reset_alarm_wakeup
			  entry (fixed bin (71));	/* External static variables */

dcl      error_table_$bad_density
			  fixed bin (35) ext static;
dcl      error_table_$device_active
			  fixed bin (35) ext static;
dcl      error_table_$device_parity
			  fixed bin (35) ext static;

/* Builtins */

dcl      (addr, addrel, bin, bit, clock, rel, substr, unspec)
			  builtin;

%page;

	wksp = arg_wksp_ptr;
	if tai.density_command = ""b then do;
	     arg_code = error_table_$bad_density;
	     return;
	end;
	dcwp = addr (tai.order_data);			/* use the  order data area as a scratch area */
	idcwp = addr (arg_buf_ptr -> tbi.idcw_word);	/* set up the current idcw */
	write_sw = substr (idcw.command, 3, 1);		/* are we writing ? */
	if write_sw then do;
	     if tai.at_bot then do;
		call REWIND (dcwp);			/* rewind to bot */
		call SET_DENSITY (dcwp);		/* and reset device density */
		call TDCW (dcwp, idcwp);		/* build idcw to bridge lists */
	     end;
	     else do;
		call BACKSPACE (1, dcwp);		/* go backspace */
		call ERASE (dcwp);
		call TDCW (dcwp, idcwp);		/* build idcw to bridge our lists */
	     end;
	     call CONNECT (addr (tai.order_data));	/* send him on his way... */
	end;

	else do;
	     if idcw.chan_cmd = MAX_READ_OPT then do;		/* have we tried all options yet */
		arg_code = error_table_$device_parity;	/* if so return code */
		return;
	     end;
	     idcw.chan_cmd = bit (bin (bin (idcw.chan_cmd) + 1, 6), 6);
						/* add one to the channel command */
	     call BACKSPACE (1, dcwp);		/* go backspace */
	     call TDCW (dcwp, idcwp);
	     call CONNECT (addr (tai.order_data));	/* send him on his way */
	end;
	return;

backspace:
     entry (arg_wksp_ptr, arg_buf_ptr, arg_status_ptr, arg_lost_status_cnt, arg_code);

	wksp = arg_wksp_ptr;			/* setup the workspace pointer */
	statp = addr (arg_status_ptr -> istat.iom_stat);
	dcwp = addr (tai.order_data);
	idcwp = addr (arg_buf_ptr -> tbi.idcw_word);	/* get the current idcw */

	if status.initiate then
	     backspace_cnt = arg_lost_status_cnt;
	else backspace_cnt = arg_lost_status_cnt + 1;
	call BACKSPACE (backspace_cnt, dcwp);		/* go backspace */
	call TDCW (dcwp, idcwp);
	call CONNECT (addr (tai.order_data));		/* send him on his way.... */
	return;

eof:
     entry (arg_wksp_ptr, arg_code);

	wksp = arg_wksp_ptr;
	idcwp = addr (tai.order_data (1));
	dcwp = addr (tai.order_idcw);
	call BACKSPACE (1, dcwp);
	call ERASE (dcwp);
	call CONNECT (addr (tai.order_idcw));
	return;


/* This procedure will backspace the tape */

BACKSPACE:
     proc (rec_bk, dcwp);
dcl      rec_bk		  fixed bin parameter;	/* records to be backspaced */
dcl      dcwp		  ptr parameter;		/* pointer into IDCW list */

	dcwp -> idcw = idcw;
	dcwp -> idcw.command = BACKSPACE_ONE_RECORD;
	dcwp -> idcw.control = CONTINUE_NO_MARKER;
	dcwp -> idcw.chan_cmd = NONDATA_TRANSFER;
	dcwp -> idcw.count = bit (bin (rec_bk, 6), 6);
	dcwp = addrel (dcwp, 1);

     end BACKSPACE;

BLOCK:
     proc;


dcl      1 auto_event_wait_info aligned like event_wait_info;

	event_wait_channel.channel_id = tai.event_id;
	if tai.special_status_expected then
	     call timer_manager_$alarm_wakeup (TWO_MINUTES, RELATIVE_SECONDS, tai.event_id);
	call ipc_$block (addr (event_wait_channel), addr (auto_event_wait_info), (0));
	if tai.special_status_expected then
	     call timer_manager_$reset_alarm_wakeup (tai.event_id);
						/* remove the extra event */

     end BLOCK;

CONNECT:
     proc (idcwp);

dcl      idcwp		  ptr parameter;		/* pointer to start of dcw list */

	deadline = clock () + TEN_SECONDS;
	do while ("1"b);
	     do tries = 1 to 10;
		call ioi_$connect (tai.ioi_index, bin (rel (idcwp)), arg_code);
		if arg_code ^= error_table_$device_active then
		     return;
	     end;
	     if clock () > deadline then
		return;
	end;

     end CONNECT;

/* This procedure will erase one record on the tape */

ERASE:
     proc (dcwp);
dcl      dcwp		  ptr parameter;		/* pointer into IDCW list */

	dcwp -> idcw = idcw;
	dcwp -> idcw.command = ERASE_COMMAND;
	dcwp -> idcw.control = CONTINUE_NO_MARKER;
	dcwp -> idcw.chan_cmd = NONDATA_TRANSFER;
	dcwp -> idcw.count = ONE_COUNT;
	dcwp = addrel (dcwp, 1);

     end ERASE;

/* This procedure will rewind the tape */

REWIND:
     proc (dcwp);

dcl      dcwp		  ptr parameter;		/* pointer into IDCW list */

	dcwp -> idcw = idcw;
	dcwp -> idcw.command = REWIND_COMMAND;
	dcwp -> idcw.control = NO_CONTINUE_NO_MARKER;
	dcwp -> idcw.chan_cmd = NONDATA_TRANSFER;
	dcwp -> idcw.count = ONE_COUNT;

	call ioi_$get_special_status (tai.ioi_index, status_present, status_special, code);
						/* First call flushes residue status */
	call CONNECT (dcwp);
	status_present = "0"b;
	call ioi_$get_special_status (tai.ioi_index, status_present, status_special, code);

	tai.special_status_expected = "1"b;
	do block_count = 1 to 2;                          /* Allows up to four minutes to rewind. */
	     if ^status_present then do;
		call BLOCK;
		call ioi_$get_special_status (tai.ioi_index, status_present, status_special, code);
	     end;
	end;
	tai.special_status_expected = "0"b;

	isp = tape_ioi_utils$get_status (wksp);		/* call to get_status sets indexes correctly */
	return;

     end REWIND;

/* This procedure will set the device density to the stored value */

SET_DENSITY:
     proc (dcwp);
dcl      dcwp		  ptr parameter;		/* pointer into IDCW list */

	dcwp -> idcw = idcw;
	dcwp -> idcw.command = tai.density_command;
	dcwp -> idcw.control = CONTINUE_NO_MARKER;
	dcwp -> idcw.chan_cmd = NONDATA_TRANSFER;
	dcwp -> idcw.count = ONE_COUNT;
	dcwp = addrel (dcwp, 1);

     end SET_DENSITY;

/* This procedure will build a TDCW to join two idcw lists */

TDCW:
     proc (dcwp, dest_idcwp);
dcl      dcwp		  ptr parameter;		/* pointer into IDCW list */
dcl      dest_idcwp		  ptr parameter;		/* pointer to the rest of the list */

	tdcwp = dcwp;				/* point as to a tdcw */
	unspec (tdcw) = ""b;
	tdcw.address = rel (dest_idcwp);
	tdcw.type = TDCW_TYPE;
	tdcw.rel = RELATIVE_MODE;

     end TDCW;

%page;
%include event_wait_channel;
%page;
%include event_wait_info;
%page;
%include tape_ioi_workspace;
%page;
%include iom_pcw;
%page;
%include iom_dcw;
%page;
%include iom_stat;
%page;
%include ioi_stat;
%page;
%include io_status_word;
%page;
%include interrupt_levels;
%page;
%include tape_ioi_result_indexes;
%page;
%include tape_ioi_buffer_status;
%page;
%include tape_ioi_constants;

     end tape_ioi_error_retry;
