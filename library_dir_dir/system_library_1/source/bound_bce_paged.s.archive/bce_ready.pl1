/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1984 *
        *                                                         *
        *********************************************************** */
bce_ready: proc (prompt);

/* print ready message for bootload Multics.
Written June 1983 by Keith Loepere. */

/* format: style4,indattr,ifthenstmt,ifthen,idind33,^indcomtxt */

dcl  addr			        builtin;
dcl  bce_data$error_put_chars	        entry (ptr, ptr, fixed bin, fixed bin (35)) ext variable;
dcl  buffer		        char (40);
dcl  buffer_len		        fixed bin;
dcl  clock		        builtin;
dcl  code			        fixed bin (35);
dcl  date_time_		        entry (fixed bin (71), char (*));
dcl  ioa_$rsnnl		        entry() options(variable);
dcl  length		        builtin;
dcl  new_line		        bit (1) aligned;
dcl  output_buffer		        char (buffer_len) based (addr (buffer));
dcl  prompt		        char (*) parameter;
dcl  substr		        builtin;
dcl  time_string		        char (24);

	new_line = "1"b;
	go to join;

nnl: entry (prompt);

	new_line = "0"b;

join:
	call date_time_ (clock (), time_string);
	call ioa_$rsnnl ("^a (^a) ^a: ", buffer, buffer_len, prompt, 
	     COLLECTION_1_PHASE_NAMES (sys_info$collection_1_phase), 
	     substr (time_string, 11, 6));
	if new_line then do;
	     buffer_len = buffer_len + 1;
	     substr (output_buffer, buffer_len, 1) = "
";
	end;
	call bce_data$error_put_chars (addr (bce_data$error_put_chars), addr (output_buffer), length (output_buffer), code);
	return;
%page; %include collection_1_phases;
     end;
