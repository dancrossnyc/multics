/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) Honeywell Bull Inc., 1987                *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1984 *
        *                                                         *
        *********************************************************** */


/****^  HISTORY COMMENTS:
  1) change(86-05-13,GJohnson), approve(86-05-13,MCR7387),
     audit(86-05-13,Martinson), install(86-05-14,MR12.0-1056):
     Correct error message documentation.
                                                   END HISTORY COMMENTS */


bce_abs_seg: proc; return;

/* format: style4,indattr,ifthenstmt,ifthen,idind33,^indcomtxt */

/* Maintains a list of abs-segs to be freed (sdw's zeroed) at the end of a bce
pass.  Keith Loepere, May 1984. */

dcl  abs_seg_count		        fixed bin init (0) static;
dcl  abs_seg_index		        fixed bin;
dcl  abs_seg_list		        (128) fixed bin (17) unal static;
dcl  addr			        builtin;
dcl  baseptr		        builtin;
dcl  dimension		        builtin;
dcl  free_sdw		        fixed bin (71) init (0) static options (constant);
dcl  pmut$swap_sdw		        entry (ptr, ptr);
dcl  seg_num		        fixed bin (15) parameter;
dcl  syserr		        entry options (variable);

add: entry (seg_num);				/* add to list */

	abs_seg_count = abs_seg_count + 1;
	if abs_seg_count > dimension (abs_seg_list, 1) then call syserr (CRASH, "bce_abs_seg: Too many bce abs-segs.");
	abs_seg_list (abs_seg_count) = seg_num;
	return;

free: entry;					/* zero abs-seg sdws */

	do abs_seg_index = 1 to abs_seg_count;
	     call pmut$swap_sdw (baseptr (abs_seg_list (abs_seg_index)), addr (free_sdw));
	end;
	abs_seg_count = 0;
	return;
%page; %include syserr_constants;
%page;

/* BEGIN MESSAGE DOCUMENTATION

   Message:
   bce_abs_seg: Too many bce abs-segs.

   S: $crash

   T: $init

   M: More per-bce pass abs-segs were encountered than fit in the abs-seg
   array.  This is a software error.

   END MESSAGE DOCUMENTATION */

     end;
