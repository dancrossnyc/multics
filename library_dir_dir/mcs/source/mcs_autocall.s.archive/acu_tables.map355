* ***********************************************************
* *                                                         *
* * Copyright, (C) Honeywell Information Systems Inc., 1982 *
* *                                                         *
* * Copyright (c) 1972 by Massachusetts Institute of        *
* * Technology and Honeywell Information Systems, Inc.      *
* *                                                         *
* ***********************************************************

	lbl	,acu_tables
	ttl	acu_tables
	editp	on
	pmc	off
	pcc	off
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
*	acu_tables, acu
*
*	     these are the control tables for managing auto-call units
*
*	split off from control_tables by Robert Coren 4/29/76
*         Modified 81-01-13 by E. N. Kittlitz to allow central hangups while
*            dialing out.
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	eject
acu	null
	rem
	symdef	acu
	rem
	symdef	acutst
	rem
	symref	chkaut
	symref	hungup
	symref	bscacu	return from bisync autocall
	symref	g15acu	return from g115 autocall
	rem
	start	acu,,w3atcm090000
	pmc	save,on
	tib
	csbits
	tconst
	pmc	restore
	ttls	acu - use auto call unit to dial a phone number
acutst	ifhsla	canacu	/* only hsla's support acu's */
	clrflg	tfacu	/* not acu'ing anymore */
	signal	acung	/* no good acu attempt */
	waitm		/* continue waiting */
	rem
canacu	setime	60	/* timer for dialing process */
	contrl	stat	/* get line's status */
	rem
	wait	acuoot,0,tstat	/* wait for status */
	status	0,pwi,nopwr	/* check that acu has power */
	status	dlo,0,busy	/* and line is not being used */
	status	pwi,dlo,acurdy	/* line is all set for acu */
	rem
acurdy	config
	rmode	fg.icw	/* need single icw configuration */
	rem
	dcwlst		/* dcw list to 'write' phone number */
	cmd	sdtr+srts+sxmit,scrq
	output	(outmsg)	/* use t.ocp as addr of buffer */
	cmd	rxmit+sterm
	rem
	wait	acuoot,0,tstat	/* wait for terminate status */
	status	term,0,acusr
	rem
acusr	wait	acuoot,0,tstat	/* once started, wait for dialup status */
	status	acr,0,acuoot	/* acr - abandon call and retry */
	status	cts+dsr+ads,0,acudl /* dial succeeded */
	rem
nopwr	signal	acupwi	/* inform 6180 */
	goto	noacu
	rem
busy	signal	acudlo
	goto	noacu
	rem
tstat	tstflg	(tfhang),achng	/* hangup order? */
	waitm			/* nope - continue waiting */
	rem
achng	clrflg	tfhang		/* acknowledge hangup */
	signal	hangup
	goto	otjoin		/* finish hanging up */
	rem
acuoot	signal	acuacr
otjoin	config
	smode	fg.icw	/* back to 2 icw configuration */
	contrl	rdtr+rrts,rcrq
	stpchn
	dmpout
	goto	noacu
	rem
noacu	clrflg	tfacu	/* not acu'ing anymore */
	goto	hungup
	rem
acudl	config
	smode	fg.icw	/* back to 2 icw configuration */
	iftype	ttasci,kprts	/* don't turn off rts for ascii */
	contrl	rrts,rcrq
	goto	acufin
kprts	contrl	0,rcrq
acufin	null
	clrflg	tfacu	/* acu successful, reset the flag */
	iftype	ttbsc,bscacu	/* return from bisync autocall */
	iftype	ttg115,g15acu	/* return from g115 autocall */
	goto	chkaut	/* tell 6180 */
	end
