* ***********************************************************
* *                                                         *
* * Copyright, (C) Honeywell Information Systems Inc., 1982 *
* *                                                         *
* * Copyright (c) 1972 by Massachusetts Institute of        *
* * Technology and Honeywell Information Systems, Inc.      *
* *                                                         *
* ***********************************************************

	lbl	,sim_tables
	ttl	sim_tables -- to control sync line simulator
	pcc	off
	pmc	off
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*	sim_tables
*
*	these tables implement the protocol of the
*	sync line simulator designed for the usgs benchmark.
*	the simulator is based on the vip7700 protocol.
*
*	these cannot be used with the g115 protocol since they
*	share the same line type, 6.
*
* 	coded by adsit and grady for usgs.
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	rem
sim	null
	rem
	symdef	sim
	symdef	gstar
	rem
	symref	begin
	symref	error
	symref	hanga
	rem
	start	sim
	rem
	pmc	save,on
	tib
	csbits
	tconst
	pmc	restore
	ttls	constants for sync line simulator tables
chr100	bool	100
chr140	bool	140
chr141	bool	141
chr124	bool	124
chr146	bool	146
	rem
iscn6	chstr	(rescan,search,soh,ignore,strlrc,search,etx,ignore,cmplrc)
iscn7	chstr	(rescan,search,soh,ignore,ignore,match,ack)
iscn8	chstr	(rescan,search,soh,ignore,ignore,match,dle)
	rem
oscn1	chstr	(rescan,search,etx)
oscn2	chstr	(rescan,search,soh,ignore,strlrc,search,etx,ignore,outlrc)
oscn3	chstr	(rescan,chktrm,etx)
	rem
ackmsg	chstr	(syn,syn,syn,syn,soh,chr141,ack,stx,etx,chr146,eot,seteom)
nakmsg	chstr	(syn,syn,syn,syn,soh,chr141,nak,stx,chr100,chr141,etx,chr
	etc	124,eot,seteom)
idlmsg	chstr	(syn,syn,syn,syn,soh,chr100,dle,chr100,etx,seteom)
	rem
	ttls	dial up control for simulator lines
gstar	tstflg	tflisn,slisn	/* shall we listen to line? */
	wait	0,0,begin
	rem
slisn	contrl	sdtr+srts+stat	/* bring up dataset leads */
	rem
	wait	0,0,tshang	/* look for dial or hanh */
	status	cd+cts+dsr,0,sdiald
	rem
sdiald	signal	dialup
	goto	sget	/* start off protocol */
	ttls	sget and srcvd input for sync lines
sget	tstwrt	sendr
	tstflg	tfhang,shang
	rem
	contrl	srec+rxmit
	setime	0
	wait	0,sendr,tshang
	status	brkchr,0,srcvd
	status	parity,0,nakit
	status	exh,0,pause
	status	xte,0,pause
	status	0,dsr+cd,shang
	rem
srcvd	inscan	iscn6,nakit
	inscan	iscn8,notidl
	dumpin
	goto	sntack
	rem
notidl	sendin
	dmpout
	rem
	bldmsg	ackmsg,error
	rem
sndmsg	holdot
	dcwlst
	cmd	sxmit
	output	(outmsg)
	cmd	sterm+rxmit
	rem
	wait	0,0,0
	status	term,0,sntack
	status	0,dsr+cd,shang
	rem
sntack	setime	0
	tstflg	tfhang,shang
	rem
	wait	0,0,tshang
	status	brkchr,0,srcvd
	status	parity,0,nakit
	status	exh,0,pause
	status	xte,0,pause
	status	0,dsr+cd,shang
	rem
pause	stpchn
	dumpin
	dmpout
	setime	1
	wait	nakit1,0,tshang
	status	0,dsr+cd,shang
	rem
nakit	stpchn
	dumpin
	dmpout
	rem
nakit1	tcntr	2000,rstcnt
	acntr	1
	contrl	srec
	rem
nakit2	bldmsg	nakmsg,error
	goto	sndmsg
	rem
rstcnt	scntr	0
	goto	nakit1
tshang	tstflg	tfhang,shang
	waitm
	rem
shang	stpchn
	dumpin
	dmpout
	goto	hanga
	ttls	sendr for sync lines
sendr	outscn	oscn3,gtmore
	goto	sendit
	rem
gtmore	signal	sndout
	wait	0,sendr,tshang
	rem
sendit	holdot
	rem
resend	dcwlst
	cmd	sxmit+srec
	output	(outmsg)
	cmd	rxmit
	rem
sndone	setime	30
	tstflg	tfhang,shang
	rem
	wait	badack,0,tshang
	status	brkchr,0,gotack
	status	parity,0,badack
	status	exh,0,badack
	rem
badack	stpchn
	dumpin
	goto	resend
	rem
gotack	inscan	iscn6,badack
	inscan	iscn7,badack
	dumpin
	dmpout
	setime	10
	contrl	rrec
	wait	sndidl,sendr,tshang
	rem
sndidl	bldmsg	idlmsg,error
	outscn	oscn2,error
	holdot
	rem
	dcwlst
	cmd	sxmit
	output	(outmsg)
	cmd	sterm+rxmit
	rem
	setime	0
	wait	0,0,0
	status	term,0,sntidl
	rem
sntidl	dmpout
	tstflg	tfhang,shang
	tstwrt	sendr
	wait	0,sendr,tshang
	end
