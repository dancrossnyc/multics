/****^  ***********************************************************
        *                                                         *
        * Copyright, (C) BULL HN Information Systems Inc., 1989   *
        *                                                         *
        * Copyright, (C) Honeywell Information Systems Inc., 1982 *
        *                                                         *
        * Copyright (c) 1972 by Massachusetts Institute of        *
        * Technology and Honeywell Information Systems, Inc.      *
        *                                                         *
        *********************************************************** */




/****^  HISTORY COMMENTS:
  1) change(89-04-23,Zimmerman), approve(89-04-23,MCR8060),
     audit(89-05-05,RWaters), install(89-05-24,MR12.3-1048):
     MCR8060 cobol_receive_gen.pl1 Reformatted code to new Cobol standard.
                                                   END HISTORY COMMENTS */


/* Modified on 10/02/77 by Bob Chang to fix the bug for mcs_icdp. */
/* Modified on 03/03/77 by Bob Chang to implement communication receive verb.	*/
/* Created as a stub on 11/18/76 by ORN */

/* format: style3 */
cobol_receive_gen:
     proc (in_token_ptr, next_stmt_tag);

/* Declaration for static data.	*/
dcl	1 pr_struc	static,
	  2 what_ptr	fixed bin init (2),
	  2 pointer_no	bit (3),
	  2 lock		fixed bin init (0),
	  2 switch	fixed bin init (0);

dcl	1 alpha_type9	static,
	  2 header	(4) fixed bin init (112, 0, 0, 9),
	  2 repl_ptr	(2) ptr init ((2) null ()),
	  2 fill1		bit (108) init (""b),
	  2 file_key_info,
	    3 fb1		(3) fixed bin init (0, 0, 0),
	    3 size	fixed bin init (0),
	    3 fb2		(2) fixed bin init (0, 0),
	    3 flags1	bit (36) init ("000000100100000000010000000100000000"b),
	    3 flags2	bit (36) init (""b),
	    3 seg		fixed bin init (0),
	    3 off		fixed bin,
	  2 fill2		(7) fixed bin init (0, 0, 0, 0, 0, 0, 0);

dcl	inst_seq		(12) bit (18) unaligned static init ("110000000000000000"b, "010101010001000000"b,
						/* spri2	pr6|offset	*/
			"000000000000000000"b, "010011101000000111"b,
						/* lda	0,dl		*/
			"110000000000000000"b, "111101101001000000"b,
						/* sta	pr6|offset	*/
			"110000000000000000"b, "100101000001000000"b,
						/* stz	pr6|offset	*/
			"110000000000000000"b, "010011100001000000"b,
						/* szn	pr6|offset	*/
			"000000000000000000"b, "110000000000000100"b);
						/* tze	0,ic		*/


/*	Automatic data	*/
dcl	next_stmt_tag	fixed bin,		/* PARAMETER for NO DATA statement	*/
	stoff		fixed bin,
	dn_ptr		ptr,
	temp		fixed bin;

/* External procedure	*/
dcl	cobol_call_op	entry (fixed bin, fixed bin),
	cobol_pointer_register$priority
			entry (fixed bin, fixed bin, bit (3)),
	cobol_emit	entry (ptr, ptr, fixed bin),
	cobol_define_tag	entry (fixed bin),
	cobol_make_tagref	entry (fixed bin, fixed bin, ptr),
	cobol_set_pr	entry (ptr, ptr),
	cobol_reg_manager$after_op
			entry (fixed bin),
	cobol_get_size	entry (ptr, fixed bin, fixed bin);

start:						/* Generate epp2 instruction for communication token.	*/
	cdtoken_ptr = in_token.token_ptr (2);
	alpha_type9.seg = cdtoken.cd_seg;
	eos_ptr = in_token.token_ptr (in_token.n);
	alpha_type9.off = cdtoken.cd_off - 60;
	call cobol_set_pr (addr (pr_struc), addr (alpha_type9));

/* Allocate 12 words in stack frame for parameters	*/
	stoff = 74;				/* Communication stack frame  from pr6|74	*/

/* Store cd_token address.	*/
	substr (inst_seq (1), 4, 15) = substr (unspec (stoff), 22, 15);
	call cobol_emit (addr (inst_seq (1)), null, 1);

/* Set up parameter for message type.	*/
	temp = stoff + 4;
	if end_stmt.b | end_stmt.c
	then do;
		if ^end_stmt.b
		then substr (inst_seq (3), 17, 2) = "01"b;
		else if end_stmt.c
		then substr (inst_seq (3), 17, 2) = "11"b;
		else substr (inst_seq (3), 17, 2) = "10"b;
		substr (inst_seq (5), 4, 15) = substr (unspec (temp), 22, 15);
		call cobol_emit (addr (inst_seq (3)), null, 2);
	     end;
	else do;
		substr (inst_seq (7), 4, 15) = substr (unspec (temp), 22, 15);
		call cobol_emit (addr (inst_seq (7)), null, 1);
	     end;

/* Generate epp2 instruction for receiving data item.	*/
	dn_ptr = in_token.token_ptr (3);
	call cobol_set_pr (addr (pr_struc), dn_ptr);

/* Store into stack frame.	*/
	temp = stoff + 2;
	substr (inst_seq (1), 4, 15) = substr (unspec (temp), 22, 15);
	call cobol_emit (addr (inst_seq (1)), null, 1);

/* Set up the size of the data token.	*/
	temp = stoff + 5;
	call cobol_get_size (dn_ptr, temp, 0);

/* Call cobol_operators_	*/
	call cobol_call_op (70, 0);

	call cobol_reg_manager$after_op (70);

/* Handle NO DATA statement.	*/
	if end_stmt.b
	then do;
		temp = stoff + 7;
		substr (inst_seq (9), 4, 15) = substr (unspec (temp), 22, 15);
		call cobol_emit (addr (inst_seq (9)), null, 2);
		next_stmt_tag = cobol_$next_tag;
		cobol_$next_tag = cobol_$next_tag + 1;
		call cobol_make_tagref (next_stmt_tag, cobol_$text_wd_off - 1, null);
	     end;

exit:
	return;

/*****	Declaration for builtin function	*****/

dcl	(substr, mod, binary, fixed, addr, addrel, rel, length, string, unspec, null, index)
			builtin;

/*****	End of declaration for builtin function	*****/


%include cobol_in_token;
%include cobol_;
%include cobol_type19;
%include cobol_type9;
%include cobol_type13;
     end cobol_receive_gen;
