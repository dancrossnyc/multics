/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */
%;
/* ***************************************
   *				 *
   *				 *
   * Copyright (c) 1975, 1976 by         *
   * Honeywell Information Systems, Inc. *
   *				 *
   *				 *
   *************************************** */
sort_merge_print_report:  proc;

/*   EXTERNAL ENTRIES     */
dcl  ioa_$ioa_stream  entry options(variable);

/*   EXTERNAL STATIC     */
%include sort_ext;

/*   INTERNAL STATIC   (constants only)   */
dcl (on init("1"b),
     off init("0"b) ) bit(1) internal static;

/*   Start   */

  if report_sw ^= "01"b then do;
     if input_driver_is_sort = on & output_driver_is_sort = on then do;
	if read_count = release_count & read_count = return_count & read_count = write_count then do;
	     call print_sorted;
	     end;
	else if release_count = return_count then do;
	     call print_read;
	     call print_sorted;
	     call print_written;
	     end;
	else do;		/*   release_count /= return_count   */
	     call print_read;
	     call print_released;
	     call print_returned;
	     call print_written;
	     end;
	end;
     else if input_driver_is_sort = on & output_driver_is_sort = off then do;
	if read_count = release_count & read_count = return_count then do;
	     call print_sorted;
	     end;
	else if release_count = return_count then do;
	     call print_read;
	     call print_sorted;
	     end;
	else do;		/*   release_count /= return_count   */
	     call print_read;
	     call print_released;
	     call print_returned;
	     end;
	end;
     else if input_driver_is_sort = off & output_driver_is_sort = on then do;
	if release_count = return_count & release_count = write_count then do;
	     call print_sorted;
	     end;
	else if release_count = return_count then do;
	     call print_sorted;
	     call print_written;
	     end;
	else do;		/*   release_count /= return_count   */
	     call print_released;
	     call print_returned;
	     call print_written;
	     end;
	end;
     else do;		/*   neither sort's(merge's) input file proc nor sort's(merge's) output file proc   */
	if release_count = return_count then do;
	     call print_sorted;
	     end;
	else do;		/*   release_count /= return_count   */
	     call print_released;
	     call print_returned;
	     end;
	end;
  end;		/*   of if report_sw ^= "01"b then do;   */
  return;	/*   To caller (sort_ or merge_)  	*/


print_sorted:  proc;
     call print_input_ins_dele;
     if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw, 
		"^7d records ^aed.", release_count, substr(whoami, 1, 4));
     call print_output_ins_dele;
end print_sorted;


print_read:  proc;
     if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw, 
		"^7d records read.", read_count);
end print_read;


print_written:  proc;
     if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw, 
		"^7d records written.", write_count);
end print_written;


print_released:  proc;
     call print_input_ins_dele;
     if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw, 
		"^7d records released to ^a.", release_count, before(whoami, "_"));
end print_released;


print_returned:  proc;
     if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw, 
		"^7d records returned from ^a.", return_count, before(whoami, "_"));
     call print_output_ins_dele;
end print_returned;


print_input_ins_dele:  proc;
     if input_record_exit_sw = 1 then do;
	if input_rec_inserted ^= 0 then
	if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw,
		"^7d records inserted at input.", input_rec_inserted);
	if input_rec_deleted ^= 0 then
	if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw,
		"^7d records deleted at input.", input_rec_deleted);
	end;
end print_input_ins_dele;


print_output_ins_dele:  proc;
     if output_record_exit_sw = 1 then do;
	if output_rec_inserted ^= 0 then
	if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw,
		"^7d records inserted at output.", output_rec_inserted);
	if output_rec_deleted ^= 0 then
	if user_out_sw ^= "" then call ioa_$ioa_stream(user_out_sw,
		"^7d records deleted at output.", output_rec_deleted);
	end;
end print_output_ins_dele;


end sort_merge_print_report;
