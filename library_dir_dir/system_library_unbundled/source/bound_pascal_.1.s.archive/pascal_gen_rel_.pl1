/* *************************************************************************
   *                                                                       *
   * Copyright (c) 1980 by Centre Interuniversitaire de Calcul de Grenoble *
   * and Institut National de Recherche en Informatique et Automatique     *
   *                                                                       *
   ************************************************************************* */
pascal_gen_rel_: proc;

/*

   Procedures called to generate rel info bit string


*/



/* Parameters for the entry points : */

	dcl     string		 bit (5);		/* rel info bit string for one half word */
	dcl     nbr		 fixed bin;	/* nbr of times */

%include pascal_context_;


	dcl     sect		 fixed bin;

	dcl     zeros		 bit (15) int static init ("0"b) options (constant);

	dcl     substr		 builtin;
	dcl     copy		 builtin;

/* based */

	dcl     1 nbzeros		 based,
		2 pad		 bit (26) unal,
		2 length		 bit (10) unal;

/*  */
text: entry (string, nbr);

	sect = txt;
	go to join;

def: entry (string, nbr);

	sect = def;
	go to join;

link: entry (string, nbr);

	sect = lnk;
	go to join;

symb: entry (string, nbr);

	sect = sym;
	go to join;

ps_def: entry (string, nbr);

	sect = ps_def;

join:
	if nbr < 0 then return;

	nbr_of_calls (sect) = nbr_of_calls (sect) + nbr;

	if string = "0"b then do;
		nbr_of_zeros (sect) = nbr_of_zeros (sect) + nbr;
		do while (nbr_of_zeros (sect) >= 1023);
		     nbr_of_zeros (sect) = nbr_of_zeros (sect) - 1023;
		     rel_info.string (sect) = rel_info.string (sect) || "111101111111111"b;
		end;
	     end;
	else do;
		if nbr_of_zeros (sect) ^= 0 then do;
			if nbr_of_zeros (sect) < 15 then do;
				rel_info (sect).string = rel_info (sect).string || substr (zeros, 1, nbr_of_zeros (sect));
			     end;
			else do;
				rel_info (sect).string = rel_info (sect).string || "11110"b || addr (nbr_of_zeros (sect)) -> length;
			     end;
			nbr_of_zeros (sect) = 0;
		     end;
		rel_info (sect).string = rel_info (sect).string || copy (string, nbr);
	     end;

	return;
     end pascal_gen_rel_;
