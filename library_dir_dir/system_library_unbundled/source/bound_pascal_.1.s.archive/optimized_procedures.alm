" *************************************************************************
" *                                                                       *
" * Copyright (c) 1980 by Centre Interuniversitaire de Calcul de Grenoble *
" * and Institut National de Recherche en Informatique et Automatique     *
" *                                                                       *
" * Copyright, (C) Massachusetts Institute of Technology, 1973            *
" *                                                                       *
" *************************************************************************

" HISTORY COMMENTS:
"  1) change(86-10-09,JMAthane), approve(86-10-09,MCR7521),
"     audit(86-10-09,JPFauche), install(86-11-12,MR12.0-1208):
"     Pascal Version 8.03 for MR12.0.
"                                                      END HISTORY COMMENTS

"	OPTIMIZED PROCEDURES OF THE MULTICS PASCAL COMPILER
"
"
"	RACINE>SEARCH
"	-----------------
"
"
	segdef	search
"
"
	equ	maxident,32
	equ	recidscopelength,58
	equ	nxtelplaceincontextable,8
	equ	nameplaceincontextable,0
	equ	return_ptr,20
	equ	trans_op_tv_ptr,40
	equ	work_0,80
"
"
search:
	epaq	0,ic
	lprp4	7|22,*au
	epp0	7|trans_op_tv_ptr,*
	epp0	0|8,*
	ldq	<RACINE.defs>|[top]
	tmi	end_downto
	epp3	<RACINE.defs>|[aval]
downto_loop:
	stq	6|work_0
	mpy	recidscopelength,dl
	epp1	<RACINE.defs>|[display],ql*
while_loop:
	eax1	1|-1
	tze	end_while
	cmpc	(pr),(pr),fill(040)
	desc9a	1|nameplaceincontextable,maxident
	desc9a	3|0,maxident
	tnz	aval_not_found
	ldq	6|work_0
	stq	<RACINE.defs>|[disx]
	spri1	<RACINE.defs>|[ctptr]
	tra	6|return_ptr,*
aval_not_found:
	epp1	1|nxtelplaceincontextable,*
	tra	while_loop
end_while:
	ldq	6|work_0
	sbq	1,dl
	tpl	downto_loop
end_downto:
	stz	<RACINE.defs>|[disx]
	spri1	<RACINE.defs>|[ctptr]
	tra	6|return_ptr,*
"
"
"	END OF SEARCH
"
"
"
"
"	RACINE>SRCHREC
"	------------------
"
"
	segdef	srchrec
"
"
srchrec:
	epaq	0,ic
	lprp4	7|22,*au
	epaq	0,ic
	epp1	0|2,*
	epp1	1|0,*
	epp0	7|trans_op_tv_ptr,*
	epp0	0|8,*
	epp3	<RACINE.defs>|[aval]
rec_while_loop:
	eax1	1|-1
	tze	rec_end_while
	cmpc	(pr),(pr),fill(040)
	desc9a	1|nameplaceincontextable,maxident
	desc9a	3|0,maxident
	tnz	rec_aval_not_found
rec_end_while:
	spri1	<RACINE.defs>|[ctptr]
	tra	6|return_ptr,*
rec_aval_not_found:
	epp1	1|nxtelplaceincontextable,*
	tra	rec_while_loop
"
"
"
"	END OF SRCHREC
"
"
"
	end
