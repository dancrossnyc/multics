/* **************************************************************
   *                                                            *
   * Copyright, (C) Massachusetts Institute of Technology, 1984 *
   *                                                            *
   ************************************************************** */
convert_forum:
	proc ();

/* Converts version 1 forum to version 2 forum
    Jay Pattin 1/8/83 */

declare	arg			char (arg_len) based (arg_ptr),
	arg_count			fixed bin,
	arg_idx			fixed bin,
	arg_len			fixed bin (21),
	arg_ptr			ptr,
	directory			char (168),
	name			char (32),
	status			fixed bin (35),
	whoami			char (32) static options (constant) init ("convert_forum");

declare	(com_err_, com_err_$suppress_name)
				entry options (variable),
	cu_$arg_count		entry (fixed bin, fixed bin (35)),
	cu_$arg_ptr		entry (fixed bin, ptr, fixed bin (21), fixed bin (35)),
	expand_pathname_$add_suffix	entry (char (*), char (*), char (*), char (*), fixed bin (35)),
	forum_$convert		entry (char (*), char (*), fixed bin (35));

	call cu_$arg_count (arg_count, status);
	if status ^= 0 then do;
ERR:	     call com_err_ (status, whoami);
	     return;
	end;

	if arg_count = 0 then do;
	     call com_err_$suppress_name (0, whoami, "Usage:  convert_forum meeting_path");
	     return;
	end;

	call cu_$arg_ptr (1, arg_ptr, arg_len, status);
	if status ^= 0 then goto ERR;
	call expand_pathname_$add_suffix (arg, "control", directory, name, status);
	if status ^= 0 then do;
	     call com_err_ (status, whoami, "Finding ""^a"".", arg);
	     return;
	end;

	call forum_$convert (directory, rtrim (name), status);
	if status ^= 0 then do;
	     call com_err_ (status, whoami, "Converting ""^a"".", arg);
	     return;
	end;

	return;
     end convert_forum;
