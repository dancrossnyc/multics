" ***********************************************************
" *                                                         *
" * Copyright, (C) Honeywell Information Systems Inc., 1982 *
" *                                                         *
" * Copyright (c) 1972 by Massachusetts Institute of        *
" * Technology and Honeywell Information Systems, Inc.      *
" *                                                         *
" ***********************************************************

"  Modified by RH Morrison 6/22/76

name	cv_bcd_to_ascii_
entry	cv_bcd_to_ascii_

cv_bcd_to_ascii_:

	push

	eppbp	ap|2,*		bp -> input data
	eppbp	bp|0,*

	eppap	ap|4,*		ap -> output data
	eppap	ap|0,*

	eax7	80   		init total character count
	eax4	0   		init ascii word offset
	eax3	0   		init bcd word offset
	eax2	4   		init ascii char count
	eax1	6   		init bcd char count
	ldq	bp|0		init btemp
	stq	btemp

continue:

	cmpx2	=0,du		check for full ascii word
	tnz	moreascii		need more ascii characters
	sta	ap|0,4		store full ascii word in output string
	adx4	=1,du		increment ascii word count
	eax2	4   		re-init ascii character count

	cmpx7	=0,du		check for completion
	tze	fin

moreascii:

	cmpx1	=0,du		check for used bcd word
	tnz	morebcd		need more bcd characters
	adx3	=1,du		increment bcd word count
	eax1	6		re-init bcd character count
	ldq	bp|0,3		get next full bcd word
	stq	btemp		save it

morebcd:

	ldq	btemp		load current bcd word
	qlr	6		make next bcd char leftmost
	stq	btemp		save for next time
	anq	=o000077,dl	isolate current character
	ldq	btable,ql		load ascii character into q
	lls	9		concatenate in a
	sbx2	=1,du		decrement ascii char count
	sbx1	=1,du		decrement bcd char count
	sbx7	=1,du		decrement total character count
	tra	continue		convert another character

fin:

	return

btable:	vfd	a9/0,27/0
	vfd	a9/1,27/0
	vfd	a9/2,27/0
	vfd	a9/3,27/0
	vfd	a9/4,27/0
	vfd	a9/5,27/0
	vfd	a9/6,27/0
	vfd	a9/7,27/0
	vfd	a9/8,27/0
	vfd	a9/9,27/0
	vfd	a9/[,27/0
	vfd	a9/#,27/0
	vfd	a9/@,27/0
	vfd	a9/:,27/0
	vfd	a9/>,27/0
	vfd	a9/?,27/0
	vfd	a9/ ,27/0
	vfd	a9/a,27/0
	vfd	a9/b,27/0
	vfd	a9/c,27/0
	vfd	a9/d,27/0
	vfd	a9/e,27/0
	vfd	a9/f,27/0
	vfd	a9/g,27/0
	vfd	a9/h,27/0
	vfd	a9/i,27/0
	vfd	a9/&,27/0
	vfd	a9/.,27/0
	vfd	a9/],27/0
	vfd	a9/(,27/0
	vfd	a9/<,27/0
	vfd	a9/\,27/0
	vfd	a9/^,27/0
	vfd	a9/j,27/0
	vfd	a9/k,27/0
	vfd	a9/l,27/0
	vfd	a9/m,27/0
	vfd	a9/n,27/0
	vfd	a9/o,27/0
	vfd	a9/p,27/0
	vfd	a9/q,27/0
	vfd	a9/r,27/0
	vfd	a9/-,27/0
	vfd	a9/$,27/0
	vfd	a9/*,27/0
	vfd	a9/),27/0
	vfd	o9/073,27/0		";
	vfd	a9/',27/0
	vfd	a9/+,27/0
	vfd	a9//,27/0
	vfd	a9/s,27/0
	vfd	a9/t,27/0
	vfd	a9/u,27/0
	vfd	a9/v,27/0
	vfd	a9/w,27/0
	vfd	a9/x,27/0
	vfd	a9/y,27/0
	vfd	a9/z,27/0
	vfd	a9/_,27/0			"<-
	vfd	o9/054,27/0		",
	vfd	a9/%,27/0
	vfd	a9/=,27/0
	vfd	a9/",27/0
	vfd	a9/!,27/0

temp	btemp

end
