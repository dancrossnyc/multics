/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */
gfms_gtss_attributes_: proc (dir, ent, fep, date);

/* Provide gtss attributes on file dir>ent

   Author: Dave Ward	10/17/80
   Change: Dave Ward	10/30/80 fep parameter, new file name list.
   Change: Dave Ward	11/12/80 change date to a parameter.
   Change: Dave Ward	11/21/80 corrected setting mode random.
*/
dcl  date                     bit(36)aligned parm;
dcl  dir                      char(168)parm;
dcl  ent                      char(32)parm;
dcl  fep                      ptr parm;
	fnep = fep;
	gtss_file_values.version = 1;
	gtss_file_values.dname = dir;
	gtss_file_values.ename = " ";
	gtss_file_values.new_ename = ent;
	gtss_file_values.change_name = "0"b;		/* Do not change name of file. */
	gtss_file_values.set_switch.mode_random
	     , gtss_file_values.set_switch.maxll
	     , gtss_file_values.set_switch.curll
	     , gtss_file_values.set_switch.busy
	     , gtss_file_values.set_switch.attr
	     , gtss_file_values.set_switch.null_file
	     , gtss_file_values.set_switch.number_allocations
	     , gtss_file_values.set_switch.creation_date
	     = "1"b;

	gtss_file_values.data_flags.mode_random = ((file_name_entry.attr.mode & "0100"b) ^= "0"b);
	gtss_file_values.data_fields.maxll = file_name_entry.attr.max_size_llinks;
	gtss_file_values.data_fields.curll = file_name_entry.llinks;
	gtss_file_values.data_flags.busy = "0"b;
	gtss_file_values.attributes.attr = file_name_entry.user_info.value;
	gtss_file_values.data_flags.null_file = "0"b;
	gtss_file_values.data_fields.number_allocations = 0;
	gtss_file_values.creation_date = gfms_ascii_ ((date));

	call gtss_attributes_mgr_$set (addr (gtss_file_values), code);
	if code ^= 0 then
	     call com_err_ (
	     code
	     , "gfms_gtss_attributes_"
	     , "For ^a>^a."
	     , dir
	     , ent
	     );
	return;
%page;
/*   Variables for gfms_gtss_attributes_	*/
/*   IDENTIFIER		ATTRIBUTES	*/
dcl  code                     fixed bin(35);
dcl  com_err_                 entry() options(variable);
dcl  gtss_attributes_mgr_$set entry (ptr, fixed bin(35));
dcl  gfms_ascii_              entry (bit(*)) returns(char(*));
%page;
%include gfms_file_name_list;
%page;
%include gtss_file_values;
%page;
%include gfms_description_tree;
%page;
%include gfms_ext;
     end gfms_gtss_attributes_;
