/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */

check_pointer_: proc (pptr, msg);

dcl  pptr ptr,
     msg char (32);

dcl  uppt ptr based (pptr);
dcl  ch char (1);
dcl  upch char (1) unal based (uppt);
dcl  any_other condition;
dcl  code fixed bin (35);
dcl  continue_to_signal_ entry (fixed bin (35));
dcl  find_condition_info_ entry (ptr, ptr, fixed bin (35));

	msg = "";
	on condition (any_other) call handler;
	ch = upch;
quit:
	return;

packed:	entry (pptr, msg);

dcl  pch char (1) based (ppt);
dcl  ppt ptr unal based (pptr);

	msg = "";
	on condition (any_other) call handler;
	ch = pch;
	return;

handler:	proc;

dcl 1 cond_info,
%include cond_info;

	     call find_condition_info_ (null (), addr (cond_info), code);
	     if (condition_name = "quit")
	     | (condition_name = "alrm")
	     | (condition_name = "cput")
	     then do;
		call continue_to_signal_ (code);
		return;
	     end;
	     msg = condition_name;
	     goto quit;
	end;
     end;
