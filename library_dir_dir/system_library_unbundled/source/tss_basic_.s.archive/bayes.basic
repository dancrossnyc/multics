10 REM BAYES
12REM WRITTEN BY R.C.HEWITT, JAN 7,1969
17 DIM Y(200)
100 GO TO 675
101 GO TO 440
199 GO TO 300
200 DATA .50,.75,.90,.95,.999,.9999,.99999
299 DATA 10
300 READ N1
305 LET Y1=0
310 LET N3=N1-1
315 FOR I=1 TO N1
320 READ Y(I)
325 LET Y1=Y1+Y(I)
330 NEXT I
335 LET Y3=Y1/N1
340 LET Y1=0
345 FOR I=1 TO N1
350 LET Y1=Y1+(Y(I)-Y3)*(Y(I)-Y3)
355 NEXT I
360 LET S1=Y1/N1
365 READ N2
370 LET Y1=0
375 LET N4=N2-1
380 FOR I=1 TO N2
385 READ Y(I)
390 LET Y1=Y1+Y(I)
395 NEXT I
400 LET Y4=Y1/N2
405 LET Y1=0
410 FOR I=1 TO N2
415 LET Y1=Y1+(Y(I)-Y4)*(Y(I)-Y4)
420 NEXT I
425 LET S2=Y1/N2
430 LET Y5=Y3-Y4
435 GO TO 470
440 READ Y,N1,Y3,S1,Z,N2,Y4,S2
445 LET N3=N1-1
450 LET N4=N2-1
455 LET Y5=Y3-Y4
460 LET S1=S1*S1
465 LET S2=S2*S2
470 PRINT TAB(8);"STATISTIC";TAB(30);"SAMPLE 1";TAB(45);"SAMPLE 2"
475 PRINT
480 PRINT "SAMPLE MEAN";TAB(30);Y3,Y4
485 PRINT "SAMPLE STD DEVIATION";TAB(30);SQR(S1),SQR(S2)
490 PRINT "SAMPLE SIZE";TAB(30);N1,N2
495 PRINT "SAMPLE VARIANCE";TAB(30);S1,S2
500 PRINT
505 PRINT "DIFF OF MEANS";TAB(37);Y5
510 PRINT
515 PRINT
520 PRINT "HIGHEST POSTERIOR DENSITY INTERVALS"
521 PRINT "DIFFERENCE BETWEEN MEANS:"
525 PRINT
530 PRINT" HPD LEVEL","LOWER LIM","UPPER LIM"
535 PRINT
540 READ P
545 IF P=10 THEN 885
550 LET P=(1-P)/2
555 LET T=SQR(LOG(1/P^2))
560 LET X=2.515517+0.802853*T+0.010328*T*T
565 LET X=T-X/(1+1.432788*T+0.189269*T*T+0.001308*T*T*T)
570 LET G1=X*(1+X*X)/4
575 LET G2=X*(1+16*X*X/3*(1+5*X*X/16))/32
580 LET G3=-5*X*(1-17*X*X/15*(1+19*X*X/17*(1+3*X*X/19)))/128
585 LET G4=1-1482*X*X/1920*(1+776*X*X/1482*(+79*X*X/776))
590 LET G4=-945*X*(1+1920*X*X/245*G4)/92160
595 LET S4=S2/N2
600 LET S3=S1/N1
605 LET C1=S4/(S3+S4)
610 LET C2=S3/(S3+S4)
615 LET N6=N4/(N4-2)
620 LET N5=N3/(N3-2)
625 LET F=(N6*C1+N5*C2)*(N6*C1+N5*C2)
630 LET F=F/((N6*N6*C1*C1)/(N4-4)+(N5*N5*C2*C2)/(N3-4))
635 LET B=F+4
640 LET A=(B-2)*(N6*C1+N5*C2)/B
645 LET T5=X+G1/B+G2/B^2+G3/B^3+G4/B^4
650 LET T6=SQR(A*(S3+S4))*T5
655 LET Y6=Y5-T6
660 LET Y9=Y5+T6
665 PRINT (1-2*P)*100,Y8,Y9
670 GO TO 540
675 PRINT " THIS PROGRAM COMPUTES THE HIGHEST POSTERIOR DENSITY "
680 PRINT "INTERVALS FOR THE DIFFERENCE BETWEEN TWO POPULATION MEANS"
685 PRINT "USING BAYESIAN STATISTICAL INFERENCE.  THE DATA MAY BE"
690 PRINT "ENTERED IN EITHER OF TWO WAYS:"
695 PRINT
700 PRINT "     A. TYPE THE DATA ON ONE LINE AS:"
705 PRINT
710 PRINT "      100 DATA H1,N1,M1,S1,H2,N2,M2,S2"
715 PRINT "      RUN"
720 PRINT
725 PRINT "     WHERE,"
730 PRINT "       H1=DUMMY TO AGREE WITH CONDIF*** INPUT"
735 PRINT "          (ENTER '0' AS AN APPROPRIATE NUMBER)"
740 PRINT "       N1=SIZE OF SAMPLE 1"
745 PRINT "       M1=ARITHMETIC MEAN OF SAMPLE 1"
750 PRINT "       S1=STANDARD DEVIATION OF SAMPLE 1(BASED ON"
755 PRINT "          DIVISOR OF N1)"
760 PRINT
765 PRINT "      H2,N2,M2,S2, ARE THE SAME FOR SAMPLE 2"
770 PRINT
775 PRINT "     B. TYPE THE DATA ON MORE THAN ONE LINE AS:"
780 PRINT
785 PRINT "      100 DATA N1,Y(1),Y(2),Y(3),Y(4)"
790 PRINT "      101 DATA Y(5),   . . .    ,Y(N1)"
795 PRINT "      102 DATA N2,Z(1),Z(2),Z(3),Z(4)"
800 PRINT "      103 DATA Z(5),   . . .    ,Z(N2)"
805 PRINT "      RUN"
810 PRINT
815 PRINT "     WHERE,"
820 PRINT "       N1=THE SIZE OF SAMPLE 1"
825 PRINT "       N2=THE SIZE OF SAMPLE 2"
830 PRINT "       Y(I),I=1 TO N1  ARE THE ELEMENTS OF SAMPLE 1"
835 PRINT "       Z(I),I=1 TO N2  ARE THE ELEMENTS OF SAMPLE 2"
840 PRINT
845 PRINT
850 PRINT " IF DIFFERENT HIGHEST POSTERIOR DENSITY INTERVALS ARE"
855 PRINT "DESIRED, THEN THEY MAY BE ENTERED AS:"
860 PRINT
865 PRINT "       200 DATA L1,L2,L3,L4,L5,..."
870 PRINT
875 PRINT "       WHERE ALL L=L1,L2,L3,... ARE SUCH THAT"
880 PRINT "            0.5< L < 1.0"
885 STOP
890 END
