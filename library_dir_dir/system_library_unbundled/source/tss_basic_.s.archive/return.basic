10REM  RETURN      (BASIC PROGRAM BEGINS AT LINE 115)
12REM REVISED AUGUST 1972
15REM
20REM  DESCRIPTION-- THIS BASIC PROGRAM COMPUTES A MATRIX OF
30REM  RETURNS FOR AN INVESTMENT IN A SECURITY, IT THEN COMPUTES
35REM  AN AVERAGE RETURN FOR A PURCHASE AT THE BEGINNING OF A
40REM  YEAR, SELLING AT EACH OF THE FOLLOWING YEAR ENDS, AND
45REM  COMPUTES AN AVERAGE RETURN FOR ALL DESIGNATED HOLDING
50REM  PERIODS. IT ALSO COMPUTES AN AVERAGE RETURN, STANDARD
55REM  DEVIATION AND COEFFICIENT OF VARIATION FOR DIFFERENT
60REM  LENGTH HOLDING PERIODS.
70REM
75REM-------------------------------------------------------------
80REM INSTRUCTIONS--SAMPLE DATA-NO. OF YRS.,BASE YR.-DIVIDENDS
85REM               PER YR.,ANNUAL CLOSING PRICE ARE PRESENTLY
90REM               ENTERED STARTING IN LINE 1000
95REM               TYPE "RUN" AND PROGRAM WILL ASK IF YOU HAVE
100REM              ALREADY ENTERED OR WISH TO ENTER DATA.
102REM
105REM-------------------------------------------------------------
110 DIM D(25),P(25),W(25,25)
115 PRINT "DO YOU WANT INSTRUCTIONS FOR SUPPLYING DATA";
120 INPUT A$
125 IF A$="DATA" THEN 240
130 IF A$="NO" THEN 265
135 PRINT
140 PRINT"OR HOW MANY YEARS DO YOU WISH TO ENTER PRICE AND DIVIDEND"
145 PRINT"DATA, REMEMBERING THE NEED FOR A BASE YEAR. E.G., 10 YEARS"
150 PRINT"OF DATA ARE NEEDED TO GIVE 9 ANNUAL RETURNS. THE MAXIMUM"
155 PRINT"NUMBER OF YEARS IS 25.  ";
160 INPUT N
165 PRINT
170 PRINT"WHAT IS THE FIRST YEAR FOR WHICH YOU WISH TO ENTER DATA";
175 INPUT Y1
180 PRINT
185 PRINT"ENTER THE DIVIDEND PER SHARE (DPS) AND THE CLOSING SECURITY"
190 PRINT"PRICE (CSP), SEPARATED BY A COMMA, FOR EACH OF THE FOLLOWING"
195 PRINT"YEARS:"
200 PRINT
205 PRINT TAB(12);"DPS, CSP"
210 PRINT
215 FOR I=1 TO N
220 PRINT"YEAR";Y1+(I-1);
225 INPUT D(I),P(I)
230 NEXT I
235 GOTO 285
240 READ N,Y1
245 FOR I=1 TO N
250 READ D(I),P(I)
255 NEXT I
260 GOTO 285
265 PRINT
270 PRINT "NO. OF YEARS, FIRST YEAR";
275 INPUT N,Y1
280 GOTO 200
285 PRINT
290 PRINT"-----------------------------------------------------------"
300 FOR I = 1 TO (N-1)
305 FOR J = (I+1) TO N
310 LET B = 0
315 FOR C = 1 TO 5
320 LET Q = .1^C
325 LET B = B + Q
330 LET E = 0
335 FOR F = (I+1) TO J
340 LET E = (D(F)*(1/((1+B)^(F-I)))) + E
345 NEXT F
350 LET E = E + P(J)*(1/((1+B)^(J-I)))
355 IF E-P(I)>0 THEN 325
360 LET B = B-Q
365 NEXT C
370 IF B = 0 THEN 380
375 GO TO 390
380  LET B = -.8
385 GO TO 315
390 LET W(I,J) =B
395 NEXT J
400 NEXT I
405 PRINT
410 PRINT
415 PRINT
420 PRINT
425 PRINT"                 RETURNS ON INVESTMENTS"
430 PRINT"                 ----------------------"
435 PRINT TAB(11);"(ANNUAL RATES COMPOUNDED ANNUALLY)"
440 PRINT
445 PRINT
450 FOR K=1 TO (N-1) STEP4
455 IF N-1-K<4 THEN540
460 PRINT"   TO",Y1+K,Y1+(K+1),Y1+(K+2),Y1+(K+3)
465 PRINT"FROM"
470 FOR I=1 TO K
475 PRINT Y1+(I-1),
480 FOR J=(K+1) TO (K+4)
485 PRINT W(I,J),
490 NEXT J
495 NEXT I
500 PRINT Y1+K," ",W(K+1,K+2),W(K+1,K+3),W(K+1,K+4)
505 PRINT Y1+K+1," "," ",W(K+2,K+3),W(K+2,K+4)
510 PRINT Y1+K+2," "," "," ",W(K+3,K+4)
515 PRINT
520 PRINT
525 PRINT
530 NEXT K
535 GO TO 665
540 PRINT"   TO",
545 FOR I=K TO (N-1)
550 PRINT Y1+I,
555 NEXT I
560 LET Q=4-(N-K)
565 GOSUB 645
570 PRINT"FROM"
575 FOR I=1 TO N-1
580 PRINT Y1+(I-1),
585 IF I<=(N-(N-K)) THEN 610
590 LET Q=I-K
595 GOSUB 645
600 LET J=K+1+Q
605 GO TO 615
610 FOR J=K+1 TO N
615 PRINT W(I,J),
620 NEXT J
625 LET Q=4-(N-K)
630 GOSUB 645
635 NEXT I
640 GO TO 665
645 FOR H=1 TO Q
650 PRINT " ",
655 NEXT H
660 RETURN
665 PRINT
670 PRINT
675 PRINT
680 LET X=0
685 LET Y=0
690 LET V=0
695 FOR I=1 TO (N-1)
700 FOR J=(I+1) TO N
705 LET X=W(I,J)+X
710 LET Y=W(I,J)+Y
715 NEXT J
720 PRINT"AVERAGE RETURN FROM";Y1+(I-1);"=";X/(N-I)
725 LET V=(N-I)+V
730 PRINT
735 LET X=0
740 NEXT I
745 PRINT
750 PRINT" AVERAGE RETURN FOR ALL"
755 PRINT"POSSIBLE HOLDING PERIODS =";Y/V
760 PRINT
765 PRINT
770 PRINT"               ----------------------------"
775 PRINT
780 PRINT
785 PRINT TAB(35);"AVERAGE";TAB(47);"STANDARD";TAB(61);"COEFF."
790 PRINT TAB(36);"RETURN";TAB(47);"DEVIATION";TAB(60);"VARIATION"
795 PRINT
800 LET V3=0
805 LET N2 = 0
810 LET X2 = 0
815 LET Y2 = 0
820 LET V2 = 0
825 FOR I = 1 TO (N-1)
830 FOR J = 1 TO N
835 IF J + I > N THEN 855
840 LET X2 = W(J,J+I) + X2
845 LET V2 = W(J,J+I) + V2
850 NEXT J
855 LET X2 = X2/(J-1)
860 FOR J = 1 TO N
865 IF J + I > N THEN 885
870 LET Y2 = (W(J,J+I)-X2)^2+Y2
875 LET V3 = (W(J,J+I))^2+V3
880 NEXT J
885 LET Y2 = SQR(Y2/(J-1))
890 PRINT "FOR ALL";I;"YEAR HOLDING PERIODS:";TAB(35);X2;TAB(47);Y2;
895 PRINT TAB(60);Y2/X2
900 PRINT
905 LET X2 = 0
910 LET Y2=C3=0
915 NEXT I
920 IF V-1 =0 THEN 930
925 LET C3=SQR((V*V3-V2*V2)/V/(V-1))
930 PRINT "FOR ALL POSSIBLE HOLDING PERIODS:";TAB(35);V2/V;TAB(47);C3;
935 PRINT TAB(60);C3/(V2/V)
940 PRINT
945 PRINT
950 PRINT
955 PRINT"-----------------------------------------------------------"
1000 DATA 10,1962
1010 DATA 0,39.25
1020 DATA 1.02,43.65
1030 DATA 1.10,46.75
1040 DATA 1.20,60.125
1050 DATA 1.30,60.00
1060 DATA 1.30,58.00
1070 DATA 1.30,50.25
1080 DATA 1.30,49.125
1090 DATA 1.30,47.25
1100 DATA 1.40,66.50
9999 END
