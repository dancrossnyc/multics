3REM T R U I N T
7 PRINT "DO YOU WANT INSTRUCTIONS, (1=YES, 0=NO)"
8 INPUT Y7
9 IF Y7=0 THEN 80
10 PRINT"TRUINT"
20 PRINT "THIS PROGRAM WILL CALCULATE THE TRUE ANNUAL INTEREST"
30 PRINT "RATE CHARGED ON AN INSTALLMENT LOAN.  YOU SUPPLY THE"
40 PRINT "VALUES OF FOUR VARIABLES:  A = AMOUNT OF LOAN (IN $),"
50 PRINT "P = AMOUNT OF EACH PAYMENT ($), N = THE TOTAL NUMBER"
60 PRINT "OF PAYMENTS DUE, AND  K = THE NUMBER OF PAYMENTS DUE"
70 PRINT "IN ONE YEAR.  ";
80 PRINT "WHAT ARE A,P,N,K";
90 INPUT A, P, N, K
100 PRINT
110 IF N=1 THEN 510
120 IF P*N>=A THEN 180
130 PRINT
140 PRINT "THAT'S NOT REASONABLE.  THE PAYMENTS ADD UP"
150 PRINT "TO LESS THAN THE AMOUNT OWED.  TRY AGAIN:"
160 PRINT
170 GOTO 80
180 LET R=0
190 LET D=100
200 GOSUB 290
210 IF P=P1 THEN 390
220 IF P>P1 THEN 250
230 LET R=R-D
240 GOTO 260
250 LET R=R+D
260 LET D=D/2
270 IF D<0.0001 THEN 390
280 GOTO 200
290 LET R1=R/(100*K)
300 LET Q=1+R1
310 IF N*LOG(Q)/LOG(10)<=75 THEN 340
320 LET P1=A*R1
330 RETURN
340 IF Q>1 THEN 370
350 LET P1=A/N
360 RETURN
370 LET P1=A*Q^N*R1/(Q^N-1)
380 RETURN
390 LET R=0.01*INT(0.5+100*R)
400 LET R2=.01*INT(.5+10000*((1+R/(100*K))^K-1))
410 IF R<199.5 THEN 460
420 PRINT
430 PRINT "RATE WOULD BE OVER 200 PERCENT.  TRY AGAIN:"
440 PRINT
450 GOTO 80
460 PRINT "TRUE ANNUAL INTEREST RATE (NOMINAL)=";R
470 PRINT "TRUE ANNUAL INTEREST RATE (EFFECTIVE)=";R2
480 PRINT
490 PRINT "ANOTHER CASE?  (TYPE 'S' TO STOP NOW)."
500 GOTO 80
510 LET R=(P/A-1)*K
520 LET R=100*R
530 GOTO 390
9999 END
