3REM B I C O N F
10 PRINT "DO YOU WANT INSTRUCTIONS (1=YES, 0=NO)";
20 INPUT Y7
30 IF Y7=1 THEN 700
70 DEF FNA(V)=1E-4*INT(.5+1E6*V)
75 PRINT "WHAT ARE X,N,C";
80 INPUT Y,N,C
90 LET P0=Y/N
100 LET Q0=1-P0
110 LET G0=6*SQR(P0*Q0/N)
120 LET P1=P0-G0
130 IF P1>0 THEN 150
140 LET P1=0
150 LET P1=.1*INT(10*P1)
160 LET P2=P0+G0
170 IF P2<1 THEN 190
180 LET P2=1
190 LET P2=1-.1*INT(10-10*P2)
200 LET C0=.005*(100-C)
210 LET P=P1
220 LET T=Y-1
230 LET J=0
240 LET J=J-1
250 IF J=-7 THEN 310
260 LET P=P+10^J
270 GOSUB 520
280 IF 1-Z<C0 THEN 260
290 LET P=P-10^J
300 GOTO 240
310 LET L=P
320 LET P=P2
330 LET T=Y
340 LET J=0
350 LET J=J-1
360 IF J=-7 THEN 420
370 LET P=P-10^J
380 GOSUB 520
390 IF Z<C0 THEN 370
400 LET P=P+10^J
410 GOTO 350
420 LET U=P
430 PRINT
440 PRINT
450 PRINT "   BEST ESTIMATE OF POPULATION PROPORTION (PCT) = ";
460 PRINT FNA(Y/N)
470 PRINT
480 PRINT "   THE ";C;"PERCENT CONFIDENCE LIMITS ON THE POPULATION"
490 PRINT "   PROPORTION (PCT) ARE ";FNA(L);"AND ";FNA(U)
500 PRINT
510 STOP
520 LET Q=1-P
530 LET G=6*SQR(P*Q*N)
540 LET S=INT(N*P-G)
550 IF S>0 THEN 590
560 LET S=0
570 LET X=1
580 GOTO 630
590 LET X=P*N/S
600 FOR I=1 TO S-1
605 IF S=1 THEN 620
610 LET X=X*P*(N-I)/(S-I)
620 NEXT I
630 LET F=X*(Q^(N-S))
640 LET Z=F
650 FOR I=S+1 TO T
660 LET F=F*P*(N-I+1)/(Q*I)
670 LET Z=Z+F
680 NEXT I
690 RETURN
700 PRINT "THIS PROGRAM DETERMINES THE CONFIDENCE LIMITS"
710 PRINT "FOR A POPULATION PROPORTION BASED ON THE"
720 PRINT "EXACT BINOMIAL DISTRIBUTION.  ENTER VALUES"
730 PRINT "FOR X[SUCCESSES], N[SAMPLE SIZE], AND"
740 PRINT "C[CONFIDENCE COEFFICIENT IN PERCENT] WHEN"
750 PRINT "REQUESTED BY THE PROGRAM.  ADDITIONAL"
760 PRINT "INSTRUCTIONS MAY BE FOUND IN THE LISTING."
770 GO TO 70
9999 END
