10 REM PERT
20 REM REVISED FEB 1975
30 PRINT TAB(25);"PERT ANALYSIS PROGRAM"
40 PRINT
50 PRINT
60 READ T
70 REM
80 IF T <> 4E36 THEN 1600
90 PRINT
100 PRINT "ENTER DATA STARTING IN LINE 3000."
110 PRINT
120 PRINT "FOR PROGRAM DESCRIPTION AND INSTRUCTIONS TYPE:"
130 PRINT
140 PRINT "LIST 170 1590"
150 STOP
160 REM
170 REM
180 REM                 PROGRAM DOCUMENTATION
190 REM
200 REM         THIS PROGRAM ANALYZES A PERT NETWORK.  THE ANALYSIS
210 REM     IS ADOPTED FROM THE PROGRAM AS SET FORTH IN MC MILLAN
220 REM     AND GONZALES (PAGES 189 TO 211).
230 REM
240 REM         FOLLOWING THE CONVENTION AS DELINEATED IN M &G,
250 REM     A CIRCLE CORRESPONDS TO A 'COMPLETION EVENT' AND
260 REM     AN ARROW CORRESPONDS TO AN 'ACTIVITY'.  IN THE 
270 REM     NETWORK ANALYSIS TWO PARAMETERS ARE ASSOCIATED
280 REM     WITH EACH TIME CONSUMING ACTIVITY: A MEAN AND A
290 REM     VARIANCE.
300 REM
310 REM         FOR EACH EVENT IN  YOUR NETWORK, THE PROGRAM
320 REM     WILL DETERMINE THE FOLLOWING VARIABLES FOR YOU:
330 REM
340 REM       1)  TE = THE EARLIEST EXPECTED TIME OF OCCURRENCE
350 REM                OF COMPLETION EVENT X.
360 REM
370 REM       2)  V = THE VARIANCE ASSOCIATED WITH THE TE OF THE 
380 REM                EVENT.
390 REM
400 REM       3)  TL = THE LATEST EXPECTED TIME WHICH A COM-
410 REM                PLETION EVENT CAN BE ALLOWED TO OCCUR
420 REM                WITHOUT DISTURBING THE TE OF THE FINAL
430 REM                EVENT OF THE NETWORK.
440 REM
450 REM       4)  SLACK = TE-TL
460 REM
470 REM        THE ABOVE MENTIONED VARIABLE TE MUST NOT BE CONFUSED
480 REM     WITH T(E).  THE VALUE T(E) IS THE EXPECTED ANOUNT OF
490 REM     TIME REQUIRED FOR THE COMPLETION OF A SINGLE ACTIVITY,
500 REM     INDEPENDENT OF WHAT HAS OCCURRED BEFORE IT.  THE DATA
510 REM     FOR T(E) OF EACH EVENT WILL BE ENTERED BY YOU AS
520 REM     DESCRIBED BELOW.  THE SAME NOMENCLATURE IS USED WITH
530 REM     V AND V(E).
540 REM
550 REM         IN THIS PROGRAM NO EVENT CAN HAVE MORE THAN TWO
560 REM     IMMEDIATE PREDECESSOR EVENTS OR MORE THAN TWO
570 REM     IMMEDIATE SUCCESSOR EVENTS.  TO USE THIS PROGRAM WITH
580 REM     MORE COMPLEX NETWORKS CONTAINING MORE THAN TWO
590 REM     IMMEDIATE PREDECESSOR OR SUCCESSOR EVENTS, ENTER
600 REM     A NUMBERED 'DUMMY' EVENT THAT HAS A T(E) =0 AND
610 REM     A V(E) =0.
620 REM
630 REM         FOR EXAMPLE, IF EVENTS 1, 2 AND 3 PRECEED EVENT 5
640 REM     THEN ENTER A 'DUMMY' EVENT 4, OF ZERO (0) TIME FOR
650 REM     COMPLETION AND ZERO (0) VARIANCE, SUCH THAT EVENTS
660 REM     1 AND 2, OR 1 AND 3, OR 2 AND 3 PRECEED EVENT 4.
670 REM     THEN EVENT 5 WILL HAVE ONLY TWO PRECEEDING EVENTS AS
680 REM     ALLOWED BY THE PROGRAM. (IE, EVENT 5 WILL BE PRECEEDED
690 REM     BY 1 AND 4, OR 2 AND 4, OR 3 AND 4)  --SEE PAGE 197
700 REM     OF M & G.
710 REM
720 REM         BEGINNING IN LINE 3000 ENTER THE DATA.  FIRST ENTER
730 REM     T, THE TOTAL NUMBER OF EVENTS IN THE NETWORK INCLUDING
740 REM     DUMMY EVENTS IN LINE 3000.
750 REM
760 REM         THEN BEGINNING WITH THE FINAL EVENT AS NUMBER ONE,
770 REM     ( 1 ), ENTER IN LINE 3001 THE FOLLOWING 8 PIECES OF
780 REM     INFORMATION ABOUT THE EVENT IN THE ORDER INDICATED,
790 REM     ALL ON LINE 3001.  REPEAT THIS SEQUENCE, ENTERING THE
800 REM     8 PIECES OF DATA FOR EACH OF THE REMAINING EVENTS ON A 
810 REM     SINGLE LINE.  INCREMENT LINE NUMBERS BY 1 AFTER LINE
820 REM     3001.
830 REM
840 REM     1) THE NUMBER OF THE EVENT'S FIRST IMMEDIATE
850 REM        PREDECESSOR EVENT.
860 REM
870 REM     2) THE T(E) ASSOCIATED WITH THE ACTIVITY BOUNDED
880 REM        BY THIS COMPLETION EVENT AND ITS FIRST
890 REM        IMMEDIATE PREDECESSOR EVENT.
900 REM
910 REM     3) THE VARIANCE V(E) ASSOCIATED WITH THE ACTIVITY
920 REM        BOUNDED BY THIS COMPLETION EVENT AND ITS FIRST
930 REM        IMMEDIATE PREDECESSOR EVENT.
940 REM
950 REM     4) THE NUMBER OF THE EVENT'S SECOND IMMDEIATE
960 REM        PREDECESSOR EVENT. IF NONE, ENTER ZERO.
970 REM
980 REM     5) THE T(E) ASSOCIATED WITH THE ACTIVITY BOUNDED BY
990 REM        THIS COMPLETION EVENT AND ITS SECOND IMMEDIATE
1000 REM        PREDECESSOR EVENT.  IF NONE, ENTER ZERO.
1010 REM
1020 REM     6) THE VARIANCE V(E) ASSOCIATED WITH THE ACTIVITY
1030 REM        BOUNDED BY THIS COMPLETION EVENT AND ITS SECOND
1040 REM        IMMEDIATE PREDECESSOR EVENT. IF UNKNOWN, ENTER ZERO.
1050 REM
1060 REM     7) THE TE OF THE EVENT, WHERE KNOWN.
1070 REM        IF UNKNOWN, ENTER ZERO.
1080 REM
1090 REM     8) THE VARIANCE V ASSOCIATED WITH THE EVENT, WHERE
1100 REM        KNOWN.  IF UNKNOWN, ENTER ZERO.
1110 REM
1120 REM        THE LAST EVENT (ACTUALLY ENTERED FIRST, AS ABOVE) 
1130 REM     MUST BE LABELED ONE.  THE OTHER EVENTS NEED NOT HAVE
1140 REM   ANY ORDER.  REMEMBER, WHEN ENTERING THE DATA THAT YOU
1150 REM     ENTER T (LINE 3000), THE 8 PIECES OF DATA FOR EVENT
1160 REM     1 (LINE 3001), THEN 8 FOR EVENT 2 (LINE 3002), THEN
1170 REM     8 FOR EVENT 3 (LINE 3003), ETC., IN STRICT
1180 REM     SEQUENTIAL EVENT NUMBER ORDER, REGARDLESS OF THE
1190 REM     PHYSICAL LAYOUT ORDER OF THE NETWORK EVENTS.
1200 REM
1210 REM     T(E) IS DEFINED AS;  T(E) = ( A + 4*M + B )/6, WHERE
1220 REM
1230 REM     A = MOST OPTIMISTIC TIME FOR COMPLETION OF THE ACTIVITY
1240 REM     B = MOST PESSEMISTIC TIME FOR COMPLETION OF THE
1250 REM         ACTIVITY
1260 REM     C = MOST LIKELY TIME FOR COMPLETION OF THE ACTIVITY
1270 REM
1280 REM     VARIANCE = V(E) = ((B-A)/6)^2, WHERE A AND B ARE 
1290 REM     DEFINED AS ABOVE.
1300 REM
1310 REM
1320 REM          AT LEAST ONE EVENT IN THE NETWORK MUST HAVE ITS
1330 REM     EARLIEST COMPLETION TIME SPECIFIED ( I.E. AT LEAST
1340 REM     ONE EVENT'S TE MUST BE KNOWN.
1350 REM
1360 REM         IF HOWEVER, NO EVENT HAS A SPECIFIED TE, LET THE
1370 REM     THE TE OF THE INITIAL EVENT IN THE NETWORK BE
1380 REM     SPECIFIED AS ONE, ( 1 ).  THUS, THE INITIAL EVENT IN
1390 REM     THE NETWORK WOULD HAVE IN ITS SEVENTH DATA LOCATION
1400 REM     A ONE, INSTEAD OF A ZERO.  (IE, ASSUME EVENT NUMBER
1410 REM     9 IS THIS FIRST EVENT IN THE NETWORK AND STARTS AT 
1420 REM     TIME ZERO, ITS DATA FORM WOULD LOOK LIKE
1430 REM     XXXX DATA ------0-, HOWEVER, FOR THE PROGRAM TO 
1440 REM     OPERATE THE EVENT WOULD HAVE TO BE ASSIGNED AN
1450 REM     ARBITRARY TE, SAY 1, ITS CORRECT DATA STATEMENT
1460 REM     WOULD TAKE THE FORM,  XXXX DATA ------1-.
1470 REM
1480 REM
1490 REM          DON'T FORGET TO ENTER 'T', THE NUMBER OF EVENTS
1500 REM     IN THE NETWORK.  EACH EVENT IN THE NETWORK MUST HAVE 
1510 REM     8 PIECES OF DATA ENTERED FOR IT.
1520 REM
1530 REM          IF THERE ARE MORE THAN TEN EVENTS IN THE NETWORK
1540 REM     ENTER A DIM STATEMENT IN LINE  1600 SUCH THAT E(T,13),
1550 REM     P(T) AND S(T) ARE DIMENSIONED, WHERE T=NUMBER OF EVENTS.
1560 REM     FOR EXAMPLE, IF YOUR NETWORK HAS 18 EVENTS, THE
1570 REM     DIM STATEMENT WOULD BE DIM E(18,13),P(18),S(18).
1580 REM     AFTER THE DATA HAS BEEN ENTERED, TYPE: RUN
1590 REM
1600 DIM E(10,13),P(10),S(10)
1610 REM T READ ALREADY AT FRONT OF PROGRAM
1620 FOR N= 1 TO T
1630 FOR I= 1 TO 8
1640 READ E(N,I)
1650 NEXT I
1660 FOR I= 9 TO 13
1670 LET E(N,I) = 0
1680 NEXT I
1690 NEXT N
1700 REM FIND TE
1710 FOR I= 1 TO T
1720 LET P(I) = 0
1730 NEXT I
1740 LET I = 1
1750 LET P(1)  = 1
1760 LET N = 1
1770 LET N = E(N,1)
1780 LET I = I + 1
1790 IF I>T THEN 2820
1800 LET P(I) = N
1810 IF E(N,7) = 0 THEN 1770
1820 LET I = I - 1
1830 IF I = 0 THEN 2200
1840 LET N = P(I)
1850 IF S(N) = 2 THEN 2040
1860 IF E(N,4) <> 0 THEN 1900
1870 LET E(N,7) = E(N,2) + E(P(I+1),7)
1880 LET E(N,8) =  E(N,3) + E(P(I+1),8)
1890 GO TO 1820
1900 LET A = E(P(I+1),7) + E(N,2)
1910 LET F = E(P(I+1),8) + E(N,3)
1920 LET N = E(N,4)
1930 LET I = I + 1
1940 LET P(I) = N
1950 IF E(N,7) <> 0 THEN 1980
1960 LET S(P(I-1)) = 2
1970 GO TO 1770
1980 LET I = I - 1
1990 IF I = 0 THEN 2100
2000 LET N=P(I)
2010 LET B = E(P(I+1),7) + E(N,5)
2020 LET G = E(P(I+1),8) + E(N,6)
2030 GO TO 2120
2040 LET B = E(P(I+1),7) + E(N,5)
2050 LET G = E(P(I+1),8) + E(N,6)
2060 LET N = E(N,1)
2070 LET C = E(N,7)
2080 LET H = E(N,8)
2090 LET N = P(I)
2100 LET A = C + E(N,2)
2110 LET F = H + E(N,3)
2120 IF A > B THEN 2160
2130 LET E(N,7) = B
2140 LET E(N,8) = G
2150 GO TO 1820
2160 LET E(N,7) = A
2170 LET E(N,8) = F
2180 GO TO 1820
2190 REM  FIND SUCCESSOR EVENTS AND RECORD TE'S
2200 FOR I = 1 TO T
2210 LET N = E(I,1)
2220 IF N = 0 THEN 2290
2230 IF E(N,9) <> 0 THEN 2270
2240 LET E(N,9) = I
2250 LET E(N,10) = E(I,2)
2260 GO TO 2290
2270 LET E(N,11) = I
2280 LET E(N,12) = E(I,2)
2290 LET N = E(I,4)
2300 IF N = 0 THEN 2370
2310 IF E(N,9) <> 0 THEN 2350
2320 LET E(N,9) = I
2330 LET E(N,10) = E(I,5)
2340 GO TO 2370
2350 LET E(N,11) = I
2360 LET E(N,12) = E(I,5)
2370 NEXT I
2380 REM  FIND TL
2390 LET E(1,13) = E(1,7)
2400 FOR K= 1 TO T
2410 IF E(K,13) = 0 THEN 2440
2420 NEXT K
2430 GOTO 2740
2440 LET N = K
2450 LET I = 1
2460 LET P(1) = N
2470 IF E(N,13) <> 0 THEN 2610
2480 LET A = E(N,9)
2490 IF E(A,13) = 0 THEN 2650
2500 LET B = E(N,11)
2510 IF B = 0 THEN 2600
2520 IF E(B,13) = 0 THEN 2690
2530 LET T1 = E(A,13) - E(N,10)
2540 LET T2 = E(B,13) - E(N,12)
2550 IF T1 < T2 THEN 2580
2560 LET E(N,13) = T2
2570 GO TO 2610
2580 LET E(N,13) = E(A,13) - E(N,10)
2590 IF I = 1 THEN 2400
2600 LET E(N,13) = E(A,13) - E(N,10)
2610 IF I = 1 THEN 2400
2620 LET I = I - 1
2630 LET N = P(I)
2640 GO TO 2470
2650 LET N = A
2660 LET I = I + 1
2670 LET P(I) = N
2680 GO TO 2470
2690 LET N = B
2700 LET I = I + 1
2710 LET P(I) = N
2720 GO TO 2470
2730 REM PRINT RESULTS
2740 PRINT "EVENT NUMBER";TAB(22);"TE";TAB(38);"V";TAB(54);"TL";
2750 PRINT TAB(62);"TOTAL SLACK"
2760 PRINT "----- ------";TAB(22);"--";TAB(38);"-";TAB(54);"--";
2770 PRINT TAB(62);"----- -----"
2780 PRINT
2790 FOR N = T TO 1 STEP -1
2800 PRINT USING " -####            -####.#        -####.#         -####.#      -####.#",N,E(N,7),E(N,8),E(N,13),E(N,13)-E(N,7)
2810 NEXT N
2815 STOP
2820 PRINT "DATA NOT CONSISTENT--OR NO TE SPECIFIED"
2830 STOP
3000 DATA 4E36
9999 END
