"  ***********************************************************
"  *                                                         *
"  *                                                         *
"  * Copyright, (C) Honeywell Information Systems Inc., 1981 *
"  *                                                         *
"  *                                                         *
"  ***********************************************************

"ALM subroutine to convert from 9 bit ASCII to 9 bit ABCDEF
"ABCDEF is identical to ASCII except that the 400 bit is on for all non-alphabetic chars
"input bytes must be valid ASCII characters whose octal values
"fall in the range 000 <_ octal_value <_ 177
"
"ARG 1: pointer to source string - data to be converted
"ARG 2: pointer to target string - converted data
"
"PL/I Usage:
"
"dcl  ascii_to_abcdef_ ext entry (char (*), char (*));
"    call ascii_to_abcdef_ (input_string, output_string);
"
"
"Note: the ASCII to ABCDEF mapping used is defined in the
"      text of this procedure.  It is available to a user
"      program through the following declaration.
"
"dcl  ascii_to_abcdef_$aa_table char (128) external static;
"
"The table consists of 128 ABCDEF characters which
"correspond to the 128 ASCII characters.  The first character
"corresponds to 000, the 2nd to 001, ....., the 128th
"to 177.
"
"Converted from ascii_to_ebcdic_ by J. Stern 11/2/77
                    name      ascii_to_abcdef_
		segdef	ascii_to_abcdef_
		segdef	aa_table
ascii_to_abcdef_:	epp1	ap|2,*		address of source string to pr1
		epp3	ap|4,*		address of target string to pr3
		ldx3	0,du		set x3 not to skip parent pointer if none
		lxl2	ap|0		load argument list code value
		canx2	=o000004,du	check for code 4 - no parent pointer
		tnz	*+2		transfer if no parent pointer
		ldx3	2,du		parent pointer - set x3 to skip it
		lda	ap|6,x3*		load source string descriptor
		ldq	ap|8,x3*		load target string descriptor
		ana	mask		drop all but string size bits
		anq	mask		ditto
		even
		mvt	(pr,rl),(pr,rl),fill(040)	translate ascii to abcdef
		desc9a	1|0,al		source string
		desc9a	3|0,ql		target string
		arg	aa_table
		short_return		"exit

mask:		oct	000077777777
		even
aa_table:		oct	400401402403,404405406407
		oct	410411412413,414415416417
		oct	420421422423,424425426427
		oct	430431432433,434435436437
		oct	000441442443,444445446447
		oct	450451452453,454455456457
		oct	460461462463,464465466467
		oct	470471472473,474475476477
		oct	500101102103,104105106107
		oct	110111112113,114115116117
		oct	120121122123,124125126127
		oct	130131132533,534535536537
		oct	540141142143,144145146147
		oct	150151152153,154155156157
		oct	160161162163,164165166167
		oct	170171172573,574575576577
		bss	,96			codes > 177 translate to 000

		end
