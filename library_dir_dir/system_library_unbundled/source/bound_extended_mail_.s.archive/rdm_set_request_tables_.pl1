/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */

/* format: off */

/* Sets the request table list used by the read_mail subsystem */

/* Created:  17 September 1982 by G. Palter */
/* Modified: 13 September 1983 by G. Palter as part of the conversion of read_mail to the new mail system interface */

/* format: style4,delnl,insnl,ifthenstmt,ifthen */


rdm_set_request_tables_:
     procedure (P_rdm_invocation_ptr, P_code);


/* Parameters */

dcl  P_rdm_invocation_ptr pointer parameter;
dcl  P_code fixed binary (35) parameter;


/* read_mail request tables */

dcl  1 rdm_request_tables aligned,
       2 header like request_tables_list.header,
       2 tables (3) like request_tables_list.tables;	/* up to 3 possible tables (see below) */


/* Remaining declarations */

dcl  table_idx fixed binary;

/* format: off */
dcl (rdm_request_tables_$standard_requests, rdm_request_tables_$debug_requests, ssu_request_tables_$standard_requests)
	bit (36) aligned external;
/* format: on */

dcl  ssu_$set_request_tables entry (pointer, pointer, fixed binary (35));

dcl  addr builtin;
%page;
/* rdm_set_request_tables_: entry (P_rdm_invocation_ptr, P_code); */

	rdm_invocation_ptr = P_rdm_invocation_ptr;

	rdm_request_tables.version = REQUEST_TABLES_LIST_VERSION_1;

	table_idx = 1;				/* always include standard read_mail requests */
	rdm_request_tables.tables (1).table_ptr = addr (rdm_request_tables_$standard_requests);

	if rdm_invocation.debug_mode then do;		/* include debugging requests if needed */
	     table_idx = table_idx + 1;
	     rdm_request_tables.tables (table_idx).table_ptr = addr (rdm_request_tables_$debug_requests);
	end;

	table_idx = table_idx + 1;			/* standard subsystem requests are always last */
	rdm_request_tables.tables (table_idx).table_ptr = addr (ssu_request_tables_$standard_requests);

	rdm_request_tables.n_tables = table_idx;

	call ssu_$set_request_tables (rdm_invocation.sci_ptr, addr (rdm_request_tables), P_code);

	return;
%page;
%include rdm_invocation;
%page;
%include ssu_request_tables_list;

     end rdm_set_request_tables_;
