/* ***********************************************************
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1982 *
   *                                                         *
   *********************************************************** */

/* format: off */

/* Sets the request table list used by the send_mail subsystem */

/* Created:  17 September 1982 by G. Palter */
/* Modified: August 1983 by G. Palter to convert to new mail system interface */

/* format: style4,delnl,insnl,ifthenstmt,ifthen */


sdm_set_request_tables_:
     procedure (P_sdm_invocation_ptr, P_code);


/* Parameters */

dcl  P_sdm_invocation_ptr pointer parameter;
dcl  P_code fixed binary (35) parameter;


/* send_mail request tables */

dcl  1 sdm_request_tables aligned,
       2 header like request_tables_list.header,
       2 tables (4) like request_tables_list.tables;	/* up to 4 possible tables (see below) */


/* Remaining declarations */

dcl  table_idx fixed binary;

/* format: off */
dcl (sdm_request_tables_$standard_requests, sdm_request_tables_$original_requests, sdm_request_tables_$debug_requests,
     ssu_request_tables_$standard_requests)
	bit (36) aligned external;
/* format: on */

dcl  ssu_$set_request_tables entry (pointer, pointer, fixed binary (35));

dcl  (addr, null) builtin;
%page;
/* sdm_set_request_tables_: entry (P_sdm_invocation_ptr, P_code); */

	sdm_invocation_ptr = P_sdm_invocation_ptr;

	sdm_request_tables.version = REQUEST_TABLES_LIST_VERSION_1;

	table_idx = 1;				/* always include standard send_mail requests */
	sdm_request_tables.tables (1).table_ptr = addr (sdm_request_tables_$standard_requests);

	if sdm_invocation.rdm_invocation_ptr ^= null () then do;
	     table_idx = table_idx + 1;		/* include the original requests if in read_mail reply */
	     sdm_request_tables.tables (table_idx).table_ptr = addr (sdm_request_tables_$original_requests);
	end;

	if sdm_invocation.debug_mode then do;		/* include debugging requests if needed */
	     table_idx = table_idx + 1;
	     sdm_request_tables.tables (table_idx).table_ptr = addr (sdm_request_tables_$debug_requests);
	end;

	table_idx = table_idx + 1;			/* standard subsystem requests are always last */
	sdm_request_tables.tables (table_idx).table_ptr = addr (ssu_request_tables_$standard_requests);

	sdm_request_tables.n_tables = table_idx;

	call ssu_$set_request_tables (sdm_invocation.sci_ptr, addr (sdm_request_tables), P_code);

	return;
%page;
%include sdm_invocation;
%page;
%include ssu_request_tables_list;

     end sdm_set_request_tables_;
