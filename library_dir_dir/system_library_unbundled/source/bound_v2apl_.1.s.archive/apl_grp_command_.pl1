/* ******************************************************
   *                                                    *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   *                                                    *
   ****************************************************** */

apl_grp_command_:
	procedure (group_name);

/*
 * command to list the members of a group
 * written 73.9.06 by DAM
 */

dcl group_name char(*) parameter;



	call apl_create_save_frame_;			/* use global meanings */
	call apl_get_symbol_(before(group_name, " "), sbp, (0));
	if sbp -> symbol_bead.meaning_pointer = null then go to no_good;
	if ^ sbp -> symbol_bead.meaning_pointer -> general_bead.type.group then go to no_good;

	do n = 1 by 1 while(n <= sbp -> symbol_bead.meaning_pointer -> group_bead.number_of_members);
	   call apl_print_string_(sbp -> symbol_bead.meaning_pointer -> group_bead.member(n) ->
				symbol_bead.name);
	   end;

	call apl_print_newline_;
	call apl_flush_buffer_;
wash_ret:	 
	sbp -> symbol_bead.reference_count = sbp -> symbol_bead.reference_count - 1;
	if sbp -> symbol_bead.reference_count <= 0 then call apl_free_bead_(sbp);
	call apl_destroy_save_frame_;
	return;

no_good:	call ioa_$ioa_stream ("apl_output_", "^Rincorrect command^B");
	go to wash_ret;



dcl sbp unaligned pointer,
    n fixed bin;

dcl apl_get_symbol_ entry(char(*), unaligned pointer, fixed bin),
    apl_print_string_ entry (char (*)),
    apl_print_newline_ entry,
    apl_flush_buffer_ entry,
    apl_create_save_frame_ entry,
    apl_destroy_save_frame_ entry,
    apl_free_bead_ entry (unaligned pointer);

dcl ioa_$ioa_stream entry options(variable);

dcl (null, before) builtin;

%include apl_bead_format;
%include apl_symbol_bead;
%include apl_group_bead;




end;
