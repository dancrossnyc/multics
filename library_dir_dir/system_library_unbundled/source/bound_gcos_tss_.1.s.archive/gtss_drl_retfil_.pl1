/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

/* *************************************************************
   *						   *
   * Copyright (c) 1979 by Honeywell Information Systems, Inc. *
   *						   *
   ************************************************************* */
gtss_drl_retfil_: proc (mcpp, increment);

/* *	Close file and remove from aft.

   Authors: Robert J. Grimes	Created
   -	  Albert N. Kepner	  1978
   -	  Robert M. May
   -	  David B. Ward
   Change: Dave Ward	04/13/79 db_ debug code.
   Change: Dave Ward	08/03/79 Revised ios close call.
   Change: Bob Alvarado	12/05/79 Added code to not let sy** be removed.
   * */
dcl  mcpp ptr parm;
dcl  increment fixed bin (24)parm;

/*

   DRL RETFIL, RETURN A FILE (octal 14)


   8_________1_6_____________________

   DRL      RETFIL
   ZERO     L(filed),L(buff)

   (See TSS SYSTEM PROGRAMMER'S
   REFERENCE MANUAL DD17B, Rev. 0, July 1976
   Page 3-53)

*/


	mcp = mcpp;				/* Set local value. */
	increment = 1;				/* one arguement */
	scup = addr (mc.scu);

/* *	Obtain pointer to callers "memory".	* */
	gseg = gtss_ext_$gtss_slave_area_seg (gtss_ext_$stack_level_);

/* *	Obtain pointer to 1 word drl argument list.	* */
	arg_list_ptr = addrel (gseg, fixed (scu.ilc)+1);

/* *	Check location of caller's fileid.	* */
	if fixed (arg_list.L_fileid, 18)>fixed (gtss_ust.lsize.limit, 18) then do;
	     call gtss_abort_subsystem_ (mcp,
		"gtss_drl_retfil_",
		4,
		"RETFIL: Fileid out of memory at (^6o)",
		fixed (scu.ilc, 18));
	     return;
	end;

/* *	Obtain pointer to caller's fileid.	* */
	fileid_ptr = addrel (gseg, arg_list.L_fileid);

	if right_char = (9)"1"b then do;		/* Octal 777 => all but SY** */
	     if db_drl_retfil then
		call com_err_ (0, "gtss_drl_retfil_",
		" Returning all but SY**");
	     call all_but ("SY**");
	     return;
	end;

	if right_char = "111111110"b then do;		/* Octal 776 => all but *CF */
	     if db_drl_retfil then
		call com_err_ (0, "gtss_drl_retfil_",
		"Returning all but *CF");
	     call all_but ("*CF");
	     return;
	end;

/* *	Otherwise, designated file. * */
	if db_drl_retfil then
	     call com_err_ (0, "gtss_drl_retfil_",
	     "Returning file ""^a""",
	     fileid);
	call remove (fileid);
	return;

all_but:	proc (fileid);

/* *	Remove all files except that
   named by parameter fileid.
   * */
dcl  fileid char (8) parm;
	     do i = 1 to hbound (aft_entry, 1);
		if used (i) then
		     if fileid ^= altname (i) then call remove (fileid);
	     end;
	     return;

dcl  i fixed bin (24);
	end					/* all_but */;

remove:	proc (fileid);

/* *	Remove file named by fileid.	* */
dcl  fileid char (8) parm;
	     unspec (fms_status) = "0"b;
/** SY** cannot be removed  **/
	     if fileid = "SY**    " then return;
	     call gtss_aft_$delete (fileid, fn, code);
	     if code = 0 then do;
		call gtss_ios_close_ (fn, addr (fms_status), code);
		if fms_status.bit12 ^= "4000"b3 then do;
dcl 1 fms_status aligned,
      2 bit12 bit(12)unal,
      2 bit60 bit(60)unal;
		     call gtss_abort_subsystem_ (
			mcp
			, "gtss_drl_retfil_"
			, 0
			, "RETFIL: Can not return ""^a"" (DRL at ^6o) gtss_ios_close_ status=^w"
			, fileid
			, fixed (scu.ilc, 18)
			, fms_status
			);
		end;
	     end;

	     return;

dcl  fn fixed bin (24);
dcl  code fixed bin (35);
	end					/* remove */;

/* *	Variables for gtss_drl_retfil_:
   IDENTIFIER	ATTRIBUTES	* */
dcl  addrel builtin;
dcl  arg_list_ptr ptr init(null());
dcl  code fixed bin (35);
dcl  fileid char (8) based (fileid_ptr);
dcl  fileid_ptr ptr init(null());
dcl  fn fixed bin (24);
dcl  gseg ptr init(null());

dcl 1 arg_list aligned based (arg_list_ptr)
     , 2 L_fileid bit (18)unal
     , 2 L_buff bit (18)unal
     ;

dcl 1 fileid_2 aligned based (fileid_ptr)
     , 2 filler bit (27)unal
     , 2 right_char bit (9)
     ;

%include	gtss_ext_;

%include gtss_ust_ext_;

%include	mc;

%include gtss_entry_dcls;

%include gtss_db_names;
     end						/* gtss_drl_retfil_ */;
