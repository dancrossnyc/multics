" ***************************************************************
" *                                                             *
" * Copyright (c) 1982 by Massachusetts Institute of Technology *
" *                                                             *
" ***************************************************************

" HISTORY COMMENTS:
"  1) change(86-07-29,Pattin), approve(86-07-29,MCR7354),
"     audit(86-08-03,Margolin), install(86-08-16,MR12.0-1128):
"     Added $next_transaction and $previous_transaction entries.
"  2) change(86-07-29,Pattin), approve(86-07-29,MCR7356),
"     audit(86-08-03,Margolin), install(86-08-16,MR12.0-1128):
"     Added $rechain entry.
"                                                      END HISTORY COMMENTS


"
"	Transfer vector for bound_forum_mgr_
"	Designed to avoid problems with installations.
"
"	Jay Pattin 5/9/82
"
"	Modified to choose between version 1 and version 2 2/28/83 Jay Pattin

	name	v2forum_mgr_tv_

	temp	temp1

"	tv	target_seg,entry_name{,target_entry_name}

	macro	tv

	segdef	&2

&2:	getlp
&=&3.,.&[	tra	&1$&2
&;	tra	&1$&3
&]

	&end


"	idx_tv	v2_target_seg,v2_target_entry,v1_allowed{,real_target_name{,real_v1_target_name}
"
"	This is simple, version 2 uses negative indexes, version 1 uses positive.

	macro	idx_tv

	entry	&2

&2:	tsx3	VERSION_BY_INDEX	" version 2 returns 0,x3 - version 1 1,x3

&=&4.,.&[	tra	&1$&2		" go to version 2 entry
&;	tra	&1$&4
&]

&=&3,no&[	tra	V1_NOT_ALLOWED	" is version 1 allowed
&]
&=&5.,.&[	tra	forum_mgr_tv_$&2	" go to version 1 entry
&;	tra	forum_mgr_tv_$&5
&]
	&end

VERSION_BY_INDEX:
	epp1	pr0|2,*		" first arg is forum_idx
	szn	pr1|0		" is it positive ?
	tpl	1,x3		" yes, then version 1
	tra	0,x3		" else version 2


V1_NOT_ALLOWED:
	lda	pr0|0		" arg_count * 2 in au
	ldq	forum_et_$old_format
	stq	pr0|0,au*
	short_return
"
"	name_tv	v2_target_seg,v2_target_entry,v1_allowed{,real_target_name}
"

	macro	name_tv

	entry	&2

&2:
	tsx3	VERSION_BY_NAME	" returns tra 0,x3 if version 2 - 1,x3 if version 1

&=&4.,.&[	tra	&1$&2		" go to version 2 entry
&;	tra	&1$&4
&]

&=&3,no&[	tra	V1_NOT_ALLOWED	" is version 1 allowed
&;	tra	forum_mgr_tv_$&2	" go to version 1 entry
&]
	
	&end	

CONTROL:	aci	/control/

VERSION_BY_NAME: 
	ldx1	pr0|0			" 2 * arg_count to x1
	lxl0	pr0|0			" call_type to x0
	canx0	10,du			" is there an environment_ptr ?
	tze	2,ic
	adx1	2,du			" skip over it if yes

	ldq	pr0|4,x1*			" load descriptor of second arg (length in ql)
	epp1	pr0|4,*			" pointer to forum name in pr1

	scmr	(pr,rl),(du)		" look backwards for a period
	desc9a	pr1|0,ql			" forum name
	oct	056000000000		" period
	arg	pr6|temp1
	ttn	0,x3			" no period, goto version 2

	sbq	pr6|temp1			" position of last period
	a9bd	pr1|0,ql			" add in offset of period, pr1 now points to character after .
	lxl1	pr6|temp1			" remaining length of forum name.

	cmpc	(pr,rl),(),fill(040)	" look for 'control'
	desc9a	pr1|0,x1
	desc9a	CONTROL,7
	tze	1,x3			" if we win, it's version 1
	tra	0,x3			" if not control, it's version 2
"
	name_tv	forum_open_mgr_,open_forum,yes,open
	idx_tv	forum_open_mgr_,close_forum,yes,close

	name_tv	forum_seg_mgr_,list_forum_acl,no
	name_tv	forum_seg_mgr_,set_forum_acl,no
	name_tv	forum_seg_mgr_,replace_forum_acl,no
	name_tv	forum_seg_mgr_,delete_forum_acl,no

	name_tv	forum_open_mgr_,get_switch,no
	name_tv	forum_seg_mgr_,get_uid_file,yes
	name_tv	forum_open_mgr_,set_switch,yes
	idx_tv	forum_open_mgr_,set_switch_idx,yes

	idx_tv	forum_open_mgr_,set_event_channel_idx,yes
	idx_tv	forum_trans_mgr_,convert_attendee_idx,yes
	idx_tv	forum_trans_mgr_,validate_uid,yes

	idx_tv	forum_trans_mgr_,get_message,yes
	idx_tv	forum_trans_mgr_,enter_trans,yes
	idx_tv	forum_trans_mgr_,read_trans,yes

	idx_tv	forum_trans_mgr_,rechain,no
	idx_tv	forum_trans_mgr_,set_delete_sw,yes

	idx_tv	forum_trans_mgr_,forum_limits,yes,,real_forum_limits
	name_tv	forum_open_mgr_,list_users,yes
	idx_tv	forum_open_mgr_,list_users_idx,yes

	idx_tv	forum_trans_mgr_,trans_ref_info,yes,,real_trans_ref_info
	idx_tv	forum_trans_mgr_,trans_time_info,yes
	idx_tv	forum_trans_mgr_,next_transaction,no
	idx_tv	forum_trans_mgr_,previous_transaction,no

	idx_tv	forum_trans_mgr_,check_user,yes
	idx_tv	forum_trans_mgr_,set_seen_switch,no
	idx_tv	forum_trans_mgr_,get_transaction_map,no
	idx_tv	forum_trans_mgr_,get_transaction_map_idx,no

	name_tv	forum_open_mgr_,forum_info,yes
	idx_tv	forum_open_mgr_,forum_info_idx,yes
	name_tv	forum_seg_mgr_,get_forum_path,yes
	idx_tv	forum_seg_mgr_,get_forum_path_idx,yes

	name_tv	forum_seg_mgr_,create_forum,yes
	name_tv	forum_seg_mgr_,chname_forum,yes
	tv	forum_conversion_,convert	
	tv	forum_salvager_,copy
	idx_tv	forum_seg_mgr_,chname_forum_idx,yes
	name_tv	forum_seg_mgr_,delete_forum,yes
	idx_tv	forum_trans_mgr_,set_message,yes

	name_tv	forum_open_mgr_,change_chairman,yes
	idx_tv	forum_open_mgr_,change_chairman_idx,yes

	idx_tv	forum_salvager_,expunge,no

	name_tv	forum_open_mgr_,priv_change_chairman,yes
	tv	forum_conversion_,priv_convert	
	name_tv	forum_open_mgr_,priv_set_switch,yes
	tv	forum_open_mgr_,set_global_switch
	
	end
