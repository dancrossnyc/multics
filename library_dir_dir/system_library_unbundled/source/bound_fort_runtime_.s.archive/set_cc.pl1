/* ******************************************************
   *                                                    *
   * Copyright, (C) Honeywell Limited, 1983             *
   *                                                    *
   * Copyright (c) 1972 by Massachusetts Institute of   *
   * Technology and Honeywell Information Systems, Inc. *
   *                                                    *
   ****************************************************** */

/* Coded by A. Downing to turn carriage control
   off/on for fortran files. */

/* Modified:
	14 July 1983, Michael Mabey - add the control args: -defer, -no_defer
	22 April 1977, David Levin -change to just act as command interface
		and not attempt to validate its input
*/

/* format: style3,^delnl,linecom */
set_cc:
     proc;

dcl	cu_$arg_count	entry (fixed bin);
dcl	cu_$arg_ptr	entry (fixed bin, ptr, fixed bin, fixed bin (35));
dcl	com_err_		entry options (variable);
dcl	error_table_$wrong_no_of_args
			ext static fixed bin (35);
dcl	file_num		fixed bin;
dcl	fortran_io_$set_cc	entry (fixed bin, bit (1) aligned, fixed bin (35));
dcl	fortran_io_$set_cc_defer
			entry (fixed bin, bit (1) aligned, bit (1) aligned, bit (1) aligned, fixed bin (35));
dcl	arg		char (n) based (argp);
dcl	argp		ptr;
dcl	n		fixed bin;
dcl	i		fixed bin;
dcl	number_of_args	fixed bin;
dcl	cc		bit (1) aligned init ("1"b);
dcl	defer		bit (1) aligned init ("0"b);
dcl	defer_specified	bit (1) aligned init ("0"b);
dcl	code		fixed bin (35);
dcl	(verify, substr)	builtin;

	call cu_$arg_count (number_of_args);			/* get number of args */
	if number_of_args < 2
	then do;
		call com_err_ (error_table_$wrong_no_of_args, "set_cc", "^/Usage: set_cc filenn -control_args");
		return;
	     end;

	call cu_$arg_ptr (1, argp, n, code);		/* Get first one, file name. */
	if code ^= 0
	then do;
		call com_err_ (code, "set_cc");
		return;
	     end;

/* validate it */

	if substr (arg, 1, 4) ^= "file" | verify (substr (arg, 5, 2), "0123456789") ^= 0 | substr (arg, 7) ^= ""
	then do;
		call com_err_ (0, "set_cc", "filenn is filename, where nn is two digit value of file number. ""^a""",
		     arg);
		return;
	     end;

	file_num = convert (file_num, substr (arg, 5));	/* convert to binary */

	do i = 2 to number_of_args;			/* process the remaining arguments */
	     call cu_$arg_ptr (i, argp, n, code);
	     if code ^= 0
	     then do;
		     call com_err_ (code, "set_cc");
		     return;
		end;
	     if arg = "-on"
	     then cc = "1"b;
	     else if arg = "-off"
	     then cc = "0"b;
	     else if arg = "-defer"
	     then defer, defer_specified = "1"b;
	     else if arg = "-no_defer"
	     then do;
		     defer = "0"b;
		     defer_specified = "1"b;
		end;
	     else do;
		     call com_err_ (0, "set_cc", "Valid arguments are: -on; -off; -defer; -no_defer.  Not ""^a"".", arg);
		     return;
		end;
	     end /* of while loop */;

	call fortran_io_$set_cc_defer (file_num, cc, defer, defer_specified, code);
	if code ^= 0
	then call com_err_ (code, "set_cc", "File number ^d", file_num);

     end /* set_cc */;
