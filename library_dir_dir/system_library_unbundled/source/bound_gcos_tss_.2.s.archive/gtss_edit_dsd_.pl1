/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

gtss_edit_dsd_: proc (bp, bl) returns (fixed bin (24));

/**	This function is called to edit certain lines
	that do not appear in the output stream.
	e.g. the $*$talk request line.
	It will return a value of 1 if the buffer
	contains a line which should not
	appear in the stream, else it will return 0.

	Author: Al Dupuis 05/16/79.


**/
	i = index (cs, "$*$");			/* is it a request line */
	if i = 0 then do;
	     i = index (cs, "cout");			/* or the cout command */
	     if i = 0 then i = index (cs, "COUT");
	     if i ^= 0 then return (1);
	end;
	if i = 0 then return (0);			/* no, then it's normal input */
	rls = substr (cs, i + 3);			/* all of line after $*$  */
	rls = substr (rls, 1, length (rls) - 1);	/* get rid of NL */
	rls = ltrim (rls);				/* get rid of any leading blanks */
	if (length (rls) > 3) then request = substr (rls, 1, 4); /* pick up the keyword */
	else request = substr (rls, 1, 3);
	request = translate (request, "ABCDEFGHIJKLMNOPQRSTUVWXYZ", /* convert to upper */
	     "abcdefghijklmnopqrstuvwxyz");

	if (gtss_dsd_lookup_ (request) ^= 0) then return (1);
	else return (0);

/**  **/

dcl bp ptr parm;
dcl bl fixed bin (21) parm;
dcl cs char (bl) based (bp);
dcl (substr, ltrim, length, index, translate) builtin;
dcl request char (8) varying;
dcl rls char (252) varying;
dcl i fixed bin (24);
/**  **/

%include gtss_entry_dcls;

     end						/* gtss_edit_dsd_ */;
