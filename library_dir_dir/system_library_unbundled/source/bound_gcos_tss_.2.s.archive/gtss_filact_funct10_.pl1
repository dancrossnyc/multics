/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */

/* *************************************************************
   *						   *
   * Copyright (c) 1979 by Honeywell Information Systems, Inc. *
   *						   *
   ************************************************************* */

gtss_filact_funct10_: proc (mcp_val, high_val, gseg_val, arg_ptr_val, buffer_ptr_val, code);

/**	Derail FILACT function 10 (MC - Modify Catalog).

	All parameters are input parameters except code.

	code returned 0 => Successful.
	code returned 4 => GCOS err4 (see gtss_pnterr structure).
	code returned other => multics error code.

   Author:  Paul W. Benjamin		10/02/79

   Change:  Paul Benjamin	10/08/79	Enable setting of permissions
   Change:  Paul Benjamin     12/17/79  Deal with propagation acls.
   Change:  Ron Barstad       06/11/82  Fix stringsize condition on pstr
**/
dcl  mcp_val                  ptr parm;
dcl  high_val                 fixed bin(18)unsigned unal parm;
dcl  gseg_val                 ptr parm;
dcl  arg_ptr_val              ptr parm;
dcl  buffer_ptr_val           ptr parm;
dcl  code                     fixed bin(35)parm;
	mcp = mcp_val;
	mem_top = high_val;
	gseg = gseg_val;
	arg_ptr = arg_ptr_val;
	buffer_ptr = buffer_ptr_val;

	code = 0;					/* Successful. */

/**	Obtain modify catalog arglist. */
	if (MC_args.L_arglist < mem_bottom) |
	((MC_args.L_arglist +3) > mem_top) then do;
return_err4:   ;
	     code = 4;
	     return;
	end;
	arglist_ptr = addrel (gseg, MC_args.L_arglist);

/**	Obtain return status.	**/
	call validate_status (MC_arglist.L_status_return, status_ptr);

	if get_ascii_file_name ("0"b, MC_arglist.L_cat_filedescr, addr (ascii_cat_file)) then do;
could_not_modify_catalog:
	     if substr (status_word.status, 2, 11) = "0"b then
		status_word.status = gtss_filact_error_status_ (code);
	     call bcd_message (
		status2.L_bcd_message
		, status2.message_words
		, buffer_ptr
		, "Could not modify catalog."||rtrim (dname)||">"||rtrim (ename)
		);
	     status_word.pd = get_faulty_cat_file_entry ();
ret:	     ;
	     return;
	end;


	call gtss_expand_pathname_ (
	     addr (ascii_cat_file)
	     , dname
	     , ename
	     , code
	     );
	if code ^= 0 then goto could_not_modify_catalog;

/**	Obtain new name (if any).	**/
	if (MC_arglist.L_newname < mem_bottom) |
	(MC_arglist.L_newname+4 > mem_top) then goto return_err4;
	new_name_ptr = addrel (gseg, MC_arglist.L_newname);

	if MC_new_name.newname.left_word ^= (36)"1"b then /* New name desired. */
	     call gtss_bcd_ascii_$lc (
	     addr (MC_new_name.newname)
	     , 12
	     , addr (new_ename)
	     );

	call set_general_permissions;
	call set_specific_permissions;
	if new_ename ^= "" then do;
	     call hcs_$chname_file (dname, ename, ename, new_ename, code);
	     if code ^= 0 then do;
		if code = error_table_$namedup | code = error_table_$segnamedup
		then status_word.status = "401100"b3;
		else status_word.status = "400500"b3;
		goto could_not_modify_catalog;
	     end;
	     old_dir = rtrim (dname) || ">" || ename;
	     new_dir = rtrim (dname) || ">" || new_ename;
	     do i = 1 to 20;
		if aft_entry.used (i) = "1"b then do;
		     if substr (gtss_disk.dir_name (i), 1, length (old_dir)) = rtrim (old_dir) then
			gtss_disk.dir_name (i) = rtrim (new_dir)||substr (gtss_disk.dir_name (1), length (old_dir)+1);
		end;
	     end;
	end;
	status_word.status = "400000"b3;
	return;

set_general_permissions: proc;


/**	Obtain permissions.	**/
	     if MC_arglist.L_permissions = 0 then return;
	     if (MC_arglist.L_permissions < mem_bottom) |
	     (MC_arglist.L_permissions > mem_top) then goto return_err4;
	     pp = addrel (gseg, MC_arglist.L_permissions);
	     if substr (pp -> b36, 1, 18) = (18)"1"b then return;
	     a = empty ();
	     call hcs_$list_dir_acl (
		dname
		, ename
		, gtss_ext_$hcs_work_area_ptr
		, da_ptr
		, null ()
		, acl_count
		, code
		);
	     if code ^= 0 then goto could_not_modify_catalog;
	     if pp -> fb18 = 0 then			/* Delete general permissions. */
		do i = 1 to acl_count;
		if index (da_name (i), ".*.g") ^= 0 then do; /* Found propagation acl, delete it. */
		     del_name = da_name (i);
		     call hcs_$delete_dir_acl_entries (
			dname
			, ename
			, addr (delete_acl)
			, 1
			, code
			);
		     if code ^= 0 then goto could_not_modify_catalog;
		     i = acl_count;
		end;
	     end;
	     else

/* => Set specific general permissions. */
	     do i = 1 to acl_count;
		if index (da_name (i), ".*.g") ^= 0 then do; /* Replace acl if propagation acl found. */
		     pstr = substr(char (pp -> b36),1,10);
		     da_name (i) = pstr||".*.g";
		     call hcs_$replace_dir_acl (
			dname
			, ename
			, da_ptr
			, acl_count
			, "1"b
			, code
			);
		     if code ^= 0 then goto could_not_modify_catalog;
		     i = acl_count;
		end;
		else if i = acl_count then do;	/* Add propagation acl if not found. */
		     pstr = substr(char (pp -> b36),1,10);
		     access_name = pstr||".*.g";
		     modes = "0"b;
		     status_code = 0;
		     call hcs_$add_dir_acl_entries (
			dname
			, ename
			, addr (dir_acl)
			, 1
			, code
			);
		     if code ^= 0 then goto could_not_modify_catalog;
		end;
	     end;
	     return;

dcl  acl_count fixed bin;
dcl  a area (255*1024) based (gtss_ext_$hcs_work_area_ptr);
dcl  pp ptr;
dcl  fb18 fixed bin (18)unsigned unal based;
dcl  b36 bit (36)aligned based;
dcl  pstr char (10);

dcl 1 delete_acl,
    2 del_name char (32),
    2 status_code2 fixed bin (35);

dcl 1 dir_acl,
    2 access_name char (32),
    2 modes bit (36),
    2 status_code fixed bin (35);

dcl  da_ptr ptr;

dcl 1 da_array (acl_count) based (da_ptr),
	2 da_name char(32),
	2 da_modes bit(36),
	2 da_code fixed bin(35);
	end					/* set_general_permissions */;

set_specific_permissions: proc;
	     if (L_options<mem_bottom) |
	     ((L_options+2)>mem_top) then goto return_err4;
	     op = addrel (gseg, L_options);

/* Determine number of specific user permissions. */
	     do n = 0 by 1 while (end_of_list ^= -1);
		if (L_options + 3 + (3*n))>mem_top then goto could_not_modify_catalog;
	     end;

	     if n < 1 then return;

/* User specific permissions. */
	     acl_count = n;
	     allocate dir_acl set (acl_ptr);
	     acl_count = 0;
	     rp_sw = "0"b;
	     a = empty ();
	     call hcs_$list_dir_acl (
		dname
		, ename
		, gtss_ext_$hcs_work_area_ptr
		, da_ptr
		, null ()
		, da_count
		, code
		);
	     if code ^= 0 then goto could_not_modify_catalog;
	     c = da_count;
	     do k = 1 to n;
		found_sw = "0"b;
		pp = addr (user (k).specific_permission);
		if pp -> b36 ^= (36)"1"b then do;
		     call gtss_bcd_ascii_ (
			addr (user.id (k))
			, 12
			, addr (ascii_id)
			);
		     if pp -> fb18 = 0 then do i = 1 to da_count; /* delete */
			if index (da_name (i), "."||rtrim (ascii_id)||".g") ^= 0 then do;
			     acl_count = acl_count + 1;
			     del_name (acl_count) = da_name (i);
			     i = da_count;
			end;
		     end;
		     else do i = 1 to c;		/* Replace */
			if index (da_name (i), "."||rtrim (ascii_id)||".g") ^= 0 then do;
			     found_sw = "1"b;
			     pstr = substr(char (pp -> b36),1,10);
			     da_name (i) = pstr||"."||rtrim (ascii_id)||".g";
			     rp_sw = "1"b;
			     i = c;
			end;
		     end;
		     if pp -> fb18 ^= 0 & found_sw = "0"b then do;
			da_count = da_count + 1;
			pstr = substr(char (pp -> b36),1,10);
			da_name (da_count) = pstr||"."||rtrim (ascii_id)||".g";
			da_modes (da_count) = "0"b;
			rp_sw = "1"b;
		     end;
		end;
	     end;
	     if rp_sw = "1"b then do; /* If replacing then do that first. */
		call hcs_$replace_dir_acl (
		     dname
		     , ename
		     , da_ptr
		     , da_count
		     , "1"b
		     , code
		     );
		if code ^= 0 then goto could_not_modify_catalog;
	     end;
	     if acl_count ^= 0 then do; /* If deleting then do that now. */
		call hcs_$delete_dir_acl_entries (
		     dname
		     , ename
		     , acl_ptr
		     , acl_count
		     , code
		     );
		if code ^= 0 then goto could_not_modify_catalog;
	     end;
	     free dir_acl;
	     return;

dcl a area(1000) based (gtss_ext_$hcs_work_area_ptr);

dcl da_ptr ptr;

dcl da_count fixed bin;

dcl 1 da_array (da_count) based (da_ptr),
     2 da_name char(32),
     2 da_modes bit(36),
     2 da_code fixed bin(35);
dcl  acl_count                fixed bin;
dcl  acl_ptr                  ptr init(null());
dcl  ascii_id                 char(12);
dcl  b36                      bit(36)aligned based;
dcl  c                        fixed bin;
dcl  fb18                     fixed bin(18)unsigned unal based;
dcl  found_sw		bit(1);
dcl  k                        fixed bin;
dcl  n                        fixed bin(24);
dcl  op                       ptr;
dcl  pp                       ptr;
dcl  pstr		char(10);
dcl  rp_sw		bit(1);

dcl 1 option_args aligned based(op)
,     2 word1
,       3 opt (0:35)bit(1)unal
,     2 word2
,       3 initial_size fixed bin(18)unsigned unal
,       3 max_size     fixed bin(18)unsigned unal
,     2 user (n)
,       3 id           bit(72)
,       3 specific_permission bit(36)
,     2 end_of_list    fixed bin(35)
,     2 user_attributes bit(36)
;

dcl 1 delete_acl  (acl_count) aligned based(acl_ptr),
      2 del_name char(32),
      2 status_code2 fixed bin(35);

dcl 1 dir_acl (acl_count)aligned based(acl_ptr),
      2 access_name char(32),
      2 modes       bit(36),
      2 status_code fixed bin(35);
	end					/* set_specific_permissions */;

/**	(MC) Modify Catalog Declarations.	**/
dcl  new_name_ptr             ptr init(null());

dcl 1 MC_new_name		aligned based(new_name_ptr)
,     3 newname
,         4 left_word	bit(36)
,         4 right_word	bit(36)
,     3 newpassword		bit(72)
;

dcl 1 MC_args		aligned based(arg_ptr)
,     3 word1
,       4 zero		fixed bin(18)unsigned unal
,       4 L_arglist		fixed bin(18)unsigned unal
,     3 word2
,       4 MC_function_no	fixed bin(18)unsigned unal
,       4 L_buffer		fixed bin(18)unsigned unal
;

dcl 1 MC_arglist		aligned based(arglist_ptr)
,     3 word1
,       4 L_status_return	fixed bin(18)unsigned unal
,       4 zero		fixed bin(18)unsigned unal
,     3 word2
,       4 L_cat_filedescr	fixed bin(18)unsigned unal
,       4 L_permissions	fixed bin(18)unsigned unal
,     3 word3
,       4 L_options		fixed bin(18)unsigned unal
,       4 L_newname		fixed bin(18)unsigned unal
;

%include gtss_filact_intp3x;

%include gtss_filact_intp7x;


%include gtss_filact_intp2x;

%include gtss_filact_intp1x;

/** Declarations for gtss_drl_filact_:
     IDENTIFIER		ATTRIBUTES	**/
dcl  msf_manager_$acl_replace entry(ptr,ptr,fixed bin,bit(1),fixed bin(35));
dcl  addr                     builtin;
dcl  addrel                   builtin;
dcl  altname_ptr              ptr init(null());
dcl  append_mode              bit (36) static int options(constant)init ("100000000000"b3);
dcl  arglist_ptr              ptr init(null());
dcl  arg_ptr                  ptr init(null());
dcl  attribute_segment_ptr    ptr init(null());
dcl  bit72                    bit(72)aligned based;
dcl  cat_filedescr_name_offset fixed bin(18)unsigned unal ;
dcl  descriptor_ptr           ptr init(null());
dcl  divide                   builtin;
dcl  dname		char (168) unal;
dcl  ename		char (32) unal;
dcl  entry_name               char (12) init (" ");
dcl  error_table_$namedup     fixed bin (35) ext;
dcl  error_table_$segnamedup  fixed bin (35) ext;
dcl  get_system_free_area_ entry returns (ptr);
dcl  gseg                     ptr init(null());
dcl  hcs_$add_dir_acl_entries entry (char(*), char(*), ptr, fixed bin, fixed bin(35));
dcl  hcs_$chname_file	entry (char(*), char(*),char(*),char(*),fixed bin(35));
dcl  hcs_$delete_dir_acl_entries entry (char(*), char(*), ptr, fixed bin, fixed bin(35));
dcl  hcs_$list_dir_acl entry (char(*), char(*), ptr, ptr, ptr, fixed bin, fixed bin(35));
dcl  hcs_$replace_dir_acl entry (char(*), char(*), ptr, fixed bin, bit(1), fixed bin(35));
dcl  mem_top                  fixed bin(18)unsigned aligned;
dcl  i                        fixed bin(24);
dcl  ioa_                     entry options (variable);
dcl  mem_bottom               fixed bin(18)unsigned aligned static int options(constant)init(103);
dcl  mod                      builtin;
dcl  new_ename		char (32) unal init ("");
dcl  nic                      fixed bin(24);
dcl new_dir char(168);
dcl old_dir char(168);
dcl  p                        ptr init(null());
dcl  rel                      builtin;
dcl  status_ptr		ptr init(null());
dcl  substr                   builtin;

dcl 1 status2		aligned based(status_ptr)
,     3 word1
,       4 status_code	bit(12)unal
,       4 zero1		bit(06)unal
,       4 zero2		fixed bin(18)unsigned unal
,     3 word2
,       4 L_bcd_message	bit(18) unal
,       4 message_words	fixed bin(18)unsigned unal
;

dcl 1 bcdname aligned based(p),
    2 first8 bit(48)unal,
    2 last4  bit(24)unal;


dcl 1 status_word aligned based (status_ptr),
    2 status bit(18) unal,
    2 pd fixed bin(18)unsigned unal,
    2 null_bit bit (1) unal,
    2 user_attributes bit (35) unal;

%include gtss_ust_ext_;

%include gtss_ext_;

%include gtss_filact_status;

%include mc;

%include gtss_entry_dcls;

%include gtss_ascii_file_names;

%include gtss_FMS_catalog;

%include gtss_dfd_ext_;

%include gse_ext_;
     end						/* gtss_filact_funct10_ */;
