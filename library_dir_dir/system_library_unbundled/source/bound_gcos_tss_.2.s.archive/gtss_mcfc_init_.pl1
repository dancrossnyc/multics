/* ***********************************************************
   *                                                         *
   *                                                         *
   * Copyright, (C) Honeywell Information Systems Inc., 1981 *
   *                                                         *
   *                                                         *
   *********************************************************** */




/****^  HISTORY COMMENTS:
  1) change(2021-05-27,GDixon), approve(2021-05-27,MCR10090),
     audit(2021-06-03,Swenson), install(2021-06-08,MR126g-0057):
     Bypass dps8m simulator linkage-fault bug.  See Ticket #124
     for bug description.
                                                   END HISTORY COMMENTS */



gtss_mcfc_init_: proc (rs);

/* Set gtss_ext_$mcfc (data base values for
   multiple caller file control). Required
   before any calls made to gtss_mcfc_.
   Record callers lock id.
   Set default mcfc wait time (30 seconds).
   Return rs "true" if successful, else "false"
   (i.e. => can be called as active function.

   Author: Dave Ward	08/08/79
   Change: Dave Ward	11/28/79 Use object referencing directory.
   Change:  Ron Barstad  02/07/83  Remove false reference to >exl... for mcfc files
*/
dcl  rs                       char(*)var parm;
label_to_locate_this_object:
	call hcs_$fs_get_path_name (
	     codeptr (label_to_locate_this_object)
	     , dir_for_this_object
	     , 0
	     , ""
	     , c
	     );
	if c ^= 0 then do;
	     call com_err_ (
		c
		, "gtss_mcfc_init_"
		, "Could not obtain directory of mcfc data base."
		);
	     rs = "false";
	     return;
	end;

dcl  snap_link bit(36) aligned;			/* Snap link to access gtss_ext_$mcfc internal static.    */
          snap_link = gtss_ext_$mcfc.multics_lock_id;	/*  - The mcfc_ptr array just below begins setting that   */
						/*    static data.  dps8m simulator bug has problems in   */
						/*    link-snap if assigning to a non-zero offset from    */
						/*    the known link address.	 After link is snapped,     */
						/*    these problems do not occur.  For more info, see:   */
						/*     https://gitlab.com/dps8m/dps8m/-/issues/25	        */
	do i = 1 to hbound (mcfc_ptr, 1);
/* Set pointers to multiple caller file control
   (mcfc) data structures.
*/
	     call hcs_$initiate (
		dir_for_this_object
		, "GTSS.MCFC."||mcfc_name (i)
		, ""				/* Null reference name. */
		, 0				/* Segment no. not reserved. */
		, 1				/* Not a copy. */
		, mcfc_ptr (i)
		, c
		);
	     if c ^= 0 then
		if c ^= error_table_$segknown then do;
		     call com_err_ (
			c
			, "gtss_mcfc_init_"
			, "Could not obtain GTSS.MCFC.(FILES NAMES CALLERS_(1 2 3 4))."
			);
		     rs = "false";
		     return;
		end;
	end;

/* Obtain caller's (unique)
   lock identifier.
*/
	call get_lock_id_ (gtss_ext_$mcfc.multics_lock_id);

/* Set default wait time for mcfc callers.
   The number of real-time seconds a process
   is put to sleep awaiting use of mcfc.
*/
	gtss_ext_$mcfc.wait_time = 30;

	rs = "true";
	return;

/* Variables for gtss_mcfc_init_:
   IDENTIFIER		ATTRIBUTES */
dcl  c                        fixed bin(35);
dcl  codeptr                  builtin;
dcl  com_err_                 entry options(variable);
dcl  dir_for_this_object      char(168);
dcl  error_table_$segknown    fixed bin (35)ext;
dcl  get_lock_id_             entry(bit(36)aligned);
dcl  hcs_$fs_get_path_name    entry (ptr, char (*), fixed bin, char (*), fixed bin (35));
dcl  hcs_$initiate            entry(char(*),char(*),char(*),fixed bin(1),fixed bin(2),ptr,fixed bin(35));
dcl  i                        fixed bin;
dcl  mcfc_ptr                 (6)ptr aligned based(addr(gtss_ext_$mcfc.files_ptr));

dcl  mcfc_name                (6)char(9)var static int options(constant) init(
/* 1 */ "FILES"
,/* 2 */ "NAMES"
,/* 3 */ "CALLERS_1"
,/* 4 */ "CALLERS_2"
,/* 5 */ "CALLERS_3"
,/* 6 */ "CALLERS_4"
);

%include gtss_ext_;
     end						/* gtss_mcfc_init_ */;
